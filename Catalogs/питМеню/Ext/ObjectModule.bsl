#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Модуль справочника

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТА

Перем СтарыйВидМеню;				// Владелец меню перед сменой владельца (вида меню)

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОПОЛНИТЕЛЬНЫХ МЕТОДОВ ОБЪЕКТА


// Возвращает структуру обязательных / уникальных реквизитов элемента.
// Если ДляЭлемента = Истина, возвращаемая структура содержит реквизиты для проверки элемента.
// Если ДляГруппы = Истина, аналогично для группы
// Возвращаемая структура содержит строковые идентификаторы реквизитов или вложенные структуры для табличных частей.
// Для реквизита значение структуры содержит число 1-Обязательный, 3-Уникальный.
//
// Параметры:
//  ДляЭлемента	– Булево			– Возвращаемая структура должна содержать реквизиты для проверки элемента.
//  ДляГруппы	– Булево 			– Возвращаемая структура должна содержать реквизиты для проверки группы элементов.
//
// Возвращаемое значение:
//   ОбязательныеРеквизиты   		– Структура, содержащая реквизиты для проверки элемента или группы элементов.
//
Функция ПолучитьОбязательныеРеквизиты(ДляЭлемента=Истина, ДляГруппы=Ложь) Экспорт
	ОбязательныеРеквизиты	= Новый Структура();
		
	Если ДляЭлемента Тогда
	КонецЕсли;
	
	Если ДляГруппы Тогда
	КонецЕсли;
	
	Возврат ОбязательныеРеквизиты;
КонецФункции // ПолучитьОбязательныеРеквизиты()

// Проверяет корректность заполнения объекта.
// Возвращает Истина если все заполнено корректно и Ложь иначе.
// В случае некорректного заполнения формирует строку с описанием возникших ошибок "Ошибки".
// На вход может быть передана структура ДопРеквизиты с дополнительными реквизитами для проверки.
// Может управляться флагами выполняемых проверок Заполнение, Уникальность
// (могут быть и другие необязательные).
// Обычно выполняется универсальным обработчиком, но могут быть добавлены дополнительные проверки.
//
// Параметры:
//  Ошибки 			– Строка		– Описание ошибок.
//  ДопРеквизиты	– Структура		– Дополнительные реквизиты для проверки.
//  Заполнение		– Булево		– Флаг для проверки заполнения.
//  Уникальность	– Булево		– Флаг для проверки уникальности.
//
// Возвращаемое значение:
//   Булево				   			– Правильность заполнения объекта.
//
Функция ПроверитьКорректность(Ошибки="", ДопРеквизиты=Неопределено, Заполнение=Истина, Уникальность=Истина) Экспорт
	Корректность = питОбщегоНазначения.ПроверитьКорректность(ЭтотОбъект, Ошибки, ДопРеквизиты, Заполнение, Уникальность);
	
	Если НЕ Корректность Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстОшибки = "";
	Если НЕ Справочники.питМеню.ПроверитьДопустимыйВидНоменклатуры(Номенклатура, ТекстОшибки) Тогда
		Ошибки = СокрЛП(Ошибки + Символы.ПС + ТекстОшибки);
		Возврат Ложь;
	КонецЕсли; 
	
	Возврат Истина;
	
КонецФункции // ПроверитьКорректность()


// Возвращает максимальный номер в группе
//
// Возвращаемое значение:
//	Число - максимальный номер в группе справочника "питМеню".
//
Функция ПолучитьМаксимальныйНомерВГруппе() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(Меню.НомерВГруппе), 0) КАК НомерВГруппе
	|ИЗ
	|	Справочник.питМеню КАК Меню
	|ГДЕ
	|	Меню.Родитель = &Родитель
	|	И Меню.Владелец = &Владелец";
	
	Запрос.УстановитьПараметр("Родитель", Родитель);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	
	Результат	= Запрос.Выполнить();
	Выборка		= Результат.Выбрать();
	МаксНомер	= 0;
	Если Выборка.Следующий() Тогда
		МаксНомер	= Выборка.НомерВГруппе;
	КонецЕсли;
	Возврат МаксНомер;	
	
КонецФункции

// Проверка уникальности товара в прайсе.
//
// Параметры:
//	ТекущийПрайсЛист - СправочникСсылка - Элемент списка прайс-листа.
//
// Возвращаемое значение:
//	Булево	- Уникальность товара
//
Функция ТоварУникаленВПрайсе(ТекущийПрайсЛист) Экспорт
	СтрокаУникальность = ?(ТекущийПрайсЛист.ЭтоГруппа,"Наименование","Номенклатура");
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	ПрайсЛист." +СтрокаУникальность+"
	|ИЗ
	|	Справочник.питМеню КАК ПрайсЛист
	|ГДЕ
	|ПрайсЛист."+СтрокаУникальность+" = &ИсходнаяНоменклатура И
	|ПрайсЛист.Ссылка <> &ИсходныйПрайсЛист И
	|ПрайсЛист.Владелец = &ИсходныйВладелец
	|");
	Запрос.УстановитьПараметр("ИсходнаяНоменклатура",?(ТекущийПрайсЛист.ЭтоГруппа,ТекущийПрайсЛист.Наименование,ТекущийПрайсЛист.Номенклатура));
	Запрос.УстановитьПараметр("ИсходныйПрайсЛист",ТекущийПрайсЛист.Ссылка);
	Запрос.УстановитьПараметр("ИсходныйВладелец",ТекущийПрайсЛист.Владелец);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество()<>0 Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
КонецФункции // ТоварУникаленВПрайсе()

// Запросом получается выборка, упорядоченная по номеру в группе и каждый элемент выборки заново меняет номер.
//
// Параметры:
//	НоваяНумерация - Булево - признак новой нумерации
//
Процедура ПеренумероватьЭлементы(НоваяНумерация = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрайсЛист.НомерВГруппе КАК НомерВГруппе,
	|	ПрайсЛист.Ссылка
	|ИЗ
	|	Справочник.питМеню КАК ПрайсЛист
	|ГДЕ
	|	ПрайсЛист.Родитель = &Родитель
	|	И ПрайсЛист.Владелец = &Владелец
	|	И НЕ ПрайсЛист.Ссылка = &Ссылка 
	|УПОРЯДОЧИТЬ ПО
	|	НомерВГруппе";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Если НоваяНумерация Тогда
		парРодитель = Ссылка.Родитель;
		Запрос.УстановитьПараметр("Ссылка", Справочники.питМеню.ПустаяСсылка());
	Иначе
		парРодитель = Ссылка.Родитель;
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("Родитель", парРодитель);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ТекНомер = 0;
	
	Пока Выборка.Следующий() Цикл
		ТекНомер = ТекНомер + 1;
		СпрОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СпрОбъект.НомерВГруппе = ТекНомер;
		Попытка
			СпрОбъект.ОбменДанными.Загрузка = Истина;
			СпрОбъект.ДополнительныеСвойства.Вставить("НеУникальностьНомера", НЕ НоваяНумерация);
			СпрОбъект.Записать();
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки(), СпрОбъект);
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СТАНДАРТНЫХ СОБЫТИЙ ОБЪЕКТА

// Стандартный обработчик ПередЗаписью элемента справочника
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
		
	// Проверим, заполнен ли номер в группе
	// если забыли присвоить.
	Если НомерВГруппе = 0 ИЛИ НЕ ЭтотОбъект.Родитель = Ссылка.Родитель Тогда
		МаксНомер = ПолучитьМаксимальныйНомерВГруппе();
		НомерВГруппе = МаксНомер + 1;
		Если НЕ ЭтотОбъект.Родитель = Ссылка.Родитель И НЕ ЭтоНовый() Тогда
			// При смене родителя не забудем заново пронумеровать элементы старой группы.
			ПеренумероватьЭлементы();
		КонецЕсли;
	КонецЕсли;
	
	ДопРеквизиты	= Неопределено;
	Заполнение		= Истина;
	Уникальность	= Истина;
	ПроизошлаОшибка	= Ложь;
	ТекстОшибки		= "";
	
	// Выполним проверку корректности заполнения
	Если Не ЭтотОбъект.ПометкаУдаления Тогда
		ПроизошлаОшибка = Не ПроверитьКорректность(ТекстОшибки, ДопРеквизиты, Заполнение, Уникальность);
	КонецЕсли; 
	
	// Индикация ошибок
	Если (ПроизошлаОшибка) И (Не ПустаяСтрока(ТекстОшибки)) Тогда
		Имя		= ЭтотОбъект.Метаданные().Представление();
		ТекстСообщения	= "Перед записью элемента - "+Имя+":"""+СокрЛП(ЭтотОбъект)+""" обнаружены ошибки :
		|"+ТекстОшибки;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , ,Отказ);
	КонецЕсли;	
	
	СтарыйВидМеню = Ссылка.Владелец;
КонецПроцедуры // ПередЗаписью()

// Стандартный обработчик ПередУдалением элемента.
Процедура ПередУдалением(Отказ)
	
	Попытка
		// Для редких случаев, когда ЭтотОбъект неопределен
		// проверку на режим обмена данными делаем через попытку.
		Если ОбменДанными.Загрузка Тогда
			Возврат;
		КонецЕсли;
	Исключение 
	КонецПопытки;
	
	ПеренумероватьЭлементы();
	
	Если ЗначениеЗаполнено(MRS_КодSimphony) Тогда

		КассовыйУзел = ПолучитьКассовыйУзелДляПозиции(Владелец);

		Если ЗначениеЗаполнено(КассовыйУзел) Тогда
			MRS_ИнтеграцияSimphonyСервер.ДобавитьВОчередьВыгрузки(Ссылка, "DELETE", КассовыйУзел);
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры // ПередУдалением()

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Определяем, нужно ли ставить в очередь
	 ТребуетсяВыгрузка = Ложь;
	
	 Если MRS_КодSimphony = 0 Тогда
		
	     // Новая позиция - всегда в очередь
	     ТребуетсяВыгрузка = Истина;
	     СтатусСинхронизацииSimphony = Перечисления.MRS_СтатусыСинхронизацииSimphony.НеВыгружено;
		
	 Иначе
         // Проверяем, изменились ли поля, требующие синхронизации
         ИзмененныеПоля = Новый Массив;
         ИзмененныеПоля.Добавить("Наименование");
         ИзмененныеПоля.Добавить("Номенклатура");
         ИзмененныеПоля.Добавить("Выключен");
		
         ДанныеДоИзменения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, ИзмененныеПоля);
		
         Для Каждого Поле Из ИзмененныеПоля Цикл
			
             Если ЭтотОбъект[Поле] <> ДанныеДоИзменения[Поле] Тогда
                 ТребуетсяВыгрузка = Истина;
                 Прервать;
             КонецЕсли;
			
         КонецЦикла;
	 КонецЕсли;
	
	 Если ТребуетсяВыгрузка Тогда
		
	     // Получаем кассовый узел 
	     КассовыйУзел = ПолучитьКассовыйУзелДляПозиции(Владелец);
		
	     Если ЗначениеЗаполнено(КассовыйУзел) Тогда
			
	         Операция = ?(ЗначениеЗаполнено(MRS_КодSimphony), "UPDATE", "CREATE");
	         MRS_ИнтеграцияSimphonyСервер.ДобавитьВОчередьВыгрузки(Ссылка, Операция, КассовыйУзел);
			
	     КонецЕсли;
		
	 КонецЕсли;
 КонецПроцедуры
 
////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

#Область СлужебныеПроцедурыИФункции

// Получает кассовый узел для позиции меню
//
// Возвращаемое значение:
//  ПланОбменаСсылка.питУдаленныеКассы - кассовый узел
//
Функция ПолучитьКассовыйУзелДляПозиции(ВидМеню)
	
	Запрос = Новый Запрос;

	Запрос.Текст =
	"ВЫБРАТЬ
	|	питУдаленныеКассыВидыМеню.Ссылка КАК КассовыйУзел,
	|	питУдаленныеКассыВидыМеню.Ссылка.Префикс КАК Префикс
	|ИЗ
	|	ПланОбмена.питУдаленныеКассы.ВидыМеню КАК питУдаленныеКассыВидыМеню
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.питВидыМеню КАК питВидыМеню
	|		ПО (питУдаленныеКассыВидыМеню.ВидМеню = питВидыМеню.Ссылка)
	|ГДЕ
	|	питВидыМеню.Ссылка = &Ссылка";

	Запрос.УстановитьПараметр("Ссылка", ВидМеню);//(Справочники.питВидыМеню)
	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Возврат Выборка.КассовыйУзел;
	КонецЦикла;;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти
#КонецЕсли



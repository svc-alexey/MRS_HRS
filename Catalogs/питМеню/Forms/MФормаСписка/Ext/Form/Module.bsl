// Модуль формы списка справочника

Перем СпособРасчетаРецептур;  			// Способ подбора основной рецептуры для блюда.

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Функция ПолучитьИнформациюОбОшибке(ИнформацияОбОшибке)
	
	Результат = ИнформацияОбОшибке;
	Если ИнформацияОбОшибке <> Неопределено Тогда
		Если ИнформацияОбОшибке.Причина <> Неопределено Тогда
			Результат = ПолучитьИнформациюОбОшибке(ИнформацияОбОшибке.Причина);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ДействиеПодбор(ТабличноеПоле)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	МассивТиповНоменклатуры = Новый Массив();
	МассивТиповНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар"));
	ДоступныеТипыНоменклатуры = Новый ФиксированныйМассив(МассивТиповНоменклатуры);
	ПараметрыФормы.Вставить("Отбор", Новый Структура("ТипНоменклатуры", ДоступныеТипыНоменклатуры));
	
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаВыбора", 
			ПараметрыФормы, 
			ЭтаФорма, 
			УникальныйИдентификатор,,,, 
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	ДействиеПодбор(Список);	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	Доступно = ВладелецУстановлен(ЭтаФорма);
	Если Доступно Тогда
		ПараметрыФормы = Новый Структура("Владелец", ОтборВладелец);
		ОткрытьФорму("Справочник.питМеню.Форма.ФормаАвтозаполнения",ПараметрыФормы,ЭтаФорма,,,, Новый ОписаниеОповещения("ЗаполнитьЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	Иначе
		ПоказатьПредупреждение(Неопределено, "Владелец не выбран!");
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	ПараметрыФормы = Новый Структура("ВидПрайсЛиста", ОтборВладелец);
	ОткрытьФорму("Справочник.питМеню.Форма.ФормаВыгрузки",ПараметрыФормы,ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаименованияГрупп(Команда)
	// Меняем наименование элементов меню на наименование номенклатуры.
	// Группы не трогаем.
	МассивОшибок = Новый Массив;
	ОбновитьНаименования(Истина,МассивОшибок);
	Если МассивОшибок.Количество() > 0 Тогда
		ТекстОшибки = "";
		Статус = Неопределено;
		Для Каждого Ошибка Из МассивОшибок Цикл
			Ошибка.Свойство("Текст",ТекстОшибки);
			Ошибка.Свойство("Статус",Статус);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецЦикла;
	КонецЕсли;
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаименованияПозиций(Команда)
	// Меняем наименование элементов меню на наименование номенклатуры.
	// Группы не трогаем.
	МассивОшибок = Новый Массив;
	ОбновитьНаименования(Ложь,МассивОшибок);
	Если МассивОшибок.Количество() > 0 Тогда
		ТекстОшибки = "";
		Статус = Неопределено;
		Для Каждого Ошибка Из МассивОшибок Цикл
			Ошибка.Свойство("Текст",ТекстОшибки);
			Ошибка.Свойство("Статус",Статус);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецЦикла;
	КонецЕсли;
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоВыпускам(Команда)
	ПараметрыФормы = Новый Структура("Владелец", ОтборВладелец);
	ОткрытьФорму("Справочник.питМеню.Форма.ФормаЗаполненияПоПланамМеню",ПараметрыФормы,ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВсеГруппы(Команда)
	// Все группы меню (только в случае наличия группы верхнего уровня).
	ДляГрупп = Ложь; // Группы уже должны быть
	ОбновитьПоДеревуНоменклатуры(,ДляГрупп);
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ОчиститьЗавершение", ЭтотОбъект), "Удалить все позиции из меню?",РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет,);
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда 
		Возврат; 
	КонецЕсли;
	
	ОчиститьСервер();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьСервер()
	ВладелецПрайсЛиста = ПолучитьТекущегоВладельца();
	Справочники.питМеню.ОчиститьПрайсЛист(ВладелецПрайсЛиста)
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьСтруктуруГрупп(Команда)
	ДляГрупп = Истина; // Группы нет
	ОбновитьПоДеревуНоменклатуры(, ДляГрупп);
	Элементы.Список.Обновить();
КонецПроцедуры

&НаСервере
// Рекурсивная процедура обновления по дереву номенклатуры.
//
// Параметры:
//	Родитель - Родительский узел дерева.
//  ДляГрупп - Флаг принадлежности к группе.
//
// Возвращаемое значение:
//	Нет.
//
Процедура ОбновитьПоДеревуНоменклатуры(Родитель = Неопределено, ДляГрупп = Ложь)
	Если Родитель = Неопределено Тогда
		Родитель = Справочники.питМеню.ПустаяСсылка();
	КонецЕсли;
	РезультатЗапроса	= СформироватьДеревоНоменклатуры(Родитель);
	МенюРодитель		= Родитель;
	Уровень				= 0;
	ОбновитьПоВеткеНоменклатуры(РезультатЗапроса, МенюРодитель, Уровень, ДляГрупп); 
	
КонецПроцедуры

&НаСервере
// Возвращает номенклатуру, которая иерархически принадлежит указанному родителю.
//
// Параметры:
//	Родитель - Указанная номенклатура.
//
// Возвращаемое значение:
//	РезультатЗапроса - результат поисков.
//
Функция СформироватьДеревоНоменклатуры(Родитель)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка,
	|	Номенклатура.Родитель,
	|	Номенклатура.ЭтоГруппа
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В ИЕРАРХИИ(&Родитель)
	|	И НЕ Номенклатура.Ссылка = &Родитель
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура.Ссылка ИЕРАРХИЯ";
	
	Запрос.УстановитьПараметр("Родитель", Родитель.Номенклатура);
	РезультатЗапроса	= Запрос.Выполнить();   
	Возврат РезультатЗапроса;
	
КонецФункции

&НаСервере
// Обновляет данные на основе переданной выборки.
//
// Параметры:
//	ВыборкаРодитель - Выборка, на основе которой обновляются данные.
//  МенюРодитель    - Родительское меню, из которого происходит обновление.
//  Уровень         - Уровень в выборке по иерархии.
//  ДляГрупп        - Флаг принадлежности к группе.
//
// Возвращаемое значение:
//	Нет.
//
Процедура ОбновитьПоВеткеНоменклатуры(ВыборкаРодитель, МенюРодитель, Уровень = 0, ДляГрупп = Ложь)
	ЗаполнятьЭлементыГруппы	= Ложь;
	НомРодитель				= МенюРодитель.Номенклатура;
	Если Не ДляГрупп Тогда
		Если ЗначениеЗаполнено(НомРодитель) Тогда
			Действие				= " заполняется из " + "<" + МенюРодитель.Родитель + ">";
			ЗаполнятьЭлементыГруппы = Истина;
		Иначе
			Действие				= " не заполняется. Не указано соответствие с номенклатурой.";
		КонецЕсли;
		Если ЗначениеЗаполнено(МенюРодитель) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Группа меню " + "<" + МенюРодитель.Наименование + ">" + Действие;
			Сообщение.Сообщить();
		КонецЕсли;
	Иначе
		ЗаполнятьЭлементыГруппы		= Истина;
	КонецЕсли;
	
	
	Выборка	= ВыборкаРодитель.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		// Проверим, надо ли выводить эту строчку выборки.
		Если Выборка.Уровень() <> Уровень Тогда
			Продолжить;
		КонецЕсли;
		НомСсылка		= Выборка.Ссылка;
		ЗначениеОтбораВладелец = ПолучитьТекущегоВладельца();
		МенюСсылка		= Справочники.питМеню.НайтиПоРеквизиту("Номенклатура", НомСсылка, МенюРодитель, ЗначениеОтбораВладелец);
		ЭтоНовыйЭлемент = Ложь;
		Если ЗаполнятьЭлементыГруппы Тогда
			Если Не ЗначениеЗаполнено(МенюСсылка) Тогда
				// Нет такой группы меню
				ЭтоНовыйЭлемент	= Истина;
				Если ДляГрупп И НомСсылка.ЭтоГруппа Тогда
					ОбновитьГруппуМеню(НомСсылка, МенюСсылка, МенюРодитель);
					
				ИначеЕсли НЕ ДляГрупп И НЕ НомСсылка.ЭтоГруппа Тогда
					ОбновитьЭлементМеню(НомСсылка, МенюСсылка, МенюРодитель);
				КонецЕсли;
			Иначе
				Если НЕ ДляГрупп И НЕ НомСсылка.ЭтоГруппа Тогда
					ОбновитьЭлементМеню(НомСсылка, МенюСсылка, МенюРодитель);
				ИначеЕсли НомСсылка.ЭтоГруппа Тогда
					ОбновитьГруппуМеню(НомСсылка, МенюСсылка, МенюРодитель);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.ЭтоГруппа Тогда
			ОбновитьПоВеткеНоменклатуры(Выборка, МенюСсылка, 1+Выборка.Уровень(), ДляГрупп);
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры	

&НаСервере
// Добавление групп меню на основе заданной номенклатуры.
//
// Параметры:
//  НомСсылка       - Номенклатура, на основе которой происходит обновление группы меню.
//  МенюСсылка      - Элемент меню (прайс-листа).
//  МенюРодитель    - Родительский элемент меню.
//  НомНаименование - Заданное наименование элемента меню.
//
// Возвращаемое значение:
//	Нет.
//
Процедура ОбновитьГруппуМеню(НомСсылка, МенюСсылка, МенюРодитель, НомНаименование = "")
	
	Действие		= "";
	// Добавляем только позиции
	Статус			= СтатусСообщения.Информация;
	НазначенРодитель= Ложь;
	Если ЗначениеЗаполнено(МенюСсылка) Тогда
		Если МенюСсылка.НомерВГруппе = 0 Тогда
			МенюОбъект	= МенюСсылка.ПолучитьОбъект();
			Попытка
				МенюОбъект.Записать();
				МенюСсылка	= МенюОбъект.Ссылка;
				Действие	= "Присвоен номер группе меню ";
			Исключение
				Действие	= "Ошибка записи при присвоении номера группе меню: " + ПолучитьИнформациюОбОшибке(ИнформацияОбОшибке()).Описание;
				Статус		= СтатусСообщения.Внимание;
			КонецПопытки;
		КонецЕсли;
	Иначе
		НазначенРодитель	= Истина;
		МенюОбъект			= Справочники.питМеню.СоздатьГруппу();
		МенюОбъект.Родитель = МенюРодитель;
		ЗначениеОтбораВладелец = ПолучитьТекущегоВладельца();
		МенюОбъект.Владелец = ЗначениеОтбораВладелец;
		Если ПустаяСтрока(НомНаименование) Тогда
			МенюОбъект.Наименование = НомСсылка.Наименование;
		Иначе
			МенюОбъект.Наименование = НомНаименование;
		КонецЕсли; 
		
		МенюОбъект.Номенклатура	= НомСсылка;
		МенюОбъект.УстановитьНовыйКод();
		Попытка
			МенюОбъект.Записать();
			МенюСсылка	= МенюОбъект.Ссылка;
			Действие	= "Добавлена группа меню ";
		Исключение
			Действие	= "Ошибка записи при добавлении группы меню: " + ПолучитьИнформациюОбОшибке(ИнформацияОбОшибке()).Описание;
			Статус		= СтатусСообщения.Внимание;
		КонецПопытки;
	КонецЕсли;
	Если Действие <> "" Тогда
		Представление	= "<" + МенюСсылка.Наименование + ">";
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Действие + Представление;
		Сообщение.Сообщить();
		Если Статус = СтатусСообщения.Информация Тогда
			Если ЗначениеЗаполнено(МенюРодитель) И НазначенРодитель Тогда
				Представление	= "Группе меню " + Представление;
				Действие		= " назначен родитель " + "<" + МенюРодитель.Наименование + ">";
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = Представление + Действие;
				Сообщение.Сообщить();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
// Процедура обновления заданного элемента меню.
//
// Параметры:
//  НомСсылка    - Номенклатура, на основе которой происходит обновление.
//  МенюСсылка   - Элемент меню (прайс-листа).
//  МенюРодитель - Родительский элемент меню.
//
// Возвращаемое значение:
//	Нет.
//
Процедура ОбновитьЭлементМеню(НомСсылка, МенюСсылка, МенюРодитель)
	
	Статус	= СтатусСообщения.Информация;
	ТекстОшибки = "";
	// Добавляем только позиции
	Если  ЗначениеЗаполнено(МенюСсылка) Тогда
		Если МенюСсылка.Номенклатура = НомСсылка Тогда
			Действие = " уже есть в группе меню ";
			Если МенюСсылка.НомерВГруппе = 0 Тогда
				МенюОбъект = МенюСсылка.ПолучитьОбъект();
				Попытка
					МенюОбъект.Записать();
				Исключение
					Действие	= " ошибка записи: " + ПолучитьИнформациюОбОшибке(ИнформацияОбОшибке()).Описание;
					Статус		= СтатусСообщения.Внимание;
				КонецПопытки;
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(НомСсылка) Тогда
				МенюОбъект				= МенюСсылка.ПолучитьОбъект();
				МенюОбъект.Номенклатура = НомСсылка;
				Попытка
					МенюОбъект.Записать();
					Действие			= " добавлена в группу меню ";
				Исключение
					Действие			= " ошибка записи: " + ПолучитьИнформациюОбОшибке(ИнформацияОбОшибке()).Описание;
					Статус				= СтатусСообщения.Внимание;
				КонецПопытки;
			Иначе
				Действие				= " уже есть в группе меню ";
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли НЕ Справочники.питМеню.ПроверитьДопустимыйВидНоменклатуры(НомСсылка, ТекстОшибки) Тогда
		Действие	= " ошибка добавления в меню. " + ТекстОшибки;
		Статус		= СтатусСообщения.Внимание;
		
	Иначе
		МенюОбъект						= Справочники.питМеню.СоздатьЭлемент();
		МенюОбъект.ЕдиницаИзмерения		= НомСсылка.ЕдиницаИзмерения;
		МенюОбъект.Родитель				= МенюРодитель;
		МенюОбъект.Владелец				= ПолучитьТекущегоВладельца();
		МенюОбъект.Наименование			= НомСсылка.Наименование;
		МенюОбъект.Номенклатура			= НомСсылка;
		
		Если НомСсылка.питВидНоменклатуры = Перечисления.питВидыНоменклатуры.Блюдо Тогда
			Если Не ЗначениеЗаполнено(СпособРасчетаРецептур) Тогда
				//СпособРасчетаРецептур	= Константы.питСпособРасчетаРецептур.Получить();
				СпособРасчетаРецептур	= питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("СпособРасчетаРецептур");
			КонецЕсли;
			МенюОбъект.Рецептура		= питПроизводство.ВернутьОсновнуюРецептуру(НомСсылка,,Перечисления.питХозяйственныеОперации.РецептураПриготовление,ТекущаяДата(),СпособРасчетаРецептур);
			
		Иначе
			МенюОбъект.Рецептура	= Документы.питРецептура.ПустаяСсылка();
		КонецЕсли;
		
		МенюОбъект.УстановитьНовыйКод();
		Попытка
			МенюОбъект.Записать();
			МенюСсылка	= МенюОбъект.Ссылка;
			Действие	= " добавлена в группу меню ";
		Исключение
			Действие	= " ошибка записи: " + ПолучитьИнформациюОбОшибке(ИнформацияОбОшибке()).Описание;
			Статус		= СтатусСообщения.Внимание;
		КонецПопытки;
	КонецЕсли;
	
	Представление		= "Номенклатура ";
	Если Статус			= СтатусСообщения.Информация Тогда
		Действие		= Действие + "<" + МенюСсылка.Родитель + ">";
	КонецЕсли;		
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Представление + "<" + НомСсылка.Наименование + ">" + Действие;
	Сообщение.Сообщить();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	питИзменениеОтраслевыхФормСервер.ПриСозданииНаСервереВНачале(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Параметры.Отбор.Свойство("Владелец") Тогда
		ОтборВладелец = Параметры.Отбор.Владелец;
		Элементы.СтраницыОтборВидПрайсЛиста.ТекущаяСтраница = Элементы.ГруппаОтображениеВидПрайсЛиста;
		Параметры.Отбор.Удалить("Владелец");
	Иначе
		Если ОтборВладелец.Пустая() Тогда
			ОтборВладелец = Справочники.питВидыМеню.ОсновнойВидМеню;
		КонецЕсли;
		Элементы.СтраницыОтборВидПрайсЛиста.ТекущаяСтраница = Элементы.ГруппаОтборВидПрайсЛиста;
	КонецЕсли;
	ОтборВладелецИспользование = ЗначениеЗаполнено(ОтборВладелец);
	питОбщегоНазначенияКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Владелец");
	
	Если ТолькоПросмотр Тогда
		Элементы.ФормаПодбор.Доступность    = Ложь;
		Элементы.ФормаУдалить.Доступность   = Ложь;
		Элементы.ФормаОчистить.Доступность	= Ложь;
		Элементы.ФормаЗаполнить.Доступность = Ложь;
		Элементы.ФормаВыгрузить.Доступность = Ложь;
		Элементы.ФормаОбновитьВсеГруппы.Доступность            = Ложь;
		Элементы.ФормаЗаполнитьНаименованияПозиций.Доступность = Ложь;
		Элементы.ФормаЗаполнитьНаименованияГрупп.Доступность   = Ложь;
		Элементы.ФормаСкопироватьСтруктуруГрупп.Доступность    = Ложь;
		Элементы.ФормаЗаполнитьПоВыпускам.Доступность          = Ложь;
	КонецЕсли;
	
	УправлениеДиалогом(ЭтаФорма);
	УстановитьВидимость();
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()
	//СпособРасчетаРецептур							= Константы.питСпособРасчетаРецептур.Получить();	
	СпособРасчетаРецептур	= питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("СпособРасчетаРецептур");
	СпособРасчетаРецептурВВыпускеПродукции			= Константы.питСпособРасчетаРецептур.Получить();
	// Определим видимость колонки ссылочной рецептуры.
	Если СпособРасчетаРецептурВВыпускеПродукции=Перечисления.питСпособРасчетаРецептур.ПоАктуальнойРецептуре Тогда
		Элементы.Список.ПодчиненныеЭлементы.Рецептура.Видимость			= Ложь;
	Иначе
		Элементы.Список.ПодчиненныеЭлементы.Рецептура.Видимость			= Истина;
	КонецЕсли;
	Элементы.Список.ПодчиненныеЭлементы.Владелец.Видимость	= Ложь;
	Элементы.Список.ПодчиненныеЭлементы.Код.Видимость		= Ложь;
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.питМеню);
	Элементы.КоманднаяПанельФормы.Видимость				= МожноРедактировать;
	Элементы.СписокКонтекстноеМенюУдалитьМеню.Видимость	= МожноРедактировать;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// Подключим обработку изменения отборов форм списков справочников.
	//ВидПрайсЛиста = ПолучитьТекущегоВладельца();
	// Восстановим видимость дерева групп (для иерархических справочников).
	КнопкаДерева		= ЭтаФорма.Элементы.ФормаОтображатьДерево; 
	//Показать			= ВосстановитьЗначение(СтрЗаменить(ЭтаФорма.Список,".","_")+"_СкрытьДеревоГрупп");
	Показать			= ?(Показать=Неопределено,Ложь,Показать);
	КнопкаДерева.Пометка= Показать;
	ОтображатьДерево(КнопкаДерева);
	
КонецПроцедуры


&НаКлиентеНаСервереБезКонтекста
// Управляет видимостью и доступностью элементов формы.
Процедура УправлениеДиалогом(Форма)
	Элементы = Форма.Элементы;
	Доступно = ВладелецУстановлен(Форма);
	Элементы.ФормаПодбор.Доступность	= Доступно;
	Элементы.ФормаОчистить.Доступность	= Доступно; 
	Элементы.ФормаЗаполнить.Доступность	= Доступно;
	Элементы.ФормаВыгрузить.Доступность	= Доступно;
	Элементы.ФормаОбновитьВсеГруппы.Доступность			= Доступно;
	Элементы.ФормаСкопироватьСтруктуруГрупп.Доступность = Доступно;
	Элементы.ФормаЗаполнитьПоВыпускам.Доступность		= Доступно;
	Элементы.ФормаЗаполнитьНаименованияПозиций.Доступность	= Доступно;
	Элементы.ФормаЗаполнитьНаименованияГрупп.Доступность	= Доступно;
КонецПроцедуры // УправлениеДиалогом()

&НаКлиенте
Процедура ОтображатьДерево(Команда)
	Элементы.ФормаОтображатьДерево.Пометка		= Не Элементы.ФормаОтображатьДерево.Пометка; // Инвертируем флаг
	Элементы.Дерево.Видимость = Ложь;
	Если НЕ Элементы.Дерево.Видимость Тогда
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Родитель")
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Перем Команда;
	
	Если ИсточникВыбора.ИмяФормы = "Справочник.Номенклатура.Форма.ФормаВыбора" Тогда
		
		ТекстОшибки = "";
		ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение, ТекстОшибки);
		
		Если ТекстОшибки <> "" Тогда
			ПоказатьПредупреждение(Неопределено, ТекстОшибки);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ИсточникВыбора)=Тип("УправляемаяФорма")
		И ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		
		ЗаполнитьМенюПоВыпускамПродукции(ВыбранноеЗначение);
		Элементы.Список.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение, ТекстОшибки)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		СписокТоваров = ВыбранноеЗначение;
	Иначе
		СписокТоваров = Новый Массив;
		СписокТоваров.Добавить(ВыбранноеЗначение);
	КонецЕсли;
	
	ОбработкаПодбора(Список, СписокТоваров, ТекстОшибки);
	
КонецПроцедуры


&НаСервере
// Производит заполнение прайс-листа переданными из формы подбора данными.
//
// Параметры:
//  ТабличноеПоле     - табличное поле, в которое надо добавлять подобранную позицию номенклатуры;
//  ЗначениеВыбора    - структура, содержащая параметры подбора.
//
Процедура ОбработкаПодбора(ТабличноеПоле, ЗначениеВыбора, ТекстОшибки = "")
	
	Если НЕ ТипЗнч(ЗначениеВыбора) = Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	// Ищем выбранную позицию в таблице прайс-листа.
	Запрос			= Новый ПостроительЗапроса();
	Запрос.Текст	= "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Меню.Ссылка
	|ИЗ
	|	Справочник.питМеню КАК Меню
	|{ГДЕ
	|	Меню.Ссылка.*,
	|	Меню.ПометкаУдаления,
	|	Меню.Владелец.*,
	|	Меню.ЭтоГруппа,
	|	Меню.Код,
	|	Меню.Наименование,
	|	Меню.Номенклатура.*,
	|	Меню.Рецептура.*,
	|	Меню.ЕдиницаИзмерения.*,
	|	Меню.НомерВГруппе,
	|	Меню.Выключен,
	|	Меню.ИмяКартинки,
	|	Меню.ЦветФонаКнопок}";
	
	
	Для Каждого Номенклатура Из ЗначениеВыбора Цикл
		ТекстОшибки = "";
		Если НЕ Справочники.питМеню.ПроверитьДопустимыйВидНоменклатуры(Номенклатура, ТекстОшибки) Тогда
			Продолжить;
		КонецЕсли;
		
		ЕдиницаИзмерения = Номенклатура.ЕдиницаИзмерения;
		
		// Очистим отбор
		ОтборКоличество = Запрос.Отбор.Количество();
		Для Ном = 1 По ОтборКоличество Цикл
			Запрос.Отбор.Удалить(ОтборКоличество - Ном);
		КонецЦикла;
		
		ЕдиницаИзмеренияСписок = Новый СписокЗначений;
		ЕдиницаИзмеренияСписок.Добавить(ЕдиницаИзмерения);
		ЕдиницаИзмеренияСписок.Добавить(Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
		
		НовыйЭлемент = Запрос.Отбор.Добавить("Номенклатура");
		НовыйЭлемент.Значение		= Номенклатура;
		НовыйЭлемент.ВидСравнения	= ВидСравнения.Равно;
		НовыйЭлемент.Использование	= Истина;
		
		НовыйЭлемент = Запрос.Отбор.Добавить("ЕдиницаИзмерения");
		НовыйЭлемент.ВидСравнения	= ВидСравнения.ВСписке;
		НовыйЭлемент.Значение		= ЕдиницаИзмеренияСписок;
		НовыйЭлемент.Использование	= Истина;
		
		НовыйЭлемент = Запрос.Отбор.Добавить("ЭтоГруппа");
		НовыйЭлемент.Значение		= Номенклатура.ЭтоГруппа;
		НовыйЭлемент.ВидСравнения	= ВидСравнения.Равно;
		НовыйЭлемент.Использование	= Истина;
		
		НовыйЭлемент = Запрос.Отбор.Добавить("Владелец");
		НовыйЭлемент.Значение		= ПолучитьТекущегоВладельца();
		НовыйЭлемент.ВидСравнения	= ВидСравнения.Равно;
		НовыйЭлемент.Использование	= Истина;
		
		//ВидПрайсЛиста	= НовыйЭлемент.Значение;
		
		Запрос.Выполнить();
		Выборка	= Запрос.Результат.Выбрать();
		
		Если Выборка.Количество() = 0 Тогда
			Если Номенклатура.ЭтоГруппа Тогда
				ЭлементМеню					= Справочники.питМеню.СоздатьГруппу();	
			Иначе
				ЭлементМеню					= Справочники.питМеню.СоздатьЭлемент();
				
				Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
					ЭлементМеню.ЕдиницаИзмерения = ЕдиницаИзмерения;
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(СпособРасчетаРецептур) Тогда
					//СпособРасчетаРецептур	= Константы.питСпособРасчетаРецептур.Получить();
					СпособРасчетаРецептур	= питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("СпособРасчетаРецептур");
				КонецЕсли;
				ЭлементМеню.Рецептура		= питПроизводство.ВернутьОсновнуюРецептуру(Номенклатура,,Перечисления.питХозяйственныеОперации.РецептураПриготовление,ТекущаяДата(),СпособРасчетаРецептур);
			КонецЕсли;
			
			ЭлементМеню.Владелец    		= ?(ЗначениеЗаполнено(ОтборВладелец), ОтборВладелец, Справочники.питВидыМеню.ОсновнойВидМеню);
			ЭлементМеню.Наименование		= Номенклатура.Наименование;
			ЭлементМеню.Номенклатура		= Номенклатура;
			Если ЗначениеЗаполнено(Элементы.Список.ТекущийРодитель) Тогда
				ЭлементМеню.Родитель		= Элементы.Список.ТекущийРодитель;
			КонецЕсли;
			ЭлементМеню.УстановитьНовыйКод();
			
			Попытка
				ЭлементМеню.Записать();
				ЭлементМеню					= ЭлементМеню.Ссылка;
			Исключение
				ЭлементМеню = Неопределено;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Запись меню """+ЭлементМеню+""": "+ОписаниеОшибки());
			КонецПопытки;
		Иначе
			Выборка.Следующий();
			ЭлементМеню	= Выборка.Ссылка;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЭлементМеню <> Неопределено Тогда
		Элементы.Список.ТекущаяСтрока  = ЭлементМеню;
		Элементы.Список.ТекущийЭлемент = Элементы.Список.ПодчиненныеЭлементы.Номенклатура;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПодбора()

&НаСервере
Процедура ЗаполнитьМенюПоВыпускамПродукции(МассивДокументов)
	
	// Очистим имеющийся состав меню.
	Владелец = ПолучитьТекущегоВладельца();
	РезультатОчистки = Справочники.питМеню.ОчиститьПрайсЛист(Владелец);
	Если НЕ РезультатОчистки Тогда
		Возврат;
	КонецЕсли; 
	
	// Заполним новым составом (категория номенклатуры - это группа меню).
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВыпускПродукцииТовары.Номенклатура КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ВыпускПродукцииТовары.Номенклатура.Родитель) КАК Наименование,
	|	ВыпускПродукцииТовары.Номенклатура
	|ИЗ
	|	Документ.питВыпускБлюд.Товары КАК ВыпускПродукцииТовары
	|ГДЕ
	|	ВыпускПродукцииТовары.Ссылка В(&МассивСсылок)
	|ИТОГИ ПО
	|	ВыпускПродукцииТовары.Номенклатура.Родитель";
	
	Запрос.УстановитьПараметр("МассивСсылок", МассивДокументов);
	ВыборкаКатегории = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаКатегории.Следующий() Цикл
		
		НомСсылка = Справочники.Номенклатура.НайтиПоНаименованию(ВыборкаКатегории.Наименование, Истина);
		МенюСсылка = Справочники.питМеню.ПустаяСсылка();
		МенюРодитель = Справочники.питМеню.ПустаяСсылка();
		
		Если НЕ НомСсылка.Пустая() ИЛИ НЕ ПустаяСтрока(ВыборкаКатегории.Наименование) Тогда
			ОбновитьГруппуМеню(НомСсылка, МенюСсылка, МенюРодитель, ВыборкаКатегории.Наименование);
		КонецЕсли; 
		
		МенюРодитель = МенюСсылка;
		
		Выборка = ВыборкаКатегории.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НомСсылка = Выборка.Ссылка;
			МенюСсылка = Справочники.питМеню.ПустаяСсылка();
			
			ОбновитьЭлементМеню(НомСсылка, МенюСсылка, МенюРодитель);
			
		КонецЦикла; 
		
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	Попытка
		// Идентификатор может быть отличен от "Список",
		// позиционируемся на введенный объект через попытку.
		ЭтаФорма.Элементы.Список.ТекущаяСтрока = НовыйОбъект.Ссылка;
	Исключение КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновлениеЗначенийСвойств" И Источник = Элементы.Список.ТекущаяСтрока Тогда
		//РаботаСДиалогами.ИзменитьПредставлениеКнопкиВыбораСвойств(ЭтаФорма, Параметр);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьТекущегоВладельца()
	ВладелецОтбор = питОбщегоНазначенияКлиентСервер.ЭлементОтбораСпискаПоИмени(Список, "Владелец");
	Если ВладелецОтбор <> Неопределено Тогда
		ВладелецОтбор = ВладелецОтбор.ПравоеЗначение;
	Иначе
		ВладелецОтбор = Справочники.питВидыМеню.ПустаяСсылка()
	КонецЕсли;
	Возврат ВладелецОтбор
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВладелецУстановлен(Форма)
	ВладелецОтбор = питОбщегоНазначенияКлиентСервер.ЭлементОтбораСпискаПоИмени(Форма.Список, "Владелец");
	Если ВладелецОтбор <> Неопределено Тогда
		ЗначениеОтбораВладелец = ?(ВладелецОтбор = Неопределено, Форма.ВидПрайсЛистаПустаяСсылка, ВладелецОтбор.ПравоеЗначение);
		ИспользованиеОтбораВладелец = ?(ВладелецОтбор = Неопределено, Ложь, ВладелецОтбор.Использование);
		
		Доступно = ЗначениеЗаполнено(ЗначениеОтбораВладелец) И ИспользованиеОтбораВладелец;
	Иначе
		Доступно = Ложь;
	КонецЕсли;
	
	Возврат Доступно;
КонецФункции

&НаСервере
Процедура ОбновитьНаименования(ДляГрупп = Ложь, МассивОшибок = "")
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Меню.Ссылка КАК МенюСсылка,
	|	Меню.Номенклатура.Ссылка КАК НомСсылка,
	|	Меню.Родитель,
	|	Меню.ЭтоГруппа,
	|	Меню.Наименование КАК МенюНаименование,
	|	Меню.Номенклатура.Наименование КАК НомНаименование,
	|	Меню.НомерВГруппе
	|ИЗ
	|	Справочник.питМеню КАК Меню
	|ГДЕ
	|	"+?(ДляГрупп,"","Не ")+"Меню.ЭтоГруппа
	|	И НЕ Меню.Наименование = Меню.Номенклатура.Наименование
	|	И Меню.Владелец = &Владелец
	|УПОРЯДОЧИТЬ ПО
	|	Меню.НомерВГруппе";
	Запрос.УстановитьПараметр("ДляГрупп", ДляГрупп);
	Запрос.УстановитьПараметр("Владелец", ПолучитьТекущегоВладельца());
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Статус					= СтатусСообщения.Информация;
		МенюСсылка				= Выборка.МенюСсылка;
		МенюОбъект				= МенюСсылка.ПолучитьОбъект();
		МенюОбъект.Наименование	= Выборка.НомНаименование;
		
		Попытка
			МенюОбъект.Записать();
			Действие	= " изменилось на ";
		Исключение
			Действие	= " ошибка записи: " + ПолучитьИнформациюОбОшибке(ИнформацияОбОшибке()).Описание;
			Статус		= СтатусСообщения.Внимание;
		КонецПопытки;
		Если Статус = СтатусСообщения.Информация Тогда
			Действие	= Действие + "<" + МенюСсылка.Наименование + ">";
		КонецЕсли;
		СтруктураОшибок = Новый Структура;
		СтруктураОшибок.Вставить("Текст","Наименование " + "<" + Выборка.МенюНаименование + ">" + Действие);
		СтруктураОшибок.Вставить("Статус", Статус);
		МассивОшибок.Добавить(СтруктураОшибок);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УменьшитьНомер(Команда)
	ТекСтрока = Элементы.Список.ТекущаяСтрока;
	Если ТекСтрока = Неопределено ТОгда
		Возврат;
	КонецЕсли;
	Ошибки = "";
	УменьшитьНомерНаСервере(ТекСтрока, Ошибки);
	Если Ошибки <> "" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Запись меню """+ТекСтрока+""": "+Ошибки);
	КонецЕсли;
	Элементы.Список.Обновить();
КонецПроцедуры

&НаСервере
Процедура УменьшитьНомерНаСервере(ПрайсЛист, Ошибки = "")
	ТекНомерВГруппе	= ПрайсЛист.НомерВГруппе;
	Если ТекНомерВГруппе <=1 Тогда
		Возврат;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Меню.Ссылка,
	|	Меню.НомерВГруппе КАК НомерВГруппе
	|ИЗ
	|	Справочник.питМеню КАК Меню
	|ГДЕ
	|	Меню.Владелец = &ВидМеню
	|	И (Меню.НомерВГруппе < &НомерВГруппе ИЛИ (Меню.НомерВГруппе = &НомерВГруппе И Меню.Ссылка < &Ссылка))
	|	И Меню.Родитель =  &ТекРодитель
	|УПОРЯДОЧИТЬ ПО
	|	НомерВГруппе УБЫВ";
	Запрос.УстановитьПараметр("ВидМеню",ПолучитьТекущегоВладельца());
	Запрос.УстановитьПараметр("НомерВГруппе",ТекНомерВГруппе);
	Запрос.УстановитьПараметр("ТекРодитель",ПрайсЛист.Родитель);
	Запрос.УстановитьПараметр("Ссылка",ПрайсЛист);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		// Запомним первый больший приоритет
		ПервыйНомерВГруппе = Выборка.НомерВГруппе;
		Если ПервыйНомерВГруппе = ТекНомерВГруппе Тогда
			ПервыйНомерВГруппе = ПервыйНомерВГруппе - 1;
		Иначе
			ОбъектМеню = Выборка.Ссылка.ПолучитьОбъект();
			ОбъектМеню.НомерВГруппе = ПервыйНомерВГруппе + 1;
			ОбъектМеню.ДополнительныеСвойства.Вставить("СменаНомера",Истина);
			Попытка
				ОбъектМеню.Записать();
			Исключение
				Ошибки = Ошибки + ОписаниеОшибки();
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки(), ОбъектМеню);
			КонецПопытки;
		КонецЕсли;
		
		ОбъектМеню = ПрайсЛист.ПолучитьОбъект();
		ОбъектМеню.НомерВГруппе = ПервыйНомерВГруппе;
		ОбъектМеню.ДополнительныеСвойства.Вставить("СменаНомера",Истина);
		Попытка
			ОбъектМеню.Записать();
		Исключение
			Ошибки = Ошибки + ОписаниеОшибки();
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки(), ОбъектМеню);
		КонецПопытки;
		
	КонецЕсли;	
	
	// Возможно дублирование номеров, если есть повторение, то сдвинем их в другую сторону.
	Пока Выборка.Следующий() И (ПервыйНомерВГруппе <= Выборка.НомерВГруппе) И (Выборка.НомерВГруппе > 0) Цикл
		
		Если ПервыйНомерВГруппе > 1 Тогда
			ПервыйНомерВГруппе = ПервыйНомерВГруппе - 1 ;
		КонецЕсли;
		
		ОбъектМеню = Выборка.Ссылка.ПолучитьОбъект();
		ОбъектМеню.НомерВГруппе = ПервыйНомерВГруппе;
		ОбъектМеню.ДополнительныеСвойства.Вставить("СменаНомера",Истина);
		Попытка
			ОбъектМеню.Записать();
		Исключение
			Ошибки = Ошибки + ОписаниеОшибки();
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки(), ОбъектМеню);
		КонецПопытки;
		
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура УвеличитьНомер(Команда)
	ТекСтрока = Элементы.Список.ТекущаяСтрока;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Ошибки = "";
	УвеличитьНомерНаСервере(ТекСтрока, Ошибки);
	Если Ошибки <> "" Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Запись меню """+ТекСтрока+""": "+Ошибки);
	КонецЕсли;
	Элементы.Список.Обновить();
КонецПроцедуры

&НаСервере
Процедура УвеличитьНомерНаСервере(ПрайсЛист,Ошибки = "")
	ТекНомерВГруппе	= ПрайсЛист.НомерВГруппе;	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Меню.Ссылка,
	|	Меню.НомерВГруппе КАК НомерВГруппе
	|ИЗ
	|	Справочник.питМеню КАК Меню
	|ГДЕ
	|	Меню.Владелец = &ВидМеню
	|	И (Меню.НомерВГруппе > &НомерВГруппе ИЛИ (Меню.НомерВГруппе = &НомерВГруппе И Меню.Ссылка > &Ссылка))
	|   И Меню.Родитель =  &ТекРодитель
	|УПОРЯДОЧИТЬ ПО
	|	НомерВГруппе";
	Запрос.УстановитьПараметр("ВидМеню",ПолучитьТекущегоВладельца());
	Запрос.УстановитьПараметр("НомерВГруппе",ТекНомерВГруппе);
	Запрос.УстановитьПараметр("ТекРодитель",ПрайсЛист.Родитель);
	Запрос.УстановитьПараметр("Ссылка",ПрайсЛист);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		// Запомним первый минимальный приоритет
		ПервыйНомерВГруппе = Выборка.НомерВГруппе;
		Если ПервыйНомерВГруппе = ТекНомерВГруппе Тогда
			ПервыйНомерВГруппе = ПервыйНомерВГруппе + 1;
		Иначе
			ОбъектМеню = Выборка.Ссылка.ПолучитьОбъект();
			ОбъектМеню.НомерВГруппе = ПервыйНомерВГруппе - 1;
			ОбъектМеню.ДополнительныеСвойства.Вставить("СменаНомера",Истина);
			Попытка
				ОбъектМеню.Записать();
			Исключение
				Ошибки = Ошибки + ОписаниеОшибки();
			КонецПопытки;
		КонецЕсли;
		
		ОбъектМеню = ПрайсЛист.ПолучитьОбъект();
		ОбъектМеню.НомерВГруппе = ПервыйНомерВГруппе;
		ОбъектМеню.ДополнительныеСвойства.Вставить("СменаНомера",Истина);
		Попытка
			ОбъектМеню.Записать();
		Исключение
			Ошибки = Ошибки + ОписаниеОшибки();
		КонецПопытки;
		
	КонецЕсли;
	
	// Возможно дублирование номеров, если есть повторение, то сдвинем их в другую сторону.
	Пока Выборка.Следующий() И (ПервыйНомерВГруппе >= Выборка.НомерВГруппе) Цикл
		
		ПервыйНомерВГруппе = ПервыйНомерВГруппе + 1 ;
		
		ОбъектМеню = Выборка.Ссылка.ПолучитьОбъект();
		ОбъектМеню.НомерВГруппе = ПервыйНомерВГруппе;
		ОбъектМеню.ДополнительныеСвойства.Вставить("СменаНомера",Истина);
		Попытка
			ОбъектМеню.Записать();
		Исключение
			Ошибки = Ошибки + ОписаниеОшибки();
		КонецПопытки;
		
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидПрайсЛистаИспользованиеПриИзменении(Элемент)
	
	питОбщегоНазначенияКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Владелец");
	УправлениеДиалогом(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидПрайсЛистаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ОтборВладелец) Тогда
		ОтборВладелецИспользование = Истина;
	КонецЕсли;
	
	питОбщегоНазначенияКлиентСервер.УстановитьБыстрыйОтбор(ЭтотОбъект, "Владелец");
	
	УправлениеДиалогом(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьМеню(Команда)
	текСтрока = Элементы.Список.ТекущаяСтрока;
	Если ЗначениеЗаполнено(текСтрока) Тогда
		УдалитьМенюСервер(текСтрока);
		Элементы.Список.Обновить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦены(Команда)
	
	// Формирование списка вариантов выбора
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить("Выделенные", "Только выделенные позиции");
	СписокВыбора.Добавить("Группа", "Всю текущую группу");
	СписокВыбора.Добавить("ВсеМеню", "Все меню текущего вида");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьЦеныВыборРежима", ЭтотОбъект);
	ПоказатьВыборИзМеню(ОписаниеОповещения, СписокВыбора, Элементы.ФормаУстановитьЦены);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦеныВыборРежима(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Режим = ВыбранныйЭлемент.Значение;
	
	// Получение выделенных строк
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	
	// Создание документа установки цен
	РезультатСоздания = СоздатьДокументУстановкиЦенНаСервере(Режим, ВыделенныеСтроки);
	
	Если РезультатСоздания.Успешно Тогда
		
		ТекстСообщения = СтрШаблон("Создан документ с %1 позициями номенклатуры", РезультатСоздания.КоличествоПозиций);
		ПоказатьОповещениеПользователя("Установка цен", , ТекстСообщения, БиблиотекаКартинок.Информация32);
		
		// Открытие созданного документа
		ОткрытьФорму("Документ.УстановкаЦенНоменклатуры.Форма.ФормаДокумента", 
			Новый Структура("Ключ", РезультатСоздания.Документ));
		
	Иначе
		
		ПоказатьПредупреждение(, РезультатСоздания.Сообщение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоставитьВОчередьВыгрузки(Команда)
	
	// Формирование списка вариантов выбора
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.Добавить("Выделенные", "Только выделенные позиции");
	СписокВыбора.Добавить("Группа", "Всю текущую группу");
	СписокВыбора.Добавить("ВсеМеню", "Все меню текущего вида");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоставитьВОчередьВыгрузкиВыборРежима", ЭтотОбъект);
	ПоказатьВыборИзМеню(ОписаниеОповещения, СписокВыбора, Элементы.ФормаПоставитьВОчередьВыгрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоставитьВОчередьВыгрузкиВыборРежима(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Режим = ВыбранныйЭлемент.Значение;
	
	// Получение выделенных строк
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	
	// Вызов серверной процедуры
	Результат = ПоставитьВОчередьВыгрузкиНаСервере(Режим, ВыделенныеСтроки);
	
	// Вывод результата
	Если Результат.Успешно Тогда
		
		Если Результат.КоличествоПропущено > 0 Тогда
			ТекстСообщения = СтрШаблон("Поставлено в очередь: %1 позиций. Пропущено: %2 (нет записи о цене).", 
				Результат.КоличествоПозиций, Результат.КоличествоПропущено);
		Иначе
			ТекстСообщения = СтрШаблон("Поставлено в очередь: %1 позиций.", Результат.КоличествоПозиций);
		КонецЕсли;
		
		ПоказатьОповещениеПользователя("Постановка в очередь", , ТекстСообщения, БиблиотекаКартинок.Информация32);
		
	Иначе
		
		ПоказатьПредупреждение(, Результат.Сообщение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоставитьВОчередьВыгрузкиНаСервере(Знач Режим, Знач ВыделенныеСтроки)
	
	Результат = Новый Структура("Успешно, Сообщение, КоличествоПозиций, КоличествоПропущено", Ложь, "", 0, 0);
	
	Попытка
		
		// Получение вида меню
		ВидМеню = ПолучитьТекущегоВладельца();
		
		Если Не ЗначениеЗаполнено(ВидМеню) Тогда
			Результат.Сообщение = "Не выбран вид меню";
			Возврат Результат;
		КонецЕсли;
		
		// Получение кассового узла по виду меню
		КассовыйУзел = ПолучитьКассовыйУзелПоВидуМеню(ВидМеню);
		
		Если Не ЗначениеЗаполнено(КассовыйУзел) Тогда
			Результат.Сообщение = "Не найден кассовый узел для данного вида меню";
			Возврат Результат;
		КонецЕсли;
		
		// Проверка вида цен у кассового узла
		ВидЦен = ПолучитьВидЦенКассовогоУзла(КассовыйУзел);
		
		Если Не ЗначениеЗаполнено(ВидЦен) Тогда
			Результат.Сообщение = СтрШаблон("Не указан вид цен для кассового узла ""%1""", Строка(КассовыйУзел));
			Возврат Результат;
		КонецЕсли;
		
		// Формирование списка позиций в зависимости от режима
		МассивПозиций = Новый Массив;
		
		Если Режим = "Выделенные" Тогда
			
			МассивПозиций = ПолучитьВыделенныеПозиции(ВыделенныеСтроки);
			
		ИначеЕсли Режим = "Группа" Тогда
			
			// Вся текущая группа
			ТекущийРодитель = Элементы.Список.ТекущийРодитель;
			МассивПозиций = ПолучитьВсеПозицииГруппы(ТекущийРодитель, ВидМеню);
			
		ИначеЕсли Режим = "ВсеМеню" Тогда
			
			// Все позиции вида меню
			МассивПозиций = ПолучитьВсеПозицииВидаМеню(ВидМеню);
			
		Иначе
			
			Результат.Сообщение = "Неизвестный режим постановки в очередь";
			Возврат Результат;
			
		КонецЕсли;
		
		Если МассивПозиций.Количество() = 0 Тогда
			Результат.Сообщение = "Не найдено позиций для постановки в очередь";
			Возврат Результат;
		КонецЕсли;
		
		// Получение данных о позициях с проверкой наличия цен
		ТаблицаПозицийСЦенами = ПолучитьПозицииСПроверкойЦен(МассивПозиций, КассовыйУзел);
		
		КоличествоДобавлено = 0;
		КоличествоПропущено = 0;
		
		// Постановка позиций в очередь с проверкой цены
		Для Каждого СтрокаПозиции Из ТаблицаПозицийСЦенами Цикл
			
			Если СтрокаПозиции.ЕстьЦена Тогда
				
				MRS_ИнтеграцияSimphonyСервер.ДобавитьВОчередьВыгрузки(СтрокаПозиции.Позиция, "CREATE", КассовыйУзел);
				КоличествоДобавлено = КоличествоДобавлено + 1;
				
			Иначе
				
				КоличествоПропущено = КоличествоПропущено + 1;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Результат.Успешно = Истина;
		Результат.КоличествоПозиций = КоличествоДобавлено;
		Результат.КоличествоПропущено = КоличествоПропущено;
		
	Исключение
		
		Результат.Сообщение = "Ошибка постановки в очередь: " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьВыделенныеПозиции(Знач ВыделенныеСтроки)
	
	МассивПозиций = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		
		Если ТипЗнч(ВыделеннаяСтрока) = Тип("СправочникСсылка.питМеню") Тогда
			
			Если Не ВыделеннаяСтрока.ЭтоГруппа Тогда
				МассивПозиций.Добавить(ВыделеннаяСтрока);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивПозиций;
	
КонецФункции

&НаСервере
Функция ПолучитьВсеПозицииГруппы(Родитель, ВидМеню)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	питМеню.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.питМеню КАК питМеню
		|ГДЕ
		|	НЕ питМеню.ЭтоГруппа
		|	И питМеню.Родитель = &Родитель
		|	И питМеню.Владелец = &ВидМеню
		|	И НЕ питМеню.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Родитель", Родитель);
	Запрос.УстановитьПараметр("ВидМеню", ВидМеню);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

&НаСервере
Функция ПолучитьВсеПозицииВидаМеню(ВидМеню)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	питМеню.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.питМеню КАК питМеню
		|ГДЕ
		|	НЕ питМеню.ЭтоГруппа
		|	И питМеню.Владелец = &ВидМеню
		|	И НЕ питМеню.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ВидМеню", ВидМеню);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

&НаСервере
Функция ПолучитьКассовыйУзелПоВидуМеню(ВидМеню)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	питВидыМенюКассовыеУзлы.КассовыйУзел КАК КассовыйУзел
		|ИЗ
		|	Справочник.питВидыМеню.КассовыеУзлы КАК питВидыМенюКассовыеУзлы
		|ГДЕ
		|	питВидыМенюКассовыеУзлы.Ссылка = &ВидМеню";
	
	Запрос.УстановитьПараметр("ВидМеню", ВидМеню);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.КассовыйУзел;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ВыгрузитьМеню(Команда)
	
	ВидМеню = ПолучитьТекущегоВладельцаНаКлиенте();
	
	Если Не ЗначениеЗаполнено(ВидМеню) Тогда
		ПоказатьПредупреждение(, "Не выбран вид меню для выгрузки");
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = СтрШаблон("Выгрузить все позиции меню ""%1"" в Simphony?", Строка(ВидМеню));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузитьМенюПодтверждение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьМенюПодтверждение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	// Вызов серверной процедуры
	Результат = ВыгрузитьМенюВSimphonyНаСервере();
	
	// Вывод результата
	Если Результат.Успешно Тогда
		
		ТекстСообщения = СтрШаблон("Выгружено: %1, Ошибок: %2", Результат.КоличествоУспешно, Результат.КоличествоОшибок);
		
		Если Результат.КоличествоОшибок > 0 Тогда
			ПоказатьПредупреждение(, ТекстСообщения);
		Иначе
			ПоказатьОповещениеПользователя("Выгрузка меню", , ТекстСообщения, БиблиотекаКартинок.Успешно32);
		КонецЕсли;
		
	Иначе
		
		ПоказатьПредупреждение(, Результат.Сообщение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВыгрузитьМенюВSimphonyНаСервере()
	
	Результат = Новый Структура("Успешно, Сообщение, КоличествоУспешно, КоличествоОшибок", Ложь, "", 0, 0);
	
	Попытка
		
		// Получение вида меню
		ВидМеню = ПолучитьТекущегоВладельца();
		
		Если Не ЗначениеЗаполнено(ВидМеню) Тогда
			Результат.Сообщение = "Не выбран вид меню";
			Возврат Результат;
		КонецЕсли;
		
		// Получение кассового узла
		КассовыйУзел = ПолучитьКассовыйУзелПоВидуМеню(ВидМеню);
		
		Если Не ЗначениеЗаполнено(КассовыйУзел) Тогда
			Результат.Сообщение = "Не найден кассовый узел для данного вида меню";
			Возврат Результат;
		КонецЕсли;
		
		// Получение всех позиций меню
		МассивПозиций = ПолучитьВсеПозицииВидаМеню(ВидМеню);
		
		Если МассивПозиций.Количество() = 0 Тогда
			Результат.Сообщение = "Нет позиций для выгрузки";
			Возврат Результат;
		КонецЕсли;
		
		// Выгрузка позиций
		КоличествоУспешно = 0;
		КоличествоОшибок = 0;
		
		Для Каждого Позиция Из МассивПозиций Цикл
			
			РезультатВыгрузки = MRS_ИнтеграцияSimphonyСервер.ВыгрузитьПозициюВSimphony(Позиция, КассовыйУзел);
			
			Если РезультатВыгрузки.Успешно Тогда
				КоличествоУспешно = КоличествоУспешно + 1;
			Иначе
				КоличествоОшибок = КоличествоОшибок + 1;
			КонецЕсли;
			
		КонецЦикла;
		
		Результат.Успешно = Истина;
		Результат.КоличествоУспешно = КоличествоУспешно;
		Результат.КоличествоОшибок = КоличествоОшибок;
		
	Исключение
		
		Результат.Сообщение = "Ошибка выгрузки меню: " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПолучитьТекущегоВладельцаНаКлиенте()
	
	Возврат ОтборВладелец;
	
КонецФункции

&НаСервере
Функция СоздатьДокументУстановкиЦенНаСервере(Знач Режим, Знач ВыделенныеСтроки)
	
	Результат = Новый Структура("Успешно, Сообщение, Документ, КоличествоПозиций", Ложь, "", Неопределено, 0);
	
	Попытка
		
		// Получение вида меню
		ВидМеню = ПолучитьТекущегоВладельца();
		
		Если Не ЗначениеЗаполнено(ВидМеню) Тогда
			Результат.Сообщение = "Не выбран вид меню";
			Возврат Результат;
		КонецЕсли;
		
		// Определение списка позиций в зависимости от режима
		МассивПозиций = Новый Массив;
		
		Если Режим = "Выделенные" Тогда
			
			// Только выделенные позиции
			Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
				Если ТипЗнч(ВыделеннаяСтрока) = Тип("СправочникСсылка.питМеню") Тогда
					МассивПозиций.Добавить(ВыделеннаяСтрока);
				КонецЕсли;
			КонецЦикла;
			
			Если МассивПозиций.Количество() = 0 Тогда
				Результат.Сообщение = "Не выбрано ни одной позиции меню";
				Возврат Результат;
			КонецЕсли;
			
		ИначеЕсли Режим = "Группа" Тогда
			
			// Все элементы из текущей группы
			Если ВыделенныеСтроки.Количество() = 0 Тогда
				Результат.Сообщение = "Не выбрана группа";
				Возврат Результат;
			КонецЕсли;
			
			ТекущийРодитель = ВыделенныеСтроки[0];
			Если ТипЗнч(ТекущийРодитель) <> Тип("СправочникСсылка.питМеню") Тогда
				Результат.Сообщение = "Не выбрана группа";
				Возврат Результат;
			КонецЕсли;
			
			// Получение всех элементов группы
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	питМеню.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.питМеню КАК питМеню
				|ГДЕ
				|	питМеню.Родитель = &Родитель
				|	И НЕ питМеню.ЭтоГруппа
				|	И питМеню.Владелец = &ВидМеню";
			Запрос.УстановитьПараметр("Родитель", ТекущийРодитель);
			Запрос.УстановитьПараметр("ВидМеню", ВидМеню);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				МассивПозиций.Добавить(Выборка.Ссылка);
			КонецЦикла;
			
			Если МассивПозиций.Количество() = 0 Тогда
				Результат.Сообщение = "В выбранной группе нет элементов";
				Возврат Результат;
			КонецЕсли;
			
		ИначеЕсли Режим = "ВсеМеню" Тогда
			
			// Все элементы вида меню (кроме групп)
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	питМеню.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.питМеню КАК питМеню
				|ГДЕ
				|	питМеню.Владелец = &ВидМеню
				|	И НЕ питМеню.ЭтоГруппа";
			Запрос.УстановитьПараметр("ВидМеню", ВидМеню);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				МассивПозиций.Добавить(Выборка.Ссылка);
			КонецЦикла;
			
			Если МассивПозиций.Количество() = 0 Тогда
				Результат.Сообщение = "В меню нет элементов";
				Возврат Результат;
			КонецЕсли;
			
		Иначе
			
			Результат.Сообщение = "Неизвестный режим работы";
			Возврат Результат;
			
		КонецЕсли;
		
		// Сбор номенклатуры из позиций меню
		МассивНоменклатуры = Новый Массив;
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	питМеню.Номенклатура КАК Номенклатура
			|ИЗ
			|	Справочник.питМеню КАК питМеню
			|ГДЕ
			|	питМеню.Ссылка В(&МассивПозиций)
			|	И НЕ питМеню.ЭтоГруппа
			|	И питМеню.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)";
		Запрос.УстановитьПараметр("МассивПозиций", МассивПозиций);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			МассивНоменклатуры.Добавить(Выборка.Номенклатура);
		КонецЦикла;
		
		Если МассивНоменклатуры.Количество() = 0 Тогда
			Результат.Сообщение = "Не найдено позиций с заполненной номенклатурой";
			Возврат Результат;
		КонецЕсли;
		
		// Создание документа
		НовыйДокумент = Документы.УстановкаЦенНоменклатуры.СоздатьДокумент();
		НовыйДокумент.Дата = ТекущаяДатаСеанса();
		НовыйДокумент.Ответственный = Пользователи.ТекущийПользователь();
		НовыйДокумент.Комментарий = СтрШаблон("Создано из вида меню ""%1""", Строка(ВидМеню));
		
		// Заполнение табличной части Товары
		Для Каждого Номенклатура Из МассивНоменклатуры Цикл
			
			НоваяСтрока = НовыйДокумент.Товары.Добавить();
			НоваяСтрока.Номенклатура = Номенклатура;
			
		КонецЦикла;
		
		// Получение вида цен из кассового узла
		КассовыйУзел = ПолучитьКассовыйУзелПоВидуМеню(ВидМеню);
		
		Если ЗначениеЗаполнено(КассовыйУзел) Тогда
			
			ВидЦен = ПолучитьВидЦенКассовогоУзла(КассовыйУзел);
			
			Если ЗначениеЗаполнено(ВидЦен) Тогда
				
				НоваяСтрокаВида = НовыйДокумент.ВидыЦен.Добавить();
				НоваяСтрокаВида.ВидЦены = ВидЦен;
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Запись документа
		НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
		
		Результат.Успешно = Истина;
		Результат.Документ = НовыйДокумент.Ссылка;
		Результат.КоличествоПозиций = МассивНоменклатуры.Количество();
		
	Исключение
		
		Результат.Сообщение = "Ошибка создания документа: " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьВидЦенКассовогоУзла(КассовыйУзел)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	питУдаленныеКассы.ВидЦен КАК ВидЦен
		|ИЗ
		|	ПланОбмена.питУдаленныеКассы КАК питУдаленныеКассы
		|ГДЕ
		|	питУдаленныеКассы.Ссылка = &КассовыйУзел";
	
	Запрос.УстановитьПараметр("КассовыйУзел", КассовыйУзел);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ВидЦен;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаСервере
Функция ПолучитьПозицииСПроверкойЦен(МассивПозиций, КассовыйУзел)
	
	// Получаем вид цен кассового узла
	ВидЦен = ПолучитьВидЦенКассовогоУзла(КассовыйУзел);
	
	Если Не ЗначениеЗаполнено(ВидЦен) Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	// Формируем таблицу позиций с информацией о наличии цен
	ТаблицаПозиций = Новый ТаблицаЗначений;
	ТаблицаПозиций.Колонки.Добавить("Позиция", Новый ОписаниеТипов("СправочникСсылка.питМеню"));
	ТаблицаПозиций.Колонки.Добавить("ЕстьЦена", Новый ОписаниеТипов("Булево"));
	
	// Запрос для проверки наличия цен для всех позиций за один раз
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	питМеню.Ссылка КАК Позиция,
		|	питМеню.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ВТ_Позиции
		|ИЗ
		|	Справочник.питМеню КАК питМеню
		|ГДЕ
		|	питМеню.Ссылка В(&МассивПозиций)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
		|	МАКСИМУМ(ЦеныНоменклатуры.Цена) КАК Цена
		|ПОМЕСТИТЬ ВТ_Цены
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|ГДЕ
		|	ЦеныНоменклатуры.ВидЦены = &ВидЦены
		|	И ЦеныНоменклатуры.Период <= &Период
		|	И ЦеныНоменклатуры.Номенклатура В
		|			(ВЫБРАТЬ
		|				ВТ_Позиции.Номенклатура
		|			ИЗ
		|				ВТ_Позиции КАК ВТ_Позиции)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЦеныНоменклатуры.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Позиции.Позиция КАК Позиция,
		|	ВЫБОР
		|		КОГДА ВТ_Цены.Номенклатура ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьЦена
		|ИЗ
		|	ВТ_Позиции КАК ВТ_Позиции
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Цены КАК ВТ_Цены
		|		ПО ВТ_Позиции.Номенклатура = ВТ_Цены.Номенклатура";
	
	Запрос.УстановитьПараметр("МассивПозиций", МассивПозиций);
	Запрос.УстановитьПараметр("ВидЦены", ВидЦен);
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УдалитьМенюСервер(Меню)
	МенюОбъект = Меню.ПолучитьОбъект();
	МенюОбъект.ОбменДанными.Загрузка = Истина; //?? временно, исправление ошибки удаления элемента меню с правами Бухгалтера.
	// Из-за вот этого(↑↑↑↑↑↑↑)флага, при удалении объекта,
	// если это группа, то платформа не производит удаление дочерних,
	// поэтому остаются битые ссылки.
	// Ищем все дочерние элементы с учетом иерархии, для того, чтобы и их тоже удалить.
	МассивДочерних = Новый Массив;
	Если МенюОбъект.ОбменДанными.Загрузка И МенюОбъект.ЭтоГруппа Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	питМеню.Ссылка
		|ИЗ
		|	Справочник.питМеню КАК питМеню
		|ГДЕ
		|	питМеню.Родитель В ИЕРАРХИИ(&Родитель)";
		Запрос.УстановитьПараметр("Родитель", Меню);
		МассивДочерних = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
	Попытка
		Если МенюОбъект.ОбменДанными.Загрузка Тогда
			// Удаление дочерних вручную
			Для каждого ТекДочернийЭлемент Из МассивДочерних Цикл
				ТекДочернийОбъект = ТекДочернийЭлемент.ПолучитьОбъект();
				ТекДочернийОбъект.ОбменДанными.Загрузка = Истина; //?? временно, исправление ошибки удаления элемента меню с правами Бухгалтера.
				ТекДочернийОбъект.Удалить();
			КонецЦикла;
		КонецЕсли;
		// Удаление непосредственно элемента меню
		МенюОбъект.Удалить();
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки;
КонецПроцедуры


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	// Проверка уникальности КодКлассаСимфони
	Если ЗначениеЗаполнено(КодКлассаСимфони) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	1 КАК Поле1
			|ИЗ
			|	Справочник.MRS_КлассыМеню КАК КлассыМеню
			|ГДЕ
			|	КлассыМеню.КодКлассаСимфони = &КодКлассаСимфони
			|	И КлассыМеню.Ссылка <> &ТекущаяСсылка";
		
		Запрос.УстановитьПараметр("КодКлассаСимфони", КодКлассаСимфони);
		Запрос.УстановитьПараметр("ТекущаяСсылка", Ссылка);
		
		Если НЕ Запрос.Выполнить().Пустой() Тогда
			
			ТекстСообщения = "Код класса Симфони должен быть уникальным!";
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "КодКлассаСимфони", , Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Проверка заполнения реквизитов ТЧ Условия
	НомерСтроки = 0;
	Для Каждого СтрокаУсловия Из Условия Цикл
		
		НомерСтроки = НомерСтроки + 1;
		ПрефиксПути = "Объект.Условия[" + Формат(НомерСтроки - 1, "ЧГ=0") + "].";
		
		Если НЕ ЗначениеЗаполнено(СтрокаУсловия.ВидУсловия) Тогда
			
			ТекстСообщения = СтрШаблон("Не заполнен Вид Условия в строке %1", НомерСтроки);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , ПрефиксПути + "ВидУсловия", , Отказ);
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтрокаУсловия.Оператор) Тогда
			
			ТекстСообщения = СтрШаблон("Не заполнен Оператор в строке %1", НомерСтроки);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , ПрефиксПути + "Оператор", , Отказ);
			
		КонецЕсли;
		
		// Для операторов "Заполнено" и "НеЗаполнено" значение не требуется
		Если НЕ ЗначениеЗаполнено(СтрокаУсловия.Значение) 
			И СтрокаУсловия.Оператор <> "Заполнено" 
			И СтрокаУсловия.Оператор <> "НеЗаполнено" Тогда
			
			ТекстСообщения = СтрШаблон("Не заполнено Значение в строке %1", НомерСтроки);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , ПрефиксПути + "Значение", , Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

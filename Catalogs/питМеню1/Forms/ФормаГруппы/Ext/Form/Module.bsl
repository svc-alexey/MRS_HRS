
// Модуль формы элемента справочника
&НаКлиенте
Перем ЦветФонаПоУмолчанию;    // Цвет фона кнопки, если он явно не задан.
Перем ЗначениеЦветФонаКнопок; // Цвет фона кнопки в виде строки.


&НаКлиенте
Процедура ЦветПриИзменении(Элемент)
	ИмяРеквизита = Элемент.Имя;
	ИмяРеквизита = СтрЗаменить(ИмяРеквизита, "Красный", "");
	ИмяРеквизита = СтрЗаменить(ИмяРеквизита, "Зеленый", "");
	ИмяРеквизита = СтрЗаменить(ИмяРеквизита, "Синий", "");
	
	ТекЦвет = Новый Цвет(ЭтаФорма[ИмяРеквизита + "Красный"], ЭтаФорма[ИмяРеквизита + "Зеленый"], ЭтаФорма[ИмяРеквизита + "Синий"]);
	ЗначениеЦветФонаКнопок = ТекЦвет;	
	Объект.ЦветФонаКнопок = ВернутьЦветФонаКнопокСтрокой(ТекЦвет);
	
	Элементы.КнопкаШаблон.ЦветФона = ЗначениеЦветФонаКнопок;
КонецПроцедуры

&НаКлиенте
Процедура ЦветФонаКнопокОчистить(Команда)
	ЗначениеЦветФонаКнопок = Новый Цвет(0, 0, 0);
	ЦветФонаКнопкиКрасный  = 0;
	ЦветФонаКнопкиЗеленый  = 0;
	ЦветФонаКнопкиСиний    = 0;
	ЦветФонаКнопок = "";
	Элементы.КнопкаШаблон.ЦветФона = ЗначениеЦветФонаКнопок;
	
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	Если НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
		Объект.Наименование = питОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Номенклатура,"Наименование");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ВернутьЦветФонаКнопокСтрокой(ТекЦвет)
	Возврат ЗначениеВСтрокуВнутр(ТекЦвет);
КонецФункции

&НаСервере
Функция ВернутьЦветФонаКнопокИзСтроки(ТекЦвет)
	Возврат ЗначениеИзСтрокиВнутр(ТекЦвет);
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
// Обработчик события "ПриОткрытии" формы
//
Процедура ПриОткрытии(Отказ)
	
	ЗначениеЦветФонаКнопок = Новый Цвет(0, 0, 0);
	
	Если Объект.Ссылка.Пустая() Тогда
		
		ЗначениеЦветФонаКнопок = ЦветФонаПоУмолчанию;
		Объект.ЦветФонаКнопок = ВернутьЦветФонаКнопокСтрокой(ЗначениеЦветФонаКнопок);
		ЦветФонаКнопкиКрасный  = ЗначениеЦветФонаКнопок.Красный;
		ЦветФонаКнопкиЗеленый  = ЗначениеЦветФонаКнопок.Зеленый;
		ЦветФонаКнопкиСиний    = ЗначениеЦветФонаКнопок.Синий;
		
	Иначе
		
		ЦветПолучен = Ложь;
		Если НЕ ПустаяСтрока(Объект.ЦветФонаКнопок) Тогда
			Попытка
				ЗначениеЦветФонаКнопок = ВернутьЦветФонаКнопокИзСтроки(Объект.ЦветФонаКнопок);
				ЦветПолучен = Истина;
			Исключение 
				ТекстОшибки = ИнформацияОбОшибке().Описание;
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Ошибка получения цвета фона кнопки: " + ТекстОшибки;
				Сообщение.Сообщить();
			КонецПопытки;
		КонецЕсли; 
		Если ЦветПолучен Тогда
			ЦветФонаКнопкиКрасный 	= ЗначениеЦветФонаКнопок.Красный;
			ЦветФонаКнопкиЗеленый 	= ЗначениеЦветФонаКнопок.Зеленый;
			ЦветФонаКнопкиСиний 	= ЗначениеЦветФонаКнопок.Синий;
		КонецЕсли; 
		
	КонецЕсли;
	
	Элементы.КнопкаШаблон.ЦветФона = ЗначениеЦветФонаКнопок;
	
КонецПроцедуры // ПриОткрытии()

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	питМеню.Ссылка
	|ИЗ
	|	Справочник.питМеню КАК питМеню
	|ГДЕ
	|	питМеню.Родитель В ИЕРАРХИИ(&Родитель)");
	
	Запрос.УстановитьПараметр("Родитель", ТекущийОбъект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ЗаголовокОшибок = "Изменение флага запрещения печати элементов меню в группе """+ТекущийОбъект.Ссылка+""":";
	Пока Выборка.Следующий() Цикл
		Если Выборка.Ссылка.Выключен <> ТекущийОбъект.Выключен Тогда
			
			// Установим флаг
			ОбъектМеню = Выборка.Ссылка.ПолучитьОбъект();
			ОбъектМеню.Выключен = ТекущийОбъект.Выключен;
			
			// Отключим проверки при записи
			ОбъектМеню.ОбменДанными.Загрузка = Истина;
			Попытка
				ОбъектМеню.Записать();
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ЗаголовокОшибок+" Запись меню """+ОбъектМеню+""". "+ОписаниеОшибки());
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

ЦветФонаПоУмолчанию = Новый Цвет(227, 220, 198);

// Модуль формы элемента справочника

////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ ОБЪЕКТ

//Перем ТЗПрайсЛиста;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаКлиенте
// Процедура выгрузки прайс-листа
//
Функция ВыгрузитьПрайсЛист(СтрокаОшибки = "")
	ЕстьЗаполненныеПоля = Ложь;
	Для Каждого ПолеВыгрузки Из СтруктураФайлаПрайсЛиста Цикл
		Если Не ЗначениеЗаполнено(ПолеВыгрузки.ИмяПоляФайла) Тогда
			Продолжить;
		КонецЕсли;
		ЕстьЗаполненныеПоля = Истина;
		Прервать;
	КонецЦикла;
	Если Не ЕстьЗаполненныеПоля Тогда
		// Если поля не заполнены, продолжать не имеет смысла.
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ни одно поле таблицы не заполнено!");
		Возврат Ложь;
	КонецЕсли;
	
	// Получим все необходимые данные для выгрузки.
	ЗаполнитьДанныеПрайсЛиста();
	РезультатВыгрузки=Истина;
	Если ФайлИсточникДанных Тогда
		// Грузим Exсel
		ТипФайла = ВРег(Прав(СокрЛП(СтрокаПодключения),4));
		Если ТипФайла = ".XLS" Тогда
			РезультатВыгрузки = ЗаписатьEXCEL();			
		ИначеЕсли ТипФайла = ".TXT" Тогда
			РезультатВыгрузки = ЗаписатьTXT();  
		ИначеЕсли ТипФайла = ".DBF" Тогда
			РезультатВыгрузки = ЗаписатьDBF();	
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Недопустимый тип файла.");
			Возврат Ложь;
		КонецЕсли; 
	Иначе // Грузим из ADO
		РезультатВыгрузки = ЗаписатьADO();
	КонецЕсли;
	
	Если РезультатВыгрузки<>Неопределено И РезультатВыгрузки Тогда
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ошибка при выгрузке прайс-листа");
		Возврат Ложь;
	КонецЕсли;
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Обработка прайс-листа завершена !");
	Возврат Истина;
КонецФункции // ВыгрузитьПрайсЛист()


&НаСервере
// Получает таблицу с данными по прайс-листу.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   ТЗПрайсЛиста   – таблица значений.
//
Функция ЗаполнитьДанныеПрайсЛиста()
	
	Если питПроизводствоПовтИсп.ПолучитьИмяОсновнойКонфигурации() = "ERPWE" Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	мМассивСчетов	= Новый Массив();
	мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["СырьеИМатериалы"]);
	мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["ТоварыНаСкладах"]);
	мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["ТоварыВРозничнойТорговле"]);
	мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ"]);
	мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["ГотоваяПродукция"]);
	мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["Полуфабрикаты"]);
	
	мМассивСубконто	= Новый Массив();
	Для Каждого ВидСубконто Из ПланыСчетов.Хозрасчетный.ТоварыНаСкладах.ВидыСубконто Цикл
		мМассивСубконто.Добавить(ВидСубконто.ВидСубконто);
	КонецЦикла;	
	
	ТекстЗапроса1	= 
					"ВЫБРАТЬ РАЗРЕШЕННЫЕ
					|	ПрайсЛист.Номенклатура КАК Номенклатура,
					|	ПрайсЛист.Номенклатура.Наименование КАК Наименование,
					|	ПрайсЛист.Номенклатура.Код КАК Артикул
					|ПОМЕСТИТЬ Позиции
					|ИЗ
					|	Справочник.питМеню КАК ПрайсЛист"+?(ЗначениеЗаполнено(ВидПрайсЛиста),"
					|ГДЕ
					|	ПрайсЛист.Владелец = &ВидПрайсЛиста","")+"
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	ВЫРАЗИТЬ(ХозрасчетныйОстатки.Субконто1 КАК Справочник.Номенклатура) КАК Номенклатура,
					|	ХозрасчетныйОстатки.КоличествоОстаток КАК Количество
					|ПОМЕСТИТЬ ТаблицаОстатков
					|ИЗ
					|	РегистрБухгалтерии.Хозрасчетный.Остатки(
					|			&ТекДата,
					|			Счет В (&СписокСчетов),
					|			&СписокСубконто,
					|			ВЫРАЗИТЬ(Субконто1 КАК Справочник.Номенклатура) В
					|				(ВЫБРАТЬ
					|					Позиции.Номенклатура
					|				ИЗ
					|					Позиции КАК Позиции)) КАК ХозрасчетныйОстатки
					|ГДЕ
					|	ХозрасчетныйОстатки.КоличествоОстаток > 0
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	Номенклатура
					|";
	ТекстЗапроса2 = "
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	Позиции.Артикул КАК Артикул,
					|	Позиции.Наименование КАК Наименование,
					|	СУММА(ЕстьNULL(ТаблицаЦен.Цена,0)) КАК Цена,
					|	СУММА(ЕстьNULL(ТаблицаОстатков.Количество,0)) КАК Количество
					|ИЗ
					|	Позиции КАК Позиции
					|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЦен КАК ТаблицаЦен
					|		ПО Позиции.Номенклатура = ТаблицаЦен.Номенклатура
					|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОстатков КАК ТаблицаОстатков
					|		ПО Позиции.Номенклатура = ТаблицаОстатков.Номенклатура
					|
					|СГРУППИРОВАТЬ ПО
					|	Позиции.Артикул,
					|	Позиции.Наименование
					|
					|УПОРЯДОЧИТЬ ПО
					|	Артикул";
	
	// +пит
	ТекстЗапросаШаблон = 
		"%1
		|;
		|%2
		|;
		|%3";
	ТекстЦеныОтборНоменклатура = "ВЫБРАТЬ
		|						Позиции.Номенклатура
		|					ИЗ
		|						Позиции";
	ТекстЗапросаЦены = питЦеныНоменклатуры.ТекстЗапросаЦеныНоменклатуры(ТекущаяДата(), 
		"ТаблицаЦен", "", ТекстЦеныОтборНоменклатура, "ХарактеристикаЦен", "ВыбТипЦен");
	ТекстЗапроса = СтрШаблон(ТекстЗапросаШаблон, ТекстЗапроса1, ТекстЗапросаЦены, ТекстЗапроса2);
	// -пит
	
	Запрос							= Новый Запрос();
	Запрос.МенеджерВременныхТаблиц	= Новый МенеджерВременныхТаблиц();	
	Запрос.Текст					= ТекстЗапроса;	
	Запрос.УстановитьПараметр("СписокСубконто", мМассивСубконто);
	Запрос.УстановитьПараметр("СписокСчетов", мМассивСчетов);		
	Запрос.УстановитьПараметр("ВидПрайсЛиста",ВидПрайсЛиста);
	Запрос.УстановитьПараметр("ВыбТипЦен",?(ЗначениеЗаполнено(ТипЦен),ТипЦен,питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("ОсновнойТипЦенПродажи")));
	Запрос.УстановитьПараметр("ТекДата",ТекущаяДата());
	
	// +пит
	Запрос.УстановитьПараметр("ХарактеристикаЦен", 
		питЦеныНоменклатуры.ПреобразоватьОтборПоХарактеристикеЦен(ТекущаяДата(), Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка()));
	// -пит
	
	ТЗПрайсЛистаСервер					= Запрос.Выполнить().Выгрузить();
	
	// Пересчет в необходимой валюте
	ВалютаБаз		= Константы.ВалютаРегламентированногоУчета.Получить();
	СтруктураКурса	= РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаБаз,ТекущаяДата()); 
	КурсБаз			= СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	СтруктураКурса	= РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта,ТекущаяДата());
	Курс = СтруктураКурса.Курс / ?(СтруктураКурса.Кратность = 0, 1, СтруктураКурса.Кратность);
	
	Массив = ТЗПрайсЛистаСервер.ВыгрузитьКолонку("Цена");
	Для Инд = 0 По Массив.ВГраница() Цикл
		Массив[Инд] = Окр(Массив[Инд]/Курс/КурсБаз,2);
	КонецЦикла; 
	ТЗПрайсЛистаСервер.ЗагрузитьКолонку(Массив,"Цена");
	ЗначениеВДанныеФормы(ТЗПрайсЛистаСервер, ТЗПрайсЛиста);
КонецФункции // ПолучитьДанныеПрайсЛиста()

&НаКлиенте
// Функция записи файла прайс-листа в excel.
//
// Параметры:
// 	ТЗПрайсЛиста - ТаблицаЗначений - Таблица значений прайс-листа.
//
// Возвращаемое значение:
//	Булево, флаг успешного выполнения записи.
Функция ЗаписатьEXCEL()
	ВсеОК			= Истина;
	
	ИмяОбрФайла		= СокрЛП(СтрокаПодключения);
	Приложение		= Новый COMОбъект("Excel.Application");
	
	Если Приложение=Неопределено Тогда
		ПоказатьПредупреждение(Неопределено, "MS Excel не загружен");
		Возврат Ложь;
	КонецЕсли;
	Приложение.Application.Workbooks.Add();
	
	// Проверим корректность колонок 
	Для каждого ПолеВыгрузки Из СтруктураФайлаПрайсЛиста Цикл
		Если Не ЗначениеЗаполнено(ПолеВыгрузки.ИмяПоляФайла) Тогда
			Продолжить;
		КонецЕсли;
		// Колонка может идентифицироваться не только номером, но и буквой.
		Колонка_Источник = ПолеВыгрузки.ИмяПоляФайла;
		// Пытаемся преобразовать в число
		Попытка
			Колонка_Источник = Число(Колонка_Источник);
		Исключение
			
			ПоказатьПредупреждение(Неопределено,"В поле таблицы для реквизита <"+ПолеВыгрузки.ИмяРеквизитаПрайсЛиста+"> заданы неверные данные.
												|" + КраткоеПредставлениеОшибки(ИнформацияОбОшибке()) + ".");
			
			Попытка 
				Для Каждого Book Из Приложение.WorkBooks Цикл
					Book.close(Ложь,"",Истина);
				КонецЦикла;
				Приложение.Quit();
				Приложение	= Неопределено;
			Исключение
			КонецПопытки;
			
			Возврат Ложь;
		КонецПопытки; 
	КонецЦикла;	
	
	КоличествоЗаписей 				= ТЗПрайсЛиста.Количество();
	ОбрабатываемаяСтрока= ?(Не ЗначениеЗаполнено(СтрокаНачало),1,СтрокаНачало);
	ТекСтрока			= 0;
	Сч					= 0;
	Пока ТекСтрока <= ТЗПрайсЛиста.Количество() - 1 И (Не ЗначениеЗаполнено(СтрокаКонец) ИЛИ ОбрабатываемаяСтрока<=СтрокаКонец) Цикл
		СтрокаПрайсЛиста		= ТЗПрайсЛиста[ТекСтрока];
		Сч						= Сч + 1;
		ОбработкаПрерыванияПользователя();
		Состояние("Выгрузка прайс-листа",Цел(100/КоличествоЗаписей*Сч));
		
		ТекЛист = Приложение.ActiveSheet;
		Для каждого ПолеВыгрузки Из СтруктураФайлаПрайсЛиста Цикл
			Если Не ЗначениеЗаполнено(ПолеВыгрузки.ИмяПоляФайла) Тогда
				Продолжить;
			КонецЕсли; 
			Попытка
				ИмяЯчейки=Число(ПолеВыгрузки.ИмяПоляФайла);
			Исключение
				ИмяЯчейки=СокрЛП(ПолеВыгрузки.ИмяПоляФайла);
			КонецПопытки;
			
			ИмяРеквизитаПрайсЛиста	= ПолеВыгрузки.ИмяРеквизитаПрайсЛиста;
			ЗначениеПоля			= СтрокаПрайсЛиста[ИмяРеквизитаПрайсЛиста];
			// Для строк, представленных в виде числа, добавим неразрывный пробел.
			Если ТипЗнч(ЗначениеПоля)  = Тип("Строка") Тогда
				Инд = 0; 
				Цифры = "0123456789";
				ТекСимвол = "";
				ЕстьНеЦифры = Ложь;
				Пока Инд < СтрДлина(ЗначениеПоля) Цикл
					ТекСимвол = Сред(ЗначениеПоля,Инд,1);
					Если Найти(Цифры,ТекСимвол)= 0  Тогда
						ЕстьНеЦифры = Истина;
						Прервать;
					КонецЕсли; 
					Инд = Инд + 1;
				КонецЦикла;
				Если НЕ ЕстьНеЦифры Тогда
					ЗначениеПоля = Символы.НПП + ЗначениеПоля;
				КонецЕсли; 
			КонецЕсли; 
			
			ТекЛист.Cells(ОбрабатываемаяСтрока,ИмяЯчейки).Value = ЗначениеПоля; 
			Если ИмяРеквизитаПрайсЛиста = "Наименование" Тогда
				ТекЛист.Cells(ОбрабатываемаяСтрока,ИмяЯчейки).ColumnWidth = 50;
			КонецЕсли;
		КонецЦикла; 
		
		ОбрабатываемаяСтрока=ОбрабатываемаяСтрока + 1;
		ТекСтрока = ТекСтрока + 1;
	КонецЦикла;
	//ФормаИндикатора.Закрыть();
	
	Для Каждого Book Из Приложение.WorkBooks Цикл
		Попытка
			Book.SaveAs(ИмяОбрФайла);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ошибка записи файла. Возможно файл открыт пользователем!");
		КонецПопытки;
		Book.close(Ложь,"",Истина);
	КонецЦикла;
	Приложение.Quit();
	Приложение=Неопределено;
	
	Возврат ВсеОК;
КонецФункции // ЗаписатьEXCEL()

&НаКлиенте
// Функция записи файла прайс-листа в текстовый файл.
//
// Параметры:
// ТЗПрайсЛиста - ТаблицаЗначений - Таблица значений прайс-листа.
//
// Возвращаемое значение:
//	Булево, флаг успешного выполнения записи.
Функция ЗаписатьTXT()
	ВсеОК = Истина;
	
	ИмяОбрФайла = СокрЛП(СтрокаПодключения);
	// Проверим наличие файла
	Файл=Новый Файл(ИмяОбрФайла);
	
	// Проверим корректность колонок 
	Для каждого ПолеВыгрузки Из СтруктураФайлаПрайсЛиста Цикл
		Если Не ЗначениеЗаполнено(ПолеВыгрузки.ИмяПоляФайла) Тогда
			Продолжить;
		КонецЕсли;
		// Колонка может идентифицироваться только номером.
		Колонка_Источник = ПолеВыгрузки.ИмяПоляФайла;
		// Попытаемся преобразовать в число
		Попытка
			Колонка_Источник = Число(Колонка_Источник);
			
			Если ПолеВыгрузки.НомерСтроки = 1 Тогда
				Максимум = Колонка_Источник;
				Минимум = Колонка_Источник;
			Иначе
				Максимум = Макс(Максимум, Колонка_Источник);
				Минимум = Мин(Минимум, Колонка_Источник);
			КонецЕсли;
		Исключение
			ПоказатьПредупреждение(Неопределено, "В поле таблицы для реквизита <"+ПолеВыгрузки.ИмяРеквизитаПрайсЛиста+"> заданы неверные данные.
												|" + КраткоеПредставлениеОшибки(ИнформацияОбОшибке()) + ".");
			Возврат Ложь;
		КонецПопытки; 
	КонецЦикла;	
	
	КоличествоЗаписей = ТЗПрайсЛиста.Количество();
	
	ФайлОбменаОбъект=Новый ЗаписьТекста(ИмяОбрФайла,КодоваяСтраница);
	ОбрабатываемаяСтрока=?(Не ЗначениеЗаполнено(СтрокаНачало),1,СтрокаНачало);
	ТекСтрока = 0;
	Сч = 0;
	Пока ТекСтрока <= ТЗПрайсЛиста.Количество() - 1 И (Не ЗначениеЗаполнено(СтрокаКонец) ИЛИ ОбрабатываемаяСтрока<=СтрокаКонец) Цикл
		Состояние("Обрабатывается строка " + ОбрабатываемаяСтрока);
		Сч = Сч + 1;
		ОбработкаПрерыванияПользователя();
		СтрокаПрайсЛиста = ТЗПрайсЛиста[ТекСтрока];
		Состояние("Выгрузка прайс-листа", Цел(100/КоличествоЗаписей*Сч));
		//ФормаИндикатора.ЭлементыФормы.Индикатор.Значение = Сч;
		ТекСтрока = ТекСтрока + 1;
		НоваяСтрокаФайла = "";
		
		МассивЗначений = Новый Массив;
		Для каждого ПолеВыгрузки Из СтруктураФайлаПрайсЛиста Цикл
			Если Не ЗначениеЗаполнено(ПолеВыгрузки.ИмяПоляФайла) Тогда
				Продолжить;
			КонецЕсли;
			ИмяРеквизитаПрайсЛиста = ПолеВыгрузки.ИмяРеквизитаПрайсЛиста;
			Попытка
				ЗначениеПоля = СтрокаПрайсЛиста[ИмяРеквизитаПрайсЛиста];
			Исключение
				ЗначениеПоля = "";
			КонецПопытки;
			Если ТипЗнч(ЗначениеПоля)=Тип("Число") Тогда
				ЗначениеПоля = Формат(ЗначениеПоля,"ЧН=0; ЧГ=");
			КонецЕсли;
			МассивЗначений.Вставить(Число(ПолеВыгрузки.ИмяПоляФайла),ЗначениеПоля)
		КонецЦикла;
		Инд = 1;
		Для каждого ЗначениеМассив Из МассивЗначений Цикл
			Если Не ЗначениеЗаполнено(ЗначениеМассив) Тогда
				Продолжить;
			КонецЕсли;
			Если Инд <> 1 Тогда
				НоваяСтрокаФайла = НоваяСтрокаФайла + Разделитель;
			КонецЕсли;
			НоваяСтрокаФайла = НоваяСтрокаФайла + ЗначениеМассив;
			Инд = Инд + 1;
		КонецЦикла;
		
		
		ФайлОбменаОбъект.ЗаписатьСтроку(НоваяСтрокаФайла);
		ОбрабатываемаяСтрока = ОбрабатываемаяСтрока + 1;
	КонецЦикла; 
	ФайлОбменаОбъект.Закрыть();
	
	Возврат ВсеОК;
КонецФункции // ПрочитатьTXT()


&НаКлиенте
// Функция записи файла прайс-листа через ADO.
//
// Параметры:
// ТЗПрайсЛиста - ТаблицаЗначений - Таблица значений прайс-листа.
//
// Возвращаемое значение:
//	Булево, флаг успешного выполнения записи.
Функция ЗаписатьADO()
	ВсеОК = Истина;
	
	// Проверим корректность колонок 
	Для каждого ПолеВыгрузки Из СтруктураФайлаПрайсЛиста Цикл
		Если Не ЗначениеЗаполнено(ПолеВыгрузки.ИмяПоляФайла) Тогда
			Продолжить;
		КонецЕсли;
		// Тип поля должен быть заполнен.
		Колонка_ТипПоля = ПолеВыгрузки.ТипПоля;
		Колонка_ДлинаПоля = ПолеВыгрузки.ДлинаПоля;
		Если НЕ ЗначениеЗаполнено(Колонка_ТипПоля) Тогда
			ПоказатьПредупреждение(Неопределено, "В поле таблицы для реквизита <"+ПолеВыгрузки.ИмяРеквизитаПрайсЛиста+"> заданы неверные данные. Не указан тип поля.");
			Возврат Ложь;
		Иначе
			// Если Строка или Число - указана длина поля.
			Если Колонка_ТипПоля = "N" ИЛИ Колонка_ТипПоля = "C" Тогда
				Если  НЕ ЗначениеЗаполнено(Колонка_ДлинаПоля) Тогда
					ПоказатьПредупреждение(Неопределено, "В поле таблицы для реквизита <"+ПолеВыгрузки.ИмяРеквизитаПрайсЛиста+"> заданы неверные данные. Не указана длина поля.");
					Возврат Ложь;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;	
	
	// Создание таблицы
	// Начальная инициализация
	Состояние("Инициализация ADOX.Catalog ...");
	Catalog = Новый COMОбъект("ADOX.Catalog");
	Попытка
		Catalog.ActiveConnection = СтрокаПодключения;
	Исключение
		ИмяБД = "";
		ЕстьИмяБД = Ложь;
		МассивСтрок = РазобратьСтрокуПодключения(СтрокаПодключения);
		Для Инд = 0 По МассивСтрок.ВГраница() Цикл
			ЗначениеСтроки = МассивСтрок[Инд];
			Если Найти(ЗначениеСтроки,"Data Source=") > 0 Тогда
				ЗначениеСтроки = СтрЗаменить(ЗначениеСтроки,"Data Source=","");
				ИмяБД = ЗначениеСтроки;
			КонецЕсли; 
		КонецЦикла;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Ошибка подключение через ADO к базе данных <"+СокрЛП(ИмяБД)+">.
		|Возможно файл БД не существует или поврежден.");
		Возврат Ложь;
	КонецПопытки;
	// Проверка на существование таблицы
	ЕстьТаблица = Ложь;
	Для НомерТаблицы = 0 По Catalog.Tables.Count-1 Цикл
		Если Catalog.Tables.Item(НомерТаблицы).Type <> "VIEW" Тогда
			ИмяТаблицыАДО = Catalog.Tables.Item(НомерТаблицы).Name;
			Если ИмяТаблицыАДО = СокрЛП(ИмяТаблицы) Тогда
				ЕстьТаблица = Истина;
				Прервать;
			КонецЕсли; 
		КонецЕсли;
	КонецЦикла;
	
	Попытка
	Если НЕ ЕстьТаблица Тогда
		// Создание новой таблицы
		Table = Новый COMОбъект("ADOX.Table");
		// Имя таблицы
		Table.Name = СокрЛП(ИмяТаблицы);
		Для каждого ПолеВыгрузки из СтруктураФайлаПрайсЛиста Цикл
			Если Не ЗначениеЗаполнено(ПолеВыгрузки.ИмяПоляФайла) Тогда
				Продолжить;
			КонецЕсли;
			// Создание новой колонки
			Column = Новый COMОбъект("ADOX.Column");
			// Имя колонки
			Column.Name = ПолеВыгрузки.ИмяПоляФайла;
			// Тип данных колонки
			Если ПолеВыгрузки.ТипПоля = "C" Тогда
				Column.Type = 202; // adVarWChar
				Column.DefinedSize = ПолеВыгрузки.ДлинаПоля; 
				Column.NumericScale = ПолеВыгрузки.ДлинаПоля;
			ИначеЕсли ПолеВыгрузки.ТипПоля = "N" Тогда
				Column.Type = 5; // adDouble
				Column.DefinedSize = ПолеВыгрузки.ДлинаПоля;
				Column.Precision = ПолеВыгрузки.ТочностьПоля;
			ИначеЕсли ПолеВыгрузки.ТипПоля = "D" Тогда
				Column.Type = 7; // adDate
			ИначеЕсли ПолеВыгрузки.ТипПоля = "B" Тогда
				Column.Type = 11; // adBoolean
			КонецЕсли; 
			// Присоединение колонки к таблице
			Table.Columns.Append(Column);
			Column = Неопределено;
		КонецЦикла; 
		// Присоединение созданной таблицы
		Catalog.Tables.Append(Table);
	КонецЕсли; 
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Создание таблицы в источнике данных: "+ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;
	
	// Добавление данных запросом
	// Создание объекта установки связи с источником данных
	Состояние("Инициализация ADODB.Connection ...");
	Connection = Новый COMОбъект("ADODB.Connection");
	
	// Подключение к источнику данных
	Попытка
		Connection.Open(СтрокаПодключения);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Подключение к источнику данных: "+ОписаниеОшибки());
		Возврат Ложь;
	КонецПопытки;
	
	// Создание объекта выполнения команды
	Command = Новый COMОбъект("ADODB.Command");
	// Указание активного соединения
	Command.ActiveConnection = Connection;
	//
	КоличествоЗаписей = ТЗПрайсЛиста.Количество();
	// Создание объекта набора записей
	Recordset = Новый COMОбъект("ADODB.Recordset");
	Recordset.Open("Select * from " + СокрЛП(ИмяТаблицы), Connection, 1, 3);
	
	Состояние("Выгрузка прайс-листа ...");
	Сч = 0;
	Для каждого СтрокаПрайсЛиста Из ТЗПрайсЛиста Цикл
		Сч = Сч + 1;
		ОбработкаПрерыванияПользователя();
		Состояние("Выгрузка прайс-листа", Цел(100/КоличествоЗаписей*Сч));
		// Добавление новой записи
		Recordset.AddNew();
		Для каждого ПолеВыгрузки Из СтруктураФайлаПрайсЛиста Цикл
			ИмяПоля = ПолеВыгрузки.ИмяПоляФайла;
			Если ЗначениеЗаполнено(ИмяПоля) Тогда
				ЗначениеПоля = СтрокаПрайсЛиста[ПолеВыгрузки.ИмяРеквизитаПрайсЛиста];
			Иначе
				Продолжить;
			КонецЕсли;
			Попытка
				Recordset.Fields(ИмяПоля).Value = ЗначениеПоля;
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В поле таблицы для реквизита <"+ПолеВыгрузки.ИмяРеквизитаПрайсЛиста+"> заданы неверные данные
																|	" + КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				Возврат Ложь;
			КонецПопытки;
		КонецЦикла; 
		// Обновление изменений
		Recordset.UpDate();
	КонецЦикла;
	
	// Закрываем соединение
	Connection.Close();
	Command = Неопределено;
	
	Возврат ВсеОК;
КонецФункции // ПрочитатьADO()

&НаКлиенте
// Функция записи файла прайс-листа в формат dBase.
//
// Параметры:
// ТЗПрайсЛиста - ТаблицаЗначений - Таблица значений прайс-листа.
// Возвращаемое значение:
//	Булево, флаг успешного выполнения записи.
//
Функция ЗаписатьDBF()
	ВсеОК = Истина;
	
	// Проверим корректность колонок 
	Для каждого ПолеВыгрузки Из СтруктураФайлаПрайсЛиста Цикл
		Если Не ЗначениеЗаполнено(ПолеВыгрузки.ИмяПоляФайла) Тогда
			Продолжить;
		КонецЕсли;
		// Тип поля должен быть заполнен
		Колонка_ТипПоля = ПолеВыгрузки.ТипПоля;
		Колонка_ДлинаПоля = ПолеВыгрузки.ДлинаПоля;
		Если НЕ ЗначениеЗаполнено(Колонка_ТипПоля) Тогда
			ПоказатьПредупреждение(Неопределено, "В поле таблицы для реквизита <"+ПолеВыгрузки.ИмяРеквизитаПрайсЛиста+"> заданы неверные данные. Не указан тип поля.");
			Возврат Ложь;
		Иначе
			// Если Строка или Число - указана длина поля
			Если Колонка_ТипПоля = "N" ИЛИ Колонка_ТипПоля = "C" Тогда
				Если  НЕ ЗначениеЗаполнено(Колонка_ДлинаПоля) Тогда
					ПоказатьПредупреждение(Неопределено, "В поле таблицы для реквизита <"+ПолеВыгрузки.ИмяРеквизитаПрайсЛиста+"> заданы неверные данные. Не указана длина поля.");
					Возврат Ложь;
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	
	КоличествоЗаписей = ТЗПрайсЛиста.Количество();
	ДБФ = Новый XBase;
	
	// Создаем новый dbf-файл
	Для Каждого ПолеВыгрузки ИЗ СтруктураФайлаПрайсЛиста Цикл
		Если Не ЗначениеЗаполнено(ПолеВыгрузки.ИмяПоляФайла) Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяПоля = ПолеВыгрузки.ИмяПоляФайла;
		Тип = ПолеВыгрузки.ТипПоля;
		Длина = Строка(?(ПолеВыгрузки.ДлинаПоля = 0,"",ПолеВыгрузки.ДлинаПоля));
		Точность = Строка(?(ПолеВыгрузки.ТочностьПоля = 0,"",ПолеВыгрузки.ТочностьПоля));
		Попытка
			ДБФ.Поля.Добавить(ИмяПоля,Тип,Длина,Точность);
		Исключение
			ПоказатьПредупреждение(Неопределено,"В поле таблицы для реквизита <"+ПолеВыгрузки.ИмяРеквизитаПрайсЛиста+"> заданы неверные данные. 
												|" + КраткоеПредставлениеОшибки(ИнформацияОбОшибке()) + ".");
			Возврат Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Попытка
		ДБФ.СоздатьФайл(СтрокаПодключения);
	Исключение
		ПоказатьПредупреждение(Неопределено,"Ошибка создания файла <"+СтрокаПодключения+">. 
											|" + КраткоеПредставлениеОшибки(ИнформацияОбОшибке()) + ".");
		Возврат Ложь;
	КонецПопытки;
	
	Состояние("Выгрузка прайс-листа ...");
	Сч = 0;
	
	Попытка
		Для каждого СтрокаПрайсЛиста Из ТЗПрайсЛиста Цикл
			Сч = Сч + 1;
			ОбработкаПрерыванияПользователя();
			//ФормаИндикатора.ЭлементыФормы.Индикатор.Значение = Сч;
			Состояние("Выгрузка прайс-листа", Цел(100/КоличествоЗаписей*Сч));
			// Добавление новой записи
			ДБФ.Добавить();
			Стр = 0;
			Для Каждого ПолеВыгрузки ИЗ СтруктураФайлаПрайсЛиста Цикл
				Стр = Стр + 1;
				ИмяПоля = ПолеВыгрузки.ИмяПоляФайла;
				Если Не ЗначениеЗаполнено(ИмяПоля) Тогда
					Продолжить;
				КонецЕсли;
				ЗначениеПоля = СтрокаПрайсЛиста[ПолеВыгрузки.ИмяРеквизитаПрайсЛиста];
				ДБФ[ИмяПоля] = ЗначениеПоля;
			КонецЦикла;
			ДБФ.Записать();
		КонецЦикла;
		ДБФ.ЗакрытьФайл();
	Исключение
		ПоказатьПредупреждение(Неопределено,"Ошибка при добавлении данных в файл dbf. 
											|" + КраткоеПредставлениеОшибки(ИнформацияОбОшибке()) + ".");
		Возврат Ложь;
	КонецПопытки;
	
	Возврат ВсеОК;
КонецФункции // ЗаписатьDBF()

&НаКлиенте
// Процедура заполнения полей для выгрузки прайс-листа
//
// Параметры:
// 	нет
//
Процедура ЗаполнитьПоляВыгрузки()
	СтруктураФайлаПрайсЛиста.Очистить();
	
	СтруктураПолейФайла = Новый Структура;
	СтруктураПолейФайла.Вставить("Артикул","Артикул номенклатуры");
	СтруктураПолейФайла.Вставить("Наименование","Наименование номенклатуры");
	СтруктураПолейФайла.Вставить("Цена","Цена номенклатурной позиции");
	СтруктураПолейФайла.Вставить("Количество","Количество номенклатуры");
	
	// Заполним таблицу с полями
	НомерСтроки = 1;
	Для каждого ПолеФайла Из СтруктураПолейФайла Цикл
		НовоеПоле 									   = СтруктураФайлаПрайсЛиста.Добавить();
		НовоеПоле.ИмяРеквизитаПрайсЛиста 			   = ПолеФайла.Ключ;
		НовоеПоле.ИмяРеквизитаСправочникаПредставление = ПолеФайла.Значение; 
		НовоеПоле.НомерСтроки						   = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла; 
	
	// Загрузим поля внешнего источника
	Если ФайлИсточникДанных Тогда 
		Элементы.СтруктураФайлаПрайсЛистаИмяПоляФайла.КнопкаСпискаВыбора = Ложь;
		Элементы.СтруктураФайлаПрайсЛистаИмяПоляФайла.РежимВыбораИзСписка = Ложь;
		
	Иначе // АДО
		Состояние("Инициализация ADODB.Connection ...");
		Попытка
			Приложение	= Новый COMОбъект("ADODB.Connection");
			Коннект		= Приложение.Open(СтрокаПодключения);
		Исключение
			ПоказатьПредупреждение(Неопределено, "Ошибка инициализации ADODB.Connection: " + ОписаниеОшибки());
			Возврат;
		КонецПопытки;
		Команда = Новый  COMОбъект("ADODB.Command");
		РезультатЗапроса = Новый  COMОбъект("ADODB.Recordset");
		РезультатЗапроса.ActiveConnection = Приложение;
		ТекстЗап="
		|SELECT TOP 1 "+СокрЛП(ИмяТаблицы)+".*
		|FROM "+СокрЛП(ИмяТаблицы);
		Команда.CommandText = ТекстЗап;
		Команда.ActiveConnection = Приложение;
		Попытка
			РезультатЗапроса=Команда.Execute();
		Исключение
			ПоказатьПредупреждение(Неопределено, "Ошибка инициализации ADODB.Connection: " + ОписаниеОшибки()); 
			Возврат;
		КонецПопытки; 
		// Выберем данные из списка
		КолПолей=РезультатЗапроса.Fields.Count;
		СписокВыбора = Элементы.СтруктураФайлаПрайсЛиста.ПодчиненныеЭлементы.ИмяПоляФайла.СписокВыбора;
		Элементы.СтруктураФайлаПрайсЛиста.ПодчиненныеЭлементы.ИмяПоляФайла.КнопкаСпискаВыбора = Истина;
		Элементы.СтруктураФайлаПрайсЛиста.ПодчиненныеЭлементы.ИмяПоляФайла.РежимВыбораИзСписка = Истина;
		СписокВыбора.Очистить();
		Для Кол=0 По КолПолей-1 Цикл
			ИмяКолонки = РезультатЗапроса.Fields(Кол).Name;
			СписокВыбора.Добавить(ИмяКолонки);
		КонецЦикла;
	КонецЕсли; 
КонецПроцедуры // ЗаполнитьПоляВыгрузки()

&НаКлиенте
// Функция разбора строки символов на массив символов
//
// Параметры:
// 	СтрокаДляРазбора - Строка - Строка для разбора
//
// Возвращаемое значение:
// 	Массив - Массив символов строки
//
Функция РазобратьСтрокуПодключения(Знач СтрокаДляРазбора)
	МассивСтрокиПодключения=Новый Массив();
	Разделитель = ";";
	Пока СтрДлина(СтрокаДляРазбора)>0 Цикл
		ПозицияРазделителя=Найти(СтрокаДляРазбора,Разделитель);
		Если ПозицияРазделителя=0 Тогда
			ПозицияРазделителя=СтрДлина(СтрокаДляРазбора)+1;
		КонецЕсли; 	
		СтрокаПараметр=Лев(СтрокаДляРазбора,ПозицияРазделителя-1);
		СтрокаДляРазбора=Сред(СтрокаДляРазбора,ПозицияРазделителя+1);
		МассивСтрокиПодключения.Добавить(СтрокаПараметр);
	КонецЦикла; 
	
	Возврат МассивСтрокиПодключения;
КонецФункции // РазобратьСтрокуTXT()

&НаКлиенте
// Устанавливает видимость элементов формы
//
Процедура УстановитьВидимость()
	
	КолонкиТЗ = Элементы.СтруктураФайлаПрайсЛиста.ПодчиненныеЭлементы;
	Если ФайлИсточникДанных Тогда
		Если Найти(ВРег(СтрокаПодключения),".DBF") > 0 Тогда
			Элементы.СтрокаПодключения.Ширина=Элементы.ФайлИсточникДанных.Ширина;
			Элементы.Разделитель.Видимость=Ложь;
			Элементы.СтрокаНачало.Видимость=Ложь;
			Элементы.СтрокаКонец.Видимость=Ложь;
			Элементы.ИмяТаблицы.Видимость=Ложь;
			// Видимость колонок СтруктураФайлаПрайсЛиста
			КолонкиТЗ.СтруктураФайлаПрайсЛистаТипПоля.Видимость 		= Истина;
			КолонкиТЗ.СтруктураФайлаПрайсЛистаДлинаПоля.Видимость 	= Истина;
			КолонкиТЗ.СтруктураФайлаПрайсЛистаТочностьПоля.Видимость 	= Истина;
		Иначе
			Если Найти(ВРег(СтрокаПодключения),".XLS") > 0 Тогда
				Элементы.СтрокаПодключения.Ширина=Элементы.ФайлИсточникДанных.Ширина;
				Элементы.Разделитель.Видимость=Ложь;
			Иначе
				Элементы.СтрокаПодключения.Ширина=Элементы.Валюта.Ширина;
				Элементы.Разделитель.Видимость=Истина;
			КонецЕсли;
			Элементы.СтрокаНачало.Видимость=Истина;
			Элементы.СтрокаКонец.Видимость=Истина;
			Элементы.ИмяТаблицы.Видимость=Ложь;
			// Видимость колонок СтруктураФайлаПрайсЛиста
			КолонкиТЗ.СтруктураФайлаПрайсЛистаТипПоля.Видимость 		= Ложь;
			КолонкиТЗ.СтруктураФайлаПрайсЛистаДлинаПоля.Видимость 	= Ложь;
			КолонкиТЗ.СтруктураФайлаПрайсЛистаТочностьПоля.Видимость 	= Ложь;
		КонецЕсли; 
	Иначе
		Элементы.СтрокаПодключения.Ширина=Элементы.ФайлИсточникДанных.Ширина;
		Элементы.Разделитель.Видимость=Ложь;
		Элементы.СтрокаНачало.Видимость=Ложь;
		Элементы.СтрокаКонец.Видимость=Ложь;
		Элементы.ИмяТаблицы.Видимость=Истина;
		// Видимость колонок СтруктураФайлаПрайсЛиста
		КолонкиТЗ.СтруктураФайлаПрайсЛистаТипПоля.Видимость 		= Истина;
		КолонкиТЗ.СтруктураФайлаПрайсЛистаДлинаПоля.Видимость 	= Истина;
		КолонкиТЗ.СтруктураФайлаПрайсЛистаТочностьПоля.Видимость 	= Истина;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Загружает настройки из внешнего файла
//
// Параметры:
//	ЭтаФорма -  Форма для загрузки настроек
//
Процедура ЗагрузитьНастройкиИзФайла(ЭтаФорма)
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Фильтр = "Файлы XML (*.xml)|*.xml";
	
	Диалог.Показать(Новый ОписаниеОповещения("ЗагрузитьНастройкиИзФайлаЗавершение", ЭтотОбъект, Новый Структура("Диалог", Диалог)));
	
КонецПроцедуры

// Действия после выбора файла
&НаКлиенте
Процедура ЗагрузитьНастройкиИзФайлаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Диалог = ДополнительныеПараметры.Диалог;
	
	Если ВыбранныеФайлы = Неопределено Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указано имя файла выгрузки!");
	КонецЕсли;
	
	Если ПустаяСтрока(Диалог.ПолноеИмяФайла) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указано имя файла выгрузки!");
		Возврат;
	КонецЕсли; 
	
	// Чтение XML
	Состояние("Чтение XML файла...");
	 ТекстОшибки = ОткрытьИПрочитатьXML(Диалог.ПолноеИмяФайла);
	Если ТекстОшибки <> "" Тогда
		ПоказатьПредупреждение(Неопределено, ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Загрузка настроек успешно завершена!");
	
КонецПроцедуры // ЗагрузитьНастройкиИзФайла()

&НаСервере
// Чтение XML
Функция ОткрытьИПрочитатьXML(ПолноеИмяФайла)
	ЧтениеXML = Новый ЧтениеXML();
	Попытка
		ЧтениеXML.ОткрытьФайл(ПолноеИмяФайла);
	Исключение
		Возврат "Ошибка чтения файла!";
	КонецПопытки;
	
	Попытка
		Пока ЧтениеXML.Прочитать() Цикл
			Если ЧтениеXML.ТипУзла<>ТипУзлаXML.НачалоЭлемента Тогда
				Продолжить;
			КонецЕсли;
			
			ТЗ = Новый ТаблицаЗначений;
			
			НомерКолонки = 0;
			
			Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
				Попытка // Файл могли изменить или изменили форму выгрузки.
					Если лев(ЧтениеXML.Имя , 4) = "ТАБ_" Тогда
						ЭтотЭлемент = ЭтаФорма[Прав(ЧтениеXML.Имя , СтрДлина(ЧтениеXML.Имя) - 4)];
						ТЗ =  ЭтотЭлемент.Выгрузить();
						Массив = ЗначениеИзСтрокиВнутр(ЧтениеXML.Значение);
						Для Каждого Колонка из Массив Цикл
							ТЗ.ЗагрузитьКолонку(Колонка, НомерКолонки);
							НомерКолонки = НомерКолонки + 1;
						КонецЦикла;
						ЭтотЭлемент.Загрузить(ТЗ); 
					Иначе
						ЭтаФорма[ЧтениеXML.Имя] = ЗначениеИзСтрокиВнутр(ЧтениеXML.Значение); 
					КонецЕсли;
				Исключение
				КонецПопытки;
			КонецЦикла; 
		КонецЦикла; 
		ЧтениеXML.Закрыть();
	Исключение
		Возврат "Не правильный формат файла!" ;
	КонецПопытки;
	
	Возврат "";
КонецФункции

&НаКлиенте
// Выгружает текущие настройки во внешний файл
//
Процедура СохранитьНастройкиВФайл()
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.МножественныйВыбор = Ложь;
	Преффикс = "";
	Если ФайлИсточникДанных Тогда
		Если Найти(ВРег(СтрокаПодключения),".TXT") > 0 Тогда
			Преффикс = "TXT";
		ИначеЕсли Найти(ВРег(СтрокаПодключения),".XLS") > 0 ИЛИ Найти(ВРег(СтрокаПодключения),".XLSX") Тогда	
			Преффикс = "XLS";
		ИначеЕсли Найти(ВРег(СтрокаПодключения),".DBF") > 0 Тогда	
			Преффикс = "DBF";	
		КонецЕсли; 
	Иначе	
		Преффикс = "ADO";
	КонецЕсли; 
	Диалог.ПолноеИмяФайла = "Прайс-лист_" + Преффикс + ".xml";
	Диалог.Расширение = "xml";
	Диалог.Фильтр = "Файлы XML (*.xml)|*.xml";
	Диалог.Показать(Новый ОписаниеОповещения("СохранитьНастройкиВФайлВыборФайла_Завершение", ЭтотОбъект, Новый Структура("Диалог", Диалог)));
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиВФайлВыборФайла_Завершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Диалог = ДополнительныеПараметры.Диалог;
	
	Если Не (ВыбранныеФайлы <> Неопределено) Тогда		
		Возврат; 					
	КонецЕсли;
	ОткрытьИЗаписатьXML(Диалог.ПолноеИмяФайла);
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Сохрание настроек успешно завершено!");
	
КонецПроцедуры // СохранитьНастройкиВФайл()

&НаСервере
// Запись XML
Процедура ОткрытьИЗаписатьXML(ПолноеИмяФайла)
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.ОткрытьФайл(ПолноеИмяФайла);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписьXML.ЗаписатьНачалоЭлемента("ЭлементыФормы");
	Для каждого ЭлементФормы Из Элементы Цикл
		Если (СокрЛП(ЭлементФормы) = "ПолеФормы" И СокрЛП(ЭлементФормы.Родитель) <> "ТаблицаФормы")
			И СокрЛП(ЭлементФормы) <> "ТаблицаФормы"  Тогда
			ЗаписьXML.ЗаписатьАтрибут(ЭлементФормы.Имя,  ЗначениеВСтрокуВнутр(ЭтаФорма[ЭлементФормы.Имя]));
		ИначеЕсли СокрЛП(ЭлементФормы) = "ТаблицаФормы" Тогда 
			
		//ИначеЕсли СокрЛП(ЭлементФормы) = "ПолеФормы" И СокрЛП(ЭлементФормы.Родитель) = "ТаблицаФормы" Тогда
		КонецЕсли; 
	КонецЦикла;
	
	//	Для каждого ЭлементФормы Из Элементы Цикл
	//	Если СокрЛП(ЭлементФормы) = "ТаблицаФормы" Тогда 
	//		ЗначТабл = РеквизитФормыВЗначение(СтруктураФайлаПрайсЛиста, "ТаблицаЗначений");
	//		ЗаписатьXML(ЗаписьXML, ЗначТабл);
	
	//	
	//	//ИначеЕсли СокрЛП(ЭлементФормы) = "ПолеФормы" И СокрЛП(ЭлементФормы.Родитель) = "ТаблицаФормы" Тогда
	//	ввв = 1;
	//	КонецЕсли; 
	//КонецЦикла;
	
	//
	ТабЗнач = СтруктураФайлаПрайсЛиста.Выгрузить();
	Массив=Новый Массив;
	Для каждого Колонка из ТабЗнач.Колонки цикл
		Массив.Добавить(ТабЗнач.ВыгрузитьКолонку(Колонка));
	КонецЦикла;   
	ЗаписьXML.ЗаписатьАтрибут("ТАБ_СтруктураФайлаПрайсЛиста",  ЗначениеВСтрокуВнутр(Массив));
	
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	ЗаписьXML.Закрыть();
КонецПроцедуры

&НаКлиенте
// Выбор файла для выгрузки прайс-листа
Процедура ВыборФайла(ПроверятьСуществование=Ложь)
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.Заголовок = "Выберите файл";
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.ИндексФильтра = 0;
	ДиалогВыбораФайла.Фильтр = "MS Excel (*.xls)|*.xls|Текстовый файл (*.txt)|*.txt|DBF 4 (DBASE IV) (*.dbf)|*.dbf";
	Файл = Новый Файл(СтрокаПодключения);
	Файл.НачатьПроверкуСуществования(Новый ОписаниеОповещения("ВыборФайлаЗавершение", ЭтотОбъект, Новый Структура("ДиалогВыбораФайла", ДиалогВыбораФайла)));
	
КонецПроцедуры // ВыборФайла

&НаКлиенте
Процедура ВыборФайлаЗавершение(Существует, ДополнительныеПараметры) Экспорт
	
	ДиалогВыбораФайла = ДополнительныеПараметры.ДиалогВыбораФайла;
	
	Если Существует Тогда
		ДиалогВыбораФайла.ПолноеИмяФайла = СтрокаПодключения;
	КонецЕсли;
	
	ДиалогВыбораФайла.Показать(Новый ОписаниеОповещения("ВыборФайлаЗавершениеЗавершение", ЭтотОбъект, Новый Структура("ДиалогВыбораФайла", ДиалогВыбораФайла)));
	
КонецПроцедуры // ВыборФайлаЗавершение

&НаКлиенте
Процедура ВыборФайлаЗавершениеЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	ДиалогВыбораФайла = ДополнительныеПараметры.ДиалогВыбораФайла;
	
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		СтрокаПодключения = ДиалогВыбораФайла.ПолноеИмяФайла;
		УстановитьВидимость();
	КонецЕсли;
	
КонецПроцедуры // ВыборФайлаЗавершениеЗавершение()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

&НаКлиенте
Процедура Выгрузить(Команда)
	СтрокаОшибки = "";
	Результат = ВыгрузитьПрайсЛист();
КонецПроцедуры



&НаКлиенте
Процедура СохранитьВФайл(Команда)
	СохранитьНастройкиВФайл();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	ЗагрузитьНастройкиИзФайла(ЭтаФорма);
	ФайлИсточникДанныхПриИзменении(Элементы.ФайлИсточникДанных);
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ ЭЛЕМЕНТОВ УПРАВЛЕНИЯ ФОРМЫ


&НаКлиенте
Процедура СтрокаПодключенияПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура СтруктураФайлаПрайсЛистаТипПоляПриИзменении(Элемент)
	Если Элемент.ТекстРедактирования = "C" Тогда
		Элементы.СтруктураФайлаПрайсЛиста.ТекущиеДанные.ТочностьПоля = 0;
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ФайлИсточникДанныхПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПодключенияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если ФайлИсточникДанных Тогда
		ВыборФайла(Истина);
	Иначе
		// Получим строку ADO подключения
		Connection=Новый COMОбъект("ADODB.Connection");
		DataLinks=Новый COMОбъект("DataLinks"); // Утилита для формирования и редактирования строки ConnectionString
		Попытка
			Connection.ConnectionString = СтрокаПодключения;
			DataLinks.PromptEdit(Connection);// Создадим новый ConnectionString
		Исключение
			Connection.ConnectionString="";
			DataLinks.PromptEdit(Connection);// Создадим новый ConnectionString
		КонецПопытки;
		СтрокаПодключения = Connection.ConnectionString;
	КонецЕсли;
	УстановитьВидимость();
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	питИзменениеОтраслевыхФормСервер.ПриСозданииНаСервереВНачале(ЭтаФорма, Отказ, СтандартнаяОбработка);
	Если Параметры.Свойство("ВидПрайсЛиста") Тогда
	
		ВидПрайсЛиста = Параметры.ВидПрайсЛиста;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидПрайсЛиста) Тогда
		ВидПрайсЛиста=Справочники.питВидыМеню.ОсновнойВидМеню;
	КонецЕсли;
	Валюта	= Константы.ВалютаРегламентированногоУчета.Получить();
	
	Элементы.КодоваяСтраница.СписокВыбора.Добавить("windows-1251","CP 1251");
	Элементы.КодоваяСтраница.СписокВыбора.Добавить("KOI8-R","KOI 8");
	Элементы.КодоваяСтраница.СписокВыбора.Добавить("cp866","CP 866");
	Элементы.КодоваяСтраница.СписокВыбора.Добавить("ISO-8859-1","ISO 8859");
	КодоваяСтраница = "windows-1251";
	
	ФайлИсточникДанных=Истина;
	СтрокаНачало=1;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗаполнитьПоляВыгрузки();
	
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПодключенияОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если ФайлИсточникДанных Тогда
		СтрокаЗапуска = СтрокаПодключения;
	Иначе	
		СтрокаЗапуска = "";
		ЕстьИмяБД = Ложь;
		МассивСтрок = РазобратьСтрокуПодключения(СтрокаПодключения);
		Для Инд = 0 По МассивСтрок.ВГраница() Цикл
			ЗначениеСтроки = МассивСтрок[Инд];
			Если Найти(ЗначениеСтроки,"Data Source=") > 0 Тогда
				ЗначениеСтроки = СтрЗаменить(ЗначениеСтроки,"Data Source=","");
				СтрокаЗапуска = ЗначениеСтроки;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли;
	
	Попытка
		НачатьЗапускПриложения(Новый ОписаниеОповещения("ЗапускПриложенияДляФайла_Завершение", ЭтотОбъект), СтрокаЗапуска);
	Исключение
		ПоказатьПредупреждение(Неопределено, "Ошибка открытия файла <" + СтрокаЗапуска + ">");
	КонецПопытки;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапускПриложенияДляФайла_Завершение(КодВозврата, ДополнительныеПараметры) Экспорт
	//Пустая процедура т.к. НачатьЗапускПриложения() без описания оповещения не работает.
	Результат = Неопределено;
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ИСПОЛНЯЕМАЯ ЧАСТЬ МОДУЛЯ

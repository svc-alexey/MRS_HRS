#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ТоварУникаленВПрайсе(ТекущийПрайсЛист) Экспорт
	СтрокаУникальность = ?(ТекущийПрайсЛист.ЭтоГруппа,"Наименование","Номенклатура");
	Запрос=Новый Запрос("
	|ВЫБРАТЬ
	|	ПрайсЛист." +СтрокаУникальность+"
	|ИЗ
	|	Справочник.питМеню КАК ПрайсЛист
	|ГДЕ
	|ПрайсЛист."+СтрокаУникальность+" = &ИсходнаяНоменклатура И
	|ПрайсЛист.Ссылка <> &ИсходныйПрайсЛист И
	|ПрайсЛист.Владелец = &ИсходныйВладелец
	|");
	Запрос.УстановитьПараметр("ИсходнаяНоменклатура",?(ТекущийПрайсЛист.ЭтоГруппа,ТекущийПрайсЛист.Наименование,ТекущийПрайсЛист.Номенклатура));
	Запрос.УстановитьПараметр("ИсходныйПрайсЛист",ТекущийПрайсЛист.Ссылка);
	Запрос.УстановитьПараметр("ИсходныйВладелец",ТекущийПрайсЛист.Владелец);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество()<>0 Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
КонецФункции // ТоварУникаленВПрайсе()

// Проверяет заполнен ли прайс-лист.
//
// Параметры:
//  ВладелецПрайсЛиста 		- Строка - Вид прайс-листа.
//
Функция ПрайсЛистЗаполнен(ВладелецПрайсЛиста) Экспорт
	Перем Запрос;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПрайсЛист.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.питМеню КАК ПрайсЛист
	|ГДЕ
	|	ПрайсЛист.Владелец = &ИсходныйВладелец";
	
	Запрос.УстановитьПараметр("ИсходныйВладелец",ВладелецПрайсЛиста);
	Результат = Запрос.Выполнить();
	
	Возврат НЕ Результат.Пустой();
	
КонецФункции



// Очищает прайс-лист для указанного вида прайс-листа.
//
// Параметры:
//  ВладелецПрайсЛиста 		- Строка - Вид прайс-листа.
//
Функция ОчиститьПрайсЛист(ВладелецПрайсЛиста) Экспорт
	Если Не ЗначениеЗаполнено(ВладелецПрайсЛиста) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не задан владелец меню!";
		Сообщение.Сообщить();
		Возврат Ложь;
	КонецЕсли;
	
	// Очистим имеющийся состав меню.
	Запрос				= Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПрайсЛист.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.питМеню КАК ПрайсЛист
	|ГДЕ
	|	ПрайсЛист.Владелец = &ИсходныйВладелец";
	Запрос.УстановитьПараметр("ИсходныйВладелец",ВладелецПрайсЛиста);
	Выборка = Запрос.Выполнить().Выбрать();
	
	УдаляемыеЭлементы	= Новый СписокЗначений();
	Пока Выборка.Следующий() Цикл
		УдаляемыеЭлементы.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	ЗаголовокОшибки = "Очистка меню """+ВладелецПрайсЛиста+""":";
	Для Каждого УдаляемЭлемент Из УдаляемыеЭлементы Цикл
		УдаляемЭлементОбъект = УдаляемЭлемент.Значение.ПолучитьОбъект();
		УдаляемЭлементОбъект.ОбменДанными.Загрузка = Истина;
		Попытка
			УдаляемЭлементОбъект.Удалить();
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Удаление элемента меню """+УдаляемЭлементОбъект+""". "+ОписаниеОшибки(), УдаляемЭлементОбъект);
			Возврат Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции


// Заполняет прайс-лист в соответствии с переданными параметрами заполнения.
// ВладелецПрайсЛиста - вид прайс-листа, который заполняем.
// СпособЗаполнения:  0 - по складским остаткам, 1 - по товарам поставщика, 2 - по справочнику "Номенклатура",
// 3 - копирование другого прайс-листа.  
// Параметры заполнения - структура: зависит от способа заполнения, ключи - Склад, Дата, Поставщик,
// ГруппаНоменклатуры, ВидПрайсЛиста.  
// Способ создания групп: 0 - не создавать, 1 - создавать по видам товаров, 2 - создавать по группам товаров.
// Текущий родитель - текущий родитель прайс-листа.
// ФлОчищать = Истина,если текущий прайс-лист будет очищен перед заполнением.
//
// Параметры:
//  ВладелецПрайсЛиста 		- Строка 			- Вид прайс-листа.
//  СпособЗаполнения 		- Число 			- Способ заполнения.
//  ПараметрыЗаполнения 	- Структура 		- Параметры заполнения.  
//  СпособСозданияГрупп 	- Число 			- Способ создания групп.
//  Текущий родитель 		- СправочникСсылка	- Текущий родитель прайс-листа.
//  ФлажокОчищать 			- Булево			- Флаг очистки прайс-листа перед заполнением.
//
Процедура ЗаполнитьПрайсЛист(ВладелецПрайсЛиста,СпособЗаполнения=2,ПараметрыЗаполнения,СпособСозданияГрупп=2,ФлажокОчищать = Истина,ТекущийРодитель = Неопределено) Экспорт
	Если Не ЗначениеЗаполнено(ВладелецПрайсЛиста) Тогда
		Ошибки = "Не задан владелец меню!";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Ошибки);
		Возврат;
	КонецЕсли;
	// Очистим, если надо
	Если ФлажокОчищать Тогда
		Если НЕ Справочники.питМеню.ОчиститьПрайсЛист(ВладелецПрайсЛиста) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыЗаполнения) Тогда 
		ПараметрыЗаполнения = Новый Структура(); 
	КонецЕсли;
	Если Не ПараметрыЗаполнения.Свойство("Склад") Тогда 
		ПараметрыЗаполнения.Вставить("Склад"); 
	КонецЕсли;
	Если Не ПараметрыЗаполнения.Свойство("Организация") Тогда 
		ПараметрыЗаполнения.Вставить("Организация"); 
	КонецЕсли;
	Если Не ПараметрыЗаполнения.Свойство("Дата") Тогда 
		ПараметрыЗаполнения.Вставить("Дата");
	КонецЕсли;
	Если Не ПараметрыЗаполнения.Свойство("Поставщик") Тогда 
		ПараметрыЗаполнения.Вставить("Поставщик"); 
	КонецЕсли;
	Если Не ПараметрыЗаполнения.Свойство("ГруппаНоменклатуры") Тогда 
		ПараметрыЗаполнения.Вставить("ГруппаНоменклатуры");
	КонецЕсли;
	Если Не ПараметрыЗаполнения.Свойство("ВидПрайсЛиста") Тогда 
		ПараметрыЗаполнения.Вставить("ВидПрайсЛиста"); 
	КонецЕсли;
	
	// Получим значения структуры
	Склад				= ПараметрыЗаполнения.Склад;
	Организация			= ?(ЗначениеЗаполнено(ПараметрыЗаполнения.Организация), ПараметрыЗаполнения.Организация, питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация"));
	Дата 				= ПараметрыЗаполнения.Дата;
	Поставщик			= ПараметрыЗаполнения.Поставщик;
	ГруппаНоменклатуры	= ПараметрыЗаполнения.ГруппаНоменклатуры;
	ВидПрайсЛиста		= ПараметрыЗаполнения.ВидПрайсЛиста;
	
	// Непосредственно заполнение
	Запрос			= Новый Запрос();
	СкладПустой		= Не ЗначениеЗаполнено(Склад); 
	ГруппаПустая	= Не ЗначениеЗаполнено(ГруппаНоменклатуры);
	Если СпособЗаполнения = 0 Тогда // По остаткам
		
		Если питПроизводствоПовтИсп.ПолучитьИмяОсновнойКонфигурации() = "ERPWE" Тогда
			Возврат;
		КонецЕсли;
		
		мМассивСчетов	= Новый Массив();
		мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["СырьеИМатериалы"]);
		мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["ТоварыНаСкладах"]);
		мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["ТоварыВРозничнойТорговле"]);
		мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["ГотоваяПродукция"]);
		мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["Полуфабрикаты"]);
		мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ"]);
		
		мМассивСубконто	= Новый Массив();
		мМассивСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
		ЕстьСкладскойУчет = НЕ (ПланыСчетов.Хозрасчетный.Товары.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады, "ВидСубконто") = Неопределено);
		Если ЕстьСкладскойУчет И ЗначениеЗаполнено(Склад) Тогда
			мМассивСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
		КонецЕсли;
		
		Запрос.УстановитьПараметр("СписокСубконто", мМассивСубконто);
		Запрос.УстановитьПараметр("СписокСчетов", мМассивСчетов);
		Запрос.УстановитьПараметр("Организация", Организация);
		
		ТекстЗапроса= "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ХозрасчетныйОстатки.Субконто1 КАК Ссылка,
		|	ХозрасчетныйОстатки.Субконто1.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ХозрасчетныйОстатки.Субконто1.Код КАК КодНоменклатуры,
		|	ХозрасчетныйОстатки.Субконто1.питВидНоменклатуры КАК ВидНоменклатуры,
		|	ХозрасчетныйОстатки.Субконто1.ЭтоГруппа КАК ЭтоГруппа,
		|	ХозрасчетныйОстатки.Субконто1.Родитель КАК Родитель,
		|	ХозрасчетныйОстатки.Субконто1.Наименование КАК Наименование
		|
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки("+?(ЗначениеЗаполнено(Дата),"&Дата","")+", Счет В (&СписокСчетов), &СписокСубконто,
		|Организация = &Организация"+?(ЕстьСкладскойУчет И ЗначениеЗаполнено(Склад),"
		|И Субконто2 = &Склад","")+?(ГруппаПустая, "", " И Субконто1 В ИЕРАРХИИ(&ГруппаНоменклатуры)")+") КАК ХозрасчетныйОстатки
		|
		|ИТОГИ ПО
		|		Субконто1 ИЕРАРХИЯ";
		
	ИначеЕсли СпособЗаполнения = 1 Тогда // По поставщику
		
		Если питПроизводствоПовтИсп.ПолучитьИмяОсновнойКонфигурации() = "ERPWE" Тогда
			Возврат;
		КонецЕсли;
		
		мМассивСчетов	= Новый Массив();
		мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["СырьеИМатериалы"]);
		мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["ТоварыНаСкладах"]);
		мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["ТоварыВРозничнойТорговле"]);
		мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["ГотоваяПродукция"]);
		мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["Полуфабрикаты"]);
		мМассивСчетов.Добавить(ПланыСчетов["Хозрасчетный"]["ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ"]);
		
		мМассивСубконто	= Новый Массив();
		мМассивСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
		
		Запрос.УстановитьПараметр("СписокСубконто", мМассивСубконто);
		Запрос.УстановитьПараметр("СписокСчетов", мМассивСчетов);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ДатаКонца", КонецДня(ТекущаяДата()));
		
		ТекстЗапроса= "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ХозрасчетныйОбороты.Субконто1 КАК Ссылка,
		|	ХозрасчетныйОбороты.Субконто1.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ХозрасчетныйОбороты.Субконто1.Код КАК КодНоменклатуры,
		|	ХозрасчетныйОбороты.Субконто1.питВидНоменклатуры КАК ВидНоменклатуры,
		|	ХозрасчетныйОбороты.Субконто1.ЭтоГруппа КАК ЭтоГруппа,
		|	ХозрасчетныйОбороты.Субконто1.Родитель КАК Родитель,
		|	ХозрасчетныйОбороты.Субконто1.Наименование КАК Наименование
		|
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Обороты("+?(ЗначениеЗаполнено(Дата),"&Дата","")+",&ДатаКонца, Регистратор, Счет В (&СписокСчетов), &СписокСубконто,
		|Организация = &Организация"+?(ГруппаПустая, "", " И Субконто1 В ИЕРАРХИИ(&ГруппаНоменклатуры)")+") КАК ХозрасчетныйОбороты
		|
		|"+?(ЗначениеЗаполнено(Поставщик),"ГДЕ
		|		ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.ПоступлениеТоваровУслуг).Контрагент = &Контрагент","")+"
		|
		|ИТОГИ ПО
		|		Субконто1 ИЕРАРХИЯ";
		
	ИначеЕсли СпособЗаполнения = 2 Тогда // По номенклатуре
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Код КАК КодНоменклатуры,
		|	питВидНоменклатуры КАК ВидНоменклатуры,
		|	ЭтоГруппа КАК ЭтоГруппа,
		|	Родитель КАК Родитель,
		|	Наименование КАК Наименование,
		|	Ссылка КАК Ссылка,
		|	ЕдиницаИзмерения КАК ЕдиницаИзмерения
		|ИЗ
		|	Справочник.Номенклатура" + ?(ГруппаПустая, "", " ГДЕ Ссылка В ИЕРАРХИИ(&ГруппаНоменклатуры) ") + "
		|УПОРЯДОЧИТЬ ПО Ссылка ИЕРАРХИЯ АВТОУПОРЯДОЧИВАНИЕ";
		
	ИначеЕсли СпособЗаполнения = 3 Тогда // По другому виду прайс-листа
		Если Не ЗначениеЗаполнено(ВидПрайсЛиста)Тогда
			Ошибки = "Не задан исходный вид меню!";			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Ошибки);
			Возврат;
		КонецЕсли;
		ТекстЗапроса = "ВЫБРАТЬ
		|	ПрайсЛист.Код КАК КодНоменклатуры,
		|	ПрайсЛист.Номенклатура.питВидНоменклатуры КАК ВидНоменклатуры,
		|	ПрайсЛист.ЭтоГруппа,
		|	ПрайсЛист.Родитель,
		|	ПрайсЛист.Наименование,
		|	ПрайсЛист.Ссылка КАК ПрайсЛистСсылка, 
		|	ВЫБОР КОГДА ПрайсЛист.ЭтоГруппа
		|	ТОГДА
		|		ПрайсЛист.Ссылка 
		|	ИНАЧЕ
		|		ПрайсЛист.Номенклатура
		|	КОНЕЦ КАК Ссылка,
		|	ПрайсЛист.ЕдиницаИзмерения КАК ЕдиницаИзмерения
		|ИЗ
		|	Справочник.питМеню КАК ПрайсЛист
		|ГДЕ
		|	ПрайсЛист.Владелец = &ВидПрайсЛиста";
		Если НЕ ГруппаПустая Тогда 
			ТекстЗапроса = ТекстЗапроса + " И ПрайсЛист.Номенклатура В ИЕРАРХИИ(&ГруппаНоменклатуры) ";
		КонецЕсли;
		ТекстЗапроса	= ТекстЗапроса + "
		|
		|УПОРЯДОЧИТЬ ПО ПрайсЛистСсылка ИЕРАРХИЯ
		|АВТОУПОРЯДОЧИВАНИЕ";
		
	КонецЕсли;
	
	Запрос.Текст		= ТекстЗапроса;
	Запрос.УстановитьПараметр("Дата", ?(Не ЗначениеЗаполнено(Дата), '00010101', КонецДня(Дата)));
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ГруппаНоменклатуры", ГруппаНоменклатуры);
	Запрос.УстановитьПараметр("Контрагент",Поставщик);
	Запрос.УстановитьПараметр("ВидПрайсЛиста",ВидПрайсЛиста);
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	// ПоГруппировкам - это для исключения из обработки детальных записей, чтобы в Меню не было дублирования элементов.
	
	СоответствиеГрупп	= Новый Соответствие;
	Всего				= Выборка.Количество();
	Счетчик				= 0;
	
	// Если у владельца стоит флаг уникальности и не очищать прайс-лист, заполним соответствие.
	Если ВладелецПрайсЛиста.ФлагУникальности И Не ФлажокОчищать И (СпособСозданияГрупп = 2 ИЛИ СпособСозданияГрупп = 1) Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	ПрайсЛист.Ссылка,
		|	ПрайсЛист.Наименование,
		|	ПрайсЛист.Номенклатура.Код КАК Код
		|ИЗ
		|	Справочник.питМеню КАК ПрайсЛист
		|ГДЕ
		|	ПрайсЛист.Владелец = &Владелец
		|	И ПрайсЛист.ЭтоГруппа = ИСТИНА");
		
		Запрос.УстановитьПараметр("Владелец", ВладелецПрайсЛиста);
		ВыборкаГруппПрайса = Запрос.Выполнить().Выбрать();
		Пока ВыборкаГруппПрайса.Следующий() Цикл
			Если СпособСозданияГрупп = 2 Тогда 
				ГруппаНоменклатуры = Справочники.Номенклатура.НайтиПоНаименованию(ВыборкаГруппПрайса.Наименование);
				Если ГруппаНоменклатуры.Пустая() Тогда
					Продолжить;
				Иначе
					ИмяГруппыВыборки = ГруппаНоменклатуры.Код;
				КонецЕсли;
			Иначе
				ИмяГруппыВыборки = ВыборкаГруппПрайса.Наименование;
			КонецЕсли;
			ИмяГруппыВыборки = "К_" + СокрЛП(ИмяГруппыВыборки);
			Если СоответствиеГрупп.Получить(ИмяГруппыВыборки) = Неопределено Тогда
				СоответствиеГрупп.Вставить(ИмяГруппыВыборки,ВыборкаГруппПрайса.Ссылка);
			КонецЕсли
		КонецЦикла; 
	КонецЕсли;
	
	ЗаголовокОшибки = "Заполнение меню """+ВладелецПрайсЛиста+""":";
	ТекстОшибки = "";
	Стр = Выборка;
	
	Пока Выборка.Следующий() Цикл
		
		// Отсеем общий итог, если группа не задана или пустые наименования.
		Если НЕ ЗначениеЗаполнено(Стр.Наименование) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ Справочники.питМеню.ПроверитьДопустимыйВидНоменклатуры(Стр.Ссылка, ТекстОшибки) Тогда
			Ошибки = "Ошибка добавления элемента """+Стр.Ссылка+""". "+ТекстОшибки + Символы.ПС;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Ошибки, Стр.Ссылка);
			Продолжить;
		КонецЕсли;
		
		Родитель	= Неопределено;
		Если ЗначениеЗаполнено(ТекущийРодитель) Тогда
			Если Найти(Строка(ТекущийРодитель), "Объект не найден") = 0 Тогда
				Родитель = ТекущийРодитель;
			КонецЕсли;	
		КонецЕсли;
		
		Если СпособСозданияГрупп = 1 И Не Стр.ЭтоГруппа Тогда
			ИмяГруппы	= СокрЛП(Строка(Стр.ВидНоменклатуры));
			Если ПустаяСтрока(ИмяГруппы) Тогда
				ИмяГруппы	= "Неопределенный вид";
			КонецЕсли;
			Группа = СоответствиеГрупп.Получить("К_"+ИмяГруппы);
			Если Группа <> Неопределено Тогда
				Родитель = Группа;
			Иначе
				НоваяГруппа				= Справочники.питМеню.СоздатьГруппу();
				НоваяГруппа.Владелец	= ВладелецПрайсЛиста;
				НоваяГруппа.УстановитьНовыйКод();
				НоваяГруппа.Наименование = ИмяГруппы;
				Если Не Родитель = Неопределено Тогда
					НоваяГруппа.Родитель	= Родитель;
				КонецЕсли;
				Попытка
					НоваяГруппа.Записать();
					СоответствиеГрупп.Вставить("К_"+ИмяГруппы, НоваяГруппа.Ссылка);
					Родитель = НоваяГруппа.Ссылка;
				Исключение
					Ошибки = "Запись группы меню """+НоваяГруппа+""". "+ОписаниеОшибки() + Символы.ПС;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Ошибки);
					Прервать;
				КонецПопытки;
			КонецЕсли;
		ИначеЕсли СпособСозданияГрупп = 2 И Стр.ЭтоГруппа Тогда
			Если СпособЗаполнения = 0 И ЗначениеЗаполнено(ГруппаНоменклатуры) Тогда
				Если ГруппаНоменклатуры.ПринадлежитЭлементу(Стр.Ссылка) Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			ИмяГруппы	= СокрЛП(Стр.КодНоменклатуры);
			Группа		= СоответствиеГрупп.Получить("К_"+ИмяГруппы);
			Если Группа <> Неопределено Тогда
				Родитель = Группа;
			Иначе
				НоваяГруппа				= Справочники.питМеню.СоздатьГруппу();
				НоваяГруппа.Владелец	= ВладелецПрайсЛиста;
				НоваяГруппа.Номенклатура= Стр.Ссылка;
				НоваяГруппа.УстановитьНовыйКод();
				НоваяГруппа.Наименование= Стр.Наименование;
				Если ЗначениеЗаполнено(Стр.Родитель) Тогда
					Группа = СоответствиеГрупп.Получить("К_"+СокрЛП(Стр.Родитель.Код));
					Если Группа <> Неопределено Тогда
						НоваяГруппа.Родитель = Группа;
					Иначе
						Если Не Родитель = Неопределено Тогда
							НоваяГруппа.Родитель = Родитель;
						КонецЕсли;
					КонецЕсли;
				Иначе
					Если Не Родитель = Неопределено Тогда
						НоваяГруппа.Родитель = Родитель;
					КонецЕсли;
				КонецЕсли;
				Попытка
					НоваяГруппа.Записать();
					СоответствиеГрупп.Вставить("К_"+ИмяГруппы, НоваяГруппа.Ссылка);
					Родитель = НоваяГруппа.Ссылка;
				Исключение
					Ошибки = "Запись группы меню """+НоваяГруппа+""". "+ОписаниеОшибки() + Символы.ПС;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Ошибки);
					Прервать;
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
		
		Если Не Стр.ЭтоГруппа Тогда
			НовыйЭлемент				= Справочники.питМеню.СоздатьЭлемент();
			НовыйЭлемент.Владелец		= ВладелецПрайсЛиста;
			НовыйЭлемент.УстановитьНовыйКод();
			НовыйЭлемент.Наименование	= Стр.Наименование;
			НовыйЭлемент.Номенклатура	= Стр.Ссылка;
			
			Если Стр.Ссылка.питВидНоменклатуры = Перечисления.питВидыНоменклатуры.Блюдо Тогда
				//СпособРасчетаРецептур	= Константы.питСпособРасчетаРецептур.Получить();
				СпособРасчетаРецептур	= питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("СпособРасчетаРецептур");
				ДатаРецептуры = ТекущаяДата();
				ОсновнаяРецептура		= питПроизводство.ВернутьОсновнуюРецептуру(Стр.Ссылка,,Перечисления.питХозяйственныеОперации.РецептураПриготовление,ДатаРецептуры,СпособРасчетаРецептур);
				НовыйЭлемент.Рецептура	= ОсновнаяРецептура;
			Иначе
				НовыйЭлемент.Рецептура = Документы.питРецептура.ПустаяСсылка();
			КонецЕсли;
			
			НовыйЭлемент.ЕдиницаИзмерения = Стр.ЕдиницаИзмерения;
			
			Группа = Неопределено;
			Если (ЗначениеЗаполнено(Стр.Родитель)) И (Не СпособСозданияГрупп = 0) Тогда
				Если СпособСозданияГрупп = 1 Тогда
					Группа = СоответствиеГрупп.Получить("К_"+СокрЛП(Строка(Стр.ВидНоменклатуры)));
				ИначеЕсли СпособСозданияГрупп = 2 Тогда
					Группа = СоответствиеГрупп.Получить("К_"+СокрЛП(Стр.Родитель.Код));
					Если Группа = Неопределено Тогда
						Если Не Родитель = Неопределено Тогда
							Группа = Родитель;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			Иначе
				Если Не Родитель = Неопределено Тогда
					Группа = Родитель;
				КонецЕсли;
			КонецЕсли;
			Если Не Группа = Неопределено Тогда
				НовыйЭлемент.Родитель = Группа;
			КонецЕсли;
			
			Если ПроверкаЭлемента(НовыйЭлемент, ВладелецПрайсЛиста) Тогда
				Попытка
					НовыйЭлемент.Записать();
				Исключение
					Ошибки = "Запись элемента меню """+НовыйЭлемент+""". "+ОписаниеОшибки() + Символы.ПС;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Ошибки);
					Прервать;
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
		Счетчик = Счетчик + 1;
	КонецЦикла;
КонецПроцедуры // ЗаполнитьПрайсЛист()

Функция ПроверитьКорректностьПередЗаполнением(ВладелецПрайсЛиста,СпособЗаполнения=2,ПараметрыЗаполнения,СпособСозданияГрупп=2,ФлажокОчищать = Истина,ТекущийРодитель = Неопределено) Экспорт
	Если Не ЗначениеЗаполнено(ПараметрыЗаполнения) Тогда 
		ПараметрыЗаполнения = Новый Структура(); 
	КонецЕсли;
	Если Не ПараметрыЗаполнения.Свойство("Склад") Тогда 
		ПараметрыЗаполнения.Вставить("Склад"); 
	КонецЕсли;
	Если Не ПараметрыЗаполнения.Свойство("Организация") Тогда 
		ПараметрыЗаполнения.Вставить("Организация"); 
	КонецЕсли;
	Если Не ПараметрыЗаполнения.Свойство("Дата") Тогда 
		ПараметрыЗаполнения.Вставить("Дата"); 
	КонецЕсли;	
	Если Не ПараметрыЗаполнения.Свойство("Поставщик") Тогда 
		ПараметрыЗаполнения.Вставить("Поставщик"); 
	КонецЕсли;
	Если Не ПараметрыЗаполнения.Свойство("ГруппаНоменклатуры") Тогда 
		ПараметрыЗаполнения.Вставить("ГруппаНоменклатуры"); 
	КонецЕсли;
	Если Не ПараметрыЗаполнения.Свойство("ВидПрайсЛиста") Тогда 
		ПараметрыЗаполнения.Вставить("ВидПрайсЛиста"); 
	КонецЕсли;
	
	// Получим значения структуры
	Склад				= ПараметрыЗаполнения.Склад;
	Организация			= ?(ЗначениеЗаполнено(ПараметрыЗаполнения.Организация), ПараметрыЗаполнения.Организация, питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация"));
	Дата 				= ПараметрыЗаполнения.Дата;
	Поставщик			= ПараметрыЗаполнения.Поставщик;
	ГруппаНоменклатуры	= ПараметрыЗаполнения.ГруппаНоменклатуры;
	ВидПрайсЛиста		= ПараметрыЗаполнения.ВидПрайсЛиста;
	Если Не ЗначениеЗаполнено(ВладелецПрайсЛиста) Тогда
		Ошибки = "Не задан владелец меню!";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Ошибки);
		Возврат Ложь;
	КонецЕсли;
	Если СпособЗаполнения = 3 Тогда // По другому виду прайс-листа
		Если Не ЗначениеЗаполнено(ВидПрайсЛиста)Тогда
			Ошибки = "Не задан исходный вид меню!";			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Ошибки);
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	Возврат Истина;
КонецФункции

// Проверяет, корректно ли заполнен новый элемент справочника.
//
// Параметры:
//  Элемент		– СправочникСсылка	– Элемент справочника.
//
Функция ПроверкаЭлемента(Элемент, Владелец)
	Разрешено = Истина;
	Если Владелец.ФлагУникальности Тогда
		Разрешено = Разрешено И Не ЗначениеЗаполнено(Справочники.питМеню.НайтиПоРеквизиту("Номенклатура", Элемент.Номенклатура, , Владелец));
		Если Не Разрешено Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Номенклатура """ + Элемент.Номенклатура + """ не уникальна, элемент не добавлен.";
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЕсли;
	Возврат Разрешено;
КонецФункции // ПроверкаЭлемента()


// Проверяет вид номенклатуры на допустимость для добавления в меню.
//  Номенклатура - проверяемый элемент номенклатуры.
//
// Возвращаемое значение:
//  ТекстОшибки - текст ошибки, если вид номенклатуры не допустимый.
//  Булево      – Истина, если эту номенклатуру можно добавлять в меню.
//
Функция ПроверитьДопустимыйВидНоменклатуры(Номенклатура, ТекстОшибки="") Экспорт
	
	ТекстОшибки="";
	
	Если Номенклатура.ЭтоГруппа ИЛИ Номенклатура.Пустая() Тогда
		Возврат Истина;
	Иначе
		ВидНоменклатуры = Номенклатура.питВидНоменклатуры;
		Если ВидНоменклатуры = Перечисления.питВидыНоменклатуры.Блюдо
			ИЛИ ВидНоменклатуры = Перечисления.питВидыНоменклатуры.Товар
			ИЛИ ВидНоменклатуры = Перечисления.питВидыНоменклатуры.БизнесЛанч
			ИЛИ ВидНоменклатуры = Перечисления.питВидыНоменклатуры.Услуга Тогда
			Возврат Истина;
		Иначе
			ТекстОшибки = "Меню может содержать только номенклатуру вида: блюдо, услуга, товар, бизнес-ланч.";
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции // ПроверитьДопустимыйВидНоменклатуры()

#КонецЕсли

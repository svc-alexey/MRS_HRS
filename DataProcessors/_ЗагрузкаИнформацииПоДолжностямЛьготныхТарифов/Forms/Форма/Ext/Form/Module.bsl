
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//ЗаполнитьШаблонДанныхДляЗагрузки();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ФайлExcelНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберите excel - файл";
	Диалог.ПолноеИмяФайла = "c:\";
	Диалог.Фильтр = "Таблицы (*.xls,*.xlsx)|*.xls;*.xlsx;|Microsoft Excel 97/2000/XP/2003 (*.xls)|*.xls|Microsoft Excel 2007/2010 (*.xlsx)|*.xlsx";
	
	Если Диалог.Выбрать() Тогда
		ФайлExcel = Диалог.ПолноеИмяФайла;
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ЗагрузитьДанные(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;	
	
	ОчиститьСообщения();
	
	Файл = Новый Файл(ФайлExcel);
	ДвоичныеДанные = Новый ДвоичныеДанные(ФайлExcel);
		
	ЗагрузитьДанныеНаСервере(ДвоичныеДанные, Файл.Расширение);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеНаСервере(ДвоичныеДанные, Расширение)
			
	ПрочитатьEXCEL(ДвоичныеДанные, Расширение);

	ПрочитатьДанныеДляЗагрузки();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьEXCEL(ДвоичныеДанные, Расширение)
	
	ИмяФайла = ПолучитьИмяВременногоФайла(Расширение);
	ДвоичныеДанные.Записать(ИмяФайла);
	
	ДанныеДляЗагрузки.Прочитать(ИмяФайла, СпособЧтенияЗначенийТабличногоДокумента.Текст);    // СпособЧтенияЗначенийТабличногоДокумента - новый параметр платформы 8.3.6. Второе значение "Текст".
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьДанныеДляЗагрузки(Команда)
	ПодготовитьДанныеДляЗагрузкиНаСервере();	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьДанныеДляЗагрузкиНаСервере()
	
	ТаблицаДляЗагрузки = ПрочитатьДанныеДляЗагрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьВ1С(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;	
	
	ЗагрузитьВ1ССервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	      
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСДаннымиДляЗагрузки                                           	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьВ1ССервер()
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПереченьРабочихМестНаИнвестиционномПроекте = Данные.Выгрузить(, "ГоловнаяОрганизация,Родитель,Наименование,КоличествоЗанятыхВИнвестПроекте");
	ПереченьРабочихМестНаИнвестиционномПроекте.Свернуть("ГоловнаяОрганизация,Родитель,Наименование,КоличествоЗанятыхВИнвестПроекте");
	ПереченьРабочихМестНаИнвестиционномПроекте.Колонки.Добавить("РабочееМестоНаИнвестиционномПроекте");
	
	Для Каждого Строка Из ПереченьРабочихМестНаИнвестиционномПроекте Цикл
		Родитель = Справочники.MRS_ПереченьРабочихМестНаИнвестиционномПроекте.НайтиПоНаименованию(Строка.Родитель, Истина);
		Если Родитель.Пустая() Тогда
	    	РодительОбъект = Справочники.MRS_ПереченьРабочихМестНаИнвестиционномПроекте.СоздатьГруппу();
			РодительОбъект.ГоловнаяОрганизация = Строка.ГоловнаяОрганизация;
			РодительОбъект.Наименование = Строка.Родитель;
			РодительОбъект.Записать();
			Родитель = РодительОбъект.Ссылка;
		КонецЕсли;
		
		Строка.РабочееМестоНаИнвестиционномПроекте = Справочники.MRS_ПереченьРабочихМестНаИнвестиционномПроекте.НайтиПоНаименованию(Строка.Наименование, Истина, Родитель);
		Если Строка.РабочееМестоНаИнвестиционномПроекте.Пустая() Тогда
	    	ЭлементОбъект = Справочники.MRS_ПереченьРабочихМестНаИнвестиционномПроекте.СоздатьЭлемент();
			ЭлементОбъект.ГоловнаяОрганизация = Строка.ГоловнаяОрганизация;
			ЭлементОбъект.Родитель = Родитель;
			ЭлементОбъект.Наименование = Строка.Наименование;
			ЭлементОбъект.КоличествоЗанятыхВИнвестПроекте = Строка.КоличествоЗанятыхВИнвестПроекте;
			ЭлементОбъект.ДатаВступленияВСилу = Объект.ДатаРегистрацииИзменений;
			ЭлементОбъект.Записать();
			Строка.РабочееМестоНаИнвестиционномПроекте = ЭлементОбъект.Ссылка;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ТекущаяСтрока Из Данные Цикл
		
		РабочееМестоНаИнвестиционномПроекте = Справочники.MRS_ПереченьРабочихМестНаИнвестиционномПроекте.ПустаяСсылка();
		
		Для Каждого Строка Из ПереченьРабочихМестНаИнвестиционномПроекте.НайтиСтроки(Новый Структура("Родитель,Наименование", ТекущаяСтрока.Родитель, ТекущаяСтрока.Наименование)) Цикл
			РабочееМестоНаИнвестиционномПроекте = Строка.РабочееМестоНаИнвестиционномПроекте;
		КонецЦикла;		
		
		Если ЗначениеЗаполнено(ТекущаяСтрока.ПозицияШтатногоРасписания) И ЗначениеЗаполнено(РабочееМестоНаИнвестиционномПроекте) Тогда
			
			МенеджерЗаписи = РегистрыСведений.MRS_ПрименяемыеТарифыСтраховыхВзносовДляПозицииШтатногоРасписания.СоздатьМенеджерЗаписи();	
			МенеджерЗаписи.Период = Объект.ДатаРегистрацииИзменений;	
			МенеджерЗаписи.ГоловнаяОрганизация = ТекущаяСтрока.ГоловнаяОрганизация;	
			МенеджерЗаписи.ПозицияШтатногоРасписания = ТекущаяСтрока.ПозицияШтатногоРасписания;	
			МенеджерЗаписи.Прочитать();
			
			Если МенеджерЗаписи.Выбран() Тогда
				ТекущаяСтрока.Существует = Истина;
				Продолжить;
			КонецЕсли;
			
			МенеджерЗаписи.Период = Объект.ДатаРегистрацииИзменений;	
			МенеджерЗаписи.ГоловнаяОрганизация = ТекущаяСтрока.ГоловнаяОрганизация;	
			МенеджерЗаписи.ПозицияШтатногоРасписания = ТекущаяСтрока.ПозицияШтатногоРасписания;	
			МенеджерЗаписи.ВидТарифа = Объект.ВидТарифа;	
			МенеджерЗаписи.Отработано = Ложь;
			МенеджерЗаписи.РабочееМестоНаИнвестиционномПроекте = РабочееМестоНаИнвестиционномПроекте;
			МенеджерЗаписи.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
			
КонецПроцедуры

&НаСервере
Функция ПрочитатьДанныеДляЗагрузки()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ШтатноеРасписание.Ссылка КАК ШтатноеРасписание,
	|	ШтатноеРасписание.Должность КАК Должность,
	|	ШтатноеРасписание.Подразделение КАК Подразделение,
	|	ШтатноеРасписание.Подразделение.Владелец КАК ПодразделениеВладелец,
	|	ВРЕГ(СОКРЛП(ШтатноеРасписание.Должность.Наименование)) КАК ДолжностьНаименование
	|ИЗ
	|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
	|ГДЕ
	|	ШтатноеРасписание.Утверждена
	|	И ШтатноеРасписание.Подразделение.Владелец = &Организация
	|	И (ШтатноеРасписание.ДатаЗакрытия = ДАТАВРЕМЯ(1, 1, 1)
	|			ИЛИ ШтатноеРасписание.ДатаЗакрытия >= &ДатаРегистрацииИзменений)
	|	И НЕ ШтатноеРасписание.Должность.Ссылка ЕСТЬ NULL";
	Запрос.УстановитьПараметр("ДатаРегистрацииИзменений", Объект.ДатаРегистрацииИзменений);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	ШтатноеРасписание = Запрос.Выполнить().Выгрузить();
	
	СоответствиеКолонок = ИнициализироватьСоответствиеКолонок();
	ТаблицаДляЗагрузки = ИнициализироватьТаблицуЗначенийДляЗагрузки(СоответствиеКолонок);
	
	Для Каждого ОбластьТД Из ДанныеДляЗагрузки.Области Цикл
		
		Область = ДанныеДляЗагрузки.ПолучитьОбласть(ОбластьТД.Имя);
		
		Для НомерСтр = 2 По Область.ВысотаТаблицы Цикл
			
			НовСтрока = ТаблицаДляЗагрузки.Добавить();
			Номер	  = НомерСтр-1;
			
			Попытка
				
				Для Сч = 1 По СоответствиеКолонок.Количество() Цикл 
					
					ИмяКолонки = СоответствиеКолонок[Сч].Имя;
					
					Значение = СокрЛП(Область.Область(НомерСтр,Сч,НомерСтр,Сч).Текст);
					Значение = СтрРазделить(Значение, Символ(160)+Символ(13)+Символы.ПС, Ложь);
					Значение = СтрСоединить(Значение, Символ(32));
					
					Если СоответствиеКолонок[Сч].Тип.СодержитТип(Тип("Число")) Тогда 
						Значение = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(Значение);						
					ИначеЕсли СоответствиеКолонок[Сч].Тип.СодержитТип(Тип("СправочникСсылка.Организации")) И ЗначениеЗаполнено(Значение) Тогда 
						Значение = Справочники.Организации.НайтиПоНаименованию(Значение);
					КонецЕсли;	
					
					НовСтрока[ИмяКолонки] = Значение;
				КонецЦикла;
				
			Исключение
				Продолжить;
			КонецПопытки;
			
		КонецЦикла;
	КонецЦикла;
	
	Данные.Очистить();
	
	Для Каждого Строка Из ТаблицаДляЗагрузки Цикл
		Если ЗначениеЗаполнено(Строка.Наименование) Тогда
			Строка.Должность = Справочники.Должности.НайтиПоНаименованию(Строка.Наименование);
		КонецЕсли;		
		
		Если ЗначениеЗаполнено(Строка.Должность) Тогда
			СтрокиШР = ШтатноеРасписание.НайтиСтроки(Новый Структура("ДолжностьНаименование", ВРег(СокрЛП(Строка(Строка.Должность))))) ;
			
			Если ЗначениеЗаполнено(СтрокиШР) Тогда
				Для Каждого СтрокаШР Из СтрокиШР Цикл
					НоваяСтрока = Данные.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
					НоваяСтрока.ПозицияШтатногоРасписания = СтрокаШР.ШтатноеРасписание;
				КонецЦикла;
			Иначе
				НоваяСтрока = Данные.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				НоваяСтрока.Комментарий = "Должности нет в ШР";
			КонецЕсли;
		Иначе
			НоваяСтрока = Данные.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.Комментарий = "Должность не существует";
		КонецЕсли;
	КонецЦикла;
	
	Возврат Данные.Выгрузить();
	
КонецФункции
	
&НаСервере
Функция ИнициализироватьСоответствиеКолонок()
	Значение = РеквизитФормыВЗначение("Объект");
	НачальныеДанные = Значение.ПолучитьМакет("ШаблонЗагрузкиДанных").ПолучитьОбласть("НачальныеДанные");
	ОбластьДанные = Значение.ПолучитьМакет("ШаблонЗагрузкиДанных").ПолучитьОбласть("ДетальныеДанные");
	
	ВозвращаемоеЗначение = Новый Соответствие();         
	Для Индекс = 1 По ОбластьДанные.Области.ДетальныеДанные.Право Цикл
		Ячейка = ОбластьДанные.Область(1, Индекс, 1, Индекс);
		ВозвращаемоеЗначение.Вставить(Индекс, ПолучитьСтруктуруКолонки(Ячейка.Параметр, Ячейка.ТипЗначения, НачальныеДанные.Область(1, Индекс, 1, Индекс).Текст));
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
КонецФункции

&НаСервере
Функция ПолучитьСтруктуруКолонки(Имя, Тип = Неопределено, Заголовок = "", Ширина = 0)

	Возврат Новый Структура("Имя, Тип, Заголовок, Ширина", Имя, Тип, Заголовок, Ширина);	

КонецФункции

&НаСервере
Функция ИнициализироватьТаблицуЗначенийДляЗагрузки(СоответствиеКолонок)

	ВозвращаемоеЗначение = Новый ТаблицаЗначений;
	
	Для Сч = 1 По СоответствиеКолонок.Количество() Цикл 
		Значение = СоответствиеКолонок[Сч];
		ВозвращаемоеЗначение.Колонки.Добавить(Значение.Имя, Значение.Тип, Значение.Заголовок, Значение.Ширина);	
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;

КонецФункции

#КонецОбласти

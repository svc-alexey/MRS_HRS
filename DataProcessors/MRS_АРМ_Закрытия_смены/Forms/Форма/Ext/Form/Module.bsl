#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЗагрузитьПараметрыФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФронтСистемаПриИзменении(Элемент)
	
	ОбновитьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеОтклоненияПриИзменении(Элемент)
	
	ОбновитьФлажки("ВсеОтклонения");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклоненияПоСуммеПриИзменении(Элемент)
	
	ОбновитьФлажки("ОтклоненияПоСумме");
	
КонецПроцедуры

&НаКлиенте
Процедура ЧекНеНайденПриИзменении(Элемент)
	
	ОбновитьФлажки("ЧекНеНайден");
	
КонецПроцедуры

&НаКлиенте
Процедура ЧекиАлкогольПриИзменении(Элемент)
	
	ОбновитьФлажки("ЧекиАлкоголь");
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураНеЗаполненаПриИзменении(Элемент)
	
	ОбновитьФлажки("НоменклатураНеЗаполнена");
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалОтбораДатаПриИзменении(Элемент)
	
	MRS_ИнтервалОтбора.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
	НачалоПериода = MRS_ИнтервалОтбора.ДатаНачала;
	КонецПериода   = MRS_ИнтервалОтбора.КонецПериода;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сверить(Команда)
	
	СверитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	
	ЗагрузитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДаты(Команда)
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ИнтервалОтбораПриИзмененииЗавершение", ЭтотОбъект);
	Диалог = Новый ДиалогРедактированияСтандартногоПериода;
	Диалог.Период = MRS_ИнтервалОтбора;
	Диалог.Показать(ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого СтрокаТаблицы Из Объект.ПараметрыКасс Цикл
		СтрокаТаблицы.Использовать = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого СтрокаТаблицы Из Объект.ПараметрыКасс Цикл
		СтрокаТаблицы.Использовать = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыКассИспользоватьПриИзменении(Элемент)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура СформироватьВедомость(Команда)
	// Проверка заполнения периода
	Если НЕ ЗначениеЗаполнено(Объект.НачалоПериода) ИЛИ НЕ ЗначениеЗаполнено(Объект.КонецПериода) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Необходимо заполнить период");
		Возврат;
	КонецЕсли;
	
	// Проверка выбора касс
	ЕстьИспользуемыеКассы = Ложь;
	Для Каждого СтрокаКассы Из Объект.ПараметрыКасс Цикл
		
		Если СтрокаКассы.Использовать Тогда
			ЕстьИспользуемыеКассы = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ЕстьИспользуемыеКассы Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не выбрано ни одной кассы. Установите флажки в таблице касс.");
		Возврат;
	КонецЕсли;
	
	// Формирование отчета на сервере
	СформироватьОтчетНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СверитьНаСервере()
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ВсеОтклонения",           ВсеОтклонения);
	СтруктураОтбора.Вставить("ЧекНеНайден",             ЧекНеНайден);
	СтруктураОтбора.Вставить("ЧекиАлкоголь",            ЧекиАлкоголь);
	СтруктураОтбора.Вставить("ОтклоненияПоСумме",       ОтклоненияПоСумме);
	СтруктураОтбора.Вставить("НоменклатураНеЗаполнена", НоменклатураНеЗаполнена);
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ПолучитьОтчетСверки(СтруктураОтбора);
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПараметрыФормы()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗагрузитьПараметрыФормы();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");

	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНаСервере()
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.ВыполнитьЗагрузкуДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалОтбораПриИзмененииЗавершение(Период, ДополнительныеПараметры) Экспорт
	
	Если Период = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	MRS_ИнтервалОтбора = Период;
	Объект.НачалоПериода = MRS_ИнтервалОтбора.ДатаНачала;
	Объект.КонецПериода  = MRS_ИнтервалОтбора.ДатаОкончания;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьФорму()
	
	ЗагрузитьПараметрыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФлажки(ИмяТекущегоФлажка)
	
	Если ЭтотОбъект[ИмяТекущегоФлажка] Тогда
		
		// Список всех флажков
		МассивФлажков = Новый Массив;
		МассивФлажков.Добавить("ОтклоненияПоСумме");
		МассивФлажков.Добавить("ЧекНеНайден");
		МассивФлажков.Добавить("ЧекиАлкоголь");
		МассивФлажков.Добавить("НоменклатураНеЗаполнена");
		
		Если ИмяТекущегоФлажка = "ВсеОтклонения" Тогда
			
			Для Каждого ИмяФлажка Из МассивФлажков Цикл
				ЭтотОбъект[ИмяФлажка] = Ложь;
			КонецЦикла;
			
		Иначе
			
			ЭтотОбъект.ВсеОтклонения = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетНаСервере()
	
	// Получение складов из выбранных касс
	МассивСкладов = ПолучитьСкладыПоКассам();
	
	Если МассивСкладов.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось определить склады по выбранным кассам");
		Возврат;
	КонецЕсли;
	
	// Получение схемы компоновки данных из отчета
	СхемаКомпоновкиДанных = Отчеты.ВедомостьПоТоварамНаСкладах.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	
	// Формирование стандартного периода
	СтандартныйПериод = Новый СтандартныйПериод;
	СтандартныйПериод.ДатаНачала = Объект.НачалоПериода;
	СтандартныйПериод.ДатаОкончания = Объект.КонецПериода;
	
	// Установка параметра "Период" если он есть в настройках
	ПараметрПериод = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Если ПараметрПериод <> Неопределено Тогда
		ПараметрПериод.Значение = СтандартныйПериод;
		ПараметрПериод.Использование = Истина;
	КонецЕсли;
	
	// Добавление отбора по складу
	ЭлементОтбора = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Склад");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ЭлементОтбора.ПравоеЗначение = МассивСкладов;
	ЭлементОтбора.Использование = Истина;
	
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных; 
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;	
	Макет = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, , , Тип("ГенераторМакетаКомпоновкиДанных"));
	Макет.ЗначенияПараметров.ТекущаяДата.Значение = ТекущаяДатаСеанса();
	
	// Создание процессора компоновки данных
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет);
	
	// Очистка документа результата
	ВедомостьОтчет.Очистить();
	
	// Создание процессора вывода результата
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ВедомостьОтчет);
	
	// Вывод результата
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);

КонецПроцедуры

&НаСервере
Функция ПолучитьСкладыПоКассам()
	
	МассивКасс = Новый Массив;
	
	Для Каждого СтрокаКассы Из Объект.ПараметрыКасс Цикл
		
		Если СтрокаКассы.Использовать И ЗначениеЗаполнено(СтрокаКассы.КассаККМ) Тогда
			МассивКасс.Добавить(СтрокаКассы.КассаККМ);
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивКасс.Количество() = 0 Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	// Пакетное получение складов через SSL функцию
	СоответствиеСкладов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивКасс, "Склад");
	
	МассивСкладов = Новый Массив;
	КэшСкладов = Новый Соответствие;
	
	Для Каждого КлючЗначение Из СоответствиеСкладов Цикл
		
		Склад = КлючЗначение.Значение;
		
		Если ЗначениеЗаполнено(Склад) И КэшСкладов.Получить(Склад) = Неопределено Тогда
			МассивСкладов.Добавить(Склад);
			КэшСкладов.Вставить(Склад, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивСкладов;
	
КонецФункции

#КонецОбласти
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ЗагрузитьПараметрыФормы();
	ОбновитьПараметрыСпискаВыпусков();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФронтСистемаПриИзменении(Элемент)
	
	ОбновитьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеОтклоненияПриИзменении(Элемент)
	
	ОбновитьФлажки("ВсеОтклонения");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклоненияПоСуммеПриИзменении(Элемент)
	
	ОбновитьФлажки("ОтклоненияПоСумме");
	
КонецПроцедуры

&НаКлиенте
Процедура ЧекНеНайденПриИзменении(Элемент)
	
	ОбновитьФлажки("ЧекНеНайден");
	
КонецПроцедуры

&НаКлиенте
Процедура ЧекиАлкогольПриИзменении(Элемент)
	
	ОбновитьФлажки("ЧекиАлкоголь");
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураНеЗаполненаПриИзменении(Элемент)
	
	ОбновитьФлажки("НоменклатураНеЗаполнена");
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалОтбораДатаПриИзменении(Элемент)
	
	ЭтотОбъект.MRS_ИнтервалОтбора.Вариант = ВариантСтандартногоПериода.ПроизвольныйПериод;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сверить(Команда)
	
	СверитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Загрузить(Команда)
	
	ЗагрузитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДаты(Команда)
	
	ОбработчикОповещения = Новый ОписаниеОповещения("ИнтервалОтбораПриИзмененииЗавершение", ЭтотОбъект);
	Диалог = Новый ДиалогРедактированияСтандартногоПериода;
	Диалог.Период = ЭтотОбъект.MRS_ИнтервалОтбора;
	Диалог.Показать(ОбработчикОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого СтрокаТаблицы Из Объект.ПараметрыКасс Цикл
		СтрокаТаблицы.Использовать = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого СтрокаТаблицы Из Объект.ПараметрыКасс Цикл
		СтрокаТаблицы.Использовать = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	ЭтотОбъект.MRS_ИнтервалОтбора.ДатаНачала = Объект.НачалоПериода;
	ОбновитьПараметрыСпискаВыпусков();
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	ЭтотОбъект.MRS_ИнтервалОтбора.ДатаОкончания = Объект.КонецПериода;
	ОбновитьПараметрыСпискаВыпусков();
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыКассИспользоватьПриИзменении(Элемент)

	ОбновитьПараметрыСпискаВыпусков();

КонецПроцедуры

&НаКлиенте
Процедура СформироватьВедомость(Команда)
	// Проверка заполнения периода
	Если НЕ ЗначениеЗаполнено(Объект.НачалоПериода) ИЛИ НЕ ЗначениеЗаполнено(Объект.КонецПериода) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Необходимо заполнить период");
		Возврат;
	КонецЕсли;
	
	// Проверка выбора касс
	ЕстьИспользуемыеКассы = Ложь;
	Для Каждого СтрокаКассы Из Объект.ПараметрыКасс Цикл
		
		Если СтрокаКассы.Использовать Тогда
			ЕстьИспользуемыеКассы = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ЕстьИспользуемыеКассы Тогда
		
		ТекстСообщения = "Не выбрано ни одной кассы. Установите флажки в таблице касс.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
		
	КонецЕсли;
	
	// Формирование отчета на сервере
	СформироватьОтчетНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетСверкаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьРасшифровку(Расшифровка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВедомостьОтчетОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьВедомостьПоРасшифровке(Расшифровка);
	
КонецПроцедуры

// Открывает отчёт «Ведомость по товарам на складах» по данным расшифровки строки ведомости.
//
// Параметры:
//  Расшифровка - Произвольный - идентификатор элемента расшифровки
//
&НаКлиенте
Процедура ОткрытьВедомостьПоРасшифровке(Расшифровка)
	
	ФормаОтчёта = ПолучитьФорму("Отчет.ВедомостьПоТоварамНаСкладах.Форма");
	
	КомпоновщикНастроек = ФормаОтчёта.Отчет.КомпоновщикНастроек;
	
	СтПериод = Новый СтандартныйПериод;
	СтПериод.ДатаНачала = Объект.НачалоПериода;
	СтПериод.ДатаОкончания = Объект.КонецПериода;
	
	УстановитьПользовательскийПараметрСКД(КомпоновщикНастроек, "Период", СтПериод);
	
	Данные = ДанныеЭлементаРасшифровки(Расшифровка);

	Если Данные = Неопределено Или ТипЗнч(Данные) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	// Отбор по номенклатуре
	Номенклатура = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Данные, "Номенклатура");

	УстановитьПользовательскийОтборСКД(КомпоновщикНастроек, "Номенклатура", Номенклатура, ВидСравненияКомпоновкиДанных.Равно);
	
	// Получение складов из выбранных касс
	МассивСкладов = ПолучитьСкладыПоКассам();
	
	// СписокЗначений Складов для СКД
	СписокСкладов = Новый СписокЗначений;
	Для каждого Склад Из МассивСкладов Цикл
	    СписокСкладов.Добавить(Склад);
	КонецЦикла;
	
	УстановитьПользовательскийОтборСКД(КомпоновщикНастроек, "Склад", СписокСкладов, ВидСравненияКомпоновкиДанных.ВСписке);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("КлючВарианта", "ВедомостьПоТоварамНаСкладах");
	ПараметрыФормы.Вставить("ПользовательскиеНастройки", КомпоновщикНастроек.ПользовательскиеНастройки); 
	
	ОткрытьФорму("Отчет.ВедомостьПоТоварамНаСкладах.Форма", ПараметрыФормы, ЭтотОбъект);
			
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПользовательскийПараметрСКД(КомпоновщикНастроек, ИмяПараметра, Значение)
	
	Перем ПараметрДанных, Настройки, ПользовательскиеНастройки, ПользовательскийПараметр;
	
	Настройки = КомпоновщикНастроек.Настройки;
	
	ПараметрДанных = Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	
	Если ЗначениеЗаполнено(ПараметрДанных.ИдентификаторПользовательскойНастройки) Тогда
		
		ПользовательскиеНастройки = КомпоновщикНастроек.ПользовательскиеНастройки;
		ПользовательскийПараметр = ПользовательскиеНастройки.Элементы.Найти(ПараметрДанных.ИдентификаторПользовательскойНастройки);
		ПользовательскийПараметр.Использование = Истина;
		ПользовательскийПараметр.Значение = Значение;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПользовательскийОтборСКД(КомпоновщикНастроек, ИмяОтбора, Значение, ВидСравнения = Неопределено)
	
	Перем	ПолеКД, Элем, ЭлементОтбора, Настройки, 
			ПользовательскиеНастройки, ПользовательскийОтбор;
	
	Настройки = КомпоновщикНастроек.Настройки;
	
	ПолеКД = Новый ПолеКомпоновкиДанных(ИмяОтбора);
	
	Для Каждого Элем Из Настройки.Отбор.Элементы Цикл
		
		Если Элем.ЛевоеЗначение = ПолеКД Тогда
			ЭлементОтбора = Элем;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЭлементОтбора = Неопределено Тогда
		Возврат;		
	КонецЕсли;		
	
	Если ЗначениеЗаполнено(ЭлементОтбора.ИдентификаторПользовательскойНастройки) Тогда
		
		ПользовательскиеНастройки = КомпоновщикНастроек.ПользовательскиеНастройки;
 		ПользовательскийОтбор = ПользовательскиеНастройки.Элементы.Найти(ЭлементОтбора.ИдентификаторПользовательскойНастройки);
		ПользовательскийОтбор.Использование = (Значение <> Неопределено);
		ПользовательскийОтбор.ВидСравнения = ?(ВидСравнения <> Неопределено, ВидСравнения, ВидСравненияКомпоновкиДанных.Равно); 
		ПользовательскийОтбор.ПравоеЗначение  = Значение;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРасшифровку(Расшифровка)
	
	Данные = ДанныеЭлементаРасшифровки(Расшифровка);
	
	Если Данные = Неопределено Или ТипЗнч(Данные) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	// Поиск первого ссылочного значения в полях расшифровки
	Для Каждого КлючЗначение Из Данные Цикл
		
		ЗначениеПоля = КлючЗначение.Значение;
		
		Если ЗначениеПоля <> Неопределено Тогда
			
			ТипЗначения = ТипЗнч(ЗначениеПоля);
			
			Если ПроверитьСсылку(ТипЗначения) Тогда   
				ПоказатьЗначение(, ЗначениеПоля);
				Прервать;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Проверяет, является ли тип ссылочным.
//
// Параметры:
//  ТипЗначения - Тип - проверяемый тип
//
// Возвращаемое значение:
//  Булево - Истина, если тип ссылочный
//
&НаСервереБезКонтекста
Функция ПроверитьСсылку(Знач ТипЗначения)
	
	Возврат ОбщегоНазначения.ЭтоСсылка(ТипЗначения);
	
КонецФункции

&НаКлиенте
Процедура ВыпускиКОбработкеПриИзменении(Элемент)
	ЭтотОбъект.ОткрытыеЦены = Ложь;
	ОбновитьПараметрыСпискаВыпусков();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытыеЦеныПриИзменении(Элемент)
	ЭтотОбъект.ВыпускиКОбработке = Ложь;
	ОбновитьПараметрыСпискаВыпусков();
КонецПроцедуры

&НаКлиенте
Процедура АнализОтрицательных(Команда)
	// Проверка заполнения периода
	Если НЕ ЗначениеЗаполнено(Объект.НачалоПериода) ИЛИ НЕ ЗначениеЗаполнено(Объект.КонецПериода) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Необходимо заполнить период");
		Возврат;
	КонецЕсли;
	
	// Проверка выбора касс
	ЕстьИспользуемыеКассы = Ложь;
	Для Каждого СтрокаКассы Из Объект.ПараметрыКасс Цикл
		
		Если СтрокаКассы.Использовать Тогда
			ЕстьИспользуемыеКассы = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ЕстьИспользуемыеКассы Тогда
		
		ТекстСообщения = "Не выбрано ни одной кассы. Установите флажки в таблице касс.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
		
	КонецЕсли;
	
	// Формирование отчета на сервере
	СформироватьАнализНаСервере();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет формирование отчета сверки на сервере.
//
&НаСервере
Процедура СверитьНаСервере()
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ВсеОтклонения",           ЭтотОбъект.ВсеОтклонения);
	СтруктураОтбора.Вставить("ЧекНеНайден",             ЭтотОбъект.ЧекНеНайден);
	СтруктураОтбора.Вставить("ЧекиАлкоголь",            ЭтотОбъект.ЧекиАлкоголь);
	СтруктураОтбора.Вставить("ОтклоненияПоСумме",       ЭтотОбъект.ОтклоненияПоСумме);
	СтруктураОтбора.Вставить("НоменклатураНеЗаполнена", ЭтотОбъект.НоменклатураНеЗаполнена);
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ПолучитьОтчетСверки(СтруктураОтбора, УникальныйИдентификатор);
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

// Загружает список касс в табличную часть формы.
//
&НаСервере
Процедура ЗагрузитьПараметрыФормы()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗагрузитьПараметрыФормы();
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");

КонецПроцедуры

// Выполняет загрузку данных из фронт-системы.
//
&НаСервере
Процедура ЗагрузитьНаСервере()
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.ВыполнитьЗагрузкуДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалОтбораПриИзмененииЗавершение(Период, ДополнительныеПараметры) Экспорт
	
	Если Период = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.MRS_ИнтервалОтбора = Период;
	Объект.НачалоПериода = ЭтотОбъект.MRS_ИнтервалОтбора.ДатаНачала;
	Объект.КонецПериода = ЭтотОбъект.MRS_ИнтервалОтбора.ДатаОкончания;
	
КонецПроцедуры

// Обновляет данные формы при изменении фронт-системы.
//
&НаСервере
Процедура ОбновитьФорму()
	
	ЗагрузитьПараметрыФормы();
	
КонецПроцедуры

// Управляет взаимным исключением флажков отборов.
// При установке одного флажка сбрасывает остальные.
//
// Параметры:
//  ИмяТекущегоФлажка - Строка - имя установленного флажка
//
&НаКлиенте
Процедура ОбновитьФлажки(ИмяТекущегоФлажка)
	
	Если ЭтотОбъект[ИмяТекущегоФлажка] Тогда
		
		// Список всех флажков
		МассивФлажков = Новый Массив;
		МассивФлажков.Добавить("ОтклоненияПоСумме");
		МассивФлажков.Добавить("ЧекНеНайден");
		МассивФлажков.Добавить("ЧекиАлкоголь");
		МассивФлажков.Добавить("НоменклатураНеЗаполнена");
		
		Если ИмяТекущегоФлажка = "ВсеОтклонения" Тогда
			
			Для Каждого ИмяФлажка Из МассивФлажков Цикл
				ЭтотОбъект[ИмяФлажка] = Ложь;
			КонецЦикла;
			
		Иначе
			
			ЭтотОбъект.ВсеОтклонения = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Формирует ведомость по товарам на складах с отрицательными остатками.
//
&НаСервере
Процедура СформироватьОтчетНаСервере()
	
	// Получение складов из выбранных касс
	МассивСкладов = ПолучитьСкладыПоКассам();
	
	Если МассивСкладов.Количество() = 0 Тогда
		
		ТекстСообщения = "Не удалось определить склады по выбранным кассам";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
		
	КонецЕсли;
	
	// Формирование ведомости через модуль объекта
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ЭтотОбъект.АдресДанныхРасшифровки = ОбработкаОбъект.СформироватьВедомость(
		МассивСкладов,
		УникальныйИдентификатор);
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

// Получает список складов по выбранным кассам.
//
// Возвращаемое значение:
//  Массив из СправочникСсылка.Склады - массив уникальных складов
//
&НаСервере
Функция ПолучитьСкладыПоКассам()
	
	МассивКасс = Новый Массив;
	
	Для Каждого СтрокаКассы Из Объект.ПараметрыКасс Цикл
		
		Если СтрокаКассы.Использовать И ЗначениеЗаполнено(СтрокаКассы.КассаККМ) Тогда
			МассивКасс.Добавить(СтрокаКассы.КассаККМ);
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивКасс.Количество() = 0 Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	// Пакетное получение складов
	СоответствиеСкладов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивКасс, "Склад");
	
	МассивСкладов = Новый Массив;
	КэшСкладов = Новый Соответствие;
	
	Для Каждого КлючЗначение Из СоответствиеСкладов Цикл
		
		Склад = КлючЗначение.Значение;
		
		Если ЗначениеЗаполнено(Склад) И КэшСкладов.Получить(Склад) = Неопределено Тогда
			МассивСкладов.Добавить(Склад);
			КэшСкладов.Вставить(Склад, Истина);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат МассивСкладов;
	
КонецФункции

// Обновляет параметры динамического списка выпусков на форме.
// Устанавливает отборы динамического списка Выпусков Блюд.
//
&НаСервере
Процедура ОбновитьПараметрыСпискаВыпусков()
	
	// Определение периода для фильтрации
	Если ЗначениеЗаполнено(Объект.НачалоПериода) И ЗначениеЗаполнено(Объект.КонецПериода) Тогда
		НачалоПериода = Объект.НачалоПериода;
		КонецПериода = Объект.КонецПериода;
	Иначе
		// Если период не задан, используем текущий месяц
		НачалоПериода = НачалоМесяца(ТекущаяДатаСеанса());
		КонецПериода = КонецМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
	
	// Получение складов из выбранных касс
	МассивСкладов = ПолучитьСкладыПоКассам();
	
	// Установка периода
	// Удаление старой группы отбора по периоду (если существует)
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
		СписокВыпуски,
		,
		"Период");
	
	// Создание группы отбора для периода с типом "И"
	ГруппаПериод = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		СписокВыпуски.Отбор.Элементы,
		"Период",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ГруппаПериод.Использование = Истина;
	
	// Добавление условия "БольшеИлиРавно"
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ГруппаПериод,
		"Дата",
		НачалоПериода,
		ВидСравненияКомпоновкиДанных.БольшеИлиРавно,
		,
		Истина);
	
	// Добавление условия "МеньшеИлиРавно"
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаПериод,
		"Дата",
		ВидСравненияКомпоновкиДанных.МеньшеИлиРавно,
		КонецПериода,
		,
		Истина);	
	
	// Установка складов
	Если МассивСкладов.Количество() > 0 Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокВыпуски,
			"Склад",
			МассивСкладов,
			ВидСравненияКомпоновкиДанных.ВСписке,
			,
			Истина);
		
	Иначе
		
		// Если кассы не выбраны, удаляем отбор по складам
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
			СписокВыпуски,
			"Склад");
		
	КонецЕсли;
	
	// Флажки отборов на форме
	Если ЭтотОбъект.ВыпускиКОбработке Тогда
		
		// Удаление отбора ОткрытыеЦены (если существует)
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
			СписокВыпуски,
			"ПЛ_Номенклатура",
			"ОткрытыеЦены");
			
		//Установка отбора по непроведенным	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокВыпуски,
			"Проведен",
			Ложь,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Истина);
		
	ИначеЕсли ЭтотОбъект.ОткрытыеЦены Тогда
		
		// Удаление отбора Проведен (если существует)
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
			СписокВыпуски,
			"Проведен",
			"Проведен");
			
		//Устанвока отбора по ОткрытыеЦены	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокВыпуски,
			"ПЛ_Номенклатура",
			Истина,
			ВидСравненияКомпоновкиДанных.Заполнено,     
			,
			Истина);

	Иначе
		
		// Удаление отбора ОткрытыеЦены (если существует)
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
			СписокВыпуски,
			"ПЛ_Номенклатура",
			"ОткрытыеЦены");
			
		// Удаление отбора Проведен (если существует)
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
			СписокВыпуски,
			"Проведен",
			"Проведен");	
			
	КонецЕсли;	
	
КонецПроцедуры

// Получает данные элемента расшифровки из временного хранилища.
//
// Параметры:
//  Расшифровка - Произвольный - идентификатор элемента расшифровки
//
// Возвращаемое значение:
//  Структура, Неопределено - структура с полями расшифровки или Неопределено при ошибке
//
&НаСервере
Функция ДанныеЭлементаРасшифровки(Знач Расшифровка)
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.АдресДанныхРасшифровки) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		ДанныеРасшифровки = ПолучитьИзВременногоХранилища(ЭтотОбъект.АдресДанныхРасшифровки);
		
		Если ДанныеРасшифровки = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ЭлементРасшифровки = ДанныеРасшифровки.Элементы[Расшифровка];
		
		Если ЭлементРасшифровки = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		// другие ссылочные типы в строке кроме номенклатуры для расшифровки нам не нужны
		Если ЭлементРасшифровки.ОсновноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.Расшифровать Тогда
			
			// в родителе все поля детальных записей
			ЭлементРасшифровки = ДанныеРасшифровки.Элементы[ЭлементРасшифровки.ПолучитьРодителей()[0].Идентификатор];		
			Поле = ЭлементРасшифровки.ПолучитьПоля().Найти("Номенклатура");
			
		Иначе
			
			Поле = ЭлементРасшифровки.ПолучитьПоля()[0]; // номенклатура первая в типовом варианте отчета
			
		КонецЕсли;
		
		// Извлечение полей расшифровки в структуру для передачи клиенту
		Результат = Новый Структура;
		
		Если НЕ ЗначениеЗаполнено(Поле.Значение) Тогда
			РасшифровкаРодителя = ДанныеРасшифровки.Элементы[ЭлементРасшифровки.ПолучитьРодителей()[0].Идентификатор];
			ПолеРодителя = РасшифровкаРодителя.ПолучитьПоля().Найти("Номенклатура");
			Результат.Вставить(ПолеРодителя.Поле, ПолеРодителя.Значение);
		Иначе
			Результат.Вставить(Поле.Поле, Поле.Значение);	
		КонецЕсли;	
				
		Возврат Результат;
		
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

// Формирует ведомость по товарам на складах с отрицательными остатками.
//
&НаСервере
Процедура СформироватьАнализНаСервере()

	// Получение складов из выбранных касс
	МассивСкладов = ПолучитьСкладыПоКассам();
	
	Если МассивСкладов.Количество() = 0 Тогда
		
		ТекстСообщения = "Не удалось определить склады по выбранным кассам";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
		
	КонецЕсли;
	
	// Формирование ведомости через модуль объекта
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	//ЭтотОбъект.АдресДанныхРасшифровки = ОбработкаОбъект.СформироватьАнализ(
	//	МассивСкладов,
	//	УникальныйИдентификатор);
	
	ОбработкаОбъект.СформироватьАнализ(
		МассивСкладов,
		УникальныйИдентификатор);
		
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

#КонецОбласти
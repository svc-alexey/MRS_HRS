#Область ПрограммныйИнтерфейс

// Создание межфирменных документов РТУ и ПТУ при проведении документа ПеремещениеТоваров
//
// Параметры:
//  Объект - ДокументОбъект.ПеремещениеТоваров - проводимый документ перемещения
//  Отказ - Булево - признак отказа от проведения
//  РежимПроведения - РежимПроведенияДокумента - режим проведения документа
//
&НаСервере
Процедура СозданиеВГОПоЗаказамНаПеремещение(Объект, Отказ, РежимПроведения) Экспорт
	
	// Проверка заполненности документа основания
	Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка различия организаций
	Если Объект.Организация = Объект.ДокументОснование.Организация Тогда
		Возврат;
	КонецЕсли;
	
	// Получение данных перемещения
	Попытка
		СтруктураЗаполнения = ПолучитьТаблицуТоваровИзПермещения(Объект.Ссылка);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	// Получение цен из документа основания
	Попытка
		ТаблицаТоваровСЦенами = ПолучитьЦеныИзДокументаОснования(Объект.ДокументОснование, Объект.Ссылка);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	// Поиск существующих связанных документов
	СвязанныеДокументы = ЗапросСвязанныхДокументов(Объект.Ссылка);
	
	ДокОбъектРТУ = Неопределено;
	ДокОбъектПТУ = Неопределено;
	
	Если СвязанныеДокументы.Следующий() Тогда
		
		// Найдены существующие документы
		Если ЗначениеЗаполнено(СвязанныеДокументы.Реализация) Тогда
			ДокОбъектРТУ = СвязанныеДокументы.Реализация.ПолучитьОбъект();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СвязанныеДокументы.Приобретение) Тогда
			ДокОбъектПТУ = СвязанныеДокументы.Приобретение.ПолучитьОбъект();
		КонецЕсли;
		
	КонецЕсли;
	
	// Расчет дат для документов
	ДатаПТУИсходного = Объект.ДокументОснование.Дата;
	ДатаПеремещения = Объект.Дата;
	
	// ДатаРТУ = ДатаПеремещения - 1 секунда, но не раньше ДатаПТУИсходного
	ДатаРТУ = ДатаПеремещения - 1;
	Если ДатаРТУ < ДатаПТУИсходного Тогда
		ДатаРТУ = ДатаПТУИсходного + 1;
	КонецЕсли;
	
	// ДатаПТУ = ДатаПеремещения + 1 секунда
	ДатаПТУ = ДатаПеремещения + 1;
	
	// Получение данных перемещения для заполнения реквизитов
	СтрокаТЧ = Новый Структура;
	СтрокаТЧ.Вставить("Ссылка", Объект.Ссылка);
	СтрокаТЧ.Вставить("Номер", Объект.ДокументОснование.Номер);
	СтрокаТЧ.Вставить("Дата", Объект.ДокументОснование.Дата);
	СтрокаТЧ.Вставить("Автор", Объект.Автор);
	СтрокаТЧ.Вставить("ПодразделениеОтправитель", Объект.ПодразделениеОтправитель);
	СтрокаТЧ.Вставить("ПодразделениеПолучатель", Объект.ПодразделениеПолучатель);
	
	// Создание/обновление документа РТУ
	Попытка
		
		Если ДокОбъектРТУ = Неопределено Тогда
			ДокОбъектРТУ = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		Иначе
			// Проверка необходимости обновления
			Если НЕ ТребуетсяОбновлениеДокументаРТУ(ДокОбъектРТУ, СтруктураЗаполнения, ТаблицаТоваровСЦенами, ДатаРТУ) Тогда
				// Обновление не требуется, переходим к ПТУ
			Иначе
				// Отменяем проведение перед обновлением
				Если ДокОбъектРТУ.Проведен Тогда
					ДокОбъектРТУ.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Инициализация реквизитов РТУ
		ИнициализироватьДокументРТУ(ДокОбъектРТУ, СтруктураЗаполнения, СтрокаТЧ, ДатаРТУ);
		
		// Заполнение табличной части Товары
		ЗаполнитьТабличнуюЧастьТовары(ДокОбъектРТУ, ТаблицаТоваровСЦенами);
		
		// Проведение документа РТУ
		ДокОбъектРТУ.Записать(РежимЗаписиДокумента.Проведение);
		
	Исключение
		ТекстОшибки = "Ошибка при создании/обновлении документа РТУ: " + ОписаниеОшибки();
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	// Создание/обновление документа ПТУ
	Попытка
		
		Если ДокОбъектПТУ = Неопределено Тогда
			ДокОбъектПТУ = Документы.ПриобретениеТоваровУслуг.СоздатьДокумент();
		Иначе
			// Проверка необходимости обновления
			Если НЕ ТребуетсяОбновлениеДокументаПТУ(ДокОбъектПТУ, СтруктураЗаполнения, ТаблицаТоваровСЦенами, ДатаПТУ, ДокОбъектРТУ.Ссылка) Тогда
				// Обновление не требуется
				Возврат;
			Иначе
				// Отменяем проведение перед обновлением
				Если ДокОбъектПТУ.Проведен Тогда
					ДокОбъектПТУ.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Инициализация реквизитов ПТУ
		ИнициализироватьДокументПТУ(СтрокаТЧ, СтруктураЗаполнения, ДокОбъектПТУ, ДокОбъектРТУ, Неопределено, ДатаПТУ);
		
		// Заполнение табличной части Товары
		ЗаполнитьТабличнуюЧастьТовары(ДокОбъектПТУ, ТаблицаТоваровСЦенами);
		
		// Установка связи с РТУ
		ДокОбъектПТУ.MRS_Реализация = ДокОбъектРТУ.Ссылка;
		
		// Проведение документа ПТУ
		ДокОбъектПТУ.Записать(РежимЗаписиДокумента.Проведение);
		
	Исключение
		ТекстОшибки = "Ошибка при создании/обновлении документа ПТУ: " + ОписаниеОшибки();
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		
		// Откатываем РТУ при ошибке ПТУ
		Если ДокОбъектРТУ <> Неопределено И ДокОбъектРТУ.Проведен Тогда
			ДокОбъектРТУ.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		КонецЕсли;
		
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

// Отмена проведения и установка пометки удаления на связанные документы РТУ и ПТУ
//
// Параметры:
//  Ссылка - ДокументСсылка.ПеремещениеТоваров - ссылка на документ перемещения
//
&НаСервере
Процедура ОтменитьПроведениеСвязанныхДокументов(Ссылка) Экспорт
	
	// Поиск связанных документов
	СвязанныеДокументы = ЗапросСвязанныхДокументов(Ссылка);
	
	Если НЕ СвязанныеДокументы.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	// Обработка РТУ
	Если ЗначениеЗаполнено(СвязанныеДокументы.Реализация) Тогда
		
		Попытка
			ДокОбъектРТУ = СвязанныеДокументы.Реализация.ПолучитьОбъект();
			
			Если ДокОбъектРТУ.Проведен Тогда
				ДокОбъектРТУ.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			КонецЕсли;
			
			ДокОбъектРТУ.УстановитьПометкуУдаления(Истина);
			
		Исключение
			ТекстОшибки = "Ошибка при отмене проведения РТУ: " + ОписаниеОшибки();
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецПопытки;
		
	КонецЕсли;
	
	// Обработка ПТУ
	Если ЗначениеЗаполнено(СвязанныеДокументы.Приобретение) Тогда
		
		Попытка
			ДокОбъектПТУ = СвязанныеДокументы.Приобретение.ПолучитьОбъект();
			
			Если ДокОбъектПТУ.Проведен Тогда
				ДокОбъектПТУ.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			КонецЕсли;
			
			ДокОбъектПТУ.УстановитьПометкуУдаления(Истина);
			
		Исключение
			ТекстОшибки = "Ошибка при отмене проведения ПТУ: " + ОписаниеОшибки();
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
				
				
#Область СлужебныеПроцедурыИФункции

// Получение связанных документов РТУ и ПТУ для документа перемещения
//
// Параметры:
//  Ссылка - ДокументСсылка.ПеремещениеТоваров - ссылка на документ перемещения
//
// Возвращаемое значение:
//  РезультатЗапроса - результат запроса со связанными документами
//
&НаСервере
Функция ЗапросСвязанныхДокументов(Ссылка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	СвязанныеДокументы.Ссылка.MRS_Перемещение КАК Перемещение,
	|	СвязанныеДокументы.Ссылка КАК Приобретение,
	|	СвязанныеДокументы.Ссылка.Проведен КАК ПриобретениеПроведен,
	|	СвязанныеДокументы.Ссылка.ПометкаУдаления КАК ПриобретениеПометкаУдаления,
	|	СвязанныеДокументы.Ссылка.MRS_Реализация КАК Реализация,
	|	СвязанныеДокументы.Ссылка.MRS_Реализация.Проведен КАК РеализацияПроведен,
	|	СвязанныеДокументы.Ссылка.MRS_Реализация.ПометкаУдаления КАК РеализацияПометкаУдаления
	|ИЗ
	|	КритерийОтбора.СвязанныеДокументы(&Ссылка) КАК СвязанныеДокументы
	|ГДЕ
	|	СвязанныеДокументы.Ссылка ССЫЛКА Документ.ПриобретениеТоваровУслуг"; 
	
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Результат = Запрос.Выполнить().Выбрать();
	
	Возврат Результат;
	
КонецФункции

// Получение цен из табличной части Товары документа основания
//
// Параметры:
//  ДокументОснование - ДокументСсылка.ПриобретениеТоваровУслуг - документ основание
//  СсылкаПеремещения - ДокументСсылка.ПеремещениеТоваров - ссылка на перемещение
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица с колонками: Номенклатура, Количество, КоличествоУпаковок, Цена
//
&НаСервере
Функция ПолучитьЦеныИзДокументаОснования(ДокументОснование, СсылкаПеремещения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПеремещениеТоваровТовары.Номенклатура КАК Номенклатура,
	|	СУММА(ПеремещениеТоваровТовары.Количество) КАК Количество,
	|	СУММА(ПеремещениеТоваровТовары.КоличествоУпаковок) КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ВТ_ТоварыПеремещения
	|ИЗ
	|	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
	|ГДЕ
	|	ПеремещениеТоваровТовары.Ссылка = &СсылкаПеремещения
	|
	|СГРУППИРОВАТЬ ПО
	|	ПеремещениеТоваровТовары.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТоварыПеремещения.Номенклатура КАК Номенклатура,
	|	ВТ_ТоварыПеремещения.Количество КАК Количество,
	|	ВТ_ТоварыПеремещения.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ЕСТЬNULL(ПриобретениеТоваровУслугТовары.Цена, 0) КАК Цена
	|ИЗ
	|	ВТ_ТоварыПеремещения КАК ВТ_ТоварыПеремещения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
	|		ПО ВТ_ТоварыПеремещения.Номенклатура = ПриобретениеТоваровУслугТовары.Номенклатура
	|			И (ПриобретениеТоваровУслугТовары.Ссылка = &ДокументОснование)";
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("СсылкаПеремещения", СсылкаПеремещения);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

// Заполнение табличной части Товары документа РТУ/ПТУ с ценами из документа основания
//
// Параметры:
//  ДокументОбъект - ДокументОбъект.РеализацияТоваровУслуг, ДокументОбъект.ПриобретениеТоваровУслуг - документ для заполнения
//  ТаблицаТоваров - ТаблицаЗначений - таблица с товарами и ценами
//
&НаСервере
Процедура ЗаполнитьТабличнуюЧастьТовары(ДокументОбъект, ТаблицаТоваров)
	
	ДокументОбъект.Товары.Очистить();
	
	ТипДокумента = ТипЗнч(ДокументОбъект.Ссылка);
	
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		НоваяСтрока = ДокументОбъект.Товары.Добавить();
		НоваяСтрока.Номенклатура = СтрокаТовара.Номенклатура;
		НоваяСтрока.Количество = СтрокаТовара.Количество;
		НоваяСтрока.КоличествоУпаковок = СтрокаТовара.КоличествоУпаковок;
		НоваяСтрока.Цена = СтрокаТовара.Цена;
		
		// Заполнение ставки НДС
		Если ТипДокумента = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			НоваяСтрока.СтавкаНДС = УчетНДСУП.ПолучитьСтавкуНДСПродажи(ДокументОбъект.Организация, ТекущаяДатаСеанса());
		ИначеЕсли ТипДокумента = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
			НоваяСтрока.СтавкаНДС = УчетНДСУП.ПолучитьСтавкуНДСЗакупки(ДокументОбъект.Организация, ТекущаяДатаСеанса());
		КонецЕсли;
		
		// Расчет суммы
		НоваяСтрока.Сумма = НоваяСтрока.Количество * НоваяСтрока.Цена;
		
		// Расчет суммы НДС
		Если НЕ ДокументОбъект.ЦенаВключаетНДС Тогда
			
			Если ЗначениеЗаполнено(НоваяСтрока.СтавкаНДС) Тогда
				ПроцентСтавкиНДС = НоваяСтрока.СтавкаНДС.Ставка;
				НоваяСтрока.СуммаНДС = НоваяСтрока.Сумма * ПроцентСтавкиНДС / 100;
			КонецЕсли;
			
		Иначе
			
			Если ЗначениеЗаполнено(НоваяСтрока.СтавкаНДС) Тогда
				ПроцентСтавкиНДС = НоваяСтрока.СтавкаНДС.Ставка;
				НоваяСтрока.СуммаНДС = НоваяСтрока.Сумма * ПроцентСтавкиНДС / (100 + ПроцентСтавкиНДС);
			КонецЕсли;
			
		КонецЕсли;
		
		// Расчет общей суммы
		НоваяСтрока.Всего = НоваяСтрока.Сумма + НоваяСтрока.СуммаНДС;
		
	КонецЦикла;
	
КонецПроцедуры

// Проверка необходимости обновления документа РТУ
//
// Параметры:
//  ДокументРТУ - ДокументОбъект.РеализацияТоваровУслуг - существующий документ РТУ
//  СтруктураЗаполнения - Структура - структура с данными для заполнения
//  ТаблицаТоваров - ТаблицаЗначений - таблица товаров с ценами
//  НоваяДата - Дата - новая дата документа
//
// Возвращаемое значение:
//  Булево - Истина, если требуется обновление
//
&НаСервере
Функция ТребуетсяОбновлениеДокументаРТУ(ДокументРТУ, СтруктураЗаполнения, ТаблицаТоваров, НоваяДата)
	
	// Проверка даты
	Если ДокументРТУ.Дата <> НоваяДата Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Проверка организации
	Если ДокументРТУ.Организация <> СтруктураЗаполнения.ОрганизацияОтправитель Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Проверка контрагента
	Если ДокументРТУ.Контрагент <> СтруктураЗаполнения.КонтрагентРТУ Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Проверка склада
	Если ДокументРТУ.Склад <> СтруктураЗаполнения.СкладОтправитель Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Проверка табличной части
	Если ДокументРТУ.Товары.Количество() <> ТаблицаТоваров.Количество() Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Проверка состава товаров и цен
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		Отбор = Новый Структура("Номенклатура", СтрокаТовара.Номенклатура);
		НайденныеСтроки = ДокументРТУ.Товары.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			Возврат Истина;
		КонецЕсли;
		
		СтрокаДокумента = НайденныеСтроки[0];
		
		Если СтрокаДокумента.Количество <> СтрокаТовара.Количество
			ИЛИ СтрокаДокумента.Цена <> СтрокаТовара.Цена Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Проверка необходимости обновления документа ПТУ
//
// Параметры:
//  ДокументПТУ - ДокументОбъект.ПриобретениеТоваровУслуг - существующий документ ПТУ
//  СтруктураЗаполнения - Структура - структура с данными для заполнения
//  ТаблицаТоваров - ТаблицаЗначений - таблица товаров с ценами
//  НоваяДата - Дата - новая дата документа
//  СсылкаРТУ - ДокументСсылка.РеализацияТоваровУслуг - ссылка на РТУ
//
// Возвращаемое значение:
//  Булево - Истина, если требуется обновление
//
&НаСервере
Функция ТребуетсяОбновлениеДокументаПТУ(ДокументПТУ, СтруктураЗаполнения, ТаблицаТоваров, НоваяДата, СсылкаРТУ)
	
	// Проверка даты
	Если ДокументПТУ.Дата <> НоваяДата Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Проверка организации
	Если ДокументПТУ.Организация <> СтруктураЗаполнения.ОрганизацияПолучатель Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Проверка контрагента
	Если ДокументПТУ.Контрагент <> СтруктураЗаполнения.КонтрагентПТУ Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Проверка склада
	Если ДокументПТУ.Склад <> СтруктураЗаполнения.СкладПолучатель Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Проверка связи с РТУ
	Если ДокументПТУ.MRS_Реализация <> СсылкаРТУ Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Проверка табличной части
	Если ДокументПТУ.Товары.Количество() <> ТаблицаТоваров.Количество() Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Проверка состава товаров и цен
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		Отбор = Новый Структура("Номенклатура", СтрокаТовара.Номенклатура);
		НайденныеСтроки = ДокументПТУ.Товары.НайтиСтроки(Отбор);
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			Возврат Истина;
		КонецЕсли;
		
		СтрокаДокумента = НайденныеСтроки[0];
		
		Если СтрокаДокумента.Количество <> СтрокаТовара.Количество
			ИЛИ СтрокаДокумента.Цена <> СтрокаТовара.Цена Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Для обработки табличной части
//
// Параметры:
//  ЭтотОбъект - ДокументОбъект - обрабатываемый документ
//
// Возвращаемое значение:
//  Структура - структура с действиями для обработки ТЧ
//
&НаСервере
Функция ПолучитьСтруктуруДействийДляТабличнойЧасти(ЭтотОбъект)
	
	СтруктураПересчетаСуммы = Новый Структура("ЦенаВключаетНДС, НалогообложениеНДС", Истина, Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	Возврат СтруктураДействий;
	
КонецФункции

// Инициализация реквизитов документа РеализацияТоваровУслуг
//
// Параметры:
//  ДокОбъектРТУ - ДокументОбъект.РеализацияТоваровУслуг - заполняемый документ
//  СтруктураЗаполнения - Структура - структура с данными для заполнения
//  СтрокаТЧ - Структура - данные документа перемещения
//  ДатаДокументов - Дата - дата создаваемого документа
//
&НаСервере
Процедура ИнициализироватьДокументРТУ(ДокОбъектРТУ, СтруктураЗаполнения, СтрокаТЧ, ДатаДокументов)
	
	ДокОбъектРТУ.Дата = ДатаДокументов;
	ДокОбъектРТУ.Организация = СтруктураЗаполнения.ОрганизацияОтправитель;
	ДокОбъектРТУ.Склад = СтруктураЗаполнения.СкладОтправитель;
	ДокОбъектРТУ.Контрагент = СтруктураЗаполнения.КонтрагентРТУ;
	ДокОбъектРТУ.Партнер = СтруктураЗаполнения.ПартнерРТУ;
	ДокОбъектРТУ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту;
	ДокОбъектРТУ.СпособДоставки = Перечисления.СпособыДоставки.Самовывоз;
	ДокОбъектРТУ.Комментарий = "#Создано на основании перемещения из-за разницы Организации в Приобретении " + СтрокаТЧ.Номер + " " + СтрокаТЧ.Дата;
	ДокОбъектРТУ.MRS_Перемещение = СтрокаТЧ.Ссылка;
	
	
	ТаблицаДоговоров = СтруктураЗаполнения.ТаблицаДоговоров;
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", СтруктураЗаполнения.ОрганизацияОтправитель);
	Отбор.Вставить("Контрагент", СтруктураЗаполнения.КонтрагентРТУ);
	Отбор.Вставить("ТипДоговора", ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПокупателем")); 
	МассивДоговоров = ТаблицаДоговоров.НайтиСтроки(Отбор);
	Если МассивДоговоров = Неопределено ИЛИ МассивДоговоров.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не смогли получить договор при создании документа РТУ по Организации " + СтруктураЗаполнения.ОрганизацияОтправитель + " и Контрагенту " + СтруктураЗаполнения.КонтрагентРТУ + " тип договора " + 
		ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПокупателем"));
		ВызватьИсключение "Операция создания документов прервана!";
	Иначе
		Договор = МассивДоговоров[0].Договор;
		ДокОбъектРТУ.Договор = Договор;
	КонецЕсли;
	
	ДокОбъектРТУ.Автор = Пользователи.ТекущийПользователь();
	ДокОбъектРТУ.ЦенаВключаетНДС = Ложь; 
	ДокОбъектРТУ.Подразделение = СтрокаТЧ.ПодразделениеОтправитель;
	ДокОбъектРТУ.Менеджер = Пользователи.ТекущийПользователь();
	
	ДокОбъектРТУ.ДополнительныеСвойства.Вставить("НеобходимостьЗаполненияКассыПриФОИспользоватьНесколькоКассЛожь", Ложь);
	ДокОбъектРТУ.ДополнительныеСвойства.Вставить("НеобходимостьЗаполненияСчетаПриФОИспользоватьНесколькоСчетовЛожь", Ложь);
	
	ЕстьКорректировки = Ложь;
	
	//Параметры печати
	ОтветственныеЛица = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(СтруктураЗаполнения.ОрганизацияОтправитель,ТекущаяДата());
	ДокОбъектРТУ.ГлавныйБухгалтер = ОтветственныеЛица.ГлавныйБухгалтерСсылка;
	ДокОбъектРТУ.Руководитель = ОтветственныеЛица.РуководительСсылка;
	//в перемещении это спр.пользователи. В физлицах может не быть 
	ОтпустилФЛ = Справочники.ФизическиеЛица.НайтиПоНаименованию(СтрокаТЧ.Автор);
	ДокОбъектРТУ.Отпустил = ОтпустилФЛ;
	
	СтруктураПараметровБанковскогоСчета = Новый Структура;
	СтруктураПараметровБанковскогоСчета = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
	СтруктураПараметровБанковскогоСчета.Организация = СтруктураЗаполнения.ОрганизацияОтправитель;
	ДокОбъектРТУ.БанковскийСчетОрганизации = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметровБанковскогоСчета); 
	ДокОбъектРТУ.БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(СтруктураЗаполнения.КонтрагентРТУ, , ДокОбъектРТУ.БанковскийСчетКонтрагента);
	
	Если Не ЗначениеЗаполнено(ДокОбъектРТУ.Валюта) Тогда
		
		ДокОбъектРТУ.Валюта = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДокОбъектРТУ.Организация);
		
	КонецЕсли;
	
	ПараметрыЗаполнения = Документы.РеализацияТоваровУслуг.ПараметрыЗаполненияНалогообложенияНДСПродажи(ДокОбъектРТУ);
	УчетНДСУП.ЗаполнитьНалогообложениеНДСПродажи(ДокОбъектРТУ.НалогообложениеНДС, ПараметрыЗаполнения);
	РеализацияТоваровУслугЛокализация.ОбработкаЗаполнения(ДокОбъектРТУ, СтрокаТЧ.Ссылка, Ложь);
	ВзаиморасчетыСервер.ОбработкаЗаполнения(ДокОбъектРТУ, СтрокаТЧ.Ссылка);
	
КонецПроцедуры

// Инициализация реквизитов документа ПриобретениеТоваровУслуг
//
// Параметры:
//  СтрокаТЧ - Структура - данные документа перемещения
//  СтруктураЗаполнения - Структура - структура с данными для заполнения
//  ДокОбъектПТУ - ДокументОбъект.ПриобретениеТоваровУслуг - заполняемый документ
//  ДокОбъектРТУ - ДокументОбъект.РеализацияТоваровУслуг - связанный документ РТУ
//  ДокОбъектСФ - Неопределено - не используется в текущей реализации
//  ДатаДокументов - Дата - дата создаваемого документа
//
&НаСервере
Процедура ИнициализироватьДокументПТУ(СтрокаТЧ, СтруктураЗаполнения, ДокОбъектПТУ, ДокОбъектРТУ, ДокОбъектСФ, ДатаДокументов)
	
	ДокОбъектПТУ.Дата = ДатаДокументов;
	ДокОбъектПТУ.Организация = СтруктураЗаполнения.ОрганизацияПолучатель;
	ДокОбъектПТУ.Подразделение = СтрокаТЧ.ПодразделениеПолучатель;
	ДокОбъектПТУ.Склад = СтруктураЗаполнения.СкладПолучатель;
	ДокОбъектПТУ.Контрагент = СтруктураЗаполнения.КонтрагентПТУ;
	ДокОбъектПТУ.Партнер = СтруктураЗаполнения.ПартнерПТУ;
	
	ТаблицаДоговоров = СтруктураЗаполнения.ТаблицаДоговоров;
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", СтруктураЗаполнения.ОрганизацияПолучатель);
	Отбор.Вставить("Контрагент", СтруктураЗаполнения.КонтрагентПТУ);
	Отбор.Вставить("ТипДоговора", ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПоставщиком"));
	
	МассивДоговоров = ТаблицаДоговоров.НайтиСтроки(Отбор);
	Если МассивДоговоров = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не смогли получить договор при создании документа РТУ по Организации " + СтруктураЗаполнения.ОрганизацияПолучатель + " и Контрагенту " + СтруктураЗаполнения.КонтрагентПТУ + " тип договора " + 
		ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПоставщиком"));
		ВызватьИсключение "Операция создания документов прервана!";
	Иначе
		Договор = МассивДоговоров[0].Договор;
		ДокОбъектПТУ.Договор = Договор;
	КонецЕсли;
	
	ДокОбъектПТУ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика;
	ДокОбъектПТУ.СпособДоставки = Перечисления.СпособыДоставки.СиламиПоставщикаДоНашегоСклада;
	ДокОбъектПТУ.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным;
	
	Если ДокОбъектСФ <> Неопределено Тогда
		ДокОбъектПТУ.НомерВходящегоДокумента = ДокОбъектСФ.Номер;
		ДокОбъектПТУ.ДатаВходящегоДокумента = ДокОбъектСФ.Дата;
	КонецЕсли;
	
	ДокОбъектПТУ.Комментарий = "#Создано на основании перемещения из-за разницы Организации в Приобретении " + СтрокаТЧ.Номер + " " + СтрокаТЧ.Дата;
	ДокОбъектПТУ.MRS_Перемещение = СтрокаТЧ.Ссылка;
	ДокОбъектПТУ.MRS_Реализация = ДокОбъектРТУ.Ссылка;
	ДокОбъектПТУ.Автор = Пользователи.ТекущийПользователь();
	ДокОбъектПТУ.Менеджер                  = Пользователи.ТекущийПользователь();
	ДокОбъектПТУ.ЦенаВключаетНДС = Ложь; 
	
	Если НЕ ЗначениеЗаполнено(ДокОбъектПТУ.Валюта) Тогда
		ДокОбъектПТУ.Валюта = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДокОбъектПТУ.Организация);
	КонецЕсли;
	
	СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
	СтруктураПараметров.Организация    		= ДокОбъектПТУ.Организация;
	СтруктураПараметров.БанковскийСчет		= ДокОбъектПТУ.БанковскийСчетОрганизации;
	ДокОбъектПТУ.БанковскийСчетОрганизации = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);
	
	ДокОбъектПТУ.БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(ДокОбъектПТУ.Контрагент, , ДокОбъектПТУ.БанковскийСчетКонтрагента);
	
	СтруктураОтветственного = ЗакупкиСервер.ПолучитьОтветственногоПоСкладу(ДокОбъектПТУ.Склад, ДокОбъектПТУ.Менеджер);
	
	Если СтруктураОтветственного <> Неопределено Тогда
		
		ДокОбъектПТУ.Принял = СтруктураОтветственного.Ответственный;
		ДокОбъектПТУ.ПринялДолжность = СтруктураОтветственного.ОтветственныйДолжность;
		
	КонецЕсли;
	
	ПараметрыЗаполнения = Документы.ПриобретениеТоваровУслуг.ПараметрыЗаполненияНалогообложенияНДСЗакупки(ДокОбъектПТУ);
	УчетНДСУП.ЗаполнитьНалогообложениеНДСЗакупки(ДокОбъектПТУ.НалогообложениеНДС, ПараметрыЗаполнения);
	
	ПараметрыЗаполнения = Документы.ПриобретениеТоваровУслуг.ПараметрыЗаполненияВидаДеятельностиНДС(ДокОбъектПТУ);
	УчетНДСУП.ЗаполнитьВидДеятельностиНДС(ДокОбъектПТУ.ЗакупкаПодДеятельность, ПараметрыЗаполнения);
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВТЧ(ДокОбъектПТУ);
	СтруктураЗаполненияСтавкиНДС = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияСтавкиНДС(ДокОбъектПТУ);
	СтруктураЗаполненияСтавкиНДС.ИнициализацияВходящегоДокумента = Истина;
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	ДокОбъектПТУ.ДополнительныеСвойства.Вставить("НеобходимостьЗаполненияСчетаПриФОИспользоватьНесколькоСчетовЛожь", Ложь);
	
	ПараметрыВыбораСтатейИАналитик = Документы.ПриобретениеТоваровУслуг.ПараметрыВыбораСтатейИАналитик(ДокОбъектПТУ.ХозяйственнаяОперация);
	ДоходыИРасходыСервер.ОбработкаЗаполнения(ДокОбъектПТУ, ПараметрыВыбораСтатейИАналитик);
	
	ПриобретениеТоваровУслугЛокализация.ОбработкаЗаполнения(ДокОбъектПТУ, СтрокаТЧ.Ссылка, Ложь);
	
	ВзаиморасчетыСервер.ОбработкаЗаполнения(ДокОбъектПТУ, СтрокаТЧ.Ссылка);
	
	СкладГруппа = Справочники.Склады.ЭтоГруппаИСкладыИспользуютсяВТЧДокументовЗакупки(ДокОбъектПТУ.Склад);
	СкладыСервер.ЗаполнитьСкладыВТабличнойЧасти(ДокОбъектПТУ.Склад, СкладГруппа, ДокОбъектПТУ.Товары, Ложь);
	
	//Данные печати
	ОтветственныеЛица = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(СтруктураЗаполнения.ОрганизацияПолучатель,ТекущаяДата());
	ДокОбъектПТУ.ГлавныйБухгалтер = ОтветственныеЛица.ГлавныйБухгалтерСсылка;
	ДокОбъектПТУ.Руководитель = ОтветственныеЛица.РуководительСсылка;
	
КонецПроцедуры

// Получение таблицы товаров из документа перемещения с данными организаций и складов
//
// Параметры:
//  Ссылка - ДокументСсылка.ПеремещениеТоваров - ссылка на документ перемещения
//
// Возвращаемое значение:
//  Структура - структура с данными перемещения:
//    * СкладОтправитель - СправочникСсылка.Склады
//    * СкладПолучатель - СправочникСсылка.Склады
//    * ОрганизацияОтправитель - СправочникСсылка.Организации
//    * ОрганизацияПолучатель - СправочникСсылка.Организации
//    * КонтрагентРТУ - СправочникСсылка.Контрагенты
//    * КонтрагентПТУ - СправочникСсылка.Контрагенты
//    * ПартнерРТУ - СправочникСсылка.Партнеры
//    * ПартнерПТУ - СправочникСсылка.Партнеры
//    * ТаблицаТоваров - ТаблицаЗначений - товары с ценами
//    * ТаблицаДоговоров - ТаблицаЗначений - договоры
//    * Проведен - Булево
//    * Ссылка - ДокументСсылка.ПеремещениеТоваров
//
&НаСервере
Функция ПолучитьТаблицуТоваровИзПермещения(Ссылка)
	
	СтруктураВозврата = Новый Структура;
	
	Текст = "ВЫБРАТЬ
	        |	ПеремещениеТоваров.СкладОтправитель КАК СкладОтправитель,
	        |	ПеремещениеТоваров.СкладПолучатель КАК СкладПолучатель,
	        |	ПеремещениеТоваров.СкладОтправитель.ПЛ_Организация КАК СкладОтправительПЛ_Организация,
	        |	ПеремещениеТоваров.СкладПолучатель.ПЛ_Организация КАК СкладПолучательПЛ_Организация,
	        |	ПеремещениеТоваров.Проведен КАК Проведен,
	        |	ПеремещениеТоваров.Ссылка КАК Ссылка,
	        |	ПеремещениеТоваров.СкладПолучатель.УчетныйВидЦены КАК УчетныйВидЦены
	        |ИЗ
	        |	Документ.ПеремещениеТоваров КАК ПеремещениеТоваров
	        |ГДЕ
	        |	ПеремещениеТоваров.Ссылка = &Ссылка
	        |	И НЕ ПеремещениеТоваров.ПометкаУдаления
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ПеремещениеТоваровТовары.Номенклатура КАК Номенклатура,
	        |	СУММА(ПеремещениеТоваровТовары.Количество) КАК Количество,
	        |	СУММА(ПеремещениеТоваровТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
	        |	0 КАК Цена,
	        |	ПеремещениеТоваровТовары.Ссылка.СкладПолучатель.УчетныйВидЦены КАК УчетныйВидЦены
	        |ПОМЕСТИТЬ ВТ_Номенклатура
	        |ИЗ
	        |	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
	        |ГДЕ
	        |	ПеремещениеТоваровТовары.Ссылка = &Ссылка
	        |	И НЕ ПеремещениеТоваровТовары.Ссылка.ПометкаУдаления
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ПеремещениеТоваровТовары.Номенклатура,
	        |	ПеремещениеТоваровТовары.Ссылка.СкладПолучатель.УчетныйВидЦены
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_Номенклатура.Номенклатура КАК Номенклатура,
	        |	ВТ_Номенклатура.Количество КАК Количество,
	        |	ВТ_Номенклатура.КоличествоУпаковок КАК КоличествоУпаковок,
	        |	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена + ЦеныНоменклатурыСрезПоследних.Цена * 0.05, 0) КАК Цена
	        |ПОМЕСТИТЬ ВТ_Цена
	        |ИЗ
	        |	ВТ_Номенклатура КАК ВТ_Номенклатура
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ТекущаяДата, ) КАК ЦеныНоменклатурыСрезПоследних
	        |		ПО ВТ_Номенклатура.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	        |			И ВТ_Номенклатура.УчетныйВидЦены = ЦеныНоменклатурыСрезПоследних.ВидЦены
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВТ_Номенклатура.Номенклатура,
	        |	ВТ_Номенклатура.Количество,
	        |	ВТ_Номенклатура.КоличествоУпаковок,
	        |	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена + ЦеныНоменклатурыСрезПоследних.Цена * 0.05, 0)
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВЫБОР
	        |		КОГДА ВТ_Цена.Цена <> 0
	        |			ТОГДА ВТ_Цена.Номенклатура
	        |		ИНАЧЕ ВТ_Номенклатура.Номенклатура
	        |	КОНЕЦ КАК Номенклатура,
	        |	ВТ_Номенклатура.Количество КАК Количество,
	        |	ВТ_Номенклатура.КоличествоУпаковок КАК КоличествоУпаковок,
	        |	ВЫБОР
	        |		КОГДА ВТ_Цена.Цена <> 0
	        |			ТОГДА ВТ_Цена.Цена
	        |		ИНАЧЕ 0
	        |	КОНЕЦ КАК Цена
	        |ИЗ
	        |	ВТ_Номенклатура КАК ВТ_Номенклатура
	        |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Цена КАК ВТ_Цена
	        |		ПО ВТ_Номенклатура.Номенклатура = ВТ_Цена.Номенклатура";

	Запрос = Новый Запрос(Текст);
	
	
	ТекущаяДата = КонецДня(ТекущаяДата());
	
	// Установка параметров.

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата);
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ВыборкаШапка = ПакетЗапросов[0].Выбрать();
	
	Пока ВыборкаШапка.Следующий() Цикл
		
		ОрганизацияПолучатель = ВыборкаШапка.СкладПолучательПЛ_Организация;
		ОрганизацияОтправитель = ВыборкаШапка.СкладОтправительПЛ_Организация;
		
		СтруктураВозврата.Вставить("СкладОтправитель", ВыборкаШапка.СкладОтправитель);
		СтруктураВозврата.Вставить("СкладПолучатель", ВыборкаШапка.СкладПолучатель);
		СтруктураВозврата.Вставить("ОрганизацияОтправитель", ОрганизацияОтправитель);
		СтруктураВозврата.Вставить("ОрганизацияПолучатель", ОрганизацияПолучатель);
		СтруктураВозврата.Вставить("Проведен", ВыборкаШапка.Проведен);
		СтруктураВозврата.Вставить("Ссылка", ВыборкаШапка.Ссылка);
		
	КонецЦикла;
	
	ЗначениеДопРеквизитаОрганизацияПолучатель = УправлениеСвойствами.ЗначениеСвойства(ОрганизацияПолучатель,"MRS_СоответсвиеОрганизацииКонтрагента");
	
	Если ЗначениеЗаполнено(ОрганизацияПолучатель) И НЕ ЗначениеЗаполнено(ЗначениеДопРеквизитаОрганизацияПолучатель) Тогда
		ТекстОшибки = НСтр("ru = 'Не настроено соответствие организации контрагента у '") + ОрганизацияПолучатель.Наименование;
		ДляАдминистратора = НСтр("ru = 'Не заполнен реквизит  MRS_СоответсвиеОрганизацииКонтрагента у :'") + " " + ОрганизацияПолучатель.Наименование;
		ВызватьИсключение(ТекстОшибки, КатегорияОшибки.НарушениеПравДоступа,, ДляАдминистратора);
	ИначеЕсли НЕ ЗначениеЗаполнено(ОрганизацияПолучатель) Тогда
		ТекстОшибки = НСтр("ru = 'Не заполнено поле Организация получатель'");
		ДляАдминистратора = НСтр("ru = 'Не заполнено поле Организация получатель:'");
		ВызватьИсключение(ТекстОшибки, КатегорияОшибки.НарушениеПравДоступа,, ДляАдминистратора);
	КонецЕсли;
	
	ЗначениеДопРеквизитаОрганизацияОтправитель = УправлениеСвойствами.ЗначениеСвойства(ОрганизацияОтправитель,"MRS_СоответсвиеОрганизацииКонтрагента");
	
	Если ЗначениеЗаполнено(ОрганизацияОтправитель) И НЕ ЗначениеЗаполнено(ЗначениеДопРеквизитаОрганизацияОтправитель) Тогда
		ТекстОшибки = НСтр("ru = 'Не настроено соответствие организации контрагента у '") + ОрганизацияОтправитель.Наименование;
		ДляАдминистратора = НСтр("ru = 'Не заполнен реквизит  MRS_СоответсвиеОрганизацииКонтрагента у :'") + " " + ОрганизацияОтправитель.Наименование;
		ВызватьИсключение(ТекстОшибки, КатегорияОшибки.НарушениеПравДоступа,, ДляАдминистратора);
	ИначеЕсли НЕ ЗначениеЗаполнено(ОрганизацияОтправитель) Тогда
		ТекстОшибки = НСтр("ru = 'Не заполнено поле Организация Отправитель'");
		ДляАдминистратора = НСтр("ru = 'Не заполнено поле Организация Отправитель:'");
		ВызватьИсключение(ТекстОшибки, КатегорияОшибки.НарушениеПравДоступа,, ДляАдминистратора);
	КонецЕсли;
	
	СтруктураВозврата.Вставить("КонтрагентРТУ", ЗначениеДопРеквизитаОрганизацияПолучатель);
	СтруктураВозврата.Вставить("ПартнерРТУ", ЗначениеДопРеквизитаОрганизацияПолучатель.Партнер);
	
	СтруктураВозврата.Вставить("КонтрагентПТУ", ЗначениеДопРеквизитаОрганизацияОтправитель);
	СтруктураВозврата.Вставить("ПартнерПТУ", ЗначениеДопРеквизитаОрганизацияОтправитель.Партнер);
	
	СтруктураВозврата.Вставить("ТаблицаТоваров", ПакетЗапросов[3].Выгрузить());
	
	//(Дополнение структуры по разделению дороворов)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	MRS_СвязиДоговоровИОрганизацийСрезПоследних.Организация КАК Организация,
	|	MRS_СвязиДоговоровИОрганизацийСрезПоследних.Контрагент КАК Контрагент,
	|	MRS_СвязиДоговоровИОрганизацийСрезПоследних.ТипДоговора КАК ТипДоговора,
	|	MRS_СвязиДоговоровИОрганизацийСрезПоследних.Договор КАК Договор
	|ИЗ
	|	РегистрСведений.MRS_СвязиДоговоровИОрганизаций.СрезПоследних КАК MRS_СвязиДоговоровИОрганизацийСрезПоследних
	|ГДЕ
	|	MRS_СвязиДоговоровИОрганизацийСрезПоследних.Организация = &ОрганизацияОтправитель
	|	И MRS_СвязиДоговоровИОрганизацийСрезПоследних.Контрагент = &КонтрагентРТУ
	|	И MRS_СвязиДоговоровИОрганизацийСрезПоследних.ТипДоговора В (ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПокупателем))
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	MRS_СвязиДоговоровИОрганизацийСрезПоследних.Организация,
	|	MRS_СвязиДоговоровИОрганизацийСрезПоследних.Контрагент,
	|	MRS_СвязиДоговоровИОрганизацийСрезПоследних.ТипДоговора,
	|	MRS_СвязиДоговоровИОрганизацийСрезПоследних.Договор
	|ИЗ
	|	РегистрСведений.MRS_СвязиДоговоровИОрганизаций.СрезПоследних КАК MRS_СвязиДоговоровИОрганизацийСрезПоследних
	|ГДЕ
	|	MRS_СвязиДоговоровИОрганизацийСрезПоследних.Организация = &ОрганизацияПолучатель
	|	И MRS_СвязиДоговоровИОрганизацийСрезПоследних.Контрагент = &КонтрагентПТУ
	|	И MRS_СвязиДоговоровИОрганизацийСрезПоследних.ТипДоговора В (ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПоставщиком))";
	
	Запрос.УстановитьПараметр("КонтрагентРТУ", СтруктураВозврата.КонтрагентРТУ);
	Запрос.УстановитьПараметр("ОрганизацияОтправитель", СтруктураВозврата.ОрганизацияОтправитель);
	Запрос.УстановитьПараметр("КонтрагентПТУ", СтруктураВозврата.КонтрагентПТУ);
	Запрос.УстановитьПараметр("ОрганизацияПолучатель", СтруктураВозврата.ОрганизацияПолучатель);
	
	ТаблицаДоговоров = Запрос.Выполнить().Выгрузить();
	СтруктураВозврата.Вставить("ТаблицаДоговоров", ТаблицаДоговоров);
	
	ИсхТабТоваров = СтруктураВозврата.ТаблицаТоваров;
	
		
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти
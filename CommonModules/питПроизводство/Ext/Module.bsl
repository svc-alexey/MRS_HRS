// Получает цену заготовки по ценам ингредиентов.
//
// Параметры:
//	Рецептура - Рецептура передела.
//	ВидЦены - Тип цен, по которому рассчитываем себестоимость.
//	Дата - Дата цены
//	Валюта - Валюта цены  
// Возвращаемое значение:
//	Число - стоимость передела в валюте Валюта.
//
Функция ПолучитьСтоимостьПеределаПоРецептуре(Рецептура, 
	ВидЦены,
	Дата, 
	Валюта, 
	КэшЦен = Неопределено, 
	ТекстПредупреждения = Неопределено, 
	НеИспользоватьСезонныеПроценты = Истина, 
	КэшЗаполненияДерева = Неопределено, 
	УточненияАвтовыбора = Неопределено, 
	СкладДляРасчетаЦены = Неопределено, 
	РазворачиватьДоИнгредиентов, 
	ГлубинаЦикла = 0, 
	РассчетПоСебестоимостиСтруктура = Неопределено) Экспорт
	
	// << MRS #Ошибка данных ДенисламовАД <Adel.Denislamov@mriyaresort.com> 09.06.25, (Исключение зацикливания рекурсии, когда рецептуру вставили самой себе в ингридиенты)
	Если ГлубинаЦикла = 20 Тогда
		Возврат 0;
	КонецЕсли;
	// >> MRS #Ошибка данных ДенисламовАД <Adel.Denislamov@mriyaresort.com> 09.06.25, ()
	СтруктураДанные = Новый Структура("Рецептура,ВидЦены,Дата,Валюта",Рецептура,ВидЦены,Дата,Валюта);
	Если КэшЦен = Неопределено Тогда
		КэшЦен = ИнициализироватьТаблицуКэшаЦен();
	Иначе
		НайденныйКэш = КэшЦен.НайтиСтроки(СтруктураДанные);
		Если НайденныйКэш.Количество() Тогда
			Возврат НайденныйКэш[0].Цена;
		КонецЕсли;
	КонецЕсли;
	
	Если ТекстПредупреждения = Неопределено Тогда
		ТекстПредупреждения = "";
	КонецЕсли;
	
	Если КэшЗаполненияДерева = Неопределено Тогда
		// Служебная структура, хранящая кэшированные значения, уменбшает общее число запросов 
		//  к базе во время заполнения дерева состава, вычисления сезонных процентов и т.д.
		КэшЗаполненияДерева = питПроизводство.ИнициализироватьСтруктуруКэшированныхЗначенийДляЗаполненияДереваСостава();
	КонецЕсли;
	
	РецептураРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Рецептура, "Количество,КоличествоУпаковок");
	
	ДеревоСостава = РассчитатьДеревоСоставаРецептуры(Рецептура,,РецептураРеквизиты.Количество,,,РецептураРеквизиты.КоличествоУпаковок,,,,,,,,,,,,,,,,,,,УточненияАвтовыбора);	
	
	СтрокиДерева = ДеревоСостава.Строки[0].Строки;
	Стоимость = 0;
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		Цена = 0;
		
		//+АвтовыборНоменклатуры
		Если СтрокаДерева.ОшибкаАвтовыбора Тогда
			ТекстПредупреждения = ТекстПредупреждения 
			+ Символы.ПС + " - " + ?(ПустаяСтрока(ТекстПредупреждения),"",", ") 
			+ "ошибка уточнения характеристики/ингредиента по рецептуре <" + ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рецептура, "Наименование") + ">";
			Продолжить;
		КонецЕсли;
		//-АвтовыборНоменклатуры
		
		Если ЗначениеЗаполнено(СтрокаДерева.Рецептура) Тогда
			// << MRS #Ошибка данных ДенисламовАД <Adel.Denislamov@mriyaresort.com> 09.06.25, (Исключение зацикливания рекурсии, когда рецептуру вставили самой себе в ингридиенты)
			//Цена = ПолучитьСтоимостьПеределаПоРецептуре(СтрокаДерева.Рецептура, ВидЦены, Дата, Валюта, КэшЦен,, НеИспользоватьСезонныеПроценты, КэшЗаполненияДерева, УточненияАвтовыбора, СкладДляРасчетаЦены, РазворачиватьДоИнгредиентов);
			Цена = ПолучитьСтоимостьПеределаПоРецептуре(СтрокаДерева.Рецептура, ВидЦены, Дата, Валюта, КэшЦен,, НеИспользоватьСезонныеПроценты, КэшЗаполненияДерева, УточненияАвтовыбора, СкладДляРасчетаЦены, РазворачиватьДоИнгредиентов, ГлубинаЦикла + 1, РассчетПоСебестоимостиСтруктура);
			// >> MRS #Ошибка данных ДенисламовАД <Adel.Denislamov@mriyaresort.com> 09.06.25, ()
			Цена = Цена / (СтрокаДерева.Рецептура.Количество);
		КонецЕсли;
		Если Цена = 0  Тогда
			Цена = ПолучитьСтоимостьИнгредиента(СтрокаДерева.Номенклатура, ВидЦены, Дата, Валюта,,,,,Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка(),СтрокаДерева.Характеристика, РассчетПоСебестоимостиСтруктура);
			Если Цена = 0 И ТекстПредупреждения <> Неопределено Тогда
				ТекстПредупреждения = ТекстПредупреждения 
				+ ?(ПустаяСтрока(ТекстПредупреждения),"",", " + Символы.ПС) 
				+ "<"
				+ СтрокаДерева.Номенклатура
				+ ?(ЗначениеЗаполнено(СтрокаДерева.Характеристика),"; "+СтрокаДерева.Характеристика,"")
				+ "> по причине: не удалось определить цену ингредиента";
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ НеИспользоватьСезонныеПроценты Тогда
			СезонныйПроцент = питЛицензированиеСервер.ПолучитьСезонныйПроцент(СтрокаДерева.Номенклатура, Дата, КэшЗаполненияДерева.КэшСезонныхПроцентов);
			СезонныйКоэффициент = 1+СезонныйПроцент/100;
		Иначе
			СезонныйКоэффициент = 1;
		КонецЕсли;
		
		Стоимость = Стоимость + Цена * Окр(СтрокаДерева.Количество * СезонныйКоэффициент, 3);
	КонецЦикла;
	
	СтрокаКэш = КэшЦен.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаКэш,СтруктураДанные);
	СтрокаКэш.Цена = Стоимость;
	
	Возврат Стоимость;
КонецФункции

// Получает ингредиентный состав переданной рецептуры.
//
// Параметры:
//  Рецептура			 - ДокументСсылка.питРецептура - Рецептура передела.
//  ТекстПредупреждения	 - Строка - Строка, если были какие-то косяки.
//  КэшЗаполненияДерева	 - Структура - Служебная структура, хранящая кэшированные значения.
//  УточненияАвтовыбора	 - ТаблицаЗначений - Таблица уточнений автовыбора.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Ингредиентный состав рецептуры.
//
Функция ПолучитьИнгредиентыПоРецептуре(Рецептура, ТекстПредупреждения = Неопределено, КэшЗаполненияДерева = Неопределено, УточненияАвтовыбора = Неопределено, 
	КолонкиСвертки = Неопределено) Экспорт
	
	Если КолонкиСвертки = Неопределено Тогда
		КолонкиСвертки = "Номенклатура, Характеристика, ЕдиницаИзмерения";
	КонецЕсли;
	
	// Таблица ингредиентов.
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Результат.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Результат.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
	Результат.Колонки.Добавить("КоличествоУпаковок", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	
	Если НЕ ЗначениеЗаполнено(Рецептура) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ТекстПредупреждения = Неопределено Тогда
		ТекстПредупреждения = "";
	КонецЕсли;
	
	Если КэшЗаполненияДерева = Неопределено Тогда
		// Служебная структура, хранящая кэшированные значения, уменбшает общее число запросов 
		//  к базе во время заполнения дерева состава, вычисления сезонных процентов и т.д.
		КэшЗаполненияДерева = питПроизводство.ИнициализироватьСтруктуруКэшированныхЗначенийДляЗаполненияДереваСостава();
	КонецЕсли;
	
	РецептураРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Рецептура, "Количество,КоличествоУпаковок");
	
	ДеревоСостава = РассчитатьДеревоСоставаРецептуры(Рецептура,,РецептураРеквизиты.Количество,,,РецептураРеквизиты.КоличествоУпаковок,,,,,,,,,,,,,,,,,,,УточненияАвтовыбора);	
	
	СтрокиДерева = ДеревоСостава.Строки[0].Строки;
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		//+АвтовыборНоменклатуры
		Если СтрокаДерева.ОшибкаАвтовыбора Тогда
			ТекстПредупреждения = ТекстПредупреждения 
			+ Символы.ПС + " - " + ?(ПустаяСтрока(ТекстПредупреждения),"",", ") 
			+ "ошибка уточнения характеристики/ингредиента по рецептуре <" + ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Рецептура, "Наименование") + ">";
			Продолжить;
		КонецЕсли;
		//-АвтовыборНоменклатуры
		
		Если ЗначениеЗаполнено(СтрокаДерева.Рецептура) Тогда
			СоставПередела = ПолучитьИнгредиентыПоРецептуре(СтрокаДерева.Рецептура, ТекстПредупреждения, КэшЗаполненияДерева, УточненияАвтовыбора);
			Для каждого ТекСтрокаСостава Из СоставПередела Цикл
				НоваяСтрока = Результат.Добавить();
				НоваяСтрока.Номенклатура = ТекСтрокаСостава.Номенклатура;
				НоваяСтрока.Характеристика = ТекСтрокаСостава.Характеристика;
				НоваяСтрока.ЕдиницаИзмерения = ТекСтрокаСостава.ЕдиницаИзмерения;
				НоваяСтрока.Характеристика = ТекСтрокаСостава.Характеристика;
			КонецЦикла;
		Иначе
			НоваяСтрока = Результат.Добавить();
			НоваяСтрока.Номенклатура = СтрокаДерева.Номенклатура;
			НоваяСтрока.Характеристика = СтрокаДерева.Характеристика;
			НоваяСтрока.ЕдиницаИзмерения = СтрокаДерева.ЕдиницаИзмерения;
			НоваяСтрока.КоличествоУпаковок = СтрокаДерева.КоличествоУпаковок;
		КонецЕсли;
		
	КонецЦикла;
	
	Результат.Свернуть(КолонкиСвертки, "КоличествоУпаковок");
	
	Возврат Результат;
КонецФункции

// Функция получения сезонных коэффициентов для списка номенклатуры.
//
// Параметры:
//  СписокНоменклатуры - Номенклатура, для которой получаем сезонный коэффициент
//  МоментВремени - Дата, на которую получается сезонный коэффициент
//  СезонныйПроцентВместоПотерьХО - Булево, режим использования сезонных процентов вместо потерь ХО
//
// Возвращаемое значение:
//  Соответствие - соответсвие номенклатуры и сезонных коэффициентов
//
Функция ПолучитьСезонныеКоэффициентыСпискаНоменклатуры(СписокНоменклатуры, 
	МоментВремени, 
	СезонныйПроцентВместоПотерьХО) Экспорт
	
	Результат = Новый Соответствие;
	
	Если ТипЗнч(СписокНоменклатуры) = Тип("Массив") Тогда
		СписокБезПустыхЗначений = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(СписокНоменклатуры);
		СписокБезПустыхЗначений = ОбщегоНазначенияУТ.УдалитьПовторяющиесяЭлементыМассива(СписокБезПустыхЗначений);
	ИначеЕсли ТипЗнч(СписокНоменклатуры) = Тип("СправочникСсылка.Номенклатура") Тогда
		СписокБезПустыхЗначений = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СписокНоменклатуры);
	Иначе
		Возврат Результат;
	КонецЕсли;
	питОбщегоНазначенияКлиентСервер.УдалитьНеЗаполненныеЭлементыМассива(СписокБезПустыхЗначений);
	
	Если ТипЗнч(МоментВремени) = Тип("МоментВремени") Тогда
		// В регистре даты записаны без времени.
		ДатаСезона = НачалоДня(МоментВремени.Дата);
	Иначе
		ДатаСезона = НачалоДня(МоментВремени);
	КонецЕсли;
	
	// Попытаемся найти процент, содержащий в составе год.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СезонныеПроценты.Номенклатура КАК Номенклатура,
	|	СезонныеПроценты.Процент КАК СезонныйПроцент,
	|	СезонныеПроценты.ИспользоватьГод КАК ИспользоватьГод,
	|	ВЫРАЗИТЬ(СезонныеПроценты.НачалоСезона КАК СТРОКА) КАК НачалоСезона,
	|	ВЫРАЗИТЬ(СезонныеПроценты.КонецСезона КАК СТРОКА) КАК КонецСезона
	|ИЗ
	|	РегистрСведений.питСезонныеПроценты КАК СезонныеПроценты
	|ГДЕ
	|	СезонныеПроценты.Номенклатура В(&Номенклатура)
	|	И (СезонныеПроценты.ИспользоватьГод
	|				И (ВЫРАЗИТЬ(СезонныеПроценты.НачалоСезона КАК ДАТА)) <= &ДатаСезона
	|				И (ВЫРАЗИТЬ(СезонныеПроценты.КонецСезона КАК ДАТА)) >= &ДатаСезона
	|			ИЛИ НЕ СезонныеПроценты.ИспользоватьГод)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СезонныеПроценты.Номенклатура,
	|	СезонныеПроценты.ИспользоватьГод УБЫВ,
	|	СезонныеПроценты.НачалоСезона";
	
	Запрос.УстановитьПараметр("ДатаСезона",   ДатаСезона);
	Если СписокБезПустыхЗначений.Количество() > 0 Тогда
		Запрос.УстановитьПараметр("Номенклатура", СписокБезПустыхЗначений);
	Иначе
		// Список номенклатуры не указан, значит собираем все сезонные проценты
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СезонныеПроценты.Номенклатура В(&Номенклатура)", "Истина");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат ПолучитьСезонныеКоэффициентыПоВыборкеЗапроса(Выборка, ДатаСезона, СезонныйПроцентВместоПотерьХО);
	
КонецФункции

// Функция расчета сезонных коэффициентов по выборке запроса.
//
// Параметры:
//  Выборка - Выборка запроса, данные из регистра СезонныеПроценты
//  ДатаСезона - Дата, на которую получается сезонный коэффициент
//  СезонныйПроцентВместоПотерьХО - Булево, режим использования сезонных процентов вместо потерь ХО
//
// Возвращаемое значение:
//  Соответсвие номенклатуры и сезонных коэффициентов
//
Функция ПолучитьСезонныеКоэффициентыПоВыборкеЗапроса(Выборка, ДатаСезона, СезонныйПроцентВместоПотерьХО)
	
	Результат = Новый Соответствие;
	
	СтрокаСезона = Формат(ДатаСезона, "ДФ=MMdd; ДП=0101");
	
	Если СезонныйПроцентВместоПотерьХО = Неопределено Тогда
		ВызватьИсключение "При вызове функции ПолучитьСезонныеКоэффициентыПоВыборкеЗапроса не заполнен обязательный параметр СезонныйПроцентВместоПотерьХО.";
	КонецЕсли;
	
	НоменклатураНайденПроцент = Неопределено;
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Номенклатура = НоменклатураНайденПроцент Тогда
			// Для этой номенклатуры уже был найден процент.
			Продолжить;
		КонецЕсли;
		
		Если НЕ Выборка.ИспользоватьГод Тогда
			
			Если НЕ (ЗначениеЗаполнено(Выборка.НачалоСезона) И ЗначениеЗаполнено(Выборка.КонецСезона)) Тогда
				// Ошибка данных, период сезона не заполнен.
				Продолжить;
			КонецЕсли;
			
			// Переставить местами месяц и число, т.е. с начала будет месяц, потом число.
			НачалаСезона = Сред(Выборка.НачалоСезона,3,2) + Лев(Выборка.НачалоСезона,2);
			КонецСезона  = Сред(Выборка.КонецСезона,3,2)  + Лев(Выборка.КонецСезона,2);
			
			// Поймем сколько у нас периодов, и собственно сформируем их для поиска попадания документа в них.
			Если (НачалаСезона <= СтрокаСезона И СтрокаСезона <= КонецСезона) // Период один
				ИЛИ (НачалаСезона > КонецСезона // Два периода
				И (НачалаСезона <= СтрокаСезона ИЛИ СтрокаСезона <= КонецСезона)) Тогда
				
				// Найден сезон по периоду. Обработка процента ниже.
				
			Иначе
				// Период не подошел.
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		Если СезонныйПроцентВместоПотерьХО Тогда
			// Процент берется от результата после применения процента. На этот коэффициент будет умножаться количество нетто из рецептуры.
			СезонныйКоэффициент = 1 + Выборка.СезонныйПроцент / (100 - Выборка.СезонныйПроцент);
		Иначе
			// Процент от исходного значения, т.е. база для процента количество брутто из рецептуры. (эта формула используется давно, возможно ошибочно)
			СезонныйКоэффициент = 1 + Выборка.СезонныйПроцент / 100;
		КонецЕсли;
		Результат.Вставить(Выборка.Номенклатура, СезонныйКоэффициент);
		
		НоменклатураНайденПроцент = Выборка.Номенклатура;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Процедура инициализирует таблицу кэша цен.
//
Функция ИнициализироватьТаблицуКэшаЦен() Экспорт
	
	КэшЦен = Новый ТаблицаЗначений;
	КэшЦен.Колонки.Добавить("Рецептура"	, Новый ОписаниеТипов("ДокументСсылка.питРецептура"));
	КэшЦен.Колонки.Добавить("ВидЦены"		, Новый ОписаниеТипов("СправочникСсылка.ВидыЦен"));
	КэшЦен.Колонки.Добавить("Дата"		, Новый ОписаниеТипов("Дата"));
	КэшЦен.Колонки.Добавить("Валюта"		, Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	КэшЦен.Колонки.Добавить("Цена"		, Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,2,ДопустимыйЗнак.Неотрицательный)));
	
	Возврат КэшЦен;
	
КонецФункции

//Функция возвращает соответствие Номенклатура - Цена (в рублях)
//в функцию передаются параметры цены - Тип (вид) цен, период (дата), валюта, курс и кратность
//номенклатура передается как массив
//
//Полученное значение используется для определения учетных цен (не цен продажи)
//
Функция ПодготовитьСоответствиеНоменклатураЦена(СтруктураПараметров) Экспорт
	
	Период       = СтруктураПараметров.Период;
	ТипЦен       = СтруктураПараметров.ТипЦен;
	Валюта       = СтруктураПараметров.Валюта;
	Курс         = СтруктураПараметров.Курс;
	Кратность    = СтруктураПараметров.Кратность;
	Номенклатура = СтруктураПараметров.Номенклатура;
	Характеристика = СтруктураПараметров.Характеристика; //+Характеристики
	РассчетПоСебестоимости = СтруктураПараметров.РассчетПоСебестоимости; // Швецов. 22.01.26 ERP-5547. Расчет по себестоимости для цен меню 
	
	СоответствиеНоменклатураЦена = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", питЦеныНоменклатуры.ПреобразоватьОтборПоХарактеристикеЦен(Период, Характеристика)); //+Характеристики
	Запрос.УстановитьПараметр("ТипЦен", ТипЦен);
	
	// Текст запроса по ценам.
	ТекстЗапросаШаблон = 
		"%1
		|;
		|%2";
	ТекстЗапросаЦены = питЦеныНоменклатуры.ТекстЗапросаЦеныНоменклатуры(Период, "втЦеныНоменклатуры",
		"Период", "Номенклатура", "Характеристика", "ТипЦен");
	ТекстЗапросаОсновной = 
	"ВЫБРАТЬ
	|	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
	|	ЦеныНоменклатуры.Цена КАК Цена,
	|	ЦеныНоменклатуры.Валюта КАК Валюта,
	|	ЦеныНоменклатуры.Упаковка КАК Упаковка,
	|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс,
	|	ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) КАК Кратность
	|ИЗ
	|	втЦеныНоменклатуры КАК ЦеныНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период, ) КАК КурсыВалютСрезПоследних
	|		ПО ЦеныНоменклатуры.Валюта = КурсыВалютСрезПоследних.Валюта";
	
	//<< Портал-Юг, Баринов, 2019.09.17 [9.29. Вывод цен в калькуляционную карту из рецептуры и выпуска блюд по рассчитанной себестоимости, вместо вывода по установленной цене]
	// Себестоимость будем брать по рассчитанной себестоимости вместо цены по типу цен.
	Если Ложь И ТипЦен = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("НормативныйТипЦенОбщепит") Тогда
		// Пока отключено.
		// Для более корректного расчёта не помешает ещё учитывать количество остатка и прихода.
		Запрос.УстановитьПараметр("Валюта", Константы.ВалютаУправленческогоУчета.Получить()); // не нашел функции с кэшем
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	МАКСИМУМ(ДатаСтоимости.Период) КАК Период,
		|	ДатаСтоимости.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
		|	ДатаСтоимости.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика
		|ПОМЕСТИТЬ ДатаСтоимости
		|ИЗ
		|	РегистрСведений.СтоимостьТоваров КАК ДатаСтоимости
		|ГДЕ
		|	ДатаСтоимости.Период <= &Период
		|	И ДатаСтоимости.АналитикаУчетаНоменклатуры.Номенклатура В(&Номенклатура)
		|	И ДатаСтоимости.АналитикаУчетаНоменклатуры.Характеристика В(&Характеристика)
		|	И ДатаСтоимости.АналитикаУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Склад)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатаСтоимости.АналитикаУчетаНоменклатуры.Номенклатура,
		|	ДатаСтоимости.АналитикаУчетаНоменклатуры.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатаСтоимости.Номенклатура КАК Номенклатура,
		|	ДатаСтоимости.Характеристика КАК Характеристика,
		|	СРЕДНЕЕ(ВЫБОР
		|			КОГДА ВидыЦен.ЦенаВключаетНДС
		|				ТОГДА СтоимостьТоваров.Стоимость
		|			ИНАЧЕ СтоимостьТоваров.СтоимостьБезНДС
		|		КОНЕЦ) КАК Цена,
		|	&Валюта КАК Валюта,
		|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
		|	КурсыВалютСрезПоследних.Курс КАК Курс,
		|	КурсыВалютСрезПоследних.Кратность КАК Кратность
		|ИЗ
		|	РегистрСведений.СтоимостьТоваров КАК СтоимостьТоваров
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатаСтоимости КАК ДатаСтоимости
		|		ПО СтоимостьТоваров.Период = ДатаСтоимости.Период
		|			И СтоимостьТоваров.АналитикаУчетаНоменклатуры.Номенклатура = ДатаСтоимости.Номенклатура
		|			И СтоимостьТоваров.АналитикаУчетаНоменклатуры.Характеристика = ДатаСтоимости.Характеристика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта = &Валюта) КАК КурсыВалютСрезПоследних
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЦен КАК ВидыЦен
		|		ПО (ВидыЦен.Ссылка = &ТипЦен)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатаСтоимости.Номенклатура,
		|	ДатаСтоимости.Характеристика,
		|	КурсыВалютСрезПоследних.Курс,
		|	КурсыВалютСрезПоследних.Кратность";
	КонецЕсли; 
	//>> Портал-Юг, Баринов, 2019.09.17
	
	Запрос.Текст = СтрШаблон(ТекстЗапросаШаблон, ТекстЗапросаЦены, ТекстЗапросаОсновной);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Рез = Новый Структура("ЦенаНоменклатуры, Упаковка", 0, Неопределено);
	Пока Выборка.Следующий() Цикл
		
		ПараметрыТекущегоКурса = Новый Структура("Валюта, Курс, Кратность");
		ЗаполнитьЗначенияСвойств(ПараметрыТекущегоКурса, Выборка);
		ПараметрыНовогоКурса = Новый Структура("Валюта, Курс, Кратность");
		ПараметрыНовогоКурса.Валюта = Валюта;
		ПараметрыНовогоКурса.Курс = Курс;
		ПараметрыНовогоКурса.Кратность = Кратность;
		
		ЦенаНоменклатуры = РаботаСКурсамиВалютКлиентСервер.ПересчитатьПоКурсу(Выборка.Цена, ПараметрыТекущегоКурса, ПараметрыНовогоКурса);
		
		Рез.ЦенаНоменклатуры = ЦенаНоменклатуры;
		Рез.Упаковка = Выборка.Упаковка;
		СоответствиеНоменклатураЦена.Вставить(Выборка.Номенклатура, Рез);
		
	КонецЦикла;

	Возврат СоответствиеНоменклатураЦена;

КонецФункции // ПодготовитьСоответствиеНоменклатураЦена()

// Получает цену ингредиента з справочника Цен.
//
// Параметры:
//	Номенклатура - номенклатура, по которой определяем цену.
//	ТипЦен - тип цен, по которому рассчитываем себестоимость.
//	Дата - дата цены
//	Валюта - валюта цены
//  
// Возвращаемое значение:
//	Число - стоимость номенклатуры в валюте Валюта.
//
// Не используется = ФормироватьСуммыКалькуляцииПоСтоимостиОстатков, СкладКомпании, Организация, УчетВПродажныхЦенах)
Функция ПолучитьСтоимостьИнгредиента(Номенклатура, 
	ТипЦен, 
	Дата, 
	Валюта, 
	ФормироватьСуммыКалькуляцииПоСтоимостиОстатков=Ложь, 
	СкладКомпании=Неопределено, Организация=Неопределено, 
	УчетВПродажныхЦенах=Ложь, 
	Упаковка = Неопределено, 
	Характеристика = Неопределено,
	РассчетПоСебестоимостиСтруктура = Неопределено) Экспорт
	
	//+Характеристики
	Если Характеристика = Неопределено Тогда
		Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	Если Упаковка = Неопределено Тогда
		Упаковка = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
	КонецЕсли;
	
	СтруктураПараметровЦены = Новый Структура();
	СтруктураПараметровЦены.Вставить("Период", Дата);
	СтруктураПараметровЦены.Вставить("ТипЦен", ТипЦен);
	СтруктураПараметровЦены.Вставить("Валюта", Валюта);
	СтруктураПараметровЦены.Вставить("Курс", 0);
	СтруктураПараметровЦены.Вставить("Кратность", 0);
	СтруктураПараметровЦены.Вставить("Номенклатура", Номенклатура);
	СтруктураПараметровЦены.Вставить("Характеристика", Характеристика); //+Характеристики
	СтруктураПараметровЦены.Вставить("РассчетПоСебестоимости", РассчетПоСебестоимостиСтруктура); // Швецов. 22.01.26 ERP-5547. Расчет по себестоимости для цен меню
	
	СоответствиеНоменклатураЦена = ПодготовитьСоответствиеНоменклатураЦена(СтруктураПараметровЦены);
	СтруктураЦены = СоответствиеНоменклатураЦена[Номенклатура];
	Если СтруктураЦены <> Неопределено Тогда
		КоэффициентИзЦены = питОбщегоНазначения.ПолучитьКоэффициентУпаковки(Номенклатура, СтруктураЦены.Упаковка);
		Цена = СтруктураЦены.ЦенаНоменклатуры  * питОбщегоНазначения.ПолучитьКоэффициентУпаковки(Номенклатура, Упаковка) / ?(КоэффициентИзЦены = 0, 1, КоэффициентИзЦены);
	Иначе
		Цена = 0;
	КонецЕсли;
	
	Возврат Цена;
	
КонецФункции

// Возвращает основную или актуальную рецептуру.
//
// Параметры:
//	Блюдо - номенклатура, для которой надо вернуть рецептуру.
//	ВидРецептуры - вид основной рецептуры.
//  ДатаНачала - дата, на которую нужно получить актуальную рецептуру.
//  СпособРасчетаРецептуры - перечисление или булево, способ определения рецептуры, по основной или по актуальной,
//               если булево, то Истина это значит расчет по актуальной рецептуре.
//  ДатаКонца - дата конца периода действия рецептуры, если указана, то ищем актуальную рецептуру, 
//               которая обеспечивает весь период.
//  ИщемВперед - булево, Истина - ищем ближайшую действующую рецептуру после ДатыНачала.
//  
// Возвращаемое значение:
//	Ссылка на документ Рецептура.
//
Функция удалитьВернутьОсновнуюРецептуру(Блюдо, Характеристика = Неопределено, ВидРецептуры, Знач ДатаНачала=Неопределено, СпособРасчетаРецептуры=Неопределено, Знач ДатаКонца=Неопределено, Знач Организация=Неопределено, ИскатьПоВсемОрганизациям=Неопределено, ИщемВперед = Ложь , Подразделение = Неопределено) Экспорт
	
	//+Характеристики
	Если Характеристика = Неопределено Тогда
		Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	Результат = Документы.питРецептура.ПустаяСсылка();
	
	Если СпособРасчетаРецептуры = Неопределено Тогда
		//СпособРасчетаРецептурыПоАктуальной = (Константы.питСпособРасчетаРецептур.Получить() = Перечисления.питСпособРасчетаРецептур.ПоАктуальнойРецептуре);
		СпособРасчетаРецептурыПоАктуальной = (питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("СпособРасчетаРецептур") = Перечисления.питСпособРасчетаРецептур.ПоАктуальнойРецептуре);
	ИначеЕсли ТипЗнч(СпособРасчетаРецептуры) = Тип("Булево") Тогда
		СпособРасчетаРецептурыПоАктуальной = СпособРасчетаРецептуры;
	Иначе
		СпособРасчетаРецептурыПоАктуальной = (СпособРасчетаРецептуры = Перечисления.питСпособРасчетаРецептур.ПоАктуальнойРецептуре);
	КонецЕсли;
	Если ИскатьПоВсемОрганизациям = Неопределено Тогда
		ИскатьПоВсемОрганизациям = питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("ИскатьРецептурыПоВсемОрганизациям");
	КонецЕсли;
	Если Организация = Неопределено Тогда
		Организация = питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
		Если Организация = Справочники.Организации.ПустаяСсылка() Тогда
			ИскатьПоВсемОрганизациям = Истина;
		КонецЕсли;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Организация) И ИскатьПоВсемОрганизациям = Ложь Тогда
		// Не указана организация, и организация обязательно должна быть.
		// Значит ничего не ищем.
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Если ИщемВперед Тогда
		УсловиеДаты = " И Рецептура.ДатаНачала > &ДатаНачала ";
		СтрСортировки = " ВОЗР ";
	Иначе 
		УсловиеДаты = "
		|	И Рецептура.ДатаНачала <= &ДатаНачала
		|	И ВЫБОР
		|			КОГДА Рецептура.ДатаКонца = &ПустаяДата
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ Рецептура.ДатаКонца >= &ДатаКонца
		|		КОНЕЦ 
		|";
		
		СтрСортировки = " УБЫВ ";
		
	КонецЕсли;
	
	
	Если НЕ СпособРасчетаРецептурыПоАктуальной Тогда
		// Выбираем основную рецептуру
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Рецептура.Ссылка
		|ИЗ
		|	Документ.питРецептура КАК Рецептура
		|ГДЕ
		|	(НЕ Рецептура.ПометкаУдаления)
		|	И Рецептура.Номенклатура = &Номенклатура
		|	И Рецептура.Характеристика = &Характеристика
		|	И Рецептура.ХозяйственнаяОперация = &ВидОперации
//		|	И Рецептура.Основная
		|"+?(ЗначениеЗаполнено(Организация),"	И Рецептура.Организация = &Организация","")+"
		|
		|
		|УПОРЯДОЧИТЬ ПО
		|	Рецептура.ДатаНачала " + СтрСортировки;

		

		
		Запрос.УстановитьПараметр("ВидОперации",ВидРецептуры);
		Запрос.УстановитьПараметр("Номенклатура",Блюдо);
		Запрос.УстановитьПараметр("Характеристика",Характеристика);
		Запрос.УстановитьПараметр("Организация",Организация);
		
	Иначе
		// Выбираем актуальную рецептуру на дату ДатаНачала.
		// Если указана ДатаКонца, то найденная рецептура должна обеспечивать весь этот период.
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Рецептура.Ссылка
		|ИЗ
		|	Документ.питРецептура КАК Рецептура
		|ГДЕ
		|	(НЕ Рецептура.ПометкаУдаления)
		|	И Рецептура.Номенклатура = &Номенклатура
		|	И Рецептура.Характеристика = &Характеристика
		|	И Рецептура.ХозяйственнаяОперация = &ВидОперации ";
		
		
		Запрос.Текст = Запрос.Текст + УсловиеДаты +?(ЗначениеЗаполнено(Организация),"	И Рецептура.Организация = &Организация","")+"
		|
		|УПОРЯДОЧИТЬ ПО
		|	Рецептура.ДатаНачала " + СтрСортировки;
		
		Запрос.УстановитьПараметр("ВидОперации",ВидРецептуры);
		Запрос.УстановитьПараметр("Номенклатура",Блюдо);
		Запрос.УстановитьПараметр("Характеристика",Характеристика);
		Запрос.УстановитьПараметр("Организация",Организация);
		
		Если НЕ ЗначениеЗаполнено(ДатаНачала) Тогда
			ДатаНачала = ТекущаяДата();
		КонецЕсли;
		Если НЕ (ДатаКонца = Дата("00010101") ИЛИ ЗначениеЗаполнено(ДатаКонца)) ИЛИ (ДатаКонца < ДатаНачала) Тогда
			ДатаКонца = ДатаНачала;
		КонецЕсли;
		Запрос.УстановитьПараметр("ДатаНачала",ДатаНачала);
		Запрос.УстановитьПараметр("ДатаКонца",ДатаКонца);
		Запрос.УстановитьПараметр("ПустаяДата",Дата("00010101"));
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если НЕ Подразделение = Неопределено Тогда
				Результат = Документы.питРецептура.ПустаяСсылка();
				ПодразделениеРазрешено = питПроизводствоПовтИсп.ПодразделениеРазрешено(Выборка.Ссылка, ПоместитьВоВременноеХранилище(Подразделение, Новый УникальныйИдентификатор), Справочники.Склады.ПустаяСсылка(), Ложь, Ложь);
				Если ПодразделениеРазрешено Тогда
					Результат = Выборка.Ссылка;
					Прервать;
				КонецЕсли;
			Иначе
				Результат = Выборка.Ссылка;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	ИначеЕсли ЗначениеЗаполнено(Организация) И ИскатьПоВсемОрганизациям <> Ложь Тогда
		// Сделан поиск по одной организации. Повторим поиск по всем организациям.
		Результат = ВернутьОсновнуюРецептуру(Блюдо, Характеристика, ВидРецептуры, ДатаНачала, СпособРасчетаРецептуры, ДатаКонца, Справочники.Организации.ПустаяСсылка(), Истина, ,Подразделение);
		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции	

// Возвращает основную или актуальную рецептуру.
//
// Параметры:
//	Блюдо - номенклатура, для которой надо вернуть рецептуру.
//	ВидРецептуры - вид основной рецептуры.
//  ДатаНачала - дата, на которую нужно получить актуальную рецептуру.
//  СпособРасчетаРецептуры - перечисление или булево, способ определения рецептуры, по основной или по актуальной,
//               если булево, то Истина это значит расчет по актуальной рецептуре.
//  ДатаКонца - дата конца периода действия рецептуры, если указана, то ищем актуальную рецептуру, 
//               которая обеспечивает весь период.
//  ИщемВперед - булево, Истина - ищем ближайшую действующую рецептуру после ДатыНачала.
//  
// Возвращаемое значение:
//	Ссылка на документ Рецептура.
//
Функция ВернутьОсновнуюРецептуру(Блюдо, Характеристика = Неопределено, ВидРецептуры, Знач ДатаНачала=Неопределено, СпособРасчетаРецептуры=Неопределено, Знач ДатаКонца=Неопределено, Знач Организация=Неопределено, ИскатьПоВсемОрганизациям=Неопределено, ИщемВперед = Ложь , Подразделение = Неопределено) Экспорт
	
	//+Характеристики
	Если Характеристика = Неопределено Тогда
		Характеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	// Инициализация результата.
	Результат = Документы.питРецептура.ПустаяСсылка();
	
	// На данный момент всегда ищем по актуальной
	СпособРасчетаРецептурыПоАктуальной = Истина;
	
	// Поиск можно выполнять по всем организациям.
	Если ИскатьПоВсемОрганизациям = Неопределено Тогда
		ИскатьПоВсемОрганизациям = питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("ИскатьРецептурыПоВсемОрганизациям");
	КонецЕсли;
	
	// Пытаемся получить организацию для поиска, если она не была передана.
	Если Организация = Неопределено Тогда
		Организация = питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	Если Организация = Справочники.Организации.ПустаяСсылка() Тогда
		ИскатьПоВсемОрганизациям = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) И ИскатьПоВсемОрганизациям = Ложь Тогда
		// Не указана организация, и организация обязательно должна быть.
		// Значит ничего не ищем.
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	
	// (!) Ищем всегда по актуальной
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	питАктуальныеРецептурыСрезПоследних.Рецептура КАК Рецептура,
	|	питАктуальныеРецептурыСрезПоследних.Период КАК ДатаНачала,
	|	питАктуальныеРецептурыСрезПоследних.ДатаОкончания КАК ДатаОкончания,
	|	питАктуальныеРецептурыСрезПоследних.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.питАктуальныеРецептуры.%Срез%(
	|			&ДатаНачала,
	|			Номенклатура = &Номенклатура
	|				И Характеристика В (&МассивХарактеристик)
	|				И Подразделение В (&Подразделение)
	|				%УсловиеОрганизация%) КАК питАктуальныеРецептурыСрезПоследних
	|УПОРЯДОЧИТЬ ПО
	//+АвтовыборНоменклатуры
	|	питАктуальныеРецептурыСрезПоследних.Характеристика УБЫВ,
	//-АвтовыборНоменклатуры
	|	питАктуальныеРецептурыСрезПоследних.Период %Сортировка%";
	
	Если НЕ ЗначениеЗаполнено(ДатаНачала) Тогда
		ДатаНачала = ТекущаяДата();
	КонецЕсли;
	Если ИщемВперед Тогда
		ДатаНачала = ДатаНачала + 1;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%Срез%", "СрезПервых");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%Сортировка%", " ВОЗР ");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%Срез%", "СрезПоследних");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%Сортировка%", " УБЫВ ");
	КонецЕсли;
	
	Если ДатаКонца = Неопределено Тогда
		ДатаКонца = ДатаНачала;
	КонецЕсли;
	
	//+АвтовыборНоменклатуры
	МассивХарактеристик = Новый Массив;
	Если ЗначениеЗаполнено(Характеристика) Тогда
		МассивХарактеристик.Добавить(Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	КонецЕсли;
	МассивХарактеристик.Добавить(Характеристика);
	//-АвтовыборНоменклатуры
	
	// Установка параметров.
	Запрос.УстановитьПараметр("ДатаНачала"				, ДатаНачала);
	Запрос.УстановитьПараметр("Номенклатура"			, Блюдо);
	Запрос.УстановитьПараметр("МассивХарактеристик"		, МассивХарактеристик);
	Запрос.УстановитьПараметр("Подразделение"			, Подразделение);
	Если ЗначениеЗаполнено(Организация) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%УсловиеОрганизация%"," И Организация = &Организация ");
		Запрос.УстановитьПараметр("Организация"			, Организация);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "%УсловиеОрганизация%","");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ИщемВперед ИЛИ НЕ ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ДатаОкончания) 
				ИЛИ ВыборкаДетальныеЗаписи.ДатаОкончания >= ДатаКонца Тогда
				Результат = ВыборкаДетальныеЗаписи.Рецептура;
				Прервать;
			Иначе
				// Рецептура не подошла по дате окончания, ищем более раннюю рекурсивно.
				Результат = ВернутьОсновнуюРецептуру(Блюдо, Характеристика, ВидРецептуры, ВыборкаДетальныеЗаписи.ДатаНачала - 1, СпособРасчетаРецептуры, ДатаКонца, ВыборкаДетальныеЗаписи.Организация, Ложь, ИщемВперед , Подразделение);
				Если ЗначениеЗаполнено(Результат) Тогда
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ЗначениеЗаполнено(Организация) И ИскатьПоВсемОрганизациям <> Ложь Тогда
		
		// Сделан поиск по одной организации. Повторим поиск по всем организациям.
		Результат = ВернутьОсновнуюРецептуру(Блюдо, Характеристика, ВидРецептуры, ДатаНачала, СпособРасчетаРецептуры, ДатаКонца, Справочники.Организации.ПустаяСсылка(), Истина, ,Подразделение);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции	

// Функция возвращает таблицу основных рецептур
//
// Параметры:
//  НаборДанныхБлюда		 - Массив, ТаблицаЗначений - Массив или таблица блюд,
//    для которых вычисляем актуальные рецептуры.
//  ДатаОбъекта				 - Дата - Дата получения рецептур.
//  Подразделение			 - СправочникСсылка.СтруктураПредприятия - Подразделение рецептур.
//  Организация				 - СправочникСсылка.Организаации - Организация рецептур.
//  ИскатьПоВсемОрганизациям - Булево - Поиск по всем организациям.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица с колонками номенклатура, характеристика, рецептура.
//
Функция ВернутьТаблицуОсновныхРецептур(НаборДанныхБлюда, Знач ДатаОбъекта, Знач Подразделение,
	Знач Организация=Неопределено, ИскатьПоВсемОрганизациям=Неопределено) Экспорт
	
	// Инициализация результата.
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Результат.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Результат.Колонки.Добавить("Рецептура", Новый ОписаниеТипов("ДокументСсылка.питРецептура"));
	
	// Поиск можно выполнять по всем организациям.
	Если ИскатьПоВсемОрганизациям = Неопределено Тогда
		ИскатьПоВсемОрганизациям = 
			питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("ИскатьРецептурыПоВсемОрганизациям");
	КонецЕсли;
	
	// Пытаемся получить организацию для поиска, если она не была передана.
	Если Организация = Неопределено ИЛИ ИскатьПоВсемОрганизациям Тогда
		Организация = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) И ИскатьПоВсемОрганизациям = Ложь Тогда
		// Не указана организация, и организация обязательно должна быть.
		// Значит ничего не ищем.
		Возврат Результат;
	КонецЕсли;
	
	ЭтоТаблицаЗначений = ТипЗнч(НаборДанныхБлюда) = Тип("ТаблицаЗначений");
	Для каждого ТекБлюдо Из НаборДанныхБлюда Цикл
		НоваяСтрокаРезультат = Результат.Добавить();
		Если ЭтоТаблицаЗначений Тогда
			НоваяСтрокаРезультат.Номенклатура = ТекБлюдо.Номенклатура;
			НоваяСтрокаРезультат.Характеристика = ТекБлюдо.Характеристика;
		Иначе
			НоваяСтрокаРезультат.Номенклатура = ТекБлюдо;
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос();
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ВТ_ТаблицаПоиска.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицаПоиска.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ВТ_ВходныеДанные
	|ИЗ
	|	&ВТ_ТаблицаПоиска КАК ВТ_ТаблицаПоиска
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	АктуальныеРецептуры.Номенклатура КАК Номенклатура,
	|	АктуальныеРецептуры.Подразделение КАК Подразделение,
	|	АктуальныеРецептуры.Организация КАК Организация,
	|	МАКСИМУМ(АктуальныеРецептуры.Период) КАК Период,
	|	АктуальныеРецептуры.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ВТ_АктуальныеРецептуры
	|ИЗ
	|	ВТ_ВходныеДанные КАК ВТ_ВходныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.питАктуальныеРецептуры КАК АктуальныеРецептуры
	|		ПО ВТ_ВходныеДанные.Номенклатура = АктуальныеРецептуры.Номенклатура
	|		И ВТ_ВходныеДанные.Характеристика = АктуальныеРецептуры.Характеристика
	|ГДЕ
	|	АктуальныеРецептуры.Период <= &ДатаОбъекта
	|	И АктуальныеРецептуры.Подразделение = &Подразделение
	|	И ВЫБОР
	|			КОГДА АктуальныеРецептуры.ДатаОкончания = &ПустаяДата
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ АктуальныеРецептуры.ДатаОкончания >= &ДатаОбъекта
	|		КОНЕЦ
	|	И &ОтборОрганизация
	|
	|СГРУППИРОВАТЬ ПО
	|	АктуальныеРецептуры.Номенклатура,
	|	АктуальныеРецептуры.Характеристика,
	|	АктуальныеРецептуры.Подразделение,
	|	АктуальныеРецептуры.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(АктуальныеРецептуры.Рецептура) КАК Рецептура,
	|	АктуальныеРецептуры.Номенклатура КАК Номенклатура,
	|	АктуальныеРецептуры.Характеристика КАК Характеристика
	|ИЗ
	|	ВТ_АктуальныеРецептуры КАК ВТ_АктуальныеРецептуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.питАктуальныеРецептуры КАК АктуальныеРецептуры
	|		ПО ВТ_АктуальныеРецептуры.Номенклатура = АктуальныеРецептуры.Номенклатура
	|			И ВТ_АктуальныеРецептуры.Подразделение = АктуальныеРецептуры.Подразделение
	|			И ВТ_АктуальныеРецептуры.Организация = АктуальныеРецептуры.Организация
	|			И ВТ_АктуальныеРецептуры.Период = АктуальныеРецептуры.Период
	|			И ВТ_АктуальныеРецептуры.Характеристика = АктуальныеРецептуры.Характеристика
	|
	|СГРУППИРОВАТЬ ПО
	|	АктуальныеРецептуры.Номенклатура,
	|	АктуальныеРецептуры.Характеристика";
	
	Запрос.УстановитьПараметр("ВТ_ТаблицаПоиска", Результат);
	
	Если НЕ ЗначениеЗаполнено(ДатаОбъекта) Тогда
		Запрос.УстановитьПараметр("ДатаОбъекта",НачалоДня(ТекущаяДатаСеанса()));
	Иначе
		Запрос.УстановитьПараметр("ДатаОбъекта",НачалоДня(ДатаОбъекта));
	КонецЕсли;
	Запрос.УстановитьПараметр("ПустаяДата",Дата("00010101"));
	Запрос.УстановитьПараметр("Подразделение"			, Подразделение);
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборОрганизация"," Организация = &Организация ");
		Запрос.УстановитьПараметр("Организация"			, Организация);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборОрганизация"," ИСТИНА ");
	КонецЕсли;
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру списка от зацикливания при расчете производства блюд.
//  
// Возвращаемое значение:
//	Таблица значений, используемая в дальнейшем для отслеживания циклических переделов.
//
Функция ВернутьСтруктуруСпискаЗащитыОтЗацикливания() Экспорт
	
	ОписаниеТиповНоменклатура = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	ОписаниеТиповХарактеристика = Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"); //+Характеристики
	ОписаниеТиповБулево       = Новый ОписаниеТипов("Булево");
	
	СписокЗащитыОтЗацикливания = Новый ТаблицаЗначений;
	СписокЗащитыОтЗацикливания.Колонки.Добавить("Номенклатура", ОписаниеТиповНоменклатура);
	СписокЗащитыОтЗацикливания.Колонки.Добавить("Характеристика", ОписаниеТиповХарактеристика); //+Характеристики
	СписокЗащитыОтЗацикливания.Колонки.Добавить("Пометка", ОписаниеТиповБулево);
	
	Возврат СписокЗащитыОтЗацикливания;
	
КонецФункции

// Возвращает структуру дерева состава блюда.
//  
// Возвращаемое значение:
//	Дерево значений, в которое будет заливаться состав блюд.
//
Функция ВернутьСтруктуруДереваСостава() Экспорт
	ОписаниеТиповНоменклатура         = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	ОписаниеТиповХарактеристика       = Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"); //+Характеристики
	ОписаниеТиповЕдиница              = Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения");
	ОписаниеТиповРецептура            = Новый ОписаниеТипов("ДокументСсылка.питРецептура");
	ОписаниеТиповЧисло                = Новый ОписаниеТипов("Число");
	ОписаниеТиповБулево               = Новый ОписаниеТипов("Булево");
	Если НЕ питПроизводствоПовтИсп.ПолучитьИмяОсновнойКонфигурации() = "ERPWE" Тогда
		ОписаниеТиповСчет                 = Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный");
	Иначе
		ОписаниеТиповСчет                 = Новый ОписаниеТипов("Строка");
	КонецЕсли;
	ОписаниеТиповСтатьяКалькуляции    = Новый ОписаниеТипов("СправочникСсылка.СтатьиКалькуляции");
	ОписаниеТиповСерия                = Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры");
	ОписаниеТиповНазначение           = Новый ОписаниеТипов("СправочникСсылка.Назначения");
	
	ДеревоСостава = Новый ДеревоЗначений();
	ДеревоСостава.Колонки.Добавить("Номенклатура",ОписаниеТиповНоменклатура);
	ДеревоСостава.Колонки.Добавить("Характеристика",ОписаниеТиповХарактеристика); //+Характеристики
	ДеревоСостава.Колонки.Добавить("Рецептура",ОписаниеТиповРецептура);
	ДеревоСостава.Колонки.Добавить("СчетУчета",ОписаниеТиповСчет);
	ДеревоСостава.Колонки.Добавить("Количество",ОписаниеТиповЧисло);
	ДеревоСостава.Колонки.Добавить("КоличествоНетто",ОписаниеТиповЧисло);
	ДеревоСостава.Колонки.Добавить("КоличествоВыход",ОписаниеТиповЧисло);
	ДеревоСостава.Колонки.Добавить("ЕдиницаИзмерения",ОписаниеТиповЕдиница);
	
	ДеревоСостава.Колонки.Добавить("КоличествоУпаковок",ОписаниеТиповЧисло);
	ДеревоСостава.Колонки.Добавить("КоличествоУпаковокНетто",ОписаниеТиповЧисло);
	ДеревоСостава.Колонки.Добавить("КоличествоУпаковокВыход",ОписаниеТиповЧисло);

	ДеревоСостава.Колонки.Добавить("Коэффициент",ОписаниеТиповЧисло);
	ДеревоСостава.Колонки.Добавить("Специя",ОписаниеТиповБулево);
	ДеревоСостава.Колонки.Добавить("ЗапретитьЗамены",ОписаниеТиповБулево);
	ДеревоСостава.Колонки.Добавить("Замена", ОписаниеТиповНоменклатура);
	ДеревоСостава.Колонки.Добавить("ХарактеристикаЗамены", ОписаниеТиповХарактеристика); //+Характеристики
	ДеревоСостава.Колонки.Добавить("НазначениеЗамены", ОписаниеТиповНазначение);
	ДеревоСостава.Колонки.Добавить("КоэффициентЗамены", ОписаниеТиповЧисло);
	ДеревоСостава.Колонки.Добавить("ЦиклическаяСсылка",ОписаниеТиповБулево);
	ДеревоСостава.Колонки.Добавить("СтатьяКалькуляции",ОписаниеТиповСтатьяКалькуляции); //+СтатьиКалькуляции
	ДеревоСостава.Колонки.Добавить("Серия",ОписаниеТиповСерия);
	ДеревоСостава.Колонки.Добавить("Назначение",ОписаниеТиповНазначение);
	ДеревоСостава.Колонки.Добавить("НеГотовить",ОписаниеТиповБулево);
	
	//+АвтовыборНоменклатуры
	ДеревоСостава.Колонки.Добавить("ОшибкаАвтовыбора", ОписаниеТиповБулево);
	//-АвтовыборНоменклатуры
	
	//+УмноеОкругление
	ДеревоСостава.Колонки.Добавить("ИдентификаторСтрокиДерева", Новый ОписаниеТипов("Строка"));
	//-УмноеОкругление
	
	Возврат  ДеревоСостава;
КонецФункции

// Рассчитывает дерево состава блюда.
//
// Параметры:
//	Рецептура - рецептура блюда, для которого рассчитывается состав.
//	СтрокаСостава - текущий передел, необходим ввиду рекурсивности расчета состава.
//	КоличествоБлюда - количество блюдо, на которое рассчитывается состав.
//	КоличествоВыход - количество выход, на которое рассчитывается состав.
//	КоличествоНетто - количество нетто, на которое рассчитывается состав.
//	СписокЗащитыОтЗацикливания - список для отслеживания циклических переделов.
//	ВыпускПродукции - ссылка на документ Выпуск продукции.
//	ВерхнееБлюдо - ссылка на самое верхнее блюдо в передельном производстве или номенклатурная группа верхнего блюда.
//	СпособРасчетаРецептуры - способ расчета рецептуры.
//  СтруктураРассчитываемогоБлюдо - структура, передающая значение блюда и единицы измерения,
//                                 когда есть пустая рецептура.
//  ИскатьПоВсемОрганизациям - для кэширования настройки пользователя, в рекурсивном вызове.
//
// Возвращаемое значение:
//	Дерево значений, рассчитанное дерево состава блюда.
//
Функция РассчитатьДеревоСоставаРецептуры(Рецептура, СтрокаСостава=Неопределено, Знач КоличествоБлюда=0, Знач КоличествоВыход=0, 
	Знач КоличествоНетто=0, Знач КоличествоУпаковокБлюда=0, Знач КоличествоУпаковокВыход=0, Знач КоличествоУпаковокНетто=0, 
	СписокЗащитыОтЗацикливания=Неопределено, ВыпускПродукции=Неопределено, ВерхнееБлюдо=Неопределено, 
	СпособРасчетаРецептуры=Неопределено, СтруктураРассчитываемогоБлюдо=Неопределено, ИскатьПоВсемОрганизациям=Неопределено,
	МассивОтбораБлюд=Неопределено, Серия = Неопределено, Назначение = Неопределено, НазначениеИнгредиентов = Неопределено, ОтключитьДополнительныеПроверки = Ложь, 
	КэшЗаполненияДерева = Неопределено, питВосприниматьБлюдоБезРецептурыКакТовар = Неопределено, ПодразделенияДляПоискаРецептуры = Неопределено, 
	питПодразделениеПроизводства = Неопределено, Характеристика = Неопределено, УточненияАвтовыбора = Неопределено,
	РассчитываемаяУпаковка = Неопределено) Экспорт
	
	Если питВосприниматьБлюдоБезРецептурыКакТовар = Неопределено Тогда
		питВосприниматьБлюдоБезРецептурыКакТовар = питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("питВосприниматьБлюдоБезРецептурыКакТовар");
	КонецЕсли;
	
	//+БизнесЛанч
	Если ТипЗнч(СтруктураРассчитываемогоБлюдо) = Тип("Структура") 
		И СтруктураРассчитываемогоБлюдо.Свойство("ЭтоБизнесЛанч") 
		И СтруктураРассчитываемогоБлюдо.ЭтоБизнесЛанч Тогда
		ЭтоБизнесЛанч = Истина;
	Иначе
		ЭтоБизнесЛанч = Ложь;
	КонецЕсли;
	//-БизнесЛанч
	
	Если Серия = Неопределено Тогда
		Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	Если Назначение = Неопределено Тогда
		Назначение = Справочники.Назначения.ПустаяСсылка();
	КонецЕсли;
	
	Если НазначениеИнгредиентов = Неопределено Тогда
		НазначениеИнгредиентов = Справочники.Назначения.ПустаяСсылка();
	КонецЕсли;
	
	Если НЕ ТипЗнч(СписокЗащитыОтЗацикливания)=Тип("ТаблицаЗначений") Тогда
		СписокЗащитыОтЗацикливания = ВернутьСтруктуруСпискаЗащитыОтЗацикливания();
	КонецЕсли;	
	
	Если СтрокаСостава=Неопределено Тогда
		СтрокаСостава = ВернутьСтруктуруДереваСостава();
	КонецЕсли;
	
	Если СпособРасчетаРецептуры=Неопределено Тогда
		//СпособРасчетаРецептуры = Константы.питСпособРасчетаРецептур.Получить();
		СпособРасчетаРецептуры = питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("СпособРасчетаРецептур");
	КонецЕсли;
	
	// Добавим строку блюда.
	// Если КоличествоБлюда=0, тогда считаем на указанное в рецептуре количество,
	// а если это состав на конкретное производство, то рассчитываем коэффициент пересчета.
	
	Если ЗначениеЗаполнено(Рецептура) Тогда
		РецептураРеквизиты = ПолучитьРеквизитыРецептуры(Рецептура, "Номенклатура,Характеристика,Номенклатура.Наименование,Характеристика.Наименование,Количество,КоличествоУпаковок,Упаковка,Организация,Подразделение", КэшЗаполненияДерева);
		//+АвтовыборНоменклатуры
		Если НЕ Характеристика = Неопределено Тогда
			РецептураРеквизиты.Характеристика = Характеристика;
		КонецЕсли;
		//-АвтовыборНоменклатуры
	Иначе
		РецептураРеквизиты = Новый Структура();
		РецептураРеквизиты.Вставить("Номенклатура"					, Справочники.Номенклатура.ПустаяСсылка());
		РецептураРеквизиты.Вставить("Характеристика"				, Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
		РецептураРеквизиты.Вставить("НоменклатураНаименование"		, "");
		РецептураРеквизиты.Вставить("ХарактеристикаНаименование"	, "");
		РецептураРеквизиты.Вставить("Количество"					, 0);
		РецептураРеквизиты.Вставить("КоличествоУпаковок"			, 0);
		РецептураРеквизиты.Вставить("Упаковка"						, Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
		РецептураРеквизиты.Вставить("Организация"					, Справочники.Организации.ПустаяСсылка());
		РецептураРеквизиты.Вставить("Подразделение"					, Справочники.СтруктураПредприятия.ПустаяСсылка());
	КонецЕсли;
	
	// +Расчет по количеству упаковок
	//РецептураРеквизиты.Вставить("Коэффициент", ?(РецептураРеквизиты.КоличествоУпаковок = 0, 1,  РецептураРеквизиты.Количество/РецептураРеквизиты.КоличествоУпаковок));
	Если РассчитываемаяУпаковка = Неопределено Тогда
		РассчитываемаяУпаковка = РецептураРеквизиты.Упаковка;
	КонецЕсли;
	КоэффициетРассчитываемойУпаковки = питОбщегоНазначения.ПолучитьКоэффициентУпаковки(РецептураРеквизиты.Номенклатура, РассчитываемаяУпаковка, ?(НЕ КэшЗаполненияДерева = Неопределено И КэшЗаполненияДерева.Свойство("КэшУпаковок"), КэшЗаполненияДерева.КэшУпаковок, Неопределено));
	Если КоэффициетРассчитываемойУпаковки = 0 Тогда
		КоэффициетРассчитываемойУпаковки = 1;
	КонецЕсли;
	КоличествоБлюда = ?(КоличествоУпаковокБлюда=0, КоличествоБлюда, КоличествоУпаковокБлюда * КоэффициетРассчитываемойУпаковки);
	КоличествоВыход = ?(КоличествоУпаковокВыход=0, КоличествоВыход, КоличествоУпаковокВыход * КоэффициетРассчитываемойУпаковки);
	КоличествоНетто = ?(КоличествоУпаковокНетто=0, КоличествоНетто, КоличествоУпаковокНетто * КоэффициетРассчитываемойУпаковки);
	// -Расчет по количеству упаковок
	
	СтрокаТаблицы = Неопределено;
	СтрокиТаблицы = СписокЗащитыОтЗацикливания.НайтиСтроки(Новый Структура("Номенклатура, Характеристика",РецептураРеквизиты.Номенклатура, РецептураРеквизиты.Характеристика));
	Если СтрокиТаблицы.Количество() Тогда
		СтрокаТаблицы = СтрокиТаблицы[0];
	КонецЕсли;
	Если СтрокаТаблицы=Неопределено Тогда
		ТекСтрока				= СписокЗащитыОтЗацикливания.Добавить();
		ТекСтрока.Номенклатура	= РецептураРеквизиты.Номенклатура;
		ТекСтрока.Характеристика	= РецептураРеквизиты.Характеристика;
	КонецЕсли;	
	
	Попытка
		КоэффициентПересчета  = ?(КоличествоБлюда=0 ИЛИ РецептураРеквизиты.Количество = 0 ,1,КоличествоБлюда/РецептураРеквизиты.Количество);
	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Ошибка расчета состава рецептуры в блюде "+СокрЛП(РецептураРеквизиты.НоменклатураНаименование)+", "+СокрЛП(РецептураРеквизиты.ХарактеристикаНаименование)+"
		|Количество или коэффициент единицы в рецептуре равен нулю";
		Сообщение.Сообщить();
		Возврат СтрокаСостава;
	КонецПопытки;
	
	//#Вставка
	Если КоличествоБлюда=0 Тогда
		Возврат СтрокаСостава;
	КонецЕсли;
	//#КонецВставки
	
	/////////////////////////////////////////////////////////
	СтруктураПоискаБлюдаВДеревеСостава = Новый Структура();
	СтруктураПоискаБлюдаВДеревеСостава.Вставить("Номенклатура",РецептураРеквизиты.Номенклатура);
	СтруктураПоискаБлюдаВДеревеСостава.Вставить("Характеристика",РецептураРеквизиты.Характеристика);
	СтруктураПоискаБлюдаВДеревеСостава.Вставить("Рецептура",Рецептура);
	
	// Определим, сортировать или нет по номеру строки рецептуры.
	Если НЕ ВыпускПродукции=Неопределено Тогда
		СортироватьПоНомеруСтрокиРецептуры	= Ложь;
	Иначе
		СортироватьПоНомеруСтрокиРецептуры	= Истина;
	КонецЕсли;
	
	// Поищем строку блюда на данном уровне вложенности, 
	// чтобы в дальнейшем не задваивать количество родителя при приготовлении.
	МассивСтрокБлюдаВДеревеСостава = СтрокаСостава.Строки.НайтиСтроки(СтруктураПоискаБлюдаВДеревеСостава,Ложь);
	Если МассивСтрокБлюдаВДеревеСостава.Количество()>0 Тогда
		СтрокаСоставаБлюдо  = МассивСтрокБлюдаВДеревеСостава[0];
		// +Расчет по количеству упаковок
		СтрокаСоставаБлюдо.Количество = СтрокаСоставаБлюдо.Количество+?(КоличествоБлюда=0, РецептураРеквизиты.Количество,КоличествоБлюда);
		СтрокаСоставаБлюдо.КоличествоНетто = СтрокаСоставаБлюдо.КоличествоНетто + КоличествоНетто;
		СтрокаСоставаБлюдо.КоличествоВыход = СтрокаСоставаБлюдо.КоличествоВыход + КоличествоВыход;
		
		СтрокаСоставаБлюдо.КоличествоУпаковок = СтрокаСоставаБлюдо.КоличествоУпаковок+?(КоличествоБлюда=0, РецептураРеквизиты.КоличествоУпаковок,КоличествоУпаковокБлюда);
		СтрокаСоставаБлюдо.КоличествоУпаковокНетто = СтрокаСоставаБлюдо.КоличествоУпаковокНетто
			+?(КоличествоУпаковокНетто=0, КоличествоНетто / КоэффициетРассчитываемойУпаковки, КоличествоУпаковокНетто);
		СтрокаСоставаБлюдо.КоличествоУпаковокВыход = СтрокаСоставаБлюдо.КоличествоУпаковокВыход
			+?(КоличествоУпаковокВыход=0, КоличествоВыход / КоэффициетРассчитываемойУпаковки, КоличествоУпаковокВыход);
		// -Расчет по количеству упаковок
		
	Иначе
		
		Если ЗначениеЗаполнено(Рецептура) Тогда
			
			СтатьяКалькуляцииБлюда = Справочники.СтатьиКалькуляции.ПустаяСсылка();
			Если НЕ ТипЗнч(СтрокаСостава) = Тип("ДеревоЗначений") Тогда
				СтатьяКалькуляцииБлюда = СтрокаСостава.СтатьяКалькуляции;
			КонецЕсли;
			
			СтрокаСоставаБлюдо					= СтрокаСостава.Строки.Добавить();
			//+УмноеОкругление
			СтрокаСоставаБлюдо.ИдентификаторСтрокиДерева = Новый УникальныйИдентификатор;
			//-УмноеОкругление
			СтрокаСоставаБлюдо.Номенклатура		= РецептураРеквизиты.Номенклатура;
			СтрокаСоставаБлюдо.Характеристика		= РецептураРеквизиты.Характеристика;
			СтрокаСоставаБлюдо.Рецептура		= Рецептура;
			// +Расчет по количеству упаковок
			СтрокаСоставаБлюдо.ЕдиницаИзмерения	= РассчитываемаяУпаковка;
			// +Расчет по количеству упаковок
			
			СтрокаСоставаБлюдо.Количество		= ?(КоличествоБлюда=0, РецептураРеквизиты.Количество, КоличествоБлюда);
			СтрокаСоставаБлюдо.КоличествоВыход	= КоличествоВыход;
			СтрокаСоставаБлюдо.КоличествоНетто	= КоличествоНетто;
			
			// +Расчет по количеству упаковок
			СтрокаСоставаБлюдо.КоличествоУпаковок		= ?(КоличествоУпаковокБлюда=0,
				КоличествоБлюда / КоэффициетРассчитываемойУпаковки, КоличествоУпаковокБлюда);
			СтрокаСоставаБлюдо.КоличествоУпаковокВыход	= ?(КоличествоУпаковокВыход=0,
				КоличествоВыход / КоэффициетРассчитываемойУпаковки, КоличествоУпаковокВыход);
			СтрокаСоставаБлюдо.КоличествоУпаковокНетто	= ?(КоличествоУпаковокНетто=0,
				КоличествоНетто / КоэффициетРассчитываемойУпаковки, КоличествоУпаковокНетто);
			// -Расчет по количеству упаковок
			
			СтрокаСоставаБлюдо.СтатьяКалькуляции = СтатьяКалькуляцииБлюда;
			СтрокаСоставаБлюдо.Серия = Серия;
			СтрокаСоставаБлюдо.Назначение = Назначение;
			
			Если НЕ ВыпускПродукции=Неопределено Тогда
				СортироватьПоНомеруСтрокиРецептуры	= Ложь;
			Иначе
				СортироватьПоНомеруСтрокиРецептуры	= Истина;
			КонецЕсли;
		ИначеЕсли ТипЗнч(СтруктураРассчитываемогоБлюдо) = Тип("Структура") Тогда
			// Рецептура не заполнена, но передана структура блюда.
			// Добавим в дерево состава одну строку - рассчитываемое блюдо.
			// Это нужно для учета остатков блюд.
			СтрокаСоставаБлюдо = СтрокаСостава.Строки.Добавить();
			//+УмноеОкругление
			СтрокаСоставаБлюдо.ИдентификаторСтрокиДерева = Новый УникальныйИдентификатор;
			//-УмноеОкругление
			СтрокаСоставаБлюдо.Номенклатура		= СтруктураРассчитываемогоБлюдо.Блюдо;
			СтрокаСоставаБлюдо.Характеристика	= СтруктураРассчитываемогоБлюдо.Характеристика;
			СтрокаСоставаБлюдо.Рецептура		= Рецептура;
			СтрокаСоставаБлюдо.ЕдиницаИзмерения = СтруктураРассчитываемогоБлюдо.ЕдиницаИзмерения;
			
			//СтрокаСоставаБлюдо.Коэффициент		= питОбщегоНазначения.ПолучитьКоэффициентУпаковки(СтруктураРассчитываемогоБлюдо.Блюдо, СтруктураРассчитываемогоБлюдо.ЕдиницаИзмерения);
			СтрокаСоставаБлюдо.Коэффициент = 1;
			
			СтрокаСоставаБлюдо.Количество		= КоличествоБлюда;
			СтрокаСоставаБлюдо.КоличествоВыход	= КоличествоВыход;
			СтрокаСоставаБлюдо.КоличествоНетто	= КоличествоНетто;
			
			СтрокаСоставаБлюдо.КоличествоУпаковок		= КоличествоУпаковокБлюда;
			СтрокаСоставаБлюдо.КоличествоУпаковокВыход	= КоличествоУпаковокВыход;
			СтрокаСоставаБлюдо.КоличествоУпаковокНетто	= КоличествоУпаковокНетто;
			
			СтрокаСоставаБлюдо.Серия		= Серия;
			СтрокаСоставаБлюдо.Назначение	= Назначение;
			
			Если НЕ(ЭтоБизнесЛанч) Тогда
				
				// Выходим из функции, у блюда нет состава.
				СписокЗащитыОтЗацикливания.Удалить(ТекСтрока);
				
				Возврат СтрокаСостава;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	// Идем по составу
	Запрос=Новый Запрос();
	
	//+БизнесЛанч
	
	Если НЕ ВыпускПродукции = Неопределено 
		И ТипЗнч(ВыпускПродукции) = Тип("ДокументОбъект.питВыпускБлюд") Тогда
		ЭтоВыпускБлюд = Истина;
	Иначе
		ЭтоВыпускБлюд = Ложь;
	КонецЕсли;
	
	Если ЭтоВыпускБлюд И ВыпускПродукции.РежимПриготовленияМодификаторов = Перечисления.питВидыПроизводстваМодификаторов.Обеспечивать Тогда
		МодификаторыНеГотовятся = Истина;
	Иначе
		МодификаторыНеГотовятся = Ложь;
	КонецЕсли;
	
	Текст = "";
	
	//+ПараметризацияРецептур
	ИспользоватьПараметризацию = ПолучитьФункциональнуюОпцию("ИспользоватьПараметризациюРесурсныхСпецификаций");
	Текст = Текст + питУправлениеСвойствами.ТекстЗапросаОтборПоСвойствам("ВТОтборПоСвойствам");
	Запрос.УстановитьПараметр("Спецификация", Рецептура);
	Если питПодразделениеПроизводства = Неопределено Тогда
		Если ТипЗнч(ВыпускПродукции) = Тип("ДокументОбъект.питВыпускБлюд")
			ИЛИ ТипЗнч(ВыпускПродукции) = Тип("ДокументОбъект.питПланМеню")
			ИЛИ ТипЗнч(ВыпускПродукции) = Тип("ДокументСсылка.питВыпускБлюд")
			ИЛИ ТипЗнч(ВыпускПродукции) = Тип("ДокументСсылка.питПланМеню")
			ИЛИ (ТипЗнч(ВыпускПродукции) = Тип("Структура") И ВыпускПродукции.Свойство("Подразделение")) Тогда
			Запрос.УстановитьПараметр("питПодразделениеРецептуры"	, ВыпускПродукции.Подразделение);
		ИначеЕсли ЗначениеЗаполнено(Рецептура) Тогда
			Запрос.УстановитьПараметр("питПодразделениеРецептуры"	, РецептураРеквизиты.Подразделение);
		КонецЕсли;
	Иначе
		Запрос.УстановитьПараметр("питПодразделениеРецептуры"	, питПодразделениеПроизводства);
	КонецЕсли;
	Запрос.УстановитьПараметр("Номенклатура"				, РецептураРеквизиты.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика"				, РецептураРеквизиты.Характеристика);
	Запрос.УстановитьПараметр("ИспользоватьПараметризацию"	, ИспользоватьПараметризацию);
	//-ПараметризацияРецептур
	
	Если ЭтоБизнесЛанч
		И ЭтоВыпускБлюд Тогда
		
		Текст = Текст + " ВЫБРАТЬ
		|	ВЫРАЗИТЬ(тчМодификаторы.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
		|	тчМодификаторы.Характеристика КАК Характеристика,
		|	тчМодификаторы.КоличествоНоменклатуры КАК КоличествоНоменклатуры,
		|	тчМодификаторы.КоличествоУпаковокНоменклатуры КАК КоличествоУпаковокНоменклатуры,
		|	тчМодификаторы.УпаковкаНоменклатуры КАК УпаковкаНоменклатуры,
		|	тчМодификаторы.ИдентификаторСтроки КАК ИдентификаторСтроки,
		//+АвтовыборНоменклатуры
		|	ЗНАЧЕНИЕ(Перечисление.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ) КАК СпособАвтовыбораНоменклатуры,
		|	ЗНАЧЕНИЕ(Перечисление.СпособыАвтовыбораХарактеристики.УказываетсяВНСИ) КАК СпособАвтовыбораХарактеристики,
		|	"""" КАК СвойствоСодержащееНоменклатуру,
		|	"""" КАК АлгоритмАвтовыбораХарактеристики,
		|	"""" КАК КлючСвязи,
		//-АвтовыборНоменклатуры
		|	тчМодификаторы.Рецептура КАК Рецептура
		|ПОМЕСТИТЬ питВыпускБлюдМодификаторы
		|ИЗ
		|	&Модификаторы КАК тчМодификаторы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	питВыпускБлюдМодификаторы.Номенклатура КАК Номенклатура,
		|	питВыпускБлюдМодификаторы.Характеристика КАК Характеристика,
		|	питВыпускБлюдМодификаторы.Номенклатура.питВидНоменклатуры КАК НоменклатурапитВидНоменклатуры,
		//+АвтовыборНоменклатуры
		|	питВыпускБлюдМодификаторы.СпособАвтовыбораНоменклатуры КАК СпособАвтовыбораНоменклатуры,
		|	питВыпускБлюдМодификаторы.СпособАвтовыбораХарактеристики КАК СпособАвтовыбораХарактеристики,
		|	питВыпускБлюдМодификаторы.СвойствоСодержащееНоменклатуру КАК СвойствоСодержащееНоменклатуру,
		|	питВыпускБлюдМодификаторы.АлгоритмАвтовыбораХарактеристики КАК АлгоритмАвтовыбораХарактеристики,
		|	питВыпускБлюдМодификаторы.КлючСвязи КАК КлючСвязи,
		//-АвтовыборНоменклатуры
		|	СУММА(питВыпускБлюдМодификаторы.КоличествоНоменклатуры) КАК КоличествоБрутто,
		|	СУММА(питВыпускБлюдМодификаторы.КоличествоНоменклатуры) КАК КоличествоНетто,
		|	СУММА(питВыпускБлюдМодификаторы.КоличествоНоменклатуры) КАК КоличествоВыход,
		|	СУММА(питВыпускБлюдМодификаторы.КоличествоУпаковокНоменклатуры) КАК КоличествоУпаковокБрутто,
		|	СУММА(питВыпускБлюдМодификаторы.КоличествоУпаковокНоменклатуры) КАК КоличествоУпаковокНетто,
		|	СУММА(питВыпускБлюдМодификаторы.КоличествоУпаковокНоменклатуры) КАК КоличествоУпаковокВыход,
		|	СУММА(питВыпускБлюдМодификаторы.КоличествоНоменклатуры) КАК КоличествоБруттоИзМодификаторов,
		|	СУММА(питВыпускБлюдМодификаторы.КоличествоНоменклатуры) КАК КоличествоНеттоИзМодификаторов,
		|	СУММА(питВыпускБлюдМодификаторы.КоличествоНоменклатуры) КАК КоличествоВыходИзМодификаторов,
		|	СУММА(питВыпускБлюдМодификаторы.КоличествоУпаковокНоменклатуры) КАК КоличествоУпаковокБруттоИзМодификаторов,
		|	СУММА(питВыпускБлюдМодификаторы.КоличествоУпаковокНоменклатуры) КАК КоличествоУпаковокНеттоИзМодификаторов,
		|	СУММА(питВыпускБлюдМодификаторы.КоличествоУпаковокНоменклатуры) КАК КоличествоУпаковокВыходИзМодификаторов,
		|	питВыпускБлюдМодификаторы.УпаковкаНоменклатуры КАК ЕдиницаИзмерения,
		|	питВыпускБлюдМодификаторы.Рецептура КАК Рецептура,
		|	&МодификаторыНеГотовятся КАК НеГотовить,
		|	ЛОЖЬ КАК Специя,
		|	ИСТИНА КАК ЗапретитьЗамены,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Замена,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаЗамены,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК НазначениеЗамены,
		|	1 КАК КоэффициентЗамены,
		|	&СтатьяКалькуляции КАК СтатьяКалькуляции
		|ИЗ
		|	питВыпускБлюдМодификаторы КАК питВыпускБлюдМодификаторы
		|ГДЕ
		|	питВыпускБлюдМодификаторы.ИдентификаторСтроки = &ИдентификаторСтроки
		|	И питВыпускБлюдМодификаторы.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	питВыпускБлюдМодификаторы.УпаковкаНоменклатуры,
		|	питВыпускБлюдМодификаторы.Номенклатура,
		|	питВыпускБлюдМодификаторы.Характеристика,
		|	питВыпускБлюдМодификаторы.Рецептура,
		//+АвтовыборНоменклатуры
		|	питВыпускБлюдМодификаторы.СпособАвтовыбораНоменклатуры,
		|	питВыпускБлюдМодификаторы.СпособАвтовыбораХарактеристики,
		|	питВыпускБлюдМодификаторы.СвойствоСодержащееНоменклатуру,
		|	питВыпускБлюдМодификаторы.АлгоритмАвтовыбораХарактеристики,
		|	питВыпускБлюдМодификаторы.КлючСвязи,
		//-АвтовыборНоменклатуры
		|	питВыпускБлюдМодификаторы.Номенклатура.питВидНоменклатуры";
		Запрос.Текст	= Текст;
		Запрос.УстановитьПараметр("ИдентификаторСтроки", СтруктураРассчитываемогоБлюдо.ИдентификаторСтроки);
		Запрос.УстановитьПараметр("СтатьяКалькуляции", ВыпускПродукции.СтатьяКалькуляции);
		Запрос.УстановитьПараметр("Модификаторы", ВыпускПродукции.Модификаторы);
		Запрос.УстановитьПараметр("МодификаторыНеГотовятся", МодификаторыНеГотовятся);
		
		//-БизнесЛанч
		
	Иначе
		
		Если ЭтоВыпускБлюд И НЕ ВерхнееБлюдо = Неопределено Тогда
			ДобавитьМодификаторыВСостав = Истина;
		Иначе
			ДобавитьМодификаторыВСостав = Ложь;
		КонецЕсли;
		
		Если ДобавитьМодификаторыВСостав Тогда
			
			Текст = Текст + "ВЫБРАТЬ РАЗРЕШЕННЫЕ 
			|	ВложенныйЗапрос.Номенклатура,
			|	ВложенныйЗапрос.Характеристика,
			|	ВложенныйЗапрос.Номенклатура.питВидНоменклатуры КАК НоменклатурапитВидНоменклатуры,
			|	ВложенныйЗапрос.КоличествоБрутто,
			|	ВложенныйЗапрос.КоличествоНетто,
			|	ВложенныйЗапрос.КоличествоВыход,
			|	ВложенныйЗапрос.КоличествоУпаковокБрутто,
			|	ВложенныйЗапрос.КоличествоУпаковокНетто,
			|	ВложенныйЗапрос.КоличествоУпаковокВыход,
			|	ВложенныйЗапрос.ЕдиницаИзмерения,
			|	ВложенныйЗапрос.Рецептура,
			|	ВложенныйЗапрос.Специя,
			|	ВложенныйЗапрос.ЗапретитьЗамены,
			|	ВложенныйЗапрос.Замена,
			|	ВложенныйЗапрос.ХарактеристикаЗамены,
			|	ВложенныйЗапрос.НазначениеЗамены,
			|	ВложенныйЗапрос.КоэффициентЗамены,
			//+АвтовыборНоменклатуры
			|	ВложенныйЗапрос.СпособАвтовыбораНоменклатуры КАК СпособАвтовыбораНоменклатуры,
			|	ВложенныйЗапрос.СпособАвтовыбораХарактеристики КАК СпособАвтовыбораХарактеристики,
			|	ВложенныйЗапрос.СвойствоСодержащееНоменклатуру КАК СвойствоСодержащееНоменклатуру,
			|	ВложенныйЗапрос.АлгоритмАвтовыбораХарактеристики КАК АлгоритмАвтовыбораХарактеристики,
			|	ВложенныйЗапрос.КлючСвязи КАК КлючСвязи,
			//-АвтовыборНоменклатуры
			|	ВложенныйЗапрос.СтатьяКалькуляции
			|ПОМЕСТИТЬ втСоставРецептуры
			|ИЗ
			|	(ВЫБРАТЬ
			|		РецептураТовары.Номенклатура КАК Номенклатура,
			|		РецептураТовары.Характеристика КАК Характеристика,
			|		РецептураТовары.КоличествоБрутто КАК КоличествоБрутто,
			|		РецептураТовары.КоличествоНетто КАК КоличествоНетто,
			|		РецептураТовары.КоличествоВыход КАК КоличествоВыход,
			|		РецептураТовары.КоличествоУпаковокБрутто КАК КоличествоУпаковокБрутто,
			|		РецептураТовары.КоличествоУпаковокНетто КАК КоличествоУпаковокНетто,
			|		РецептураТовары.КоличествоУпаковокВыход КАК КоличествоУпаковокВыход,
			|		РецептураТовары.Упаковка КАК ЕдиницаИзмерения,
			|		РецептураТовары.Рецептура КАК Рецептура,
			|		РецептураТовары.Специя КАК Специя,
			|		РецептураТовары.ЗапретитьЗамены КАК ЗапретитьЗамены,
			|		РецептураТовары.Замена КАК Замена,
			|		РецептураТовары.ХарактеристикаЗамены КАК ХарактеристикаЗамены,
			|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК НазначениеЗамены,
			|		РецептураТовары.КоэффициентЗамены КАК КоэффициентЗамены,
			//+АвтовыборНоменклатуры
			|		РецептураТовары.СпособАвтовыбораНоменклатуры КАК СпособАвтовыбораНоменклатуры,
			|		РецептураТовары.СпособАвтовыбораХарактеристики КАК СпособАвтовыбораХарактеристики,
			|		РецептураТовары.СвойствоСодержащееНоменклатуру КАК СвойствоСодержащееНоменклатуру,
			|		РецептураТовары.АлгоритмАвтовыбораХарактеристики КАК АлгоритмАвтовыбораХарактеристики,
			|		РецептураТовары.КлючСвязи КАК КлючСвязи,
			//-АвтовыборНоменклатуры
			|		РецептураТовары.СтатьяКалькуляции КАК СтатьяКалькуляции
			|	ИЗ
			|		Документ.питРецептура.Ингредиенты КАК РецептураТовары
			//+ПараметризацияРецептур
			|			ЛЕВОЕ СОЕДИНЕНИЕ ВТОтборПоСвойствам КАК ВТОтборПоСвойствам
			|				ПО РецептураТовары.КлючСвязи = ВТОтборПоСвойствам.КлючСвязи
			//-ПараметризацияРецептур
			|	ГДЕ
			|		РецептураТовары.Ссылка = &Ссылка
			//+ПараметризацияРецептур
			|		И ЕстьNULL(ВТОтборПоСвойствам.Используется, Истина)
			//-ПараметризацияРецептур
			|	) КАК ВложенныйЗапрос
			|;
			|ВЫБРАТЬ
			|	ВЫРАЗИТЬ(тчМодификаторы.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
			|	тчМодификаторы.Характеристика КАК Характеристика,
			|	тчМодификаторы.КоличествоНоменклатуры КАК КоличествоНоменклатуры,
			|	тчМодификаторы.КоличествоУпаковокНоменклатуры КАК КоличествоУпаковокНоменклатуры,
			|	тчМодификаторы.УпаковкаНоменклатуры КАК УпаковкаНоменклатуры,
			|	тчМодификаторы.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	тчМодификаторы.Рецептура КАК Рецептура
			|ПОМЕСТИТЬ
			|	питВыпускБлюдМодификаторы
			|ИЗ 
			|	&Модификаторы КАК тчМодификаторы
			|;
			|//////////////////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
			|	ВложенныйЗапрос.Характеристика КАК Характеристика,
			|	ВложенныйЗапрос.НоменклатурапитВидНоменклатуры КАК НоменклатурапитВидНоменклатуры,
			|	ВложенныйЗапрос.КоличествоБрутто КАК КоличествоБрутто,
			|	ВложенныйЗапрос.КоличествоНетто КАК КоличествоНетто,
			|	ВложенныйЗапрос.КоличествоВыход КАК КоличествоВыход,
			|	ВложенныйЗапрос.КоличествоУпаковокБрутто КАК КоличествоУпаковокБрутто,
			|	ВложенныйЗапрос.КоличествоУпаковокНетто КАК КоличествоУпаковокНетто,
			|	ВложенныйЗапрос.КоличествоУпаковокВыход КАК КоличествоУпаковокВыход,
			|	0 КАК КоличествоБруттоИзМодификаторов,
			|	0 КАК КоличествоНеттоИзМодификаторов,
			|	0 КАК КоличествоВыходИзМодификаторов,
			|	0 КАК КоличествоУпаковокБруттоИзМодификаторов,
			|	0 КАК КоличествоУпаковокНеттоИзМодификаторов,
			|	0 КАК КоличествоУпаковокВыходИзМодификаторов,
			|	ВложенныйЗапрос.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ВложенныйЗапрос.Рецептура КАК Рецептура,
			|	ЛОЖЬ КАК НеГотовить,
			|	ВложенныйЗапрос.Специя КАК Специя,
			|	ВложенныйЗапрос.ЗапретитьЗамены КАК ЗапретитьЗамены,
			|	ВложенныйЗапрос.Замена КАК Замена,
			|	ВложенныйЗапрос.ХарактеристикаЗамены КАК ХарактеристикаЗамены,
			|	ВложенныйЗапрос.НазначениеЗамены КАК НазначениеЗамены,
			|	ВложенныйЗапрос.КоэффициентЗамены КАК КоэффициентЗамены,
			//+АвтовыборНоменклатуры
			|	ВложенныйЗапрос.СпособАвтовыбораНоменклатуры КАК СпособАвтовыбораНоменклатуры,
			|	ВложенныйЗапрос.СпособАвтовыбораХарактеристики КАК СпособАвтовыбораХарактеристики,
			|	ВложенныйЗапрос.СвойствоСодержащееНоменклатуру КАК СвойствоСодержащееНоменклатуру,
			|	ВложенныйЗапрос.АлгоритмАвтовыбораХарактеристики КАК АлгоритмАвтовыбораХарактеристики,
			|	ВложенныйЗапрос.КлючСвязи КАК КлючСвязи,
			//-АвтовыборНоменклатуры
			|	ВложенныйЗапрос.СтатьяКалькуляции КАК СтатьяКалькуляции
			|ПОМЕСТИТЬ
			|	втСоставСМодификаторами
			|ИЗ 
			|	втСоставРецептуры КАК ВложенныйЗапрос
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	питВыпускБлюдМодификаторы.Номенклатура КАК Номенклатура,
			|	питВыпускБлюдМодификаторы.Характеристика КАК Характеристика,
			|	питВыпускБлюдМодификаторы.Номенклатура.питВидНоменклатуры КАК НоменклатурапитВидНоменклатуры,
			|	питВыпускБлюдМодификаторы.КоличествоНоменклатуры КАК КоличествоБрутто,
			|	питВыпускБлюдМодификаторы.КоличествоНоменклатуры КАК КоличествоНетто,
			|	питВыпускБлюдМодификаторы.КоличествоНоменклатуры КАК КоличествоВыход,
			|	питВыпускБлюдМодификаторы.КоличествоУпаковокНоменклатуры КАК КоличествоУпаковокБрутто,
			|	питВыпускБлюдМодификаторы.КоличествоУпаковокНоменклатуры КАК КоличествоУпаковокНетто,
			|	питВыпускБлюдМодификаторы.КоличествоУпаковокНоменклатуры КАК КоличествоУпаковокВыход,
			|	питВыпускБлюдМодификаторы.КоличествоНоменклатуры КАК КоличествоБруттоИзМодификаторов,
			|	питВыпускБлюдМодификаторы.КоличествоНоменклатуры КАК КоличествоНеттоИзМодификаторов,
			|	питВыпускБлюдМодификаторы.КоличествоНоменклатуры КАК КоличествоВыходИзМодификаторов,
			|	питВыпускБлюдМодификаторы.КоличествоУпаковокНоменклатуры КАК КоличествоУпаковокБруттоИзМодификаторов,
			|	питВыпускБлюдМодификаторы.КоличествоУпаковокНоменклатуры КАК КоличествоУпаковокНеттоИзМодификаторов,
			|	питВыпускБлюдМодификаторы.КоличествоУпаковокНоменклатуры КАК КоличествоУпаковокВыходИзМодификаторов,
			|	питВыпускБлюдМодификаторы.УпаковкаНоменклатуры КАК ЕдиницаИзмерения,
			|	питВыпускБлюдМодификаторы.Рецептура КАК Рецептура,
			|	&МодификаторыНеГотовятся КАК НеГотовить,
			|	ЛОЖЬ КАК Специя,
			|	ИСТИНА КАК ЗапретитьЗамены,
			|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Замена,
			|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаЗамены,
			|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК НазначениеЗамены,
			|	1 КАК КоэффициентЗамены,
			//+АвтовыборНоменклатуры
			|	ЗНАЧЕНИЕ(Перечисление.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ) КАК СпособАвтовыбораНоменклатуры,
			|	ЗНАЧЕНИЕ(Перечисление.СпособыАвтовыбораХарактеристики.УказываетсяВНСИ) КАК СпособАвтовыбораХарактеристики,
			|	"""" КАК СвойствоСодержащееНоменклатуру,
			|	"""" КАК АлгоритмАвтовыбораХарактеристики,
			|	"""" КАК КлючСвязи,
			//-АвтовыборНоменклатуры
			|	&СтатьяКалькуляции КАК СтатьяКалькуляции
			|ИЗ
			|	питВыпускБлюдМодификаторы КАК питВыпускБлюдМодификаторы
			|ГДЕ
			|	питВыпускБлюдМодификаторы.ИдентификаторСтроки = &ИдентификаторСтроки
			|	И питВыпускБлюдМодификаторы.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
			|;
			|
			|ВЫБРАТЬ
			|	втСоставСМодификаторами.Номенклатура КАК Номенклатура,
			|	втСоставСМодификаторами.Характеристика КАК Характеристика,
			|	втСоставСМодификаторами.НоменклатурапитВидНоменклатуры КАК НоменклатурапитВидНоменклатуры,
			|	СУММА(втСоставСМодификаторами.КоличествоБрутто) КАК КоличествоБрутто,
			|	СУММА(втСоставСМодификаторами.КоличествоНетто) КАК КоличествоНетто,
			|	СУММА(втСоставСМодификаторами.КоличествоВыход) КАК КоличествоВыход,
			|	СУММА(втСоставСМодификаторами.КоличествоУпаковокБрутто) КАК КоличествоУпаковокБрутто,
			|	СУММА(втСоставСМодификаторами.КоличествоУпаковокНетто) КАК КоличествоУпаковокНетто,
			|	СУММА(втСоставСМодификаторами.КоличествоУпаковокВыход) КАК КоличествоУпаковокВыход,
			|	СУММА(втСоставСМодификаторами.КоличествоБруттоИзМодификаторов) КАК КоличествоБруттоИзМодификаторов,
			|	СУММА(втСоставСМодификаторами.КоличествоНеттоИзМодификаторов) КАК КоличествоНеттоИзМодификаторов,
			|	СУММА(втСоставСМодификаторами.КоличествоВыходИзМодификаторов) КАК КоличествоВыходИзМодификаторов,
			|	СУММА(втСоставСМодификаторами.КоличествоУпаковокБруттоИзМодификаторов) КАК КоличествоУпаковокБруттоИзМодификаторов,
			|	СУММА(втСоставСМодификаторами.КоличествоУпаковокНеттоИзМодификаторов) КАК КоличествоУпаковокНеттоИзМодификаторов,
			|	СУММА(втСоставСМодификаторами.КоличествоУпаковокВыходИзМодификаторов) КАК КоличествоУпаковокВыходИзМодификаторов,
			|	втСоставСМодификаторами.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	втСоставСМодификаторами.Рецептура КАК Рецептура,
			|	втСоставСМодификаторами.Специя КАК Специя,
			|	втСоставСМодификаторами.ЗапретитьЗамены КАК ЗапретитьЗамены,
			|	втСоставСМодификаторами.Замена КАК Замена,
			|	втСоставСМодификаторами.ХарактеристикаЗамены КАК ХарактеристикаЗамены,
			|	втСоставСМодификаторами.НазначениеЗамены КАК НазначениеЗамены,
			|	втСоставСМодификаторами.КоэффициентЗамены КАК КоэффициентЗамены,
			//+АвтовыборНоменклатуры
			|	втСоставСМодификаторами.СпособАвтовыбораНоменклатуры КАК СпособАвтовыбораНоменклатуры,
			|	втСоставСМодификаторами.СпособАвтовыбораХарактеристики КАК СпособАвтовыбораХарактеристики,
			|	втСоставСМодификаторами.СвойствоСодержащееНоменклатуру КАК СвойствоСодержащееНоменклатуру,
			|	втСоставСМодификаторами.АлгоритмАвтовыбораХарактеристики КАК АлгоритмАвтовыбораХарактеристики,
			|	втСоставСМодификаторами.КлючСвязи КАК КлючСвязи,
			//-АвтовыборНоменклатуры
			|	втСоставСМодификаторами.СтатьяКалькуляции КАК СтатьяКалькуляции,
			|	втСоставСМодификаторами.НеГотовить
			|ИЗ
			|	втСоставСМодификаторами КАК втСоставСМодификаторами
			|СГРУППИРОВАТЬ ПО
			|	втСоставСМодификаторами.Номенклатура,
			|	втСоставСМодификаторами.Характеристика,
			|	втСоставСМодификаторами.НоменклатурапитВидНоменклатуры,
			|	втСоставСМодификаторами.ЕдиницаИзмерения,
			|	втСоставСМодификаторами.Рецептура,
			|	втСоставСМодификаторами.Специя,
			|	втСоставСМодификаторами.ЗапретитьЗамены,
			|	втСоставСМодификаторами.Замена,
			|	втСоставСМодификаторами.СтатьяКалькуляции,
			|	втСоставСМодификаторами.ХарактеристикаЗамены,
			|	втСоставСМодификаторами.НазначениеЗамены,
			|	втСоставСМодификаторами.КоэффициентЗамены,
			//+АвтовыборНоменклатуры
			|	втСоставСМодификаторами.СпособАвтовыбораНоменклатуры,
			|	втСоставСМодификаторами.СпособАвтовыбораХарактеристики,
			|	втСоставСМодификаторами.СвойствоСодержащееНоменклатуру,
			|	втСоставСМодификаторами.АлгоритмАвтовыбораХарактеристики,
			|	втСоставСМодификаторами.КлючСвязи,
			//-АвтовыборНоменклатуры
			|	втСоставСМодификаторами.НеГотовить
			|УПОРЯДОЧИТЬ ПО
			|	втСоставСМодификаторами.Номенклатура.Наименование
			|";
			
			Запрос.УстановитьПараметр("ИдентификаторСтроки", СтруктураРассчитываемогоБлюдо.ИдентификаторСтроки);
			Запрос.УстановитьПараметр("СтатьяКалькуляции", ВыпускПродукции.СтатьяКалькуляции);
			Запрос.УстановитьПараметр("Модификаторы", ВыпускПродукции.Модификаторы);
			
		Иначе
			
			Текст = Текст + "ВЫБРАТЬ РАЗРЕШЕННЫЕ "+?(СортироватьПоНомеруСтрокиРецептуры,"
			|	ВложенныйЗапрос.НомерСтроки,","")+"
			|	ВложенныйЗапрос.Номенклатура,
			|	ВложенныйЗапрос.Характеристика,
			|	ВложенныйЗапрос.Номенклатура.питВидНоменклатуры КАК НоменклатурапитВидНоменклатуры,
			|	ВложенныйЗапрос.КоличествоБрутто,
			|	ВложенныйЗапрос.КоличествоНетто,
			|	ВложенныйЗапрос.КоличествоВыход,
			|	ВложенныйЗапрос.КоличествоУпаковокБрутто,
			|	ВложенныйЗапрос.КоличествоУпаковокНетто,
			|	ВложенныйЗапрос.КоличествоУпаковокВыход,
			|	0 КАК КоличествоБруттоИзМодификаторов,
			|	0 КАК КоличествоНеттоИзМодификаторов,
			|	0 КАК КоличествоВыходИзМодификаторов,
			|	0 КАК КоличествоУпаковокБруттоИзМодификаторов,
			|	0 КАК КоличествоУпаковокНеттоИзМодификаторов,
			|	0 КАК КоличествоУпаковокВыходИзМодификаторов,
			|	ВложенныйЗапрос.ЕдиницаИзмерения,
			|	ВложенныйЗапрос.Рецептура,
			|	ВложенныйЗапрос.Специя,
			|	ВложенныйЗапрос.ЗапретитьЗамены,
			|	ВложенныйЗапрос.Замена,
			|	ВложенныйЗапрос.ХарактеристикаЗамены,
			|	ВложенныйЗапрос.НазначениеЗамены,
			|	ВложенныйЗапрос.КоэффициентЗамены,
			//+АвтовыборНоменклатуры
			|	ВложенныйЗапрос.СпособАвтовыбораНоменклатуры КАК СпособАвтовыбораНоменклатуры,
			|	ВложенныйЗапрос.СпособАвтовыбораХарактеристики КАК СпособАвтовыбораХарактеристики,
			|	ВложенныйЗапрос.СвойствоСодержащееНоменклатуру КАК СвойствоСодержащееНоменклатуру,
			|	ВложенныйЗапрос.АлгоритмАвтовыбораХарактеристики КАК АлгоритмАвтовыбораХарактеристики,
			|	ВложенныйЗапрос.КлючСвязи КАК КлючСвязи,
			//-АвтовыборНоменклатуры
			|	ВложенныйЗапрос.СтатьяКалькуляции,
			|	ЛОЖЬ КАК НеГотовить
			|ИЗ
			|	(ВЫБРАТЬ "+?(СортироватьПоНомеруСтрокиРецептуры,"
			|		МИНИМУМ(РецептураТовары.НомерСтроки) КАК НомерСтроки,", "")+"
			|		РецептураТовары.Номенклатура КАК Номенклатура,
			|		РецептураТовары.Характеристика КАК Характеристика,
			|		СУММА(РецептураТовары.КоличествоБрутто) КАК КоличествоБрутто,
			|		СУММА(РецептураТовары.КоличествоНетто) КАК КоличествоНетто,
			|		СУММА(РецептураТовары.КоличествоВыход) КАК КоличествоВыход,
			|		СУММА(РецептураТовары.КоличествоУпаковокБрутто) КАК КоличествоУпаковокБрутто,
			|		СУММА(РецептураТовары.КоличествоУпаковокНетто) КАК КоличествоУпаковокНетто,
			|		СУММА(РецептураТовары.КоличествоУпаковокВыход) КАК КоличествоУпаковокВыход,
			|		РецептураТовары.Упаковка КАК ЕдиницаИзмерения,
			|		РецептураТовары.Рецептура КАК Рецептура,
			|		РецептураТовары.Специя КАК Специя,
			|		РецептураТовары.ЗапретитьЗамены КАК ЗапретитьЗамены,
			|		РецептураТовары.Замена КАК Замена,
			|		РецептураТовары.ХарактеристикаЗамены КАК ХарактеристикаЗамены,
			|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК НазначениеЗамены,
			|		РецептураТовары.КоэффициентЗамены КАК КоэффициентЗамены,
			//+АвтовыборНоменклатуры
			|		РецептураТовары.СпособАвтовыбораНоменклатуры КАК СпособАвтовыбораНоменклатуры,
			|		РецептураТовары.СпособАвтовыбораХарактеристики КАК СпособАвтовыбораХарактеристики,
			|		РецептураТовары.СвойствоСодержащееНоменклатуру КАК СвойствоСодержащееНоменклатуру,
			|		РецептураТовары.АлгоритмАвтовыбораХарактеристики КАК АлгоритмАвтовыбораХарактеристики,
			|		РецептураТовары.КлючСвязи КАК КлючСвязи,
			//-АвтовыборНоменклатуры
			|		РецептураТовары.СтатьяКалькуляции КАК СтатьяКалькуляции
			|	ИЗ
			|		Документ.питРецептура.Ингредиенты КАК РецептураТовары
			//+ПараметризацияРецептур
			|			ЛЕВОЕ СОЕДИНЕНИЕ ВТОтборПоСвойствам КАК ВТОтборПоСвойствам
			|				ПО РецептураТовары.КлючСвязи = ВТОтборПоСвойствам.КлючСвязи
			//-ПараметризацияРецептур
			|	ГДЕ
			|		РецептураТовары.Ссылка = &Ссылка
			//+ПараметризацияРецептур
			|		И ЕстьNULL(ВТОтборПоСвойствам.Используется, Истина)
			//-ПараметризацияРецептур
			|	
			|	СГРУППИРОВАТЬ ПО
			|		РецептураТовары.Номенклатура,
			|		РецептураТовары.Характеристика,
			|		РецептураТовары.Упаковка,
			|		РецептураТовары.Рецептура,
			|		РецептураТовары.Специя,
			|		РецептураТовары.ЗапретитьЗамены,
			|		РецептураТовары.Замена,
			|		РецептураТовары.СтатьяКалькуляции,
			|		РецептураТовары.ХарактеристикаЗамены,
			//+АвтовыборНоменклатуры
			|		РецептураТовары.СпособАвтовыбораНоменклатуры,
			|		РецептураТовары.СпособАвтовыбораХарактеристики,
			|		РецептураТовары.СвойствоСодержащееНоменклатуру,
			|		РецептураТовары.АлгоритмАвтовыбораХарактеристики,
			|		РецептураТовары.КлючСвязи,
			//-АвтовыборНоменклатуры
			|		РецептураТовары.КоэффициентЗамены) КАК ВложенныйЗапрос
			|
			|УПОРЯДОЧИТЬ ПО "+?(СортироватьПоНомеруСтрокиРецептуры, "
			|	ВложенныйЗапрос.НомерСтроки","
			|	ВложенныйЗапрос.Номенклатура.Наименование");
			
			
		КонецЕсли;
		
		Запрос.Текст	= Текст;
		Запрос.УстановитьПараметр("Ссылка", Рецептура);
		Запрос.УстановитьПараметр("МодификаторыНеГотовятся", МодификаторыНеГотовятся);
		
	КонецЕсли;
	
	//+АвтовыборНоменклатуры
	Выгрузка = Запрос.Выполнить().Выгрузить();
	Выгрузка.Колонки.Добавить("НазначениеИнгредиентов", Новый ОписаниеТипов("СправочникСсылка.Назначения"));
	
	питУправлениеДаннымиОбИзделиях.ОбработатьНастройкиАвтовыбораНоменклатуры(Выгрузка, Рецептура, КоличествоБлюда, РецептураРеквизиты, УточненияАвтовыбора);
	//Выборка			= Запрос.Выполнить().Выбрать();
	//Пока Выборка.Следующий() Цикл
	Для каждого Выборка Из Выгрузка Цикл
		Выборка.НазначениеИнгредиентов = НазначениеИнгредиентов;
		//-АвтовыборНоменклатуры
		Если НЕ Выборка.НоменклатурапитВидНоменклатуры=Перечисления.питВидыНоменклатуры.Блюдо 
			//+АвтовыборНоменклатуры
			ИЛИ Выборка.ОшибкаАвтовыбора Тогда
			//-АвтовыборНоменклатуры
			ДобавитьСтрокуСостава(СтрокаСоставаБлюдо, Выборка, КоэффициентПересчета,,,,КэшЗаполненияДерева);
		//#Вставка
		ИначеЕсли Выборка.КоличествоБрутто = 0 Тогда
			Продолжить;
		//#КонецВставки
		Иначе
			Если НЕ МассивОтбораБлюд=Неопределено Тогда
				Если МассивОтбораБлюд.НайтиСтроки(Новый Структура("Номенклатура, Характеристика", Выборка.Номенклатура, Выборка.Характеристика)).Количество() = 0 Тогда
					СтрокаОтбора = МассивОтбораБлюд.Добавить();
					СтрокаОтбора.Номенклатура = Выборка.Номенклатура;
					СтрокаОтбора.Характеристика = Выборка.Характеристика;
				КонецЕсли;
			КонецЕсли;
			// Если не указана рецептура, пробуем найти основную
			Если Выборка.НеГотовить Тогда
				текРецептура = Документы.питРецептура.ПустаяСсылка();
				
			ИначеЕсли (Выборка.Рецептура.Пустая() или СпособРасчетаРецептуры=Перечисления.питСпособРасчетаРецептур.ПоАктуальнойРецептуре) Тогда
				Если ВыпускПродукции = Неопределено Тогда
					ВыпускПродукцииДата = ТекущаяДата();
				Иначе
					Если ТипЗнч(ВыпускПродукции) = Тип("ДокументОбъект.питВыпускБлюд") ИЛИ ТипЗнч(ВыпускПродукции) = Тип("ДокументОбъект.питПланМеню") 
						ИЛИ ТипЗнч(ВыпускПродукции) = Тип("Структура") Тогда
						ВыпускПродукцииДата = ВыпускПродукции.Дата;
					Иначе
						ВыпускПродукцииДата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыпускПродукции, "Дата");
					КонецЕсли;
				КонецЕсли;
				
				Если НЕ ПодразделенияДляПоискаРецептуры = Неопределено Тогда
					Подразделения = ПодразделенияДляПоискаРецептуры;
				КонецЕсли;
				
				Если НЕ ВыпускПродукции = Неопределено 
					И ТипЗнч(ВыпускПродукции) = Тип("ДокументОбъект.питВыпускБлюд")Тогда
					
					// Для выпуска блюд рецептура вложенных п.ф. должна искаться по организации 
					// и подразделению из шапки выпуска.
					
					Если ПодразделенияДляПоискаРецептуры = Неопределено Тогда
						Подразделения = Новый Массив(1);
						Подразделения[0] = ВыпускПродукции.Подразделение;
					КонецЕсли;
					
					ОрганизацияПоиска = ВыпускПродукции.Организация;
					
				ИначеЕсли НЕ ВыпускПродукции = Неопределено 
					И ТипЗнч(ВыпускПродукции) = Тип("Структура")Тогда
					
					// Случай, когда была передана структура со значениями организации и подразделения, 
					// по которым необходимо производить поиск.
					
					Если ПодразделенияДляПоискаРецептуры = Неопределено Тогда
						Если ВыпускПродукции.Свойство("Подразделение") Тогда
							Подразделения = Новый Массив(1);
							Подразделения[0] = ВыпускПродукции.Подразделение;
						Иначе
							Подразделения = питПроизводствоПовтИсп.НайтиПодразделенияРецептуры(Рецептура);
						КонецЕсли;
					КонецЕсли;
					
					Если ВыпускПродукции.Свойство("Организация") Тогда
						ОрганизацияПоиска = ВыпускПродукции.Организация;
					Иначе
						ОрганизацияПоиска = РецептураРеквизиты.Организация;
					КонецЕсли; 
					
				Иначе
					
					// Если ничего из вышеперечисленного не сработало,
					//,то организация и подразделения для поиска рецептуры берутся из родительской рецептуры.
					
					Если ПодразделенияДляПоискаРецептуры = Неопределено Тогда
						Подразделения = питПроизводствоПовтИсп.НайтиПодразделенияРецептуры(Рецептура);
					КонецЕсли;
					
					ОрганизацияПоиска = РецептураРеквизиты.Организация;
					
				КонецЕсли;
				
				текРецептура = ВернутьОсновнуюРецептуру(Выборка.Номенклатура, Выборка.Характеристика, Перечисления.питХозяйственныеОперации.РецептураПриготовление,ВыпускПродукцииДата,СпособРасчетаРецептуры,,ОрганизацияПоиска,ИскатьПоВсемОрганизациям,,Подразделения);
			Иначе
				текРецептура = Выборка.Рецептура;
			КонецЕсли;	
			
			СтрокаСоставаБлюдо.СтатьяКалькуляции = Выборка.СтатьяКалькуляции;
			
			// Отработаем возможные варианты
			Если НЕ ЗначениеЗаполнено(текРецептура) Тогда
				// Если не найдена рецептура, то добавим строку и сообщим, что нет состава.
				ДобавитьСтрокуСостава(СтрокаСоставаБлюдо, Выборка, КоэффициентПересчета,,,ВыпускПродукции,КэшЗаполненияДерева);
				СообщениеОбОшибке = НСтр("ru='У полуфабриката ""%1"" нет актуальной рецептуры на дату: %2.'");
				Если ВыпускПродукции = Неопределено Тогда
					ВыпускПродукцииДата = ТекущаяДата();
				Иначе
					Если ТипЗнч(ВыпускПродукции) = Тип("ДокументОбъект.питВыпускБлюд") или ТипЗнч(ВыпускПродукции) = Тип("ДокументОбъект.питПланМеню")
						ИЛИ ТипЗнч(ВыпускПродукции) = Тип("Структура") Тогда
						ВыпускПродукцииДата = ВыпускПродукции.Дата;
					Иначе
						ВыпускПродукцииДата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыпускПродукции, "Дата");
					КонецЕсли;
				КонецЕсли;
				Если НЕ Выборка.НеГотовить И НЕ питВосприниматьБлюдоБезРецептурыКакТовар Тогда
					СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеОбОшибке, Выборка.Номенклатура, ВыпускПродукцииДата);
					//ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, ВыпускПродукции, "Производство", "Объект", );
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке);
				КонецЕсли;
			Иначе
				СтрокаТаблицы = Неопределено;
				СтрокиТаблицы = СписокЗащитыОтЗацикливания.НайтиСтроки(Новый Структура("Номенклатура, Характеристика",Выборка.Номенклатура, Выборка.Характеристика));
				Если СтрокиТаблицы.Количество() Тогда
					СтрокаТаблицы = СтрокиТаблицы[0];
				КонецЕсли;
				Если НЕ СтрокаТаблицы=Неопределено Тогда
					// Зацикливание. Добавим блюдо без разбора его состава. 
					ДобавитьСтрокуСостава(СтрокаСоставаБлюдо, Выборка, КоэффициентПересчета, текРецептура, Истина, ВыпускПродукции,КэшЗаполненияДерева);
					СообщениеОбОшибке = НСтр("ru='Обнаружена циклическая ссылка ""%1"".'");
					СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеОбОшибке, Выборка.Номенклатура);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, ВыпускПродукции, "Производство", "Объект", );
					Продолжить;
				Иначе
					// Проверим ТЧ Товары у Рецептуры, если она пуста сообщим об этом.
					Если НЕ ОтключитьДополнительныеПроверки Тогда
						Если текРецептура.Ингредиенты.Количество() = 0 Тогда
							СообщениеОбОшибке = НСтр("ru='У блюда ""%1"" не заполнена табличная часть в документе ""%2"".'");
							СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СообщениеОбОшибке, Выборка.Номенклатура, текРецептура);
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке, ВыпускПродукции, "Производство", "Объект", );
						КонецЕсли;
					КонецЕсли;
					
					// Работаем с найденной рецептурой
					текКоличествоБлюда = (Выборка.КоличествоБрутто-Выборка.КоличествоБруттоИзМодификаторов)	*КоэффициентПересчета + Выборка.КоличествоБруттоИзМодификаторов;
					текКоличествоВыход = (Выборка.КоличествоВыход-Выборка.КоличествоВыходИзМодификаторов)	*КоэффициентПересчета + Выборка.КоличествоВыходИзМодификаторов;
					текКоличествоНетто = (Выборка.КоличествоНетто-Выборка.КоличествоНеттоИзМодификаторов)	*КоэффициентПересчета + Выборка.КоличествоНеттоИзМодификаторов;
					текКоличествоУпаковокБлюда = (Выборка.КоличествоУпаковокБрутто-Выборка.КоличествоУпаковокБруттоИзМодификаторов)	*КоэффициентПересчета + Выборка.КоличествоУпаковокБруттоИзМодификаторов;
					текКоличествоУпаковокВыход = (Выборка.КоличествоУпаковокВыход-Выборка.КоличествоУпаковокВыходИзМодификаторов)	*КоэффициентПересчета + Выборка.КоличествоУпаковокВыходИзМодификаторов;
					текКоличествоУпаковокНетто = (Выборка.КоличествоУпаковокНетто-Выборка.КоличествоУпаковокНеттоИзМодификаторов)	*КоэффициентПересчета + Выборка.КоличествоУпаковокНеттоИзМодификаторов;
					
					// +Расчет по количеству упаковок
					// В рекурсивный вызов добавлена единица измерения.
					// -Расчет по количеству упаковок
					РассчитатьДеревоСоставаРецептуры(текРецептура,СтрокаСоставаБлюдо,текКоличествоБлюда,текКоличествоВыход,текКоличествоНетто,текКоличествоУпаковокБлюда,текКоличествоУпаковокВыход,текКоличествоУпаковокНетто, СписокЗащитыОтЗацикливания,ВыпускПродукции,,СпособРасчетаРецептуры,,ИскатьПоВсемОрганизациям,МассивОтбораБлюд,,, НазначениеИнгредиентов, ОтключитьДополнительныеПроверки, КэшЗаполненияДерева, питВосприниматьБлюдоБезРецептурыКакТовар, ПодразделенияДляПоискаРецептуры, питПодразделениеПроизводства, Выборка.Характеристика, УточненияАвтовыбора, Выборка.ЕдиницаИзмерения);
					
					Если ВыпускПродукции=Неопределено Тогда
						// Формирование для отчетов, берем единицу блюда из таблицы текущей рецептуры.
						// Поэтому нужно исправить строку.
						СтруктураПоискаИнгредиентаВДеревеСостава = Новый Структура();
						СтруктураПоискаИнгредиентаВДеревеСостава.Вставить("Номенклатура",Выборка.Номенклатура);
						СтруктураПоискаИнгредиентаВДеревеСостава.Вставить("Характеристика",Выборка.Характеристика);
						СтруктураПоискаИнгредиентаВДеревеСостава.Вставить("Рецептура",текРецептура);
						//СтруктураПоискаИнгредиентаВДеревеСостава.Вставить("ЕдиницаИзмерения",Выборка.ЕдиницаИзмерения);
						СтруктураПоискаИнгредиентаВДеревеСостава.Вставить("Специя",Выборка.Специя);
						СтруктураПоискаИнгредиентаВДеревеСостава.Вставить("ЗапретитьЗамены",Выборка.ЗапретитьЗамены);
						// Поищем строку ингредиента на данном уровне вложенности,
						// чтобы в дальнейшем не задваивать количество родителя при приготовлении.
						МассивСтрокИнгредиентаВДеревеСостава = СтрокаСоставаБлюдо.Строки.НайтиСтроки(СтруктураПоискаИнгредиентаВДеревеСостава,Ложь);
						
						Если МассивСтрокИнгредиентаВДеревеСостава.Количество()>0 Тогда
							текСтрокаСостава = МассивСтрокИнгредиентаВДеревеСостава[0];
							Если текСтрокаСостава.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения Тогда
								// Не нужно изменять
							ИначеЕсли Окр(текСтрокаСостава.Количество,6) = Окр(Выборка.КоличествоБрутто,6) Тогда
								текСтрокаСостава.Количество       = Выборка.КоличествоБрутто;
								текСтрокаСостава.КоличествоНетто  = Выборка.КоличествоНетто;
								текСтрокаСостава.КоличествоВыход  = Выборка.КоличествоВыход;
								
								текСтрокаСостава.КоличествоУпаковок      = Выборка.КоличествоУпаковокБрутто;
								текСтрокаСостава.КоличествоУпаковокНетто = Выборка.КоличествоУпаковокНетто;
								текСтрокаСостава.КоличествоУпаковокВыход = Выборка.КоличествоУпаковокВыход;
								
								текСтрокаСостава.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
								//текСтрокаСостава.Коэффициент      = Выборка.Коэффициент;
							Иначе
								// Возможно было объединение строк
								Если текСтрокаСостава.КоличествоУпаковок = 0 ИЛИ Выборка.КоличествоУпаковокБрутто = 0 Тогда
									КоэффициентИзмененияЕдиницы = 1;
								Иначе
									КоэффициентИзмененияЕдиницы = (текСтрокаСостава.Количество/текСтрокаСостава.КоличествоУпаковок) / (Выборка.КоличествоБрутто / Выборка.КоличествоУпаковокБрутто);
								КонецЕсли;
								текСтрокаСостава.Количество       = текСтрокаСостава.Количество;
								текСтрокаСостава.КоличествоНетто  = текСтрокаСостава.КоличествоНетто;
								текСтрокаСостава.КоличествоВыход  = текСтрокаСостава.КоличествоВыход;
								
								текСтрокаСостава.КоличествоУпаковок       = текСтрокаСостава.КоличествоУпаковок*КоэффициентИзмененияЕдиницы;
								текСтрокаСостава.КоличествоУпаковокНетто  = текСтрокаСостава.КоличествоУпаковокНетто*КоэффициентИзмененияЕдиницы;
								текСтрокаСостава.КоличествоУпаковокВыход  = текСтрокаСостава.КоличествоУпаковокВыход*КоэффициентИзмененияЕдиницы;
								
								текСтрокаСостава.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
								//текСтрокаСостава.Коэффициент      = Выборка.Коэффициент;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;
	
	СписокЗащитыОтЗацикливания.Удалить(ТекСтрока);
	
	Возврат СтрокаСостава;
КонецФункции	

// Заполняет производственную таблицу значений для всех блюд данного выпуска.
//
// Параметры:
//	ВыпускПродукции – документ Выпуск продукции,  для которого будет заполнена производственная таблица.
//	РежимЗаписиПроведение - режим записи документа ВыпускПродукции, необходим для определения режима проведения.
//  УчетВПродажныхЦенах - булево, сохраняется признак ведения розничного учета в организации,
//  кэш значения учетной политики.
//  
// Возвращаемое значение:
//	Полностью заполненная Таблица производства типа ТаблицаЗначений.
//
Функция ЗаполнитьПроизводственныеТаблицыПоРецептуре(ВыпускПродукции, РежимЗаписиПроведение=Ложь, УчетВПродажныхЦенах=Неопределено, ОтключитьДополнительныеПроверки = Ложь, МассивДеревьевСостава = Неопределено, УточненияАвтовыбора = Неопределено) Экспорт
	
	Возврат питЛицензированиеСервер.ЗаполнитьПроизводственныеТаблицыПоРецептуре(ВыпускПродукции, РежимЗаписиПроведение, УчетВПродажныхЦенах, ОтключитьДополнительныеПроверки, МассивДеревьевСостава, УточненияАвтовыбора);
	
КонецФункции

// Заполняет фактические количества для списания ингредиентов в производственной таблице.
//
// Параметры:
//	ТаблицаПроизводства – рассчитанная таблица производства.
//	ТаблицаЗамен – рассчитанная таблица замен в производстве.
//	СтруктураПараметров - дополнительная структура, содержащая дополнительные сведения о производстве.
//	Пересчитывать - признак необходимости перезаполнения нормативного количества производства.
//  УчетВПродажныхЦенах - булево, сохраняется признак ведения розничного учета в организации,
//  кэш значения учетной политики.
//
// Возвращаемое значение:
//	В параметре ТаблицаПроизводства - таблица заполненная фактическими количествами для списания ингредиентов.
//
Процедура ЗаполнитьПроизводственныеТаблицыФактическимКоличеством(ТаблицаПроизводства, ТаблицаЗамен = Неопределено, СтруктураПараметров, Пересчитывать=Истина, УчетВПродажныхЦенах=Неопределено) Экспорт
	// Таблица нормативного расхода сформирована.
	// Теперь пройдемся по ней и рассчитаем фактические количества, и факт использования замен.
	#Область ОпределениеПеременных
	//Здесь определяем переменные которые задействованы в используемых механизмах, но определены в закомментированных блоках
	Если СтруктураПараметров.Свойство("СчитатьФактВТаблицеЗамен") Тогда
		СчитатьФактВТаблицеЗамен = СтруктураПараметров.СчитатьФактВТаблицеЗамен;
	Иначе
		СчитатьФактВТаблицеЗамен = Ложь;
	КонецЕсли;
		
	#КонецОбласти
	
	#Область Замены
	//ОбщептЕРП!!!
	
	// Считаем параметры для расчета
	Склад = Справочники.Склады.ПустаяСсылка();
	Если СтруктураПараметров.Свойство("Склад") И (ТипЗнч(СтруктураПараметров.Склад)=Тип("СправочникСсылка.Склады")) Тогда
		Склад = СтруктураПараметров.Склад;
	КонецЕсли;
	
	ВсеСклады = Ложь;
	ТекстЗапросаВсеСклады = "";
	СчитатьФактВТаблицеЗамен = СтруктураПараметров.СчитатьФактВТаблицеЗамен;
	
	РежимИспользованияАналогов = СтруктураПараметров.РежимИспользованияАналогов;
	
	Если СчитатьФактВТаблицеЗамен Тогда
		ПоляОтбораАналогов = "Блюдо, ХарактеристикаБлюда, Рецептура, Автопередел, ИнгредиентИсходный, ХарактеристикаИнгредиентаИсходного, НазначениеИнгредиентаИсходного, УпаковкаИнгредиентаИсходный";
		СтрокаОтбораАналогов = Новый Структура(ПоляОтбораАналогов);
		// Здесь ТаблицаЗамен имеет тип "ТаблицаЗначений".
		ТаблицаЗамен.Индексы.Добавить(ПоляОтбораАналогов);
		
		ПоляИнгредиентов = "Ингредиент,ХарактеристикаИнгредиента,НазначениеИнгредиента";
		
		// Здесь аналоги уже известны, поэтому нет запроса по аналогам.
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаИнгредиентов.Ингредиент КАК Ингредиент,
		|	ТаблицаИнгредиентов.ХарактеристикаИнгредиента КАК ХарактеристикаИнгредиента,
		|	ТаблицаИнгредиентов.НазначениеИнгредиента КАК НазначениеИнгредиента
		|ПОМЕСТИТЬ ТаблицаИнгредиентов
		|ИЗ
		|	&ТаблицаИнгредиентов КАК ТаблицаИнгредиентов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	"""" КАК ПустаяТаблица
		|";
		
	Иначе
		ПоляИнгредиентов = "Ингредиент, ХарактеристикаИнгредиента, НазначениеИнгредиента, ЗапретитьЗамены, Замена, ХарактеристикаЗамены, НазначениеЗамены, ЕдиницаИзмеренияНоменклатуры, ЕдиницаИзмеренияИнгредиента, КоэффициентЗамены";
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаИнгредиентов.Ингредиент КАК Ингредиент,
		|	ТаблицаИнгредиентов.ХарактеристикаИнгредиента КАК ХарактеристикаИнгредиента,
		|	ТаблицаИнгредиентов.НазначениеИнгредиента КАК НазначениеИнгредиента,
		|	ТаблицаИнгредиентов.ЗапретитьЗамены КАК ЗапретитьЗамены,
		|	ВЫРАЗИТЬ(ТаблицаИнгредиентов.Замена КАК Справочник.Номенклатура) КАК Замена,
		|	ВЫРАЗИТЬ(ТаблицаИнгредиентов.ХарактеристикаЗамены КАК Справочник.ХарактеристикиНоменклатуры) КАК ХарактеристикаЗамены,
		|	ВЫРАЗИТЬ(ТаблицаИнгредиентов.НазначениеЗамены КАК Справочник.Назначения) КАК НазначениеЗамены,
		|	ТаблицаИнгредиентов.ЕдиницаИзмеренияНоменклатуры КАК ЕдиницаИзмеренияНоменклатуры,
		|	ТаблицаИнгредиентов.ЕдиницаИзмеренияИнгредиента КАК ЕдиницаИзмеренияИнгредиента,
		|	ТаблицаИнгредиентов.КоэффициентЗамены КАК КоэффициентЗамены
		|ПОМЕСТИТЬ ТаблицаИнгредиентов
		|ИЗ
		|	&ТаблицаИнгредиентов КАК ТаблицаИнгредиентов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаменыИнгредиентов.Заменяемое КАК ИнгредиентИсходный,
		|	ЗаменыИнгредиентов.ЗаменяемоеХарактеристика КАК ХарактеристикаИнгредиентаИсходного,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК НазначениеИнгредиентаИсходного,
		|	ЗаменыИнгредиентов.Заменитель КАК ИнгредиентЗамена,
		|	ЗаменыИнгредиентов.ЗаменительХарактеристика КАК ХарактеристикаИнгредиентаЗамена,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК НазначениеИнгредиентаЗамена,
		|	ЗаменыИнгредиентов.Приоритет КАК Приоритет,
		|	ЗаменыИнгредиентов.Заменяемое.ЕдиницаИзмерения КАК УпаковкаИнгредиентаИсходный,
		|	1 КАК КоличествоИсходный,
		|	1 КАК КоличествоЗамена,
		|	1 КАК КоличествоУпаковокИсходный,
		|	ЗаменыИнгредиентов.Коэффициент КАК КоэффициентЗамены,
		|	ЗаменыИнгредиентов.Период КАК ПериодНачала
		|ИЗ
		|	РегистрСведений.питЗаменыИнгредиентов.СрезПоследних(
		|			&Дата,
		|			Подразделение = &Подразделение
		|				И (Заменяемое, ЗаменяемоеХарактеристика) В
		|					(ВЫБРАТЬ
		|						ТаблицаИнгредиентов.Ингредиент КАК Ингредиент,
		|						ТаблицаИнгредиентов.ХарактеристикаИнгредиента КАК ХарактеристикаИнгредиента
		|					ИЗ
		|						ТаблицаИнгредиентов КАК ТаблицаИнгредиентов
		|					ГДЕ
		|						ТаблицаИнгредиентов.ЗапретитьЗамены <> ИСТИНА)) КАК ЗаменыИнгредиентов
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ЗаменыИнгредиентов.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЗаменыИнгредиентов.ДатаОкончания >= &Дата
		|		КОНЕЦ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаИнгредиентов.Ингредиент,
		|	ТаблицаИнгредиентов.ХарактеристикаИнгредиента,
		|	ТаблицаИнгредиентов.НазначениеИнгредиента,
		|	ТаблицаИнгредиентов.Замена,
		|	ТаблицаИнгредиентов.ХарактеристикаЗамены,
		|	ТаблицаИнгредиентов.НазначениеЗамены,
		|	-1,
		|	ТаблицаИнгредиентов.ЕдиницаИзмеренияИнгредиента,
		|	1,
		|	1,
		|	1,
		|	ТаблицаИнгредиентов.КоэффициентЗамены,
		|	ДАТАВРЕМЯ(1, 1, 1)
		|ИЗ
		|	ТаблицаИнгредиентов КАК ТаблицаИнгредиентов
		|ГДЕ
		|	ТаблицаИнгредиентов.ЗапретитьЗамены <> ИСТИНА
		|	И ТаблицаИнгредиентов.Замена <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИнгредиентов.Ингредиент,
		|	ТаблицаИнгредиентов.ХарактеристикаИнгредиента,
		|	ТаблицаИнгредиентов.НазначениеИнгредиента,
		|	ТаблицаИнгредиентов.Замена,
		|	ТаблицаИнгредиентов.ХарактеристикаЗамены,
		|	ТаблицаИнгредиентов.НазначениеЗамены,
		|	ТаблицаИнгредиентов.ЕдиницаИзмеренияНоменклатуры,
		|	ТаблицаИнгредиентов.ЕдиницаИзмеренияИнгредиента,
		|	ТаблицаИнгредиентов.КоэффициентЗамены
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет,
		|	ПериодНачала УБЫВ";
		
	КонецЕсли;
	
	// Определим таблицу ингредиентов из таблицы производства.
	Если ТипЗнч(ТаблицаПроизводства) = Тип("ТаблицаЗначений") Тогда
		ТаблицаИнгредиентов = ТаблицаПроизводства.Скопировать(,ПоляИнгредиентов);
	Иначе // таблица документа
		ТаблицаИнгредиентов = ТаблицаПроизводства.Выгрузить(,ПоляИнгредиентов);
	КонецЕсли;
	ТаблицаИнгредиентов.Свернуть(ПоляИнгредиентов);
	
	// Определим аналоги, и вид номенклатуры.
	// Таблицы в запросе:
	// 0 - временная таблица ТаблицаИнгредиентов.
	// 1 - аналоги для всех ингредиентов.
	// 2 - таблица отходов, только ингредиенты с видом номенклатуры = "ТехнологическийОтход".
	// 3 - таблица для отбора остатков, ингредиенты без аналогов.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаИнгредиентов", ТаблицаИнгредиентов);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Подразделение", СтруктураПараметров.Подразделение);
	Запрос.УстановитьПараметр("Дата", ?(СтруктураПараметров.Свойство("МоментВремени"),СтруктураПараметров.МоментВремени.Дата, Неопределено));
	Запрос.Текст = ТекстЗапроса +
	";
	|
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТаблицаИнгредиентов.Ингредиент КАК Справочник.Номенклатура) КАК ТехнологическийОтход
	|ИЗ
	|	ТаблицаИнгредиентов КАК ТаблицаИнгредиентов
	|ГДЕ
	|	ВЫРАЗИТЬ(ТаблицаИнгредиентов.Ингредиент КАК Справочник.Номенклатура).питВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.питВидыНоменклатуры.ТехнологическийИнгредиент)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫРАЗИТЬ(ТаблицаИнгредиентов.Ингредиент КАК Справочник.Номенклатура)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИнгредиентов.Ингредиент КАК Номенклатура,
	|	ТаблицаИнгредиентов.ХарактеристикаИнгредиента КАК Характеристика,
	|	ВЫРАЗИТЬ(&Склад КАК Справочник.Склады) КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент,
	|	ТаблицаИнгредиентов.НазначениеИнгредиента КАК Назначение,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия
	|ИЗ
	|	ТаблицаИнгредиентов КАК ТаблицаИнгредиентов
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИнгредиентов.Ингредиент,
	|	ТаблицаИнгредиентов.ХарактеристикаИнгредиента,
	|	ТаблицаИнгредиентов.НазначениеИнгредиента
	|"+ТекстЗапросаВсеСклады+"
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	Склад";
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаОтходов  = Результат[2].Выгрузить();
	ТаблицаОтходов.Индексы.Добавить("ТехнологическийОтход");
	ОтборОстатков   = Результат[3].Выгрузить();
	
	Если НЕ СчитатьФактВТаблицеЗамен Тогда
		ТаблицаАналогов = Результат[1].Выгрузить();
		ТаблицаАналогов.Индексы.Добавить("ИнгредиентИсходный");
		
		// Добавить аналоги в ОтборОстатков.
		Для Каждого СтрокаАналога Из ТаблицаАналогов Цикл
			СтрокаОтбора = ОтборОстатков.Добавить();
			СтрокаОтбора.Номенклатура = СтрокаАналога.ИнгредиентЗамена;
			СтрокаОтбора.Характеристика = СтрокаАналога.ХарактеристикаИнгредиентаЗамена;
			СтрокаОтбора.Назначение = Справочники.Назначения.ПустаяСсылка();
			СтрокаОтбора.Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
			СтрокаОтбора.Склад = Склад;
		КонецЦикла;
		ОтборОстатков.Свернуть("Номенклатура, Характеристика, Серия, Склад, Контрагент, Назначение");

	КонецЕсли;
	#КонецОбласти

	// Определим остатки ингредиентов и всех их возможных аналогов.
	// Контрагент здесь не заполнен, т.к. в производстве контрагента нет.
	// Когда склад не указан, в таблице ОтборОстатков передаем все склады и все возможные счета учета для ингредиентов.
	// В этом случае по таблице ОтборОстатков неправильно, 
	// будут устанавливаться блокировки в процедуре ПолучитьОстаткиТоваров(), 
	// а т.к. вызов без склада нужен только для отчетов, то здесь нет транзакции и поэтому блокировки не существенны.
	
	ОтборОстатков.Свернуть("Номенклатура, Характеристика, Серия, Назначение");
	
	Если ОтборОстатков.Количество()>0 Тогда
		
		ВычитатьДвиженияПодкладныхДокументов = НЕ СтруктураПараметров.СсылкаНаДокумент = Неопределено;
		
		//+ОстаткиПоОбеспечению
		Если СтруктураПараметров.Свойство("ВидПроизводства") Тогда
			УчитыватьОбеспеченные = СтруктураПараметров.ВидПроизводства = Перечисления.питВидыПроизводства.ПеределыОтдельнойОперациейСУчетомЗапланированных;
		Иначе
			УчитыватьОбеспеченные = Ложь;
		КонецЕсли;
		//-ОстаткиПоОбеспечению
		
		ТаблицаОстатки = питОбщегоНазначенияСервер.ПолучитьОстаткиПолуфабрикатов(ОтборОстатков, СтруктураПараметров.Склад, СтруктураПараметров.МоментВремени, ВычитатьДвиженияПодкладныхДокументов, СтруктураПараметров.СсылкаНаДокумент, УчитыватьОбеспеченные);
		
		// Считаем по всем счетам, колонка СчетУчета не нужна.
		ТаблицаОстатки.Свернуть("Номенклатура, Характеристика, Назначение", "Количество");
		ТаблицаОстатки.Индексы.Добавить("Номенклатура, Характеристика, Назначение");
		ОтборОстатков = Новый Структура("Номенклатура, Характеристика, Назначение");
		
	Иначе
		
		ТаблицаОстатки = Новый ТаблицаЗначений;
		ТаблицаОстатки.Колонки.Добавить("Номенклатура");
		ТаблицаОстатки.Колонки.Добавить("Характеристика");
		ТаблицаОстатки.Колонки.Добавить("Назначение");
		ОтборОстатков = Новый Структура("Номенклатура, Характеристика, Назначение");
		
	КонецЕсли;
	
	// Для запоминания строк, на которые не хватает остатков, и нужно подобрать замены.
	СтрокиДляЗамены = Новый Массив;
	
	// Для отчетов о недовложениях или для ввода на основании всегда учитываем реальные остатки на складе.
	// Для проведения, когда отрицательные остатки разрешены, списываем товар по норме.
	ДляОтчетаНедовложений = ?(СтруктураПараметров.Свойство("ДляОтчетаНедовложений"), СтруктураПараметров.ДляОтчетаНедовложений, Ложь);
	
	// Первый этап: распределяем количества остатков по таблице производства,
	// только по тем строкам где ингредиента полностью хватает.
	// А остальные строки запоминаем для последующей обработки, и подбора аналогов.
	Для Каждого СтрокаПроизводства Из ТаблицаПроизводства Цикл
		
		// Предыдущее значение факта из документа запоминаем в другой колонке,
		// будет использоваться при анализе недовложений, для заполнения Поступления или Перемещения всеми ингредиентами.
		Если ДляОтчетаНедовложений И СчитатьФактВТаблицеЗамен Тогда
			СтрокаПроизводства.КоличествоИнгредиентаФактВДокументе = СтрокаПроизводства.КоличествоИнгредиентаФакт;
			СтрокаПроизводства.КоличествоУпаковокИнгредиентаФактВДокументе = СтрокаПроизводства.КоличествоУпаковокИнгредиентаФакт;
		КонецЕсли;
		СтрокаПроизводства.КоличествоИнгредиентаФакт = 0;
		
		// Специи и автопередельные блюда в расчете не участвуют.
		Если СтрокаПроизводства.Специя ИЛИ (СтрокаПроизводства.ИзАвтопеределов И ЗначениеЗаполнено(СтрокаПроизводства.РецептураАвтопередела)) ИЛИ НЕ ЗначениеЗаполнено(СтрокаПроизводства.Ингредиент) Тогда
			СтрокаПроизводства.КоличествоИнгредиентаФакт = СтрокаПроизводства.КоличествоИнгредиентаНорма;
			СтрокаПроизводства.КоличествоУпаковокИнгредиентаФакт = СтрокаПроизводства.КоличествоУпаковокИнгредиентаНорма;
			
			Продолжить;
		КонецЕсли;
		
		// Технологический отход (Технический ингредиент) всегда есть в нужном количестве.
		Если ТаблицаОтходов.Найти(СтрокаПроизводства.Ингредиент)<>Неопределено Тогда
			СтрокаПроизводства.КоличествоИнгредиентаФакт = СтрокаПроизводства.КоличествоИнгредиентаНорма;
			СтрокаПроизводства.КоличествоУпаковокИнгредиентаФакт = СтрокаПроизводства.КоличествоУпаковокИнгредиентаНорма;
			Продолжить;
		КонецЕсли;
		
		ОтборОстатков.Номенклатура = СтрокаПроизводства.Ингредиент;
		ОтборОстатков.Характеристика = СтрокаПроизводства.ХарактеристикаИнгредиента;
		ОтборОстатков.Назначение = СтрокаПроизводства.НазначениеИнгредиента;
		СтрокиОстатков = ТаблицаОстатки.НайтиСтроки(ОтборОстатков);
		Если СтрокиОстатков.Количество()>0 И СтрокиОстатков[0].Количество>0 Тогда
			КоличествоВОстатках = СтрокиОстатков[0].Количество;
		Иначе
			// Нет остатков, эту строку обработаем позже.
			СтрокиДляЗамены.Добавить(СтрокаПроизводства);
			Продолжить;
		КонецЕсли;
		
		// Рассчитаем количество
		КоличествоНорма = СтрокаПроизводства.КоличествоИнгредиентаНорма;
		КоличествоУпаковокНорма = СтрокаПроизводства.КоличествоУпаковокИнгредиентаНорма;
		
		// Сравниваем количества в базовых единицах, с округлением.
		Если Окр(КоличествоНорма-КоличествоВОстатках,3) > 0 Тогда
			
			// Не достаточно количества, эту строку обработаем позже.
			СтрокиДляЗамены.Добавить(СтрокаПроизводства);
			Продолжить;
		КонецЕсли;
		СтрокаПроизводства.КоличествоИнгредиентаФакт = СтрокаПроизводства.КоличествоИнгредиентаНорма;
		СтрокаПроизводства.КоличествоУпаковокИнгредиентаФакт = СтрокаПроизводства.КоличествоУпаковокИнгредиентаНорма;
		
		СтрокиОстатков[0].Количество = КоличествоВОстатках - КоличествоНорма;
	КонецЦикла;
	
	
	//// Второй этап: расчет замен.
	Для Каждого СтрокаПроизводства Из СтрокиДляЗамены Цикл
		
		///////////////////////////////////////////////////////////////////////////
		// Норма заменяемого унгредиента
		КоличествоНорма			= СтрокаПроизводства.КоличествоИнгредиентаНорма;
		КоличествоУпаковокНорма	= СтрокаПроизводства.КоличествоУпаковокИнгредиентаНорма;
		
		Если СтрокаПроизводства.КоличествоИнгредиентаНорма = 0 Тогда
			Продолжить;
		КонецЕсли;		
		КоэфУпаковкиИнгредиента = СтрокаПроизводства.КоличествоУпаковокИнгредиентаНорма / СтрокаПроизводства.КоличествоИнгредиентаНорма;
		
		///////////////////////////////////////////////////////////////////////////
		// Факт заменяемого унгредиента (инициализация)
		КоличествоФакт = 0;
		КоличествоУпаковокФакт = 0;
		
		// При целых заменах анализ исходного ингредиента делается позже.
		Если НЕ РежимИспользованияАналогов = Перечисления.питРежимыИспользованияАналогов.ЗаменятьЦеликом Тогда
			
			ОтборОстатков.Номенклатура = СтрокаПроизводства.Ингредиент;
			ОтборОстатков.Характеристика = СтрокаПроизводства.ХарактеристикаИнгредиента;
			ОтборОстатков.Назначение = СтрокаПроизводства.НазначениеИнгредиента;
			
			///////////////////////////////////////////////////////////////////////////
			// Факт заменяемого унгредиента (вычисление по остаткам)
			СтрокиОстатков = ТаблицаОстатки.НайтиСтроки(ОтборОстатков);
			Если СтрокиОстатков.Количество()>0 И СтрокиОстатков[0].Количество>0 Тогда
				КоличествоВОстатках = СтрокиОстатков[0].Количество;
				// Рассчитаем количество
				КоличествоФакт = ?(КоличествоНорма>КоличествоВОстатках, КоличествоВОстатках, КоличествоНорма);
				КоличествоУпаковокФакт = КоличествоФакт * КоэфУпаковкиИнгредиента;
				СтрокиОстатков[0].Количество = ?(КоличествоФакт=КоличествоВОстатках, 0, КоличествоВОстатках-КоличествоФакт);
			Иначе
				КоличествоФакт = 0;
				КоличествоУпаковокФакт = 0;
			КонецЕсли;
			// Если количества совпали, еще раз пересчитывать не будем,
			// так как можно попасть на ошибку округления, в результате которой количества не совпадут.
			Если КоличествоФакт = КоличествоНорма Тогда
				СтрокаПроизводства.КоличествоИнгредиентаФакт			= СтрокаПроизводства.КоличествоИнгредиентаНорма;
				СтрокаПроизводства.КоличествоУпаковокИнгредиентаФакт	= СтрокаПроизводства.КоличествоУпаковокИнгредиентаНорма;
			Иначе
				СтрокаПроизводства.КоличествоИнгредиентаФакт         = КоличествоФакт;
				СтрокаПроизводства.КоличествоУпаковокИнгредиентаФакт = КоличествоФакт * КоэфУпаковкиИнгредиента;
			КонецЕсли;
			
		КонецЕсли;
		
		// Аналоги
		Если СтрокаПроизводства.ЗапретитьЗамены 
			ИЛИ РежимИспользованияАналогов = Перечисления.питРежимыИспользованияАналогов.Запретить Тогда
			// Нет обработки аналогов
			
		ИначеЕсли СчитатьФактВТаблицеЗамен Тогда
			// Если СчитатьФактВТаблицеЗамен=Истина, значит расчет списания производится 'по факту'.
			// В этом случае просто считаем фактическое количество в переданной таблице замен.
			ЗаполнитьЗначенияСвойств(СтрокаОтбораАналогов, СтрокаПроизводства);
			СтрокаОтбораАналогов.ИнгредиентИсходный = СтрокаПроизводства.Ингредиент;
			СтрокаОтбораАналогов.ХарактеристикаИнгредиентаИсходного = СтрокаПроизводства.ХарактеристикаИнгредиента;
			СтрокаОтбораАналогов.НазначениеИнгредиентаИсходного = СтрокаПроизводства.НазначениеИнгредиента;
			СтрокаОтбораАналогов.УпаковкаИнгредиентаИсходный = СтрокаПроизводства.ЕдиницаИзмеренияИнгредиента;
			
			СтрокиАналогов = ТаблицаЗамен.НайтиСтроки(СтрокаОтбораАналогов);
			Для Каждого СтрокаАналога Из СтрокиАналогов Цикл
				
				ОтборОстатков.Номенклатура = СтрокаАналога.ИнгредиентЗамена;
				ОтборОстатков.Характеристика = СтрокаАналога.ХарактеристикаИнгредиентаЗамена;
				ОтборОстатков.Назначение = СтрокаАналога.НазначениеИнгредиентаЗамена;
				
				СтрокиОстатков = ТаблицаОстатки.НайтиСтроки(ОтборОстатков);
				Если СтрокиОстатков.Количество()>0 И СтрокиОстатков[0].Количество>0 Тогда
					КоличествоВОстатках = СтрокиОстатков[0].Количество;
				Иначе
					КоличествоВОстатках = 0;
					СтрокаАналога.КоличествоИнгредиентаЗаменаФакт = 0;
					СтрокаАналога.КоличествоУпаковокИнгредиентаЗаменаФакт = 0;
					Продолжить;
				КонецЕсли;
				
				// Рассчитаем количество
				КоличествоНормаАналогов = СтрокаАналога.КоличествоИнгредиентаЗамена;
				КоличествоФактАналогов = ?(КоличествоНормаАналогов > КоличествоВОстатках, КоличествоВОстатках, КоличествоНормаАналогов);
				
				// Если количества совпали, еще раз пересчитывать не будем,
				// так как можно попасть на ошибку округления, в результате которой количества не совпадут.
				Если КоличествоФактАналогов=КоличествоНормаАналогов Тогда
					СтрокаАналога.КоличествоИнгредиентаЗаменаФакт = СтрокаАналога.КоличествоИнгредиентаЗамена;
					СтрокаАналога.КоличествоУпаковокИнгредиентаЗаменаФакт = СтрокаАналога.КоличествоУпаковокИнгредиентаЗамена;
				ИначеЕсли  РежимИспользованияАналогов=Перечисления.питРежимыИспользованияАналогов.Разрешить Тогда
					СтрокаАналога.КоличествоИнгредиентаЗаменаФакт = КоличествоФактАналогов;
					СтрокаАналога.КоличествоУпаковокИнгредиентаЗаменаФакт = КоличествоФактАналогов * питОбщегоНазначения.ПолучитьКоэффициентУпаковки(СтрокаАналога.ИнгредиентЗамена, СтрокаАналога.УпаковкаИнгредиентаЗамена);
				Иначе
					// ЗаменятьЦеликом. Частичную замену не учитываем.
					СтрокаАналога.КоличествоИнгредиентаЗаменаФакт = 0;
					СтрокаАналога.КоличествоУпаковокИнгредиентаЗаменаФакт = 0;
					КоличествоФактАналогов = 0;
				КонецЕсли;
				
				СтрокиОстатков[0].Количество = ?(КоличествоФактАналогов=КоличествоВОстатках, 0, КоличествоВОстатках-КоличествоФактАналогов);
				
			КонецЦикла;	
			
		Иначе
			
			// Выполним подбор аналогов
			СтрокиАналогов = ТаблицаАналогов.НайтиСтроки(Новый Структура("ИнгредиентИсходный, ХарактеристикаИнгредиентаИсходного, НазначениеИнгредиентаИсходного", СтрокаПроизводства.Ингредиент, СтрокаПроизводства.ХарактеристикаИнгредиента, СтрокаПроизводства.НазначениеИнгредиента));
			
			Для Каждого СтрокаАналога Из СтрокиАналогов Цикл
				Если Окр(КоличествоФакт-КоличествоНорма,3) >= 0 Тогда
					// Выход из цикла
					Прервать;
				КонецЕсли;
				
				// Если это точечная замена из таблицы Производство, то нужно скопировать коэффициент.
				Если СтрокаАналога.Приоритет = -1 Тогда  
					Если ЗначениеЗаполнено(СтрокаПроизводства.Замена) И СтрокаПроизводства.КоэффициентЗамены<>0 Тогда
						СтрокаАналога.КоэффициентЗамены = СтрокаПроизводства.КоэффициентЗамены;
					Иначе
						// Это замена из другой рецептуры
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				ОтборОстатков.Номенклатура = СтрокаАналога.ИнгредиентЗамена;
				ОтборОстатков.Характеристика = СтрокаАналога.ХарактеристикаИнгредиентаЗамена;
				ОтборОстатков.Назначение = СтрокаАналога.НазначениеИнгредиентаЗамена;
				
				СтрокиОстатков = ТаблицаОстатки.НайтиСтроки(ОтборОстатков);
				Если СтрокиОстатков.Количество()>0 И СтрокиОстатков[0].Количество>0 Тогда
					КоличествоВОстатках = СтрокиОстатков[0].Количество;
				Иначе
					КоличествоВОстатках = 0;
					Продолжить;
				КонецЕсли;
				
				Если РежимИспользованияАналогов=Перечисления.питРежимыИспользованияАналогов.ЗаменятьЦеликом Тогда
					// Рассчитаем сколько нужно
					КоличествоНормаАналогов = КоличествоНорма * СтрокаАналога.КоэффициентЗамены;
					
					Если КоличествоНормаАналогов > КоличествоВОстатках Тогда
						// Остатков должно хватать полностью. Пропускаем.
						КоличествоФактАналогов = 0;
						КоличествоИнгредиентаИсходный = 0;
						КоличествоУпаковокИнгредиентаИсходный = 0;
					Иначе
						КоличествоФактАналогов = КоличествоНормаАналогов;
						КоличествоИнгредиентаИсходный = КоличествоНорма;
						КоличествоУпаковокИнгредиентаИсходный = КоличествоУпаковокНорма; 
						ПолнаяЗаменаНайдена = Истина;
					КонецЕсли;
					
				Иначе
					// Рассчитаем количество. Расчет в базовой единице.
					// В ТаблицаЗамен количество замены записывается в базовой единице, с округлением до 6 десятичных знаков.
					КоличествоНормаАналогов = (КоличествоНорма - КоличествоФакт) * СтрокаАналога.КоэффициентЗамены;
					
					Если КоличествоНормаАналогов > КоличествоВОстатках Тогда
						КоличествоФактАналогов = КоличествоВОстатках;
						КоличествоИнгредиентаИсходный = КоличествоФактАналогов / ?(СтрокаАналога.КоэффициентЗамены=0, 1, СтрокаАналога.КоэффициентЗамены);
						//* питОбщегоНазначения.ПолучитьКоэффициентУпаковки(СтрокаАналога.ИнгредиентИсходный, СтрокаАналога.УпаковкаИнгредиентаИсходный)
						КоличествоУпаковокИнгредиентаИсходный = КоличествоФактАналогов / ?(СтрокаАналога.КоэффициентЗамены=0, 1, СтрокаАналога.КоэффициентЗамены) / питОбщегоНазначения.ПолучитьКоэффициентУпаковки(СтрокаАналога.ИнгредиентИсходный, СтрокаАналога.УпаковкаИнгредиентаИсходный);
					Иначе
						КоличествоФактАналогов = КоличествоНормаАналогов;
						КоличествоИнгредиентаИсходный         = КоличествоНорма         - КоличествоФакт;
						КоличествоУпаковокИнгредиентаИсходный = КоличествоУпаковокНорма - КоличествоУпаковокФакт;
					КонецЕсли;
					
				КонецЕсли;
				
				СтрокиОстатков[0].Количество = КоличествоВОстатках - КоличествоФактАналогов;
				
				Если НЕ КоличествоФактАналогов=0 Тогда
					// Добавим строку в таблицу замен
					СтрокаЗамен = ТаблицаЗамен.Добавить();
					СтрокаЗамен.ИнгредиентИсходный = СтрокаПроизводства.Ингредиент;
					СтрокаЗамен.ХарактеристикаИнгредиентаИсходного = СтрокаПроизводства.ХарактеристикаИнгредиента;
					СтрокаЗамен.НазначениеИнгредиентаИсходного = СтрокаПроизводства.НазначениеИнгредиента;
					СтрокаЗамен.УпаковкаИнгредиентаИсходный = СтрокаПроизводства.ЕдиницаИзмеренияИнгредиента;
					
					Если КоличествоНорма = КоличествоИнгредиентаИсходный Тогда
						СтрокаЗамен.КоличествоИнгредиентаИсходный = СтрокаПроизводства.КоличествоИнгредиентаНорма;
						СтрокаЗамен.КоличествоУпаковокИнгредиентаИсходный = СтрокаПроизводства.КоличествоУпаковокИнгредиентаНорма;
					Иначе
						СтрокаЗамен.КоличествоИнгредиентаИсходный = КоличествоИнгредиентаИсходный; 
						СтрокаЗамен.КоличествоУпаковокИнгредиентаИсходный = КоличествоУпаковокИнгредиентаИсходный;
					КонецЕсли;
					
					СтрокаЗамен.ИнгредиентЗамена = СтрокаАналога.ИнгредиентЗамена;
					СтрокаЗамен.ХарактеристикаИнгредиентаЗамена = СтрокаАналога.ХарактеристикаИнгредиентаЗамена;
					СтрокаЗамен.НазначениеИнгредиентаЗамена = СтрокаАналога.НазначениеИнгредиентаЗамена;
					//СтрокаЗамен.УпаковкаИнгредиентаЗамена = СтрокаАналога.УпаковкаИнгредиентаЗамена;/// Замены в базовых оставляем пустым
					
					Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаЗамен, "КоэффициентЗамена") Тогда
						СтрокаЗамен.КоэффициентЗамена = СтрокаАналога.КоэффициентЗамены;
					КонецЕсли;
					
					СтрокаЗамен.КоличествоИнгредиентаЗамена         = КоличествоФактАналогов; 
					СтрокаЗамен.КоличествоУпаковокИнгредиентаЗамена = КоличествоФактАналогов; /// При заполнении ставим базовую единицу поэтому равно
					
					СтрокаЗамен.Блюдо = СтрокаПроизводства.Номенклатура;
					СтрокаЗамен.ХарактеристикаБлюда = СтрокаПроизводства.Характеристика;
					СтрокаЗамен.Рецептура = СтрокаПроизводства.Рецептура;
					СтрокаЗамен.Автопередел = СтрокаПроизводства.Автопередел;
					СтрокаЗамен.СтатьяКалькуляции = СтрокаПроизводства.СтатьяКалькуляции;
					Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаЗамен, "Серия") Тогда
						СтрокаЗамен.Серия = СтрокаПроизводства.Серия;
					КонецЕсли;
					
					// Для исключения погрешностей при пересчетах в дальнейшем, в ТаблицаЗамен округление до 6 знаков. 
					КоличествоИнгредиентаИсходный = СтрокаЗамен.КоличествоИнгредиентаИсходный;
					КоличествоУпаковокИнгредиентаИсходный = СтрокаЗамен.КоличествоУпаковокИнгредиентаИсходный;
					
					КоличествоФакт = КоличествоФакт + КоличествоИнгредиентаИсходный;
					КоличествоУпаковокФакт = КоличествоУпаковокФакт + КоличествоУпаковокИнгредиентаИсходный;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;	
		
		// Если не было сделано целой замены, спишем исходный ингредиент.
		Если КоличествоФакт = 0 
			И РежимИспользованияАналогов = Перечисления.питРежимыИспользованияАналогов.ЗаменятьЦеликом Тогда
			
			ОтборОстатков.Номенклатура = СтрокаПроизводства.Ингредиент;
			ОтборОстатков.Характеристика = СтрокаПроизводства.ХарактеристикаИнгредиента;
			ОтборОстатков.Назначение = СтрокаПроизводства.НазначениеИнгредиента;
			СтрокиОстатков = ТаблицаОстатки.НайтиСтроки(ОтборОстатков);
			Если СтрокиОстатков.Количество()>0 И СтрокиОстатков[0].Количество>0 Тогда
				КоличествоВОстатках = СтрокиОстатков[0].Количество;
				// Рассчитаем количество
				КоличествоФакт = ?(КоличествоНорма>КоличествоВОстатках, КоличествоВОстатках, КоличествоНорма);
				СтрокиОстатков[0].Количество = ?(КоличествоФакт=КоличествоВОстатках, 0, КоличествоВОстатках-КоличествоФакт);
			Иначе
				КоличествоФакт = 0;
			КонецЕсли;
			
			// Если количества совпали, еще раз пересчитывать не будем,
			// так как можно попасть на ошибку округления, в результате которой количества не совпадут.
			Если КоличествоФакт=КоличествоНорма Тогда
				СтрокаПроизводства.КоличествоИнгредиентаФакт = СтрокаПроизводства.КоличествоИнгредиентаНорма;
				СтрокаПроизводства.КоличествоУпаковокИнгредиентаФакт = СтрокаПроизводства.КоличествоУпаковокИнгредиентаНорма;
			//ИначеЕсли НЕ ЗначениеЗаполнено(СтрокаПроизводства.УпаковкаИнгредиента) Тогда // Исходный ингрещиент в базовой единице
			//	СтрокаПроизводства.КоличествоИнгредиентаФакт = КоличествоФакт;
			//	СтрокаПроизводства.КоличествоУпаковокИнгредиентаФакт = КоличествоУпаковокФакт;
			Иначе
				СтрокаПроизводства.КоличествоИнгредиентаФакт = КоличествоФакт;
				СтрокаПроизводства.КоличествоУпаковокИнгредиентаФакт = КоличествоУпаковокФакт;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Вспомогательная функция, добавляющая строку в дерево состава в соответствии с переданными параметрами.
//
// Параметры:
//	СтрокаСоставаБлюдо – родительская строка по отношению к добавляемой.
//	Выборка – очередная запись из выборки результата запроса по составу.
//	КоэффициентПересчета - коэффициент пересчета количества продуктов.
//	Рецептура - ссылка на документ рецептура.
//	ЦиклическаяСсылка - признак циклического передела.
//	ВыпускПродукции - ссылка на документ ВыпускПродукции.
//	ВерхнееБлюдоНоменклатурнаяГруппа - ссылка НоменклатурнаяГруппа из самого верхнего блюда в иерархии состава.
//  
// Возвращаемое значение:
//	Строка дерева состава.
//
Функция ДобавитьСтрокуСостава(СтрокаСоставаБлюдо, Выборка, КоэффициентПересчета, Рецептура=Неопределено,ЦиклическаяСсылка=Ложь,ВыпускПродукции=Неопределено,КэшЗаполненияДерева=Неопределено)
	
	// +Расчет по количеству упаковок
	КоэффициентЕдиницы = питОбщегоНазначения.ПолучитьКоэффициентУпаковки(Выборка.Номенклатура, Выборка.ЕдиницаИзмерения, ?(НЕ КэшЗаполненияДерева = Неопределено И КэшЗаполненияДерева.Свойство("КэшУпаковок"), КэшЗаполненияДерева.КэшУпаковок, Неопределено));
	Если КоэффициентЕдиницы = 0 Тогда
		КоэффициентЕдиницы = 1;
	КонецЕсли;
	// -Расчет по количеству упаковок
	
	СтруктураПоискаИнгредиентаВДеревеСостава = Новый Структура();
	СтруктураПоискаИнгредиентаВДеревеСостава.Вставить("Номенклатура",Выборка.Номенклатура);
	СтруктураПоискаИнгредиентаВДеревеСостава.Вставить("Характеристика",Выборка.Характеристика);
	СтруктураПоискаИнгредиентаВДеревеСостава.Вставить("Назначение",Выборка.НазначениеИнгредиентов);
	Если  НЕ  Рецептура=Неопределено Тогда
		СтруктураПоискаИнгредиентаВДеревеСостава.Вставить("Рецептура",Рецептура);
	КонецЕсли;
	СтруктураПоискаИнгредиентаВДеревеСостава.Вставить("ЕдиницаИзмерения",Выборка.ЕдиницаИзмерения);
	СтруктураПоискаИнгредиентаВДеревеСостава.Вставить("Специя",Выборка.Специя);
	СтруктураПоискаИнгредиентаВДеревеСостава.Вставить("ЗапретитьЗамены",Выборка.ЗапретитьЗамены);
	// Поищем строку ингредиента на данном уровне вложенности, 
	// чтобы в дальнейшем не задваивать количество родителя при приготовлении.
	МассивСтрокИнгредиентаВДеревеСостава = СтрокаСоставаБлюдо.Строки.НайтиСтроки(СтруктураПоискаИнгредиентаВДеревеСостава,Ложь);
	Если МассивСтрокИнгредиентаВДеревеСостава.Количество()>0 Тогда
		текСтрокаСостава  = МассивСтрокИнгредиентаВДеревеСостава[0];
		
		текСтрокаСостава.КоличествоУпаковок			= текСтрокаСостава.КоличествоУпаковок		+ (Выборка.КоличествоУпаковокБрутто-Выборка.КоличествоУпаковокБруттоИзМодификаторов)*КоэффициентПересчета + Выборка.КоличествоУпаковокБруттоИзМодификаторов;
		текСтрокаСостава.КоличествоУпаковокНетто	= текСтрокаСостава.КоличествоУпаковокНетто	+ (Выборка.КоличествоУпаковокНетто-Выборка.КоличествоУпаковокНеттоИзМодификаторов)	*КоэффициентПересчета + Выборка.КоличествоУпаковокНеттоИзМодификаторов;
		текСтрокаСостава.КоличествоУпаковокВыход	= текСтрокаСостава.КоличествоУпаковокВыход	+ (Выборка.КоличествоУпаковокВыход-Выборка.КоличествоУпаковокВыходИзМодификаторов)	*КоэффициентПересчета + Выборка.КоличествоУпаковокВыходИзМодификаторов;
		
		Если НЕ ЗначениеЗаполнено(текСтрокаСостава.Замена) И ЗначениеЗаполнено(Выборка.Замена) Тогда
			текСтрокаСостава.Замена = Выборка.Замена;
			текСтрокаСостава.ХарактеристикаЗамены = Выборка.ХарактеристикаЗамены;
			текСтрокаСостава.НазначениеЗамены = Выборка.НазначениеЗамены;
			текСтрокаСостава.КоэффициентЗамены = Выборка.КоэффициентЗамены;
		КонецЕсли;
		
		текСтрокаСостава.СтатьяКалькуляции = Выборка.СтатьяКалькуляции;
		//текСтрокаСостава.Серия = Выборка.Серия;
		
		//+АвтовыборНоменклатуры
		текСтрокаСостава.ОшибкаАвтовыбора = Выборка.ОшибкаАвтовыбора;
		//-АвтовыборНоменклатуры
		
	Иначе	
		текСтрокаСостава  = СтрокаСоставаБлюдо.Строки.Добавить();
		//+УмноеОкругление
		текСтрокаСостава.ИдентификаторСтрокиДерева = Новый УникальныйИдентификатор;
		//-УмноеОкругление
		текСтрокаСостава.Номенклатура = Выборка.Номенклатура;
		текСтрокаСостава.Характеристика = Выборка.Характеристика;
		текСтрокаСостава.Рецептура = Рецептура;
		
		текСтрокаСостава.КоличествоУпаковок			= (Выборка.КоличествоУпаковокБрутто-Выборка.КоличествоУпаковокБруттоИзМодификаторов)	*КоэффициентПересчета + Выборка.КоличествоУпаковокБруттоИзМодификаторов;
		текСтрокаСостава.КоличествоУпаковокНетто	= (Выборка.КоличествоУпаковокНетто-Выборка.КоличествоУпаковокНеттоИзМодификаторов)		*КоэффициентПересчета + Выборка.КоличествоУпаковокНеттоИзМодификаторов;
		текСтрокаСостава.КоличествоУпаковокВыход	= (Выборка.КоличествоУпаковокВыход-Выборка.КоличествоУпаковокВыходИзМодификаторов)		*КоэффициентПересчета + Выборка.КоличествоУпаковокВыходИзМодификаторов;
		
		текСтрокаСостава.ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
		
		//текСтрокаСостава.Коэффициент = Выборка.Коэффициент;
		текСтрокаСостава.Специя = Выборка.Специя;
		текСтрокаСостава.ЗапретитьЗамены = Выборка.ЗапретитьЗамены;
		текСтрокаСостава.Замена = Выборка.Замена;
		текСтрокаСостава.ХарактеристикаЗамены = Выборка.ХарактеристикаЗамены;
		текСтрокаСостава.НазначениеЗамены = Выборка.НазначениеЗамены;
		текСтрокаСостава.КоэффициентЗамены = Выборка.КоэффициентЗамены;
		текСтрокаСостава.ЦиклическаяСсылка =  ЦиклическаяСсылка;
		текСтрокаСостава.СтатьяКалькуляции = Выборка.СтатьяКалькуляции;
		текСтрокаСостава.НеГотовить = Выборка.НеГотовить;
		//текСтрокаСостава.Серия = Выборка.Серия;
		
		//+АвтовыборНоменклатуры
		текСтрокаСостава.ОшибкаАвтовыбора = Выборка.ОшибкаАвтовыбора;
		//-АвтовыборНоменклатуры
		
		текСтрокаСостава.Назначение = Выборка.НазначениеИнгредиентов;
		
	КонецЕсли;
	
	// +Расчет по количеству упаковок
	текСтрокаСостава.Количество					= текСтрокаСостава.КоличествоУпаковок			* КоэффициентЕдиницы;
	текСтрокаСостава.КоличествоНетто			= текСтрокаСостава.КоличествоУпаковокНетто		* КоэффициентЕдиницы;
	текСтрокаСостава.КоличествоВыход			= текСтрокаСостава.КоличествоУпаковокВыход		* КоэффициентЕдиницы;
	// -Расчет по количеству упаковок
	
КонецФункции	

// Рассчитывает количество замены, приведенное к количеству исходного ингредиента.
//
// Параметры:
//	СтрокаПроизводства - ссылка на строку табличной части Производство документа Выпуск продукции.
//	ТаблицаЗамен - таблица замен документа Выпуск продукции
//  
// Возвращаемое значение:
//	Число - приведенное количество замены.
Функция ПриведенноеКоличествоЗамены(СтрокаПроизводства, ТаблицаЗамен) Экспорт
	
	Если СтрокаПроизводства.ЗапретитьЗамены ИЛИ СтрокаПроизводства.Специя Тогда
		Возврат Новый Структура("КоличествоЕсть,КоличествоУпаковокЕсть", 0,0);
	КонецЕсли;
	
	// Ищем в таблице замен соответствующие строки
	СтрокаОтбораАналогов = Новый Структура("Блюдо, ХарактеристикаБлюда, Рецептура, Автопередел, ИнгредиентИсходный, ХарактеристикаИнгредиентаИсходного, НазначениеИнгредиентаИсходного, УпаковкаИнгредиентаИсходный");
	ЗаполнитьЗначенияСвойств(СтрокаОтбораАналогов, СтрокаПроизводства);
	СтрокаОтбораАналогов.ИнгредиентИсходный = СтрокаПроизводства.Ингредиент;
	СтрокаОтбораАналогов.ХарактеристикаИнгредиентаИсходного = СтрокаПроизводства.ХарактеристикаИнгредиента;
	СтрокаОтбораАналогов.НазначениеИнгредиентаИсходного = СтрокаПроизводства.НазначениеИнгредиента;
	СтрокаОтбораАналогов.УпаковкаИнгредиентаИсходный = СтрокаПроизводства.ЕдиницаИзмеренияИнгредиента;
	СтрокаОтбораАналогов.Блюдо = СтрокаПроизводства.Номенклатура;
	СтрокаОтбораАналогов.ХарактеристикаБлюда = СтрокаПроизводства.Характеристика;
	
	
	СтрокиАналогов = ТаблицаЗамен.НайтиСтроки(СтрокаОтбораАналогов);
	
	КоличествоУпаковокЕсть = 0;
	КоличествоЕсть = 0;
	
	Для Каждого СтрокаАналога Из СтрокиАналогов Цикл
		// Анализируем, сколько надо списать и сколько можно списать.
		КоличествоУпаковокЕстьИсходный = СтрокаАналога.КоличествоУпаковокИнгредиентаИсходный;
		КоличествоЕстьИсходный = СтрокаАналога.КоличествоИнгредиентаИсходный;
		
		// Изменяем количество
		КоличествоЕсть = КоличествоЕсть + КоличествоЕстьИсходный;
		КоличествоУпаковокЕсть = КоличествоУпаковокЕсть + КоличествоУпаковокЕстьИсходный;
		
	КонецЦикла;
	
	Возврат Новый Структура("КоличествоЕсть,КоличествоУпаковокЕсть", КоличествоЕсть,КоличествоУпаковокЕсть);
	
КонецФункции	

// Недовложения запрещены, остаточную нехватку пишем в КоличествоФакт.
//
// Параметры:
//	Производство - список параметров производства блюд.
//  Замены - список замен ингредиентов.
//
Процедура ЗаписатьОстаточныеНехваткиВКоличествоФакт(Производство, Замены) Экспорт
	
	ЕстьКолонкаФактЗамены = Ложь;
	Если ТипЗнч(Производство) = Тип("ДанныеФормыКоллекция") Тогда
		Если Производство.Количество()>0 Тогда
			ЕстьКолонкаФактЗамены = Производство[0].Свойство("ФактЗамены");
		КонецЕсли;
	ИначеЕсли ТипЗнч(Производство) = Тип("ТаблицаЗначений") Тогда
		Если Производство.Количество()>0 Тогда
			ЕстьКолонкаФактЗамены = (Производство.Колонки.Найти("ФактЗамены") <> Неопределено);
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого СтрокаПроизводства Из Производство Цикл
		Если СтрокаПроизводства.Специя ИЛИ СтрокаПроизводства.ИзАвтопеределов Тогда
			СтрокаПроизводства.КоличествоИнгредиентаФакт = СтрокаПроизводства.КоличествоИнгредиентаНорма;
			Продолжить;
		КонецЕсли;
		
		// Считаем количество замен
		КоличествоЗамены = ПриведенноеКоличествоЗамены(СтрокаПроизводства, Замены);
		Если ЕстьКолонкаФактЗамены Тогда
			СтрокаПроизводства.ФактЗамены = КоличествоЗамены.КоличествоУпаковокЕсть; // Факт замены только в ЕД изм. без базовой
		КонецЕсли;
		
		// Добавляем остаточную нехватку
		Если СтрокаПроизводства.КоличествоУпаковокИнгредиентаФакт + КоличествоЗамены.КоличествоУпаковокЕсть < СтрокаПроизводства.КоличествоУпаковокИнгредиентаНорма Тогда
			//Базовое
			ОстаточнаяНехватка = СтрокаПроизводства.КоличествоИнгредиентаНорма - СтрокаПроизводства.КоличествоИнгредиентаФакт - КоличествоЗамены.КоличествоЕсть;
			СтрокаПроизводства.КоличествоИнгредиентаФакт = СтрокаПроизводства.КоличествоИнгредиентаФакт + ОстаточнаяНехватка;
			
			ОстаточнаяНехватка = СтрокаПроизводства.КоличествоУпаковокИнгредиентаНорма - СтрокаПроизводства.КоличествоУпаковокИнгредиентаФакт - КоличествоЗамены.КоличествоУпаковокЕсть;
			СтрокаПроизводства.КоличествоУпаковокИнгредиентаФакт = СтрокаПроизводства.КоличествоУпаковокИнгредиентаФакт + ОстаточнаяНехватка;
		
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Процедура рассчитывает и заполняет колонку ФактЗамены в таблице Производство.
//
// Параметры:
//	Производство - список параметров производства блюд.
//  Замены - список замен ингредиентов.
//
Процедура ЗаписатьВТаблицуПроизводствоФактЗанены(Производство, Замены) Экспорт
	
	ЕстьКолонкаФактЗамены = Ложь;
	Если ТипЗнч(Производство) = Тип("ДанныеФормыКоллекция") Тогда
		Если Производство.Количество()>0 Тогда
			ЕстьКолонкаФактЗамены = Производство[0].Свойство("ФактЗамены");
		КонецЕсли;
	КонецЕсли;
	Если ТипЗнч(Производство) = Тип("ТаблицаЗначений") Тогда
		Если НЕ Производство.Колонки.Найти("ФактЗамены") = Неопределено Тогда
			ЕстьКолонкаФактЗамены = Истина;
		КонецЕсли;
	КонецЕсли;
	
	
	Для Каждого СтрокаПроизводства Из Производство Цикл
		Если СтрокаПроизводства.Специя ИЛИ СтрокаПроизводства.ИзАвтопеределов Тогда
			Продолжить;
		КонецЕсли;
		// Считаем количество замен
		КоличествоЗамены = ПриведенноеКоличествоЗамены(СтрокаПроизводства, Замены);
		Если ЕстьКолонкаФактЗамены Тогда
			СтрокаПроизводства.ФактЗамены = КоличествоЗамены.КоличествоУпаковокЕсть;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Если выпуск создан на основании плана, то остатки списываются ос склада документом 
//  "Заказ материалов в производство". Дата этого документа совпадает с датой плана меню.
//  Иначе остатки списываются документом "Передача материалов в производство",
//  дата которого совпадает с датой выпуска.
//
// Параметры:
//  ВыпускПродукции	 - ДокументСсылка.питВыпускБлюд, ДокументОбъект.питВыпускБлюд - Ссылка 
//   или объект документа выпуска.
// 
// Возвращаемое значение:
//  МоментВремени - Момент времени для контроля остатков.
//
Функция ПолучитьМоментВремениКонтроляОстатковВыпуска(ВыпускПродукции) Экспорт
	
	Если ТипЗнч(ВыпускПродукции) = Тип("Структура") И ВыпускПродукции.Свойство("Дата") Тогда
		Возврат ВыпускПродукции.Дата;
	КонецЕсли;
	
	Если НЕ (ТипЗнч(ВыпускПродукции) = Тип("ДокументСсылка.питВыпускБлюд") 
		ИЛИ ТипЗнч(ВыпускПродукции) = Тип("ДокументОбъект.питВыпускБлюд")
		ИЛИ ТипЗнч(ВыпускПродукции) = Тип("ДокументОбъект.питПланМеню")
		ИЛИ ТипЗнч(ВыпускПродукции) = Тип("ДокументСсылка.питПланМеню")) Тогда
		Возврат Дата("00010101");
	КонецЕсли;
	
	Возврат ВыпускПродукции.МоментВремени(); // Пока не используем код ниже, задел для возможных изменений
	
	Если ЗначениеЗаполнено(ВыпускПродукции.ДокументОснование) Тогда
		МоментВремени = ВыпускПродукции.ДокументОснование.МоментВремени();
	Иначе
		МоментВремени = ВыпускПродукции.МоментВремени();
	КонецЕсли;
	
	Возврат МоментВремени;
	
КонецФункции // ПолучитьМоментВремениКонтроляОстатковВыпуска()

// Процедура получает таблицу трудозатрат для загрузки в ТЧ документов "Выпуск блюд" и "План меню".
//
// Параметры:
//  ТаблицаБлюд		 - ТаблицаЗначений	 - ТаблицаБлюд, получанная при заполнении производства.
//  ИндексыТаблиц	 - Строка			 - Строка с индексами таблиц, 
//   которые должны быть получены, разделенные ";" с двух сторон. Например: ";0;1;"
//   Список индексов таблиц:
//    0 - таблица трудозатрат
//    1 - таблица побочных изделий
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица трудозатрат.
//  Массив - Массив таблиц значений.
//   Список возвращаемых таблиц:
//    0 - таблица трудозатрат
//    1 - таблица побочных изделий
//
Функция ПолучитьДополнительныеТаблицыВыпускаПоТаблицеБлюд(ТаблицаБлюд, ИндексыТаблиц) Экспорт
	
	МассивРезультат = Новый Массив; // Массив таблиц результата.
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Блюда.ИдентификаторСтроки,
	|	Блюда.Рецептура,
	|	Блюда.КоличествоНоменклатуры
	|ПОМЕСТИТЬ втБлюда
	|ИЗ
	|	&Блюда КАК Блюда
	|;";
	Если Найти(ИндексыТаблиц,";0;") Тогда
		Запрос.Текст = Запрос.Текст + "
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втБлюда.ИдентификаторСтроки КАК ИдентификаторБлюда,
		|	питРецептураТрудозатраты.ВидРабот,
		|	втБлюда.КоличествоНоменклатуры / ВЫБОР
		|		КОГДА питРецептура.Количество = 0
		|			ТОГДА 1
		|		ИНАЧЕ питРецептура.Количество
		|	КОНЕЦ * питРецептураТрудозатраты.Количество КАК Количество,
		|	питРецептураТрудозатраты.СтатьяКалькуляции,
		|	питРецептура.Подразделение
		|ИЗ
		|	втБлюда КАК втБлюда
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.питРецептура.Трудозатраты КАК питРецептураТрудозатраты
		|		ПО втБлюда.Рецептура = питРецептураТрудозатраты.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.питРецептура КАК питРецептура
		|		ПО втБлюда.Рецептура = питРецептура.Ссылка
		|;";
	КонецЕсли;
	Если Найти(ИндексыТаблиц,";1;") Тогда
		Запрос.Текст = Запрос.Текст + "ВЫБРАТЬ
		|	втБлюда.ИдентификаторСтроки КАК ИдентификаторБлюда,
		|	питРецептураПобочныеИзделия.Подразделение,
		|	питРецептураПобочныеИзделия.Получатель,
		|	питРецептураПобочныеИзделия.Номенклатура,
		|	питРецептураПобочныеИзделия.Характеристика,
		|	питРецептураПобочныеИзделия.Серия,
		|	питРецептураПобочныеИзделия.СтатусУказанияСерий,
		|	втБлюда.КоличествоНоменклатуры / ВЫБОР
		|		КОГДА питРецептура.Количество = 0
		|			ТОГДА 1
		|		ИНАЧЕ питРецептура.Количество
		|	КОНЕЦ * питРецептураПобочныеИзделия.КоличествоУпаковок КАК КоличествоУпаковок,
		|	втБлюда.КоличествоНоменклатуры / ВЫБОР
		|		КОГДА питРецептура.Количество = 0
		|			ТОГДА 1
		|		ИНАЧЕ питРецептура.Количество
		|	КОНЕЦ * питРецептураПобочныеИзделия.Количество КАК Количество,
		|	питРецептураПобочныеИзделия.Упаковка,
		|	питРецептураПобочныеИзделия.Цена,
		|	втБлюда.КоличествоНоменклатуры / ВЫБОР
		|		КОГДА питРецептура.Количество = 0
		|			ТОГДА 1
		|		ИНАЧЕ питРецептура.Количество
		|	КОНЕЦ * питРецептураПобочныеИзделия.Сумма КАК Сумма,
		|	питРецептураПобочныеИзделия.СтатьяКалькуляции,
		|	питРецептураПобочныеИзделия.НомерСтроки
		|ИЗ
		|	втБлюда КАК втБлюда
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.питРецептура.ПобочныеИзделия КАК питРецептураПобочныеИзделия
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.питРецептура КАК питРецептура
		|			ПО питРецептураПобочныеИзделия.Ссылка = питРецептура.Ссылка
		|		ПО втБлюда.Рецептура = питРецептураПобочныеИзделия.Ссылка";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Блюда", ТаблицаБлюд);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если Найти(ИндексыТаблиц,";0;") <> Неопределено Тогда
		МассивРезультат.Добавить(РезультатЗапроса[1].Выгрузить());
	КонецЕсли;
	Если Найти(ИндексыТаблиц,";1;") <> Неопределено Тогда
		МассивРезультат.Добавить(РезультатЗапроса[2].Выгрузить());
	КонецЕсли;
	
	Возврат МассивРезультат;
	
КонецФункции

// Процедура получает таблицу трудозатрат для загрузки в ТЧ документов "Выпуск блюд" и "План меню".
//
// Параметры:
//  ТаблицаБлюд	 - ТаблицаЗначений	 - ТаблицаБлюд, получанная при заполнении производства.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица трудозатрат.
//
Функция ПолучитьТаблицуТрудозатрат(ТаблицаБлюд) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Блюда.ИдентификаторСтроки,
	|	Блюда.Рецептура,
	|	Блюда.КоличествоНоменклатуры
	|ПОМЕСТИТЬ втБлюда
	|ИЗ
	|	&Блюда КАК Блюда
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втБлюда.ИдентификаторСтроки КАК ИдентификаторБлюда,
	|	питРецептураТрудозатраты.ВидРабот,
	|	втБлюда.КоличествоНоменклатуры / ВЫБОР
	|		КОГДА питРецептура.Количество = 0
	|			ТОГДА 1
	|		ИНАЧЕ питРецептура.Количество
	|	КОНЕЦ * питРецептураТрудозатраты.Количество КАК Количество,
	|	питРецептураТрудозатраты.СтатьяКалькуляции,
	|	питРецептура.Подразделение
	|ИЗ
	|	втБлюда КАК втБлюда
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.питРецептура.Трудозатраты КАК питРецептураТрудозатраты
	|		ПО втБлюда.Рецептура = питРецептураТрудозатраты.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.питРецептура КАК питРецептура
	|		ПО втБлюда.Рецептура = питРецептура.Ссылка";
	
	Запрос.УстановитьПараметр("Блюда", ТаблицаБлюд);
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Процедура обнуляет фактическое количество переделов в случае, если все что входит в его состав имеет нулевое фактическое количество.
//
// Параметры:
//  Производство - ТаблицаЗначений - ТаблицаПроизводства
//
Процедура ОбнулитьФактическоеКоличествоПеределовПоСоставу(Производство, Сортировать = Ложь) Экспорт
	
	Возврат; //debug
	
	Если Сортировать Тогда
		Производство.Сортировать("Порядок Убыв,Номенклатура Возр,Рецептура Возр");
	КонецЕсли;
	
	ТекБлюдо = Неопределено;
	ТекХарактеристика = Неопределено;
	//ТекКоличество = 
	ЕстьКоличествоФактВСоставе = Ложь;
	Для каждого ТекСтрокаПроизводство Из Производство Цикл
		Если ТекСтрокаПроизводство.Номенклатура <> ТекБлюдо И ТекСтрокаПроизводство.Характеристика <> ТекХарактеристика Тогда
			ТекБлюдо = ТекСтрокаПроизводство.Номенклатура;
			ТекХарактеристика = ТекСтрокаПроизводство.Характеристика <> ТекХарактеристика;
			ЕстьКоличествоФактВСоставе = Ложь;
		КонецЕсли;
		
		// Идея в том, чтобы обнулять количество факт переделов в блюде, если у самих переделов количество факт его составляющих тоже равно 0
		// Иначе возникает ситуация нехватки автопередела для приготовления Блюда верхнего уровня, если включить флаг "Разрешить недовложения"
		
	КонецЦикла;
	
КонецПроцедуры

Функция УстановитьЗапрещенныеТовары(ТаблицаПроизводства, ТаблицаТоваров) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	*
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	&ТаблицаТоваров КАК питВыпускБлюдТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПроизводство.Номенклатура,
	|	втПроизводство.Характеристика,
	|	втПроизводство.Рецептура,
	|	втПроизводство.ПриготовлениеЗапрещено
	|ПОМЕСТИТЬ втПроизводство
	|ИЗ
	|	&ТаблицаПроизводства КАК втПроизводство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПроизводство.Номенклатура,
	|	втПроизводство.Характеристика,
	|	втПроизводство.Рецептура,
	|	МАКСИМУМ(втПроизводство.ПриготовлениеЗапрещено) КАК ПриготовлениеЗапрещено
	|ПОМЕСТИТЬ втПроизводствоЗапрещено
	|ИЗ
	|	втПроизводство КАК втПроизводство
	|ГДЕ
	|	втПроизводство.ПриготовлениеЗапрещено = ИСТИНА
	|СГРУППИРОВАТЬ ПО
	|	втПроизводство.Номенклатура,
	|	втПроизводство.Характеристика,
	|	втПроизводство.Рецептура	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТовары.*,
	|	ВЫБОР
	|		КОГДА втПроизводствоЗапрещено.ПриготовлениеЗапрещено ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПриготовлениеЗапрещено
	|ИЗ
	|	втТовары КАК втТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПроизводствоЗапрещено КАК втПроизводствоЗапрещено
	|		ПО втТовары.Номенклатура = втПроизводствоЗапрещено.Номенклатура
	|			И втТовары.Характеристика = втПроизводствоЗапрещено.Характеристика
	|			И втТовары.Рецептура = втПроизводствоЗапрещено.Рецептура
	|";
		
	Запрос.УстановитьПараметр("ТаблицаПроизводства"	, ТаблицаПроизводства);
	Запрос.УстановитьПараметр("ТаблицаТоваров"		, ТаблицаТоваров);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	ЗапрещенныеТовары = ТаблицаПроизводства.НайтиСтроки(Новый Структура("ПриготовлениеЗапрещено", Истина));
	Для каждого ЗапрещенныйТовар Из ЗапрещенныеТовары Цикл
		ТаблицаПроизводства.Удалить(ЗапрещенныйТовар);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#Область Серии

// Устанавливаем условное оформление для статусов указания серий и добавляет условное оформление общепит
//
// Параметры:
//		Форма - Форма - Содержит данную форму
//		СерииВсегдаВТЧТовары - Булево - Истина, если у объекта нет специальной ТЧ для указания серий
//		ИмяПоляВводаСтатусаУказанияСерий - Строка - Наименование элемента формы, содержащего статус указания
//									   				серии номенклатуры,если оно отличается от "ТоварыСтатусУказанияСерий"
//		ПутьКПолюОтбора - Строка - Полный путь к реквизиту "СтатусУказанияСерий",
//									если он отличается от "Объект.Товары.СтатусУказанияСерий"
//
Процедура УстановитьУсловноеОформлениеСтатусовУказанияСерий(Форма,
															СерииВсегдаВТЧТовары,	
													  		ИмяПоляВводаСтатусаУказанияСерий = "ТоварыСтатусУказанияСерий",
													 		ПутьКПолюОтбора = "Объект.Товары.СтатусУказанияСерий") Экспорт
	
	НоменклатураСервер.УстановитьУсловноеОформлениеСтатусовУказанияСерий(Форма	, СерииВсегдаВТЧТовары, ИмяПоляВводаСтатусаУказанияСерий, ПутьКПолюОтбора);
	
КонецПроцедуры

// Устанавливаем условное оформление для серий номенклатуры и добавляет условное оформление общепит
//
// Параметры:
//		Форма - Форма - Содержит данную форму
//		ОсобыйВариантУказанияСерий - Булево, Строка - Ложь, если серии указываются в отдельной ТЧ,
//			"СерииВсегдаВТЧТовары" - если у объекта нет специальной ТЧ для указания серий,
//			"СерииПриПланированииОтгрузкиУказываютсяВТЧТовары" - если серии могут указываться в разных ТЧ,
//				при этом серии с политикой учета "При планировании отгрузки" указываются в ТЧ Товары,
//		ИмяПоляВводаСерии - Строка - Наименование элемента формы, содержащего серии номенклатуры,
//									   если оно отличается от "ТоварыСерия"
//		ПутьКПолюОтбораСтатусУказанияСерий - Строка - Полный путь к реквизиту "Статус указания серий",
//														если он отличается от "Объект.Товары.СтатусУказанияСерий"
//		ПутьКПолюОтбораТипНоменклатуры - Строка - Полный путь к реквизиту "Тип номенклатуры",
//														если он отличается от "Объект.Товары.ТипНоменклатуры"
//
Процедура УстановитьУсловноеОформлениеСерийНоменклатуры(Форма,
														ОсобыйВариантУказанияСерий,
														ИмяПоляВводаСерии = "ТоварыСерия",
														ПутьКПолюОтбораСтатусУказанияСерий = "Объект.Товары.СтатусУказанияСерий",
														ПутьКПолюОтбораТипНоменклатуры = "Объект.Товары.ТипНоменклатуры") Экспорт
	
	НоменклатураСервер.УстановитьУсловноеОформлениеСерийНоменклатуры(Форма		, ОсобыйВариантУказанияСерий, ИмяПоляВводаСерии, ПутьКПолюОтбораСтатусУказанияСерий, ПутьКПолюОтбораТипНоменклатуры);
	
	УсловноеОформление = Форма.УсловноеОформление;
	ЭлементыФормы = Форма.Элементы;
	
	// Добавим еще УО для документов общепита по "АвтосозданиюСерий"
	Если ОсобыйВариантУказанияСерий = "СерииВсегдаВТЧТовары" Тогда
		
		//Серия указывается в ТЧ Товары
		ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
		
		ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляВводаСерии].Имя);
		
		СписокСтатусовСерий = Новый СписокЗначений;
		СписокСтатусовСерий.ЗагрузитьЗначения(НоменклатураКлиентСервер.СтатусыСерийСерияНеУказана());
		
		ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбораСтатусУказанияСерий);
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		ОтборЭлемента.ПравоеЗначение = СписокСтатусовСерий;
		
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<будет создана автоматически>'"));
		
	КонецЕсли;
	
КонецПроцедуры

// Функция ищет строки ТЧТовары с неуказанной серией и создает для таких строк серию автоматически.
//  Срок годности определяется как текущая дата документа + срок годности номенклатуры.
//  Если параметр "Служебная серия" установлен в "Истина", то сначала происходит поиск служебной серии в справочнике по виду номенклатуры.
//  Если серия не найдена, то создается новая и далее служебной серией заполняется незаполненная серия в ТЧТовары.
//
// Параметры:
//  ДокументОбъект			 - ДокументОбъект - Объект документа, в котором заполняем ТЧ "Серии".
//  ПараметрыУказанияСерий	 - Структура - Параметры указания серий, возвращаемые соотвествующей процедурой модуля менеджера документа.
//  ТекстОшибки				 - Строка - Текст ошибки в случае ее возникновения.
//  СлужебнаяСерия			 - Булево - Признак того, что заполнять необходимо служебной серией.
// 
// Возвращаемое значение:
//  Булево - Результат работы функции.
//
Функция СоздатьСерииАвтоматически(ДокументОбъект, ПараметрыУказанияСерий, ТекстОшибки = "", СлужебнаяСерия = Ложь) Экспорт
	
	ВсеОк = Истина;
	
	// Имя служебной серии общепита.
	ИмяСлужебнойСерии = "Служебная серия общепит";
	
	// Статусы, для которых будем искать/создавать серии.
	СтатусыСерийСерияНеУказана = НоменклатураКлиентСервер.СтатусыСерийСерияНеУказана();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ * ПОМЕСТИТЬ втТовары ИЗ &втТовары КАК втТовары;
	|/////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТовары.*,
	|	ЕСТЬNULL(СпрНоменклатура.ВидНоменклатуры, ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)) КАК ВидНоменклатуры,
	|	ЕСТЬNULL(СпрНоменклатура.СрокГодности, 0) КАК СрокГодности,
	|	ЕСТЬNULL(ВидыНоменклатуры.ИспользоватьСрокГодностиСерии, ИСТИНА) КАК ИспользоватьСрокГодностиСерии,
	|	ЕСТЬNULL(СпрНоменклатура.ЕдиницаИзмеренияСрокаГодности, ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)) КАК ЕдиницаИзмеренияСрокаГодности
	|"+?(СлужебнаяСерия,"ПОМЕСТИТЬ втДанныеПоСериям","")+"
	|ИЗ
	|	втТовары КАК втТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|			ПО СпрНоменклатура.ВидНоменклатуры = ВидыНоменклатуры.Ссылка
	|		ПО втТовары.Номенклатура = СпрНоменклатура.Ссылка
	|"+?(СлужебнаяСерия,";
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(СерииНоменклатуры.Ссылка) КАК Ссылка,
	|	СерииНоменклатуры.ВидНоменклатуры КАК ВидНоменклатуры,
	|	СерииНоменклатуры.Наименование КАК Наименование
	|ПОМЕСТИТЬ втСлужебнаяСерия
	|ИЗ
	|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|ГДЕ
	|	СерииНоменклатуры.Наименование = &ИмяСлужебнойСерии
	|	И СерииНоменклатуры.ВидНоменклатуры В (ВЫБРАТЬ ВидНоменклатуры ИЗ втДанныеПоСериям)
	|	И НЕ СерииНоменклатуры.ПометкаУдаления
	|
	|СГРУППИРОВАТЬ ПО
	|	СерииНоменклатуры.ВидНоменклатуры,
	|	СерииНоменклатуры.Наименование
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеПоСериям.*,
	|	ISNULL(СерииСпр.Ссылка,ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) КАК СерияСсылка
	|ИЗ втДанныеПоСериям КАК втДанныеПоСериям
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСлужебнаяСерия КАК СерииСпр
	|		ПО СерииСпр.ВидНоменклатуры = втДанныеПоСериям.ВидНоменклатуры
	|			И СерииСпр.Наименование = &ИмяСлужебнойСерии
	|","");
	
	Запрос.УстановитьПараметр("втТовары"			,ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить());
	Запрос.УстановитьПараметр("ИмяСлужебнойСерии"	,ИмяСлужебнойСерии);
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		
		// Работаем с выгрузкой, т.к. необходимо будет изменять строки ТЧ.
		Выгрузка = Результат.Выгрузить();
		
		Для каждого ТекПроверяемаяСтрока Из Выгрузка Цикл
			
			Если СтатусыСерийСерияНеУказана.Найти(ТекПроверяемаяСтрока.СтатусУказанияСерий) <> Неопределено Тогда
				
				Если НЕ СлужебнаяСерия Тогда
					
					// Создаем обычную серию.
					НомерЧислом					= Справочники.СерииНоменклатуры.ВычислитьМаксимальныйНомерСерии(ТекПроверяемаяСтрока.ВидНоменклатуры) + 1;
					Номер						= Формат(НомерЧислом, "ЧЦ=8; ЧВН=; ЧГ=");
					
					СерияОбъект = Справочники.СерииНоменклатуры.СоздатьЭлемент();
					СерияОбъект.Номер			= Номер;
					СерияОбъект.ВидНоменклатуры	= ТекПроверяемаяСтрока.ВидНоменклатуры;
					
					// Срок годности определяется как дата документа + срок годности изщ реквизитов номенклатуры.
					Если ТекПроверяемаяСтрока.ИспользоватьСрокГодностиСерии Тогда
						Если ТекПроверяемаяСтрока.ЕдиницаИзмеренияСрокаГодности = Перечисления.ЕдиницыИзмеренияВремени.Сутки Тогда // Час, Месяц, Год
							СекундВОднойЕдиницеВремени = 24*60*60;
							ДобавитьВремени = ТекПроверяемаяСтрока.СрокГодности * СекундВОднойЕдиницеВремени;
							СерияОбъект.ГоденДо         = ДокументОбъект.Дата + ДобавитьВремени;
						ИначеЕсли ТекПроверяемаяСтрока.ЕдиницаИзмеренияСрокаГодности = Перечисления.ЕдиницыИзмеренияВремени.Час Тогда
							СекундВОднойЕдиницеВремени = 60*60;
							ДобавитьВремени = ТекПроверяемаяСтрока.СрокГодности * СекундВОднойЕдиницеВремени;
							СерияОбъект.ГоденДо         = ДокументОбъект.Дата + ДобавитьВремени;
						ИначеЕсли ТекПроверяемаяСтрока.ЕдиницаИзмеренияСрокаГодности = Перечисления.ЕдиницыИзмеренияВремени.Месяц Тогда
							СерияОбъект.ГоденДо         = ДобавитьМесяц(ДокументОбъект.Дата,ТекПроверяемаяСтрока.СрокГодности);
						ИначеЕсли ТекПроверяемаяСтрока.ЕдиницаИзмеренияСрокаГодности = Перечисления.ЕдиницыИзмеренияВремени.Год Тогда
							СерияОбъект.ГоденДо         = ДобавитьМесяц(ДокументОбъект.Дата,ТекПроверяемаяСтрока.СрокГодности*12);
						КонецЕсли;
					КонецЕсли;
					
					Попытка
						
						СерияОбъект.ОбменДанными.Загрузка = Истина; // чтобы не выполняся типовой код в модуле объекта.
						
						// Определяем наименование серии, добавляем суффикс "(Авто)".
						//+25
						НастройкиИспользованияСерий = Справочники.ВидыНоменклатуры.НастройкиИспользованияСерий(СерияОбъект.ВидНоменклатуры);
						//-25
						Номер = СокрЛП(СерияОбъект.Номер);
						Если НастройкиИспользованияСерий.ТочностьУказанияСрокаГодностиСерии = Перечисления.ТочностиУказанияСрокаГодности.СТочностьюДоЧасов Тогда
							СерияОбъект.ГоденДо = НачалоЧаса(СерияОбъект.ГоденДо);
						Иначе
							СерияОбъект.ГоденДо = НачалоДня(СерияОбъект.ГоденДо);
						КонецЕсли;
						//+25
						Если ЗначениеЗаполнено(НастройкиИспользованияСерий.ШаблонРабочегоНаименованияСерии) Тогда
							СерияОбъект.Наименование = НоменклатураСервер.НаименованиеПоШаблону(НастройкиИспользованияСерий.ШаблонРабочегоНаименованияСерии, СерияОбъект);
						Иначе
							СерияОбъект.Наименование = НоменклатураКлиентСервер.ПредставлениеСерииБезРасчетаПоШаблонуНаименования(НастройкиИспользованияСерий, СерияОбъект);
						КонецЕсли;
						СерияОбъект.Наименование = СерияОбъект.Наименование + " (Авто)";
						//-25
						
						СерияОбъект.Записать();
						
					Исключение
						
						ТекстОшибки = ТекстОшибки + "
						|Не удалось автоматически создать серию по причине: "+ОписаниеОшибки();
						ВсеОк = Ложь;
						Возврат ВсеОк;
						
					КонецПопытки;
					
					ТекПроверяемаяСтрока.Серия = СерияОбъект.Ссылка;
					
				Иначе // Служебная серия - Истина
					
					Если ЗначениеЗаполнено(ТекПроверяемаяСтрока.СерияСсылка) Тогда
						// Служебная серия найдена, заполним ей строку ТЧ.
						ТекПроверяемаяСтрока.Серия = ТекПроверяемаяСтрока.СерияСсылка;
					Иначе
						// Создание служебной серии.
						СерияОбъект = Справочники.СерииНоменклатуры.СоздатьЭлемент();
						СерияОбъект.Номер			= "000000000";
						СерияОбъект.ВидНоменклатуры	= ТекПроверяемаяСтрока.ВидНоменклатуры;
						СерияОбъект.ГоденДо			= Дата("39991231");
						СерияОбъект.Наименование	= ИмяСлужебнойСерии;
						Попытка
							СерияОбъект.ОбменДанными.Загрузка = Истина;
							СерияОбъект.Записать();
						Исключение
							ТекстОшибки = ТекстОшибки + "
							|Не удалось автоматически создать служебную серию по причине: "+ОписаниеОшибки();
							ВсеОк = Ложь;
							Возврат ВсеОк;
						КонецПопытки;
						ТекПроверяемаяСтрока.Серия = СерияОбъект.Ссылка;
					КонецЕсли;
					
				КонецЕсли; // Если НЕ СлужебнаяСерия Тогда
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Загрузка результата в ТЧ
		ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧТовары].Загрузить(Выгрузка);
		
		// Перерасчет статусов, они могли измениться.
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументОбъект, ПараметрыУказанияСерий);
		
	КонецЕсли;
	
	Возврат ВсеОк;
	
КонецФункции

// Процедура для всех строк в ТЧ "Товары", в которых заполнена серия, добавляет строку в ТЧ "Серии".
//  Далее ПередЗаписью документа при помощи типового метода "НоменклатураСервер.ОчиститьНеиспользуемыеСерии" 
//  система очищает либо серии в ТЧ "Товары" либо серии в ТЧ "Серии" в зависимости от типа политики учета серий.
//
// Параметры:
//  ДокументОбъект			 - ДокументОбъект - объект документа, в котором заполняем ТЧ "Серии".
//  ПараметрыУказанияСерий	 - Структура - параметры указания серий, возвращаемые соотвествующей процедурой модуля менеджера документа.
//
Процедура ЗаполнитьСерииВТЧСерииПоТЧТовары(ДокументОбъект, ПараметрыУказанияСерий) Экспорт
	
	Если Не ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.ИмяТЧТовары = ПараметрыУказанияСерий.ИмяТЧСерии Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ТекСтрокаТовары Из ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧТовары] Цикл
		Если ЗначениеЗаполнено(ТекСтрокаТовары.Серия) Тогда
			НовСтрокаСерия = ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧСерии].Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрокаСерия, ТекСтрокаТовары);
		КонецЕсли;
	КонецЦикла;
	
	// Перезаполним статусы, они могли поменяться.
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументОбъект, ПараметрыУказанияСерий);
	
КонецПроцедуры

// Процедура ищет незаполненные строки в ТЧТовары с политикой учета "Учет себестоимости по сериям", 
//  после чего ищет соответствующие строки в ТЧ "Серии", которые могли там появиться после заполнения по FEFO.
//  Далее производится перенос строк серий из ТЧСерии в ТЧТовары.
//
// Параметры:
//  ДокументОбъект			 - ДокументОбъект - объект документа, в котором заполняем ТЧ "Серии".
//  ПараметрыУказанияСерий	 - Структура - параметры указания серий, возвращаемые соотвествующей процедурой модуля менеджера документа.
//
Процедура ЗаполнитьСерииВТЧТоварыПоТЧСерииПоСебестоимости(ДокументОбъект, ПараметрыУказанияСерий, ИспользуетсяНазначение = Истина) Экспорт
	
	Если Не ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.ИмяТЧТовары = ПараметрыУказанияСерий.ИмяТЧСерии Тогда
		Возврат;
	КонецЕсли;
	
	// Поиск строк товаров с незаполненными сериями с политикой "Учет себестоимости по сериям".
	СтрокиСебестоимости = ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧТовары].НайтиСтроки(Новый Структура("СтатусУказанияСерий, Серия", 13, Справочники.СерииНоменклатуры.ПустаяСсылка()));
	Для каждого ТекСтрокаСебестоимости Из СтрокиСебестоимости Цикл
		
		Если ИспользуетсяНазначение Тогда
			// Поиск строк серий, соответствующих текущей строке товаров.
			НайденныеСтрокиСерий = ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧСерии].НайтиСтроки(Новый Структура("Номенклатура,Характеристика,Назначение"
			,ТекСтрокаСебестоимости.Номенклатура
			,ТекСтрокаСебестоимости.Характеристика
			,ТекСтрокаСебестоимости.Назначение));
		Иначе
			// Поиск строк серий, соответствующих текущей строке товаров.
			НайденныеСтрокиСерий = ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧСерии].НайтиСтроки(Новый Структура("Номенклатура,Характеристика"
			,ТекСтрокаСебестоимости.Номенклатура
			,ТекСтрокаСебестоимости.Характеристика));
		КонецЕсли;
		
		// Перенос серий в ТЧ Товары.
		Сч = 1;
		Для каждого ТекСтрокаСерии Из НайденныеСтрокиСерий Цикл
			Если Сч = 1 Тогда
				НовСтрокаСебестоимости = ТекСтрокаСебестоимости;
			Иначе
				НовСтрокаСебестоимости = ДокументОбъект[ПараметрыУказанияСерий.ИмяТЧТовары].Добавить();
				ЗаполнитьЗначенияСвойств(НовСтрокаСебестоимости,ТекСтрокаСебестоимости);
			КонецЕсли;
			НовСтрокаСебестоимости.Серия				= ТекСтрокаСерии.Серия;
			НовСтрокаСебестоимости.Количество			= ТекСтрокаСерии.Количество;
			НовСтрокаСебестоимости.КоличествоУпаковок	= ТекСтрокаСерии.Количество * питОбщегоНазначения.ПолучитьКоэффициентУпаковки(ТекСтрокаСебестоимости.Номенклатура,ТекСтрокаСебестоимости.Упаковка);
			Сч = Сч + 1;
		КонецЦикла;
		
		// Пересчет статусов, которые могли поменяться.
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументОбъект,ПараметрыУказанияСерий);
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура подбирает серии по FEFO и заполняет подобранными значениями ТЧ "Серии"
//  Учитываются движения документа, заполненные серии перезаполняются
//
// Параметры:
//  Объект							 - ДанныеФормыКоллекция или ДокументОбъект - объект, в котором нужно заполнить статусы.
//  ПараметрыУказанияСерий			 - Структура - параметры указания серий, возвращаемые соотвествующей процедурой модуля менеджера документа.
//
Процедура ЗаполнитьСерииПоFEFOТиповая(Объект,ПараметрыУказанияСерий,ДанныеДляЗаполнения=Неопределено) Экспорт
	
	Если ДанныеДляЗаполнения = Неопределено Тогда
		ДанныеДляЗаполнения = Новый Структура;
	КонецЕсли;
	
	// Меняем статусы, чтобы отработала типовая функция заполнения по FEFO
	Для каждого ТекОбрабатываемаяСтрока Из Объект[ПараметрыУказанияСерий.ИмяТЧТовары] Цикл
		
		//Если ТекОбрабатываемаяСтрока
		Если ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 1 
			ИЛИ ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 3
			ИЛИ ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 7
			ИЛИ ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 9
			ИЛИ ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 13 Тогда
			
			ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 5;
			
		//ИначеЕсли ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 2 
		//	ИЛИ ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 4
		//	ИЛИ ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 8
		//	ИЛИ ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 10
		//	ИЛИ ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 14 Тогда
		//	
		//	ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 6;
		//	
		ИначеЕсли ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 21 
			ИЛИ ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 23
			ИЛИ ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 27
			ИЛИ ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 11
			ИЛИ ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 15 Тогда
			
			ТекОбрабатываемаяСтрока.СтатусУказанияСерий = 25;
			
		КонецЕсли;
		
		Если ДанныеДляЗаполнения.Свойство("Склад") Тогда
			ТекОбрабатываемаяСтрока[ПараметрыУказанияСерий.ИмяПоляСклад] = ДанныеДляЗаполнения.Склад;
		КонецЕсли;
		Если ДанныеДляЗаполнения.Свойство("ВариантОбеспечения") Тогда
			ТекОбрабатываемаяСтрока.ВариантОбеспечения = ДанныеДляЗаполнения.ВариантОбеспечения;
		КонецЕсли;
		Если ДанныеДляЗаполнения.Свойство("Действие") Тогда
			ТекОбрабатываемаяСтрока.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить;
		КонецЕсли;
		
	КонецЦикла;
	
	СтароеПолноеИмя = ПараметрыУказанияСерий.ПолноеИмяОбъекта;
	
	Если ПараметрыУказанияСерий.ИмяТЧТовары = ПараметрыУказанияСерий.ИмяТЧСерии 
		И Найти(ПараметрыУказанияСерий.ПолноеИмяОбъекта, "пит") Тогда
		
		ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ОрдерНаПеремещениеТоваров";
		
	КонецЕсли;
	
	// Вызов типовой процедуры
	НоменклатураСервер.ЗаполнитьСерииПоFEFO(Объект,ПараметрыУказанияСерий);
	
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = СтароеПолноеИмя;
	
КонецПроцедуры

#Область НоменклатураСервер

// Процедура подбирает серии по FEFO и заполняет подобранными значениями ТЧ "Серии".
//
// Параметры:
//  Объект							 - ДанныеФормыКоллекция или ДокументОбъект - объект, в котором нужно заполнить статусы.
//  ПараметрыУказанияСерий			 - Структура - параметры указания серий, возвращаемые соотвествующей процедурой модуля менеджера документа.
//
Процедура ЗаполнитьСерииПоFEFO(Объект,ПараметрыУказанияСерий,СтатусыУказанияСерийЗаполнены = Истина) Экспорт
	
	Если Не ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не СтатусыУказанияСерийЗаполнены Тогда
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(Объект,ПараметрыУказанияСерий);
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.ИмяТЧТовары = ПараметрыУказанияСерий.ИмяТЧСерии Тогда
		//>>Швецов. 29.05.2025. Заполняем серии в документе производтсво без заказа по вскрытой таре
		//ЗаполнитьСерииПоFEFOВТЧТовары(Объект, ПараметрыУказанияСерий);
		MRS_ЗагрузкаЕГАИСФронт.ЗаполнитьМаркиПоОстаткамИСВТЧТовары(Объект, ПараметрыУказанияСерий); 
		//<<
	Иначе
		ЗаполнитьСерииПоFEFOВТЧСерии(Объект, ПараметрыУказанияСерий);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(Объект,ПараметрыУказанияСерий);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСерииПоFEFOВТЧСерии(Объект, ПараметрыУказанияСерий)
	
	Если ПараметрыУказанияСерий.ИмяПоляПомещение <> Неопределено Тогда
		// Если серии по FEFO заполняется в ТЧ "Серии", то помещения в документе не может быть.
		ТекстИсключения = НСтр("ru = 'Ошибка заполнения серий по FEFO. Есть складское помещение.';
								|en = 'An error occurred while populating FEFO batch. There is a wareroom.'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
    
    СкладыВТЧ = ПараметрыУказанияСерий.ПоляСвязи.Найти(ПараметрыУказанияСерий.ИмяПоляСклад) <> Неопределено
		Или ПараметрыУказанияСерий.ИменаПолейДополнительные.Найти(ПараметрыУказанияСерий.ИмяПоляСклад) <> Неопределено;
    
	ТекстЗапроса = ТекстЗапросаДляЗаполненияСерийПОFEFO(ПараметрыУказанияСерий);
	
	КлючевыеПоля = "Номенклатура,Характеристика";
    ПоляСвязиВТекстЗапроса = "";
    Для Каждого ПолеСвязи Из ПараметрыУказанияСерий.ПоляСвязи Цикл
        КлючевыеПоля = КлючевыеПоля + "," + ПолеСвязи;
        ПоляСвязиВТекстЗапроса = ПоляСвязиВТекстЗапроса + "
        |ДанныеДокумента." + ПолеСвязи + ",";
    КонецЦикла;
    
    ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокумента.ПоляСвязи,", ПоляСвязиВТекстЗапроса);
    
	Если ПараметрыУказанияСерий.ЭтоНакладная
		И ПараметрыУказанияСерий.СкладскиеОперации.Найти(Перечисления.СкладскиеОперации.ОтгрузкаПоПеремещению) <> Неопределено Тогда
		ИмяПоляСклад = "СкладОтправитель";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокумента.Назначение КАК Назначение", "ДанныеДокумента.НазначениеОтправителя КАК Назначение");
	ИначеЕсли ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ПередачаТоваровХранителю" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокумента.Назначение КАК Назначение", "ДанныеДокумента.НазначениеОтправителя КАК Назначение");
	//++ НЕ УТ
	ИначеЕсли ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ДвижениеПродукцииИМатериалов"
		И (ПараметрыУказанияСерий.СкладскиеОперации.Найти(Перечисления.СкладскиеОперации.ПередачаВПроизводствоОтгрузка) <> Неопределено 
			Или ПараметрыУказанияСерий.СкладскиеОперации.Найти(Перечисления.СкладскиеОперации.ОтгрузкаПоПеремещениюВПроизводстве) <> Неопределено)  Тогда
		ИмяПоляСклад = "Отправитель";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокумента.Назначение КАК Назначение", "ДанныеДокумента.НазначениеОтправителя КАК Назначение");
	ИначеЕсли ПараметрыУказанияСерий.СкладскиеОперации.Найти(Перечисления.СкладскиеОперации.ОтгрузкаПоПеремещениюВПроизводстве) <> Неопределено Тогда
		ИмяПоляСклад = "ПодразделениеОтправитель";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокумента.Назначение КАК Назначение", "ДанныеДокумента.НазначениеОтправителя КАК Назначение");
	//-- НЕ УТ
    Иначе
        ИмяПоляСклад = ПараметрыУказанияСерий.ИмяПоляСклад;
    КонецЕсли;
    
    Запрос = Новый Запрос;
    МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
   	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
    
    Если Не СкладыВТЧ Тогда
        ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеДокумента.Склад", "&Склад");
        Запрос.УстановитьПараметр("Склад",Объект[ИмяПоляСклад]);
    КонецЕсли;
    
    Запрос.Текст = ТекстЗапроса;
	ТаблицаСерий = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].Выгрузить();
	Запрос.УстановитьПараметр("ТаблицаСерий", ТаблицаСерий);
	Если ПараметрыУказанияСерий.ТоварВШапке Тогда
		ТаблицаТоваровПодмена = ТаблицаСерий.СкопироватьКолонки(); // ТаблицаЗначений
		ТаблицаТоваровПодмена.Очистить();
		ТаблицаТоваровПодмена.Колонки.Добавить("СтатусУказанияСерий",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(2,0,ДопустимыйЗнак.Неотрицательный)));
		ЗаполнитьЗначенияСвойств(ТаблицаТоваровПодмена.Добавить(),Объект);
		Запрос.УстановитьПараметр("ТаблицаТоваров", ТаблицаТоваровПодмена);
	Иначе
		Запрос.УстановитьПараметр("ТаблицаТоваров", Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить());
	КонецЕсли;
    Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
    
    РезультатЗапроса = Запрос.ВыполнитьПакет();
    
    Объект[ПараметрыУказанияСерий.ИмяТЧСерии].Загрузить(РезультатЗапроса[5].Выгрузить()); //Серии, которые не надо менять
    
    Выборка = РезультатЗапроса[4].Выбрать();
    СтруктураКоличества = Новый Структура("Количество,КоличествоУпаковок");
	ОстаткиПоСтрокамТоваров = Новый Соответствие;
	
	Пока Выборка.СледующийПоЗначениюПоля("Номенклатура") Цикл
		Пока Выборка.СледующийПоЗначениюПоля("Характеристика") Цикл
			Пока Выборка.СледующийПоЗначениюПоля("Склад") Цикл
				Пока Выборка.СледующийПоЗначениюПоля("Серия") Цикл
					ОстатокПоСерии = Выборка.СвободныйОстаток;
					КэшКлючевыхПолей = Новый Структура(КлючевыеПоля);
					Пока Выборка.Следующий() Цикл // Обход строк товаров
						Если ПараметрыУказанияСерий.ТоварВШапке Тогда
							ТекСтрока = Объект;
						Иначе
							ТекСтрока = Объект[ПараметрыУказанияСерий.ИмяТЧТовары][Выборка.НомерСтроки - 1];
						КонецЕсли;
						Если ОстаткиПоСтрокамТоваров[Выборка.НомерСтроки] = Неопределено Тогда
							ОстатокПоТовару = ТекСтрока.Количество;
						Иначе
							ОстатокПоТовару = ОстаткиПоСтрокамТоваров[Выборка.НомерСтроки];
						КонецЕсли;
						
						Если ОстатокПоТовару = 0 Тогда
							Продолжить;
						КонецЕсли;
						
						Если Не ОбщегоНазначенияУТКлиентСервер.СтруктурыРавны(ТекСтрока,КэшКлючевыхПолей,КлючевыеПоля) Тогда
							ЗаполнитьЗначенияСвойств(КэшКлючевыхПолей,ТекСтрока);
							// Выборка упорядочена по ключевым полям,
							// поэтому изменение ключевых полей говорит нужно добавить новую строку серий.
							НоваяСтрока = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].Добавить();
						КонецЕсли;
						
						ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекСтрока,,"Количество");
						НоваяСтрока.Серия = Выборка.Серия;
						ДобавляемоеКоличество = Мин(ОстатокПоТовару,ОстатокПоСерии);
						СтруктураКоличества.Количество         = НоваяСтрока.Количество + ДобавляемоеКоличество;
						СтруктураКоличества.КоличествоУпаковок = СтруктураКоличества.Количество;
						ЗаполнитьЗначенияСвойств(НоваяСтрока,СтруктураКоличества); 
						
						ОстатокПоТовару = ОстатокПоТовару - ДобавляемоеКоличество;
						ОстаткиПоСтрокамТоваров.Вставить(Выборка.НомерСтроки,ОстатокПоТовару);
						
						Если ОстатокПоТовару = 0 Тогда
							ТекСтрока.СтатусУказанияСерий = 6;
							
							Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
								НоменклатураКлиентСервер.ПересчитатьСтатусУказанияСерийПриОбработке(ПараметрыУказанияСерий,
										ТекСтрока[ИмяПоляСтатус],
										Истина,
										ДобавляемоеКоличество);
							КонецЦикла;

						КонецЕсли;
						ОстатокПоСерии = ОстатокПоСерии - ДобавляемоеКоличество;
						Если ОстатокПоСерии = 0 Тогда
							Прервать;
						КонецЕсли;
						ОстаткиПоСтрокамТоваров.Вставить(Выборка.НомерСтроки, ОстатокПоТовару)
						
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Если ПараметрыУказанияСерий.ТоварВШапке Тогда
		ЗаполнитьЗначенияСвойств(ТаблицаТоваровПодмена[0],Объект);
		ТЧТовары = ТаблицаТоваровПодмена;
	Иначе
		ТЧТовары = Объект[ПараметрыУказанияСерий.ИмяТЧТовары];
	КонецЕсли;
	НайденныеТовары = ТЧТовары.НайтиСтроки(Новый Структура("СтатусУказанияСерий",5)); // Массив из СтрокаТабличнойЧасти
	Если НайденныеТовары.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПоляСклад = ПараметрыУказанияСерий.ИмяПоляСклад;
	СкладыВТЧ = ПараметрыУказанияСерий.ПоляСвязи.Найти(ИмяПоляСклад) <> Неопределено
		Или ПараметрыУказанияСерий.ИменаПолейДополнительные.Найти(ИмяПоляСклад) <> Неопределено;
    
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ПРЕДСТАВЛЕНИЕ(ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения) КАК ЕдиницаИзмерения
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.Номенклатура";
	
	ЕдиницыИзмерения = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Стр Из НайденныеТовары Цикл
		
		Если СкладыВТЧ Тогда
			ТекущийСклад = Стр.Склад;
		Иначе
			ТекущийСклад = Объект[ИмяПоляСклад];
		КонецЕсли;
		ЭтоПодразделение = (ТипЗнч(ТекущийСклад) = Тип("СправочникСсылка.СтруктураПредприятия"));
		
		КоличествоНеРаспределено = 0;
		Если Стр.Количество = 0 Тогда
			
			ТекстСообщения = НСтр("ru = 'Товар ""%ПредставлениеТовара%"" - серии не заполнены. В табличной части ""Товары"" не указано количество.""';
									|en = 'Batches are not filled in for the ""%ПредставлениеТовара%"" goods item. Quantity is not specified in the Goods table.""'");
			
		ИначеЕсли ОстаткиПоСтрокамТоваров[Стр.НомерСтроки] = Неопределено Тогда 
			
			Если ЭтоПодразделение Тогда
				ТекстСообщения = НСтр("ru = 'Товар ""%ПредставлениеТовара%"" - серии не заполнены.
					|В подразделении ""%ПредставлениеСклада%"" не хватает ""%Количество%"" ""%ЕдиницаИзмерения%""';
					|en = 'Batches are not filled in for the ""%ПредставлениеТовара%"" goods item.
					|""%Количество%"" ""%ЕдиницаИзмерения%"" is missing in the ""%ПредставлениеСклада%"" business unit'");
			Иначе
				ТекстСообщения = НСтр("ru = 'Товар ""%ПредставлениеТовара%"" - серии не заполнены.
					|На складе ""%ПредставлениеСклада%"" не хватает ""%Количество%"" ""%ЕдиницаИзмерения%""';
					|en = 'Batches are not filled in for the ""%ПредставлениеТовара%"" goods item.
					|""%Количество%"" ""%ЕдиницаИзмерения%"" is missing in the ""%ПредставлениеСклада%"" warehouse'");
			КонецЕсли; 

			КоличествоНеРаспределено = Стр.Количество;
			
		Иначе
			
			Если ЭтоПодразделение Тогда
				ТекстСообщения = НСтр("ru = 'Товар ""%ПредставлениеТовара%"" - серии заполнены не полностью.
					|В подразделении ""%ПредставлениеСклада%"" не хватает ""%Количество%"" ""%ЕдиницаИзмерения%""';
					|en = 'The ""%ПредставлениеТовара%"" goods - batches are filled in incompletely.
					|""%Количество%"" ""%ЕдиницаИзмерения%"" is not enough in the ""%ПредставлениеСклада%"" business unit'");
			Иначе	
				ТекстСообщения = НСтр("ru = 'Товар ""%ПредставлениеТовара%"" - серии заполнены не полностью.
					|На складе ""%ПредставлениеСклада%"" не хватает ""%Количество%"" ""%ЕдиницаИзмерения%""';
					|en = 'The ""%ПредставлениеТовара%"" goods - batches are filled in incompletely.
					|""%Количество%"" ""%ЕдиницаИзмерения%"" is not enough in the ""%ПредставлениеСклада%"" warehouse'");
			КонецЕсли; 
			КоличествоНеРаспределено = ОстаткиПоСтрокамТоваров[Стр.НомерСтроки];
			
		КонецЕсли;	
		
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ПредставлениеТовара%", НоменклатураКлиентСервер.ПредставлениеНоменклатуры(Стр.Номенклатура, Стр.Характеристика));
		
		Если СкладыВТЧ Тогда
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ПредставлениеСклада%", СкладыСервер.ПолучитьПредставлениеСклада(ТекущийСклад));
		Иначе
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ПредставлениеСклада%", СкладыСервер.ПолучитьПредставлениеСклада(Объект[ИмяПоляСклад]));
		КонецЕсли;
			
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%Количество%", КоличествоНеРаспределено);
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ЕдиницаИзмерения%",ЕдиницыИзмерения.Найти(Стр.Номенклатура,"Номенклатура").ЕдиницаИзмерения);
		Если ПараметрыУказанияСерий.ТоварВШапке Тогда
			Поле = "Номенклатура";
		Иначе
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ПараметрыУказанияСерий.ИмяТЧТовары, Стр.НомерСтроки, "Номенклатура");
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,"Объект");
		
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьСерииПоFEFOВТЧТовары(Объект, ПараметрыУказанияСерий)
	
	СкладВШапке = НЕ (ПараметрыУказанияСерий.ПоляСвязи.Найти(ПараметрыУказанияСерий.ИмяПоляСклад) <> Неопределено
	Или ПараметрыУказанияСерий.ИменаПолейДополнительные.Найти(ПараметрыУказанияСерий.ИмяПоляСклад) <> Неопределено);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ТаблицаТоваров", Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить());
	
	Если ПараметрыУказанияСерий.ИмяПоляПомещение <> Неопределено Тогда
		Помещение = Объект[ПараметрыУказанияСерий.ИмяПоляПомещение];
	Иначе
		Помещение = Справочники.СкладскиеПомещения.ПустаяСсылка();
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Помещение",Помещение);
	Запрос.УстановитьПараметр("Ссылка",Объект.Ссылка);
	//Запрос.УстановитьПараметр("Склад",Объект[ПараметрыУказанияСерий.ИмяПоляСклад]);
	
	Данные = ДанныеДляЗаполненияСерийПоFEFOВЭтапе(Объект, ПараметрыУказанияСерий, Запрос);
	
	ВыборкаНомераСтрок = Данные.ВыборкаНомераСтрок;
	СоответствиеОстатки = Данные.СоответствиеОстатки;
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	Если ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.РасходныйОрдерНаТовары"
		И ТипЗнч(Объект) = Тип("ДанныеФормыСтруктура") Тогда
		
		СтруктураПересчетаКоличестваУпаковок = Новый Структура("ПересчитатьКоличествоУпаковок, ПересчитатьВесОбъем");
		
	Иначе
		СтруктураПересчетаКоличестваУпаковок = Новый Структура("ПересчитатьКоличествоУпаковок");
	КонецЕсли;
	
	НомерСтроки = 0;
	
	Пока ВыборкаНомераСтрок.Следующий() Цикл
		
		СтрокаТоваров = Объект[ПараметрыУказанияСерий.ИмяТЧТовары][ВыборкаНомераСтрок.НомерСтроки + НомерСтроки - 1];
		
		ОстатокТовара = СтрокаТоваров.Количество;
		
		Если СкладВШапке Тогда
			Попытка
				СтрокаТоваров.Склад = Объект[ПараметрыУказанияСерий.ИмяПоляСклад];
			Исключение
			КонецПопытки;
		КонецЕсли;
		
		ИзменяемаяСтрока = СтрокаТоваров;
		
		Если СкладВШапке Тогда
			ОстаткиСерийПоТовару = ОстаткиСерийПоТоваруЭтапа(СтрокаТоваров, СоответствиеОстатки, ВыборкаНомераСтрок.Назначение,ПараметрыУказанияСерий.ИмяПоляСклад, Объект[ПараметрыУказанияСерий.ИмяПоляСклад]);
		Иначе
			ОстаткиСерийПоТовару = ОстаткиСерийПоТоваруЭтапа(СтрокаТоваров, СоответствиеОстатки, ВыборкаНомераСтрок.Назначение,ПараметрыУказанияСерий.ИмяПоляСклад);
		КонецЕсли;
		
		Если ОстаткиСерийПоТовару = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтруктураОстаток из ОстаткиСерийПоТовару Цикл
			Если СтруктураОстаток.СвободныйОстаток = 0 Тогда
				Продолжить;
			КонецЕсли;
			КоличествоВСтроку = Мин(ОстатокТовара, СтруктураОстаток.СвободныйОстаток);
			
			ИзменяемаяСтрока.Количество = КоличествоВСтроку;
			ИзменяемаяСтрока.Серия = СтруктураОстаток.Серия;
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ИзменяемаяСтрока, СтруктураПересчетаКоличестваУпаковок, КэшированныеЗначения);
			
			ОстатокТовара = ОстатокТовара - ИзменяемаяСтрока.Количество;
			СтруктураОстаток.СвободныйОстаток = СтруктураОстаток.СвободныйОстаток - ИзменяемаяСтрока.Количество;
			
			Если ОстатокТовара > 0 Тогда
				НомерСтроки = НомерСтроки + 1;
				ИзменяемаяСтрока = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Вставить(ИзменяемаяСтрока.НомерСтроки);
				
				ЗаполнитьЗначенияСвойств(ИзменяемаяСтрока, СтрокаТоваров,,"Количество,КоличествоУпаковок,Серия");
				ИзменяемаяСтрока.Количество = ОстатокТовара;
				ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ИзменяемаяСтрока, СтруктураПересчетаКоличестваУпаковок, КэшированныеЗначения);
			Иначе
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = ТекстЗапросаПроверкаЗаполненияСерийПоFEFOВЭтапе(Объект, ПараметрыУказанияСерий, СкладВШапке);
	
	Если СкладВШапке Тогда
		Запрос.УстановитьПараметр("Склад", Объект[ПараметрыУказанияСерий.ИмяПоляСклад]);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("СтатусыСерийСерияНеУказана", НоменклатураКлиентСервер.СтатусыСерийСерияНеУказана());
	
	Запрос.УстановитьПараметр("ТаблицаТоваров", Объект[ПараметрыУказанияСерий.ИмяТЧТовары].Выгрузить());
	
	ВыборкаНезаполненные = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаНезаполненные.Следующий() Цикл
		
		КоличествоНеРаспределено = 0;
		Если ВыборкаНезаполненные.ЭтоТоварноеМесто Тогда
			
			ТекстСообщения = НСтр("ru = 'В строке %НомерСтроки% для товара ""%ПредставлениеТовара%"" серии не заполнены, т.к. указана упаковка - товарное место. Заполнение серий FEFO по товарным местам не поддерживается.'");
			
		ИначеЕсли ВыборкаНезаполненные.Количество = 0 Тогда
			
			ТекстСообщения = НСтр("ru = 'В строке %НомерСтроки% для товара ""%ПредставлениеТовара%"" серии не заполнены, т.к. не указано количество товаров.'");
			
		Иначе
			
			ТекстСообщения = НСтр("ru = 'В строке %НомерСтроки% для товара ""%ПредставлениеТовара%"" серии не заполнены. На складе ""%ПредставлениеСклада%"" не хватает ""%Количество%"" ""%ЕдиницаИзмерения%""'");
			КоличествоНеРаспределено = ВыборкаНезаполненные.Количество;
			
		КонецЕсли;	
		
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%НомерСтроки%", ВыборкаНезаполненные.НомерСтроки);
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ПредставлениеТовара%", НоменклатураКлиентСервер.ПредставлениеНоменклатуры(ВыборкаНезаполненные.Номенклатура, ВыборкаНезаполненные.Характеристика));
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ПредставлениеСклада%", СкладыСервер.ПолучитьПредставлениеСклада(ВыборкаНезаполненные.Склад,Помещение));
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%Количество%", КоличествоНеРаспределено);
		ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ЕдиницаИзмерения%",ВыборкаНезаполненные.ЕдиницаИзмерения);
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ПараметрыУказанияСерий.ИмяТЧТовары, ВыборкаНезаполненные.НомерСтроки, "Серия");
		//ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,Поле,"Объект");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаДляЗаполненияСерийПОFEFO(ПараметрыУказанияСерий)
	
	Если ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ОтчетОРозничныхПродажах" Тогда
		ИспользоватьНазначения = Ложь;
	Иначе
		ИспользоватьНазначения = Истина;
	КонецЕсли;
	
	ТекстыЗапроса = Новый Массив;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.НомерСтроки КАК НомерСтроки,
	|	ДанныеДокумента.Склад КАК Склад,
	|	ДанныеДокумента.Номенклатура КАК Номенклатура,
	|	ДанныеДокумента.Характеристика КАК Характеристика,
	|	"+?(ИспользоватьНазначения,"ДанныеДокумента.Назначение,","ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,")+"
	|	ДанныеДокумента.ПоляСвязи,
	|	ДанныеДокумента.Количество КАК Количество
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.СтатусУказанияСерий В (5, 6)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Номенклатура КАК Номенклатура,
	|	ДанныеДокумента.Характеристика КАК Характеристика,
	|	ДанныеДокумента.Серия КАК Серия,
	|	ДанныеДокумента.Склад КАК Склад,
	|	ДанныеДокумента.ПоляСвязи,
	|	ДанныеДокумента.Количество КАК Количество
	|ПОМЕСТИТЬ ТаблицаСерий
	|ИЗ
	|	&ТаблицаСерий КАК ДанныеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваровДляЗапроса.Склад КАК Склад,
	|	ТаблицаТоваровДляЗапроса.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваровДляЗапроса.Характеристика КАК Характеристика,
	|	ТаблицаТоваровДляЗапроса.Назначение КАК Назначение,
	|	СУММА(ТаблицаТоваровДляЗапроса.Количество) КАК Количество
	|ПОМЕСТИТЬ ТаблицаТоваровДляЗапроса
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваровДляЗапроса
	|ГДЕ
	|	ТаблицаТоваровДляЗапроса.Количество > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваровДляЗапроса.Склад,
	|	ТаблицаТоваровДляЗапроса.Номенклатура,
	|	ТаблицаТоваровДляЗапроса.Характеристика,
	|	ТаблицаТоваровДляЗапроса.Назначение";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СерииТоваров.Номенклатура КАК Номенклатура,
	|	СерииТоваров.Характеристика КАК Характеристика,
	|	"+?(ИспользоватьНазначения,"СерииТоваров.Назначение,","ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,")+"
	|	СерииТоваров.Склад,
	|	СерииТоваров.Серия,
	|	СУММА(СерииТоваров.СвободныйОстаток) КАК СвободныйОстаток,
	|	СерииТоваров.Серия.ГоденДо КАК ГоденДо,
	|	СерииТоваров.Серия.Номер КАК Номер
	|ПОМЕСТИТЬ ТаблицаОстатковПоСериям
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|		ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
	|		ТоварыНаСкладахОстатки.Серия КАК Серия,
	|		ТоварыНаСкладахОстатки.Назначение КАК Назначение,
	|		ТоварыНаСкладахОстатки.Склад КАК Склад,
	|		ТоварыНаСкладахОстатки.ВНаличииОстаток - ТоварыНаСкладахОстатки.КОтгрузкеОстаток КАК СвободныйОстаток
	|	ИЗ
	|		РегистрНакопления.ТоварыНаСкладах.Остатки(
	|				,
	|				(Номенклатура, Характеристика, Назначение, Склад) В
	|					(ВЫБРАТЬ
	|						ТаблицаТоваровДляЗапроса.Номенклатура,
	|						ТаблицаТоваровДляЗапроса.Характеристика,
	|						ТаблицаТоваровДляЗапроса.Назначение,
	|						ТаблицаТоваровДляЗапроса.Склад
	|					ИЗ
	|						ТаблицаТоваровДляЗапроса)) КАК ТоварыНаСкладахОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыНаСкладах.Номенклатура,
	|		ТоварыНаСкладах.Характеристика,
	|		ТоварыНаСкладах.Серия,
	|		ТоварыНаСкладах.Назначение,
	|		ТоварыНаСкладах.Склад,
	|		ВЫБОР
	|			КОГДА ТоварыНаСкладах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТоварыНаСкладах.ВНаличии + ТоварыНаСкладах.КОтгрузке
	|			ИНАЧЕ ТоварыНаСкладах.ВНаличии - ТоварыНаСкладах.КОтгрузке
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваровДляЗапроса КАК ТаблицаТоваровДляЗапроса
	|			ПО ТоварыНаСкладах.Номенклатура = ТаблицаТоваровДляЗапроса.Номенклатура
	|				И ТоварыНаСкладах.Характеристика = ТаблицаТоваровДляЗапроса.Характеристика
	|				И ТоварыНаСкладах.Назначение = ТаблицаТоваровДляЗапроса.Назначение
	|				И ТоварыНаСкладах.Склад = ТаблицаТоваровДляЗапроса.Склад
	|				И (ТоварыНаСкладах.Регистратор = &Ссылка)
	|	) КАК СерииТоваров
	|ГДЕ
	|	СерииТоваров.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	СерииТоваров.Номенклатура,
	|	СерииТоваров.Характеристика,
	|	СерииТоваров.Назначение,
	|	СерииТоваров.Склад,
	|	СерииТоваров.Серия,
	|	СерииТоваров.Серия.ГоденДо,
	|	СерииТоваров.Серия.Номер
	|
	|ИМЕЮЩИЕ
	|	СУММА(СерииТоваров.СвободныйОстаток) > 0";
	
	//+пит
	////++ НЕ УТ
	//Если ПараметрыУказанияСерий.ИмяПоляСклад = "Подразделение"
	//	ИЛИ ПараметрыУказанияСерий.ИмяПоляСклад = "ПодразделениеПолучатель"
	//	ИЛИ ПараметрыУказанияСерий.ИмяПоляСклад = "ПодразделениеОтправитель" Тогда
	//	
	//	ТекстЗапроса =
	//	"ВЫБРАТЬ
	//	|	СерииТоваров.Номенклатура КАК Номенклатура,
	//	|	СерииТоваров.Характеристика КАК Характеристика,
	//	|	СерииТоваров.Назначение,
	//	|	СерииТоваров.Склад,
	//	|	СерииТоваров.Серия,
	//	|	СУММА(СерииТоваров.СвободныйОстаток) КАК СвободныйОстаток,
	//	|	СерииТоваров.Серия.ГоденДо КАК ГоденДо,
	//	|	СерииТоваров.Серия.Номер КАК Номер
	//	|ПОМЕСТИТЬ ТаблицаОстатковПоСериям
	//	|ИЗ
	//	|	(ВЫБРАТЬ
	//	|		МатериалыВПроизводствеОстатки.Номенклатура КАК Номенклатура,
	//	|		МатериалыВПроизводствеОстатки.Характеристика КАК Характеристика,
	//	|		МатериалыВПроизводствеОстатки.Серия КАК Серия,
	//	|		МатериалыВПроизводствеОстатки.Назначение КАК Назначение,
	//	|		МатериалыВПроизводствеОстатки.Подразделение КАК Склад,
	//	|		МатериалыВПроизводствеОстатки.КоличествоОстаток КАК СвободныйОстаток
	//	|	ИЗ
	//	|		РегистрНакопления.МатериалыИРаботыВПроизводстве.Остатки(
	//	|				,
	//	|				(Номенклатура, Характеристика, Назначение, Подразделение) В
	//	|					(ВЫБРАТЬ
	//	|						ТаблицаТоваровДляЗапроса.Номенклатура,
	//	|						ТаблицаТоваровДляЗапроса.Характеристика,
	//	|						ТаблицаТоваровДляЗапроса.Назначение,
	//	|						ТаблицаТоваровДляЗапроса.Склад
	//	|					ИЗ
	//	|						ТаблицаТоваровДляЗапроса)) КАК МатериалыВПроизводствеОстатки
	//	|	
	//	|	ОБЪЕДИНИТЬ ВСЕ
	//	|	
	//	|	ВЫБРАТЬ
	//	|	МатериалыИРаботыВПроизводстве.Номенклатура,
	//	|	МатериалыИРаботыВПроизводстве.Характеристика,
	//	|	МатериалыИРаботыВПроизводстве.Серия,
	//	|	МатериалыИРаботыВПроизводстве.Назначение,
	//	|	МатериалыИРаботыВПроизводстве.Подразделение,
	//	|	-МатериалыИРаботыВПроизводстве.Количество
	//	|	ИЗ
	//	|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК МатериалыИРаботыВПроизводстве
	//	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваровДляЗапроса КАК ТаблицаТоваровДляЗапроса
	//	|		ПО МатериалыИРаботыВПроизводстве.Номенклатура = ТаблицаТоваровДляЗапроса.Номенклатура
	//	|			И МатериалыИРаботыВПроизводстве.Характеристика = ТаблицаТоваровДляЗапроса.Характеристика
	//	|			И МатериалыИРаботыВПроизводстве.Назначение = ТаблицаТоваровДляЗапроса.Назначение
	//	|			И МатериалыИРаботыВПроизводстве.Подразделение = ТаблицаТоваровДляЗапроса.Склад
	//	|			И (МатериалыИРаботыВПроизводстве.Регистратор = &Ссылка)
	//	|) КАК СерииТоваров
	//	|ГДЕ
	//	|	СерииТоваров.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	//	|
	//	|СГРУППИРОВАТЬ ПО
	//	|	СерииТоваров.Номенклатура,
	//	|	СерииТоваров.Характеристика,
	//	|	СерииТоваров.Назначение,
	//	|	СерииТоваров.Склад,
	//	|	СерииТоваров.Серия,
	//	|	СерииТоваров.Серия.ГоденДо,
	//	|	СерииТоваров.Серия.Номер
	//	|
	//	|ИМЕЮЩИЕ
	//	|	СУММА(СерииТоваров.СвободныйОстаток) > 0";
	//	
	//КонецЕсли;
	////-- НЕ УТ
	//-пит
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Номенклатура,
	|	ДанныеДокумента.Характеристика,
	|	ДанныеДокумента.Склад,
	|	ДанныеДокумента.НомерСтроки,
	|	ТаблицаОстатковПоСериям.Серия КАК Серия,
	|	ЕСТЬNULL(ТаблицаОстатковПоСериям.СвободныйОстаток, 0) КАК СвободныйОстаток
	|ИЗ
	|	ТаблицаОстатковПоСериям КАК ТаблицаОстатковПоСериям
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваров КАК ДанныеДокумента
	|		ПО (ДанныеДокумента.Номенклатура = ТаблицаОстатковПоСериям.Номенклатура)
	|			И (ДанныеДокумента.Характеристика = ТаблицаОстатковПоСериям.Характеристика)
	|			И (ДанныеДокумента.Назначение = ТаблицаОстатковПоСериям.Назначение)
	|			И (ДанныеДокумента.Склад = ТаблицаОстатковПоСериям.Склад)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДокумента.Номенклатура,
	|	ДанныеДокумента.Характеристика,
	|	ДанныеДокумента.Склад,
	|	ТаблицаОстатковПоСериям.ГоденДо,
	|	ТаблицаОстатковПоСериям.Номер,
	|	ТаблицаОстатковПоСериям.Серия,
	|	ДанныеДокумента.ПоляСвязи,
	|	ДанныеДокумента.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.ПоляСвязи,
	|	ДанныеДокумента.Номенклатура,
	|	ДанныеДокумента.Характеристика,
	|	ДанныеДокумента.Серия,
	|	ДанныеДокумента.Количество
	|ИЗ
	|	ТаблицаСерий КАК ДанныеДокумента
	|ГДЕ
	|	НЕ (ДанныеДокумента.Номенклатура, ДанныеДокумента.Склад) В
	|				(ВЫБРАТЬ
	|					ТаблицаТоваров.Номенклатура,
	|					ТаблицаТоваров.Склад
	|				ИЗ
	|					ТаблицаТоваров)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ДанныеДляЗаполненияСерийПоFEFOВЭтапе(Объект, ПараметрыУказанияСерий, Запрос)
	
	Если ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.ОтчетОРозничныхПродажах" Тогда
		ИспользоватьНазначения = Ложь;
	Иначе
		ИспользоватьНазначения = Истина;
	КонецЕсли;
	
	СкладВШапке = НЕ(ПараметрыУказанияСерий.ПоляСвязи.Найти(ПараметрыУказанияСерий.ИмяПоляСклад) <> Неопределено
	Или ПараметрыУказанияСерий.ИменаПолейДополнительные.Найти(ПараметрыУказанияСерий.ИмяПоляСклад) <> Неопределено);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.НомерСтроки,
	|	&Склад КАК Склад,
	|	ВЫРАЗИТЬ(ДанныеДокумента.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ДанныеДокумента.Характеристика КАК Характеристика,
	|	"+?(ИспользоватьНазначения,"ДанныеДокумента.Назначение,","ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,")+"
	|	ВЫРАЗИТЬ(ДанныеДокумента.Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения) КАК Упаковка
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.СтатусУказанияСерий В (&МассивСтатусовНеУказана)
	|	И ДанныеДокумента.Количество > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.НомерСтроки,
	|	ДанныеДокумента.Склад КАК Склад,
	|	ДанныеДокумента.Номенклатура КАК Номенклатура,
	|	ДанныеДокумента.Характеристика КАК Характеристика,
	|	"+?(ИспользоватьНазначения,"ДанныеДокумента.Назначение,","ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,")+"
	|	ДанныеДокумента.Упаковка
	|ПОМЕСТИТЬ ТаблицаТоваровДляЗапроса
	|ИЗ
	|	ТаблицаТоваров КАК ДанныеДокумента
	|ГДЕ
	|	ЕСТЬNULL(ДанныеДокумента.Упаковка.ТипУпаковки, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ТипыУпаковокНоменклатуры.ТоварноеМесто)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Склад,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.НомерСтроки,
	|	ДанныеДокумента.Назначение
	|ИЗ
	|	ТаблицаТоваровДляЗапроса КАК ДанныеДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СерииТоваров.Склад КАК Склад,
	|	СерииТоваров.Номенклатура КАК Номенклатура,
	|	СерииТоваров.Характеристика КАК Характеристика,
	|	СерииТоваров.Назначение КАК Назначение,
	|	СерииТоваров.Серия КАК Серия,
	|	СУММА(СерииТоваров.СвободныйОстаток) КАК СвободныйОстаток,
	|	СерииТоваров.Серия.ГоденДо КАК ГоденДо,
	|	СерииТоваров.Серия.Номер КАК Номер
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыНаСкладахОстатки.Склад КАК Склад,
	|		ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|		ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
	|		ТоварыНаСкладахОстатки.Серия КАК Серия,
	|		ТоварыНаСкладахОстатки.Назначение КАК Назначение,
	|		ТоварыНаСкладахОстатки.ВНаличииОстаток - ТоварыНаСкладахОстатки.КОтгрузкеОстаток КАК СвободныйОстаток
	|	ИЗ
	|		РегистрНакопления.ТоварыНаСкладах.Остатки(
	|				,
	|				(Склад, Номенклатура, Характеристика, Назначение) В
	|					(ВЫБРАТЬ
	|						ТаблицаТоваровДляЗапроса.Склад,
	|						ТаблицаТоваровДляЗапроса.Номенклатура,
	|						ТаблицаТоваровДляЗапроса.Характеристика,
	|						ТаблицаТоваровДляЗапроса.Назначение
	|					ИЗ
	|						ТаблицаТоваровДляЗапроса)) КАК ТоварыНаСкладахОстатки
	|	
	|	) КАК СерииТоваров
	|ГДЕ
	|	СерииТоваров.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	СерииТоваров.Склад,
	|	СерииТоваров.Номенклатура,
	|	СерииТоваров.Характеристика,
	|	СерииТоваров.Назначение,
	|	СерииТоваров.Серия,
	|	СерииТоваров.Серия.ГоденДо,
	|	СерииТоваров.Серия.Номер
	|
	|ИМЕЮЩИЕ
	|	СУММА(СерииТоваров.СвободныйОстаток) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Склад,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	ГоденДо,
	|	Номер,
	|	Серия";
	
	Если СкладВШапке Тогда
		Запрос.УстановитьПараметр("Склад", Объект[ПараметрыУказанияСерий.ИмяПоляСклад]);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Склад", "ДанныеДокумента." + ПараметрыУказанияСерий.ИмяПоляСклад);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивСтатусовНеУказана", НоменклатураКлиентСервер.СтатусыСерийСерияНеУказана());
	
	Запрос.Текст = ТекстЗапроса;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаНомераСтрок = РезультатЗапроса[2].Выбрать();
	
	СоответствиеОстатки = Новый Соответствие;
	ВыборкаОстатки = РезультатЗапроса[3].Выбрать();
	
	Пока ВыборкаОстатки.СледующийПоЗначениюПоля("Склад") Цикл
		СоответствиеОстатки.Вставить(ВыборкаОстатки.Склад, Новый Соответствие);
		СоответствиеСклад = СоответствиеОстатки[ВыборкаОстатки.Склад];
		Пока ВыборкаОстатки.СледующийПоЗначениюПоля("Номенклатура") Цикл
			СоответствиеСклад.Вставить(ВыборкаОстатки.Номенклатура, Новый Соответствие);
			СоответствиеНоменклатура = СоответствиеСклад[ВыборкаОстатки.Номенклатура];
			Пока ВыборкаОстатки.СледующийПоЗначениюПоля("Характеристика") Цикл
				СоответствиеНоменклатура.Вставить(ВыборкаОстатки.Характеристика, Новый Соответствие);
				СоответствиеХарактеристика = СоответствиеНоменклатура[ВыборкаОстатки.Характеристика];
				Пока ВыборкаОстатки.СледующийПоЗначениюПоля("Назначение") Цикл
					СоответствиеХарактеристика.Вставить(ВыборкаОстатки.Назначение, Новый Массив);
					МассивОстатки = СоответствиеХарактеристика[ВыборкаОстатки.Назначение];
					Пока ВыборкаОстатки.Следующий() Цикл
						СтруктураОстаток = Новый Структура("Серия,СвободныйОстаток");
						ЗаполнитьЗначенияСвойств(СтруктураОстаток,ВыборкаОстатки);
						МассивОстатки.Добавить(СтруктураОстаток);
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Новый Структура("ВыборкаНомераСтрок, СоответствиеОстатки", ВыборкаНомераСтрок, СоответствиеОстатки);
	
КонецФункции

// Функция - Остатки серий по товару этапа
//
// Параметры:
//  СтрокаТоваров		 - 	 - 
//  СоответствиеОстатки	 - 	 - 
//  Назначение			 - 	 - 
//  ИмяПоляСклад		 - 	 - 
//  Склад				 - СправочникСсылка.Склады, Неопределено - Если склад определен, то берем его остатки, иначе по "ИмяПоляСклад".
// 
// Возвращаемое значение:
//   - 
//
Функция ОстаткиСерийПоТоваруЭтапа(СтрокаТоваров, СоответствиеОстатки, Назначение, ИмяПоляСклад = "Склад", Склад = Неопределено)
	
	ТекущийСклад = ?(Склад = Неопределено, СтрокаТоваров[ИмяПоляСклад], Склад);
	
	Если СоответствиеОстатки[ТекущийСклад] = Неопределено			
		Или СоответствиеОстатки[ТекущийСклад][СтрокаТоваров.Номенклатура] = Неопределено
		Или СоответствиеОстатки[ТекущийСклад][СтрокаТоваров.Номенклатура][СтрокаТоваров.Характеристика] = Неопределено
		Или СоответствиеОстатки[ТекущийСклад][СтрокаТоваров.Номенклатура][СтрокаТоваров.Характеристика][Назначение] = Неопределено Тогда
		
		Остатки = Неопределено;
		
	Иначе
		
		Остатки = СоответствиеОстатки[ТекущийСклад][СтрокаТоваров.Номенклатура][СтрокаТоваров.Характеристика][Назначение];
		
	КонецЕсли;
	
	Возврат Остатки;
	
КонецФункции

Функция ТекстЗапросаПроверкаЗаполненияСерийПоFEFOВЭтапе(Объект, ПараметрыУказанияСерий, СкладВШапке)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеДокумента.НомерСтроки КАК НомерСтроки,
	|	&Склад КАК Склад,
	|	ДанныеДокумента.Номенклатура КАК Номенклатура,
	|	ДанныеДокумента.Характеристика КАК Характеристика,
	|	ДанныеДокумента.Количество КАК Количество,
	|	ДанныеДокумента.Упаковка КАК Упаковка
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.СтатусУказанияСерий В (&СтатусыСерийСерияНеУказана)
	|;
	|///////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.Склад КАК Склад,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика КАК Характеристика,
	|	ТаблицаТоваров.Количество КАК Количество,
	|	ПРЕДСТАВЛЕНИЕ(ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
	|	ВЫБОР КОГДА ВЫРАЗИТЬ(ТаблицаТоваров.Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения).ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковокНоменклатуры.ТоварноеМесто)
	|		ТОГДА ИСТИНА
	|	ИНАЧЕ ЛОЖЬ КОНЕЦ КАК ЭтоТоварноеМесто
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров";
	
	Если НЕ СкладВШапке Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Склад", "ДанныеДокумента." + ПараметрыУказанияСерий.ИмяПоляСклад);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

Функция НайтиПодразделенияПоРазрешенным(РазрешенныеПодразделения) Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	питРецептураРазрешенныеПодразделения.Подразделение КАК Подразделение,
	|	питРецептураРазрешенныеПодразделения.ВключаяПодчиненные КАК ВключаяПодчиненные
	|ПОМЕСТИТЬ втПодразделенияПроизводства
	|ИЗ &РазрешенныеПодразделения КАК питРецептураРазрешенныеПодразделения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтруктураПредприятия.Ссылка КАК Подразделение
	|ПОМЕСТИТЬ втПодразделенияСИерархией
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|ГДЕ
	|	СтруктураПредприятия.Ссылка В ИЕРАРХИИ
	|			(ВЫБРАТЬ
	|				втПодразделенияПроизводства.Подразделение
	|			ИЗ
	|				втПодразделенияПроизводства КАК втПодразделенияПроизводства
	|			ГДЕ
	|				втПодразделенияПроизводства.ВключаяПодчиненные = ИСТИНА)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	втПодразделенияПроизводства.Подразделение
	|ИЗ
	|	втПодразделенияПроизводства КАК втПодразделенияПроизводства
	|ГДЕ
	|	втПодразделенияПроизводства.ВключаяПодчиненные = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПодразделенияСИерархией.Подразделение КАК Подразделение
	|ИЗ
	|	втПодразделенияСИерархией КАК втПодразделенияСИерархией
	|
	|СГРУППИРОВАТЬ ПО
	|	втПодразделенияСИерархией.Подразделение
	|";
	
	Запрос.УстановитьПараметр("РазрешенныеПодразделения", РазрешенныеПодразделения);
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Подразделение");
	
	Возврат Результат;
	
КонецФункции

// Функция инициализирует структуру кэшированных значений для заполнения дерева состава.
// 
// Возвращаемое значение:
//  Структура - Структура кэша.
//
Функция ИнициализироватьСтруктуруКэшированныхЗначенийДляЗаполненияДереваСостава() Экспорт
	
	СтруктураКэша = Новый Структура;
	
	ТаблицаКэшРецептур = Новый ТаблицаЗначений;
	ТаблицаКэшРецептур.Колонки.Добавить("Рецептура"			, Новый ОписаниеТипов("ДокументСсылка.питРецептура"));
	ТаблицаКэшРецептур.Колонки.Добавить("СтруктураЗначений"	, Новый ОписаниеТипов("Структура"));
	
	КвалификаторыЧисла = Новый КвалификаторыЧисла(5, 2, ДопустимыйЗнак.Любой);
	ОписаниеЧисла = Новый ОписаниеТипов("Число", КвалификаторыЧисла);
	ТаблицаКэшСезонныхПроцентов = Новый ТаблицаЗначений;
	ТаблицаКэшСезонныхПроцентов.Колонки.Добавить("Номенклатура"			, Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаКэшСезонныхПроцентов.Колонки.Добавить("ДатаСезона"			, Новый ОписаниеТипов("Дата"));
	ТаблицаКэшСезонныхПроцентов.Колонки.Добавить("СезонныйПроцент"		, ОписаниеЧисла);
	
	СтруктураКэша.Вставить("КэшДанныхРецептур"		, ТаблицаКэшРецептур);
	СтруктураКэша.Вставить("КэшСезонныхПроцентов"	, ТаблицаКэшСезонныхПроцентов);
	СтруктураКэша.Вставить("КэшУпаковок"			, ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения());
	
	Возврат СтруктураКэша;
	
КонецФункции

Функция ПолучитьРеквизитыРецептуры(Рецептура, СтрокаСвойстваПолучить, СтруктураКэша)
	
	Результат = Неопределено;
	
	Если СтруктураКэша = Неопределено 
		ИЛИ НЕ (ТипЗнч(СтруктураКэша) = Тип("Структура") 
		И СтруктураКэша.Свойство("КэшДанныхРецептур")) Тогда
		
		Результат = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Рецептура, СтрокаСвойстваПолучить);
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		НайденныйКэш = СтруктураКэша.КэшДанныхРецептур.НайтиСтроки(Новый Структура("Рецептура", Рецептура));
		Если НайденныйКэш.Количество() Тогда
			Результат = НайденныйКэш[0].СтруктураЗначений;
		Иначе
			Результат = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Рецептура, СтрокаСвойстваПолучить);
			НовСтрокаКэш = СтруктураКэша.КэшДанныхРецептур.Добавить();
			НовСтрокаКэш.Рецептура			= Рецептура;
			НовСтрокаКэш.СтруктураЗначений	= Результат;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура УстановитьВидимостьНадписиРазныеЕдиницы(Объект, Элементы, ИмяТЧТовары = "Товары") Экспорт
	
	ЕдиницыРавны = Истина;
	
	Если Объект.ХозяйственнаяОперация = Перечисления.питХозяйственныеОперации.РецептураРазделка Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ * ПОМЕСТИТЬ ТаблицаТоваров ИЗ &ТаблицаТоваров КАК ТаблицаТоваров;
		|ВЫБРАТЬ
		|	МИНИМУМ(ВЫБОР
		|			КОГДА ВЫБОР
		|					КОГДА питРазделкаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|						ТОГДА питРазделкаТовары.Номенклатура.ЕдиницаИзмерения
		|					ИНАЧЕ питРазделкаТовары.Упаковка
		|				КОНЕЦ = &УпаковкаШапки
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ) КАК ЕдиницыИзмеренияРавны
		|ИЗ
		|	ТаблицаТоваров КАК питРазделкаТовары
		|";
		Запрос.УстановитьПараметр("УпаковкаШапки", ?(ЗначениеЗаполнено(Объект.Упаковка), Объект.Упаковка, ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Номенклатура, "ЕдиницаИзмерения")));
		Запрос.УстановитьПараметр("ТаблицаТоваров", Объект[ИмяТЧТовары].Выгрузить());
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если НЕ ВыборкаДетальныеЗаписи.ЕдиницыИзмеренияРавны = NULL Тогда
				ЕдиницыРавны = ВыборкаДетальныеЗаписи.ЕдиницыИзмеренияРавны;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Элементы.ТоварыНадписьРазныеЕдиницы.Видимость = НЕ ЕдиницыРавны;
	
КонецПроцедуры

#Область Недовложения

// Анализирует состав и количество недовложений в производстве.
//
// Параметры:
//	ВыпускБлюд 				- Ссылка на документ ВыпускБлюд, для которого анализируются недовложения.
//	Склад 					- Склад, на котором анализируем недовложения.
//	Пересчитывать 			- Признак необходимости пересчета таблицы производства перед анализом.
//	ДляОтчетаНедовложений 	- Признак расчета данных для отчета, а не проведения, 
//							и следовательно не учитываем константу ОтключитьКонтрольОтрицательныхОстатков.
//  
// Возвращаемое значение:
//	Структура значений, содержащая таблицу производства и таблицу замен.
//
Функция АнализНедовложений(ВыпускБлюд, Склад=Неопределено, Пересчитывать=Неопределено, ДляОтчетаНедовложений=Ложь, УчетВПродажныхЦенах=Неопределено) Экспорт
	
	//////////////////////////////////////////////////////////////////////
	// 1. Получение реквизитов документа "ВыпускБлюд"
	
	Если ТипЗнч(ВыпускБлюд)=Тип("ДокументСсылка.питВыпускБлюд")
		ИЛИ ТипЗнч(ВыпускБлюд)=Тип("ДокументСсылка.питПланМеню") Тогда
		
		Если ТипЗнч(ВыпускБлюд)=Тип("ДокументСсылка.питВыпускБлюд") Тогда
			ВыпускБлюдРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВыпускБлюд,
				"Ссылка,Организация,Дата,Подразделение,РежимРасчетаСписанияВПроизводство,РежимИспользованияАналогов,Проведен,Склад");
		Иначе
			ВыпускБлюдРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВыпускБлюд,
				"Ссылка,Организация,Дата,Подразделение,РежимИспользованияАналогов,Проведен,ЦеховаяКладовая");
			ВыпускБлюдРеквизиты.Вставить("РежимРасчетаСписанияВПроизводство", Перечисления.питРежимыРасчетаСписанияВПроизводство.ПоНорме);
			ВыпускБлюдРеквизиты.Вставить("Склад", ВыпускБлюдРеквизиты.ЦеховаяКладовая);
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	МАКСИМУМ(питПланМенюИнгредиенты.ИзмененФакт) КАК ИзмененФакт
				|ИЗ
				|	Документ.питПланМеню.Ингредиенты КАК питПланМенюИнгредиенты
				|ГДЕ
				|	питПланМенюИнгредиенты.Ссылка = &Ссылка";
			Запрос.УстановитьПараметр("Ссылка", ВыпускБлюдРеквизиты.Ссылка);
			РезультатЗапроса = Запрос.Выполнить();
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			Если ВыборкаДетальныеЗаписи.Следующий() Тогда
				Если НЕ ВыборкаДетальныеЗаписи.ИзмененФакт = NULL И ВыборкаДетальныеЗаписи.ИзмененФакт Тогда
					ВыпускБлюдРеквизиты.РежимРасчетаСписанияВПроизводство = Перечисления.питРежимыРасчетаСписанияВПроизводство.ПоФакту;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		//+АвтовыборНоменклатуры
		УточненияАвтовыбора = питУправлениеДаннымиОбИзделиях.ПолучитьТаблицуУточненийАвтовыбораПоДокументу(ВыпускБлюд);
		//+АвтовыборНоменклатуры
	ИначеЕсли ТипЗнч(ВыпускБлюд)=Тип("ДокументОбъект.питВыпускБлюд")
		ИЛИ ТипЗнч(ВыпускБлюд)=Тип("ДокументОбъект.питПланМеню") Тогда
		ВыпускБлюдРеквизиты = Новый Структура(
			"Ссылка,Организация,Дата,Подразделение,РежимРасчетаСписанияВПроизводство,РежимИспользованияАналогов,Проведен");
		ЗаполнитьЗначенияСвойств(ВыпускБлюдРеквизиты, ВыпускБлюд);
		Если ТипЗнч(ВыпускБлюд)=Тип("ДокументОбъект.питПланМеню") Тогда
			ВыпускБлюдРеквизиты.РежимРасчетаСписанияВПроизводство = Перечисления.питРежимыРасчетаСписанияВПроизводство.ПоНорме;
			Для каждого ТекИнгредиент Из ВыпускБлюд.Ингредиенты Цикл
				Если ТекИнгредиент.ИзмененФакт Тогда
					ВыпускБлюдРеквизиты.РежимРасчетаСписанияВПроизводство = Перечисления.питРежимыРасчетаСписанияВПроизводство.ПоФакту;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		//+АвтовыборНоменклатуры
		УточненияАвтовыбора = ВыпускБлюд.УточненияАвтовыбора.Выгрузить();
		//+АвтовыборНоменклатуры
	Иначе
		ВызватьИсключение НСтр("ru = 'Для анализа недовложений не указан документ ""Выпуск блюд""'");
	КонецЕсли;
	
	//////////////////////////////////////////////////////////////////////
	// 2. Определение, требуется ли пересчитывать производственные таблицы
	
	Если Пересчитывать=Неопределено Тогда
		
		// Если при режиме проведения 'По факту' таблицы заполнены некорректно, предупредим об этом.
		// И если пользователь согласен, то перезаполним, а если нет, то анализируем, как есть.
		//Если НЕ ВыпускБлюдРеквизиты.Проведен Тогда
		//	Пересчитывать = Истина;
		Если ВыпускБлюдРеквизиты.РежимРасчетаСписанияВПроизводство=Перечисления.питРежимыРасчетаСписанияВПроизводство.ПоНорме Тогда
			Пересчитывать = Истина;
		Иначе
			ПроверитьКорректностьЗаполнения = Истина;
			// Проверим на корректность заполнения. 'По факту' или 'По норме первого проведения'.
			// ?? доработать функцию проверки корректности
			Если ПроверитьКорректностьЗаполнения Тогда
				Пересчитывать = Ложь;
			Иначе
				Пересчитывать = Истина;
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Таблица Производства заполнена некорректно! Документ """+ВыпускБлюдРеквизиты.Ссылка+""" рассчитан по норме.";
				Сообщение.Сообщить();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	Если Пересчитывать = Неопределено Тогда
		Пересчитывать = Истина;
	КонецЕсли;
	
	//////////////////////////////////////////////////////////////////////
	// 3. Определение структуры параметров, необходимой для заполнения
	// производственных таблиц и заполнения фактического количества.
	
	СтруктураПараметров = Новый Структура("МоментВремени, СчитатьФактВТаблицеЗамен, Склад, Организация, Подразделение, РежимИспользованияАналогов, ОрганизацияПрименяетУСН, ДляОтчетаНедовложений");
	ЗаполнитьЗначенияСвойств(СтруктураПараметров, ВыпускБлюдРеквизиты);
	СтруктураПараметров.Вставить("СсылкаНаДокумент", ?(ВыпускБлюдРеквизиты.Ссылка.Пустая(), Неопределено, ВыпускБлюдРеквизиты.Ссылка));
	СтруктураПараметров.МоментВремени = ВыпускБлюд.МоментВремени();
	СтруктураПараметров.ДляОтчетаНедовложений = ДляОтчетаНедовложений;
	
	//////////////////////////////////////////////////////////////////////
	// 4. Если таблицы заполнены не корректно или это нормативное списание,
	// то пересчитываем таблицы производства и замен.
	
	Если Пересчитывать Тогда
		
		//////////////////////////////////////////////////////////////////////
		// 4.1.1 Пересчет таблиц производства.
		
		ОписаниеТиповНоменклатура         = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
		ОписаниеТиповХарактеристика       = Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры");
		ОписаниеТиповНазначение           = Новый ОписаниеТипов("СправочникСсылка.Назначения");
		ОписаниеТиповЕдиница              = Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения");
		ОписаниеТиповЧисло                = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(18,6));
		ОписаниеТиповБулево               = Новый ОписаниеТипов("Булево");
		
		ТаблицаПроизводства = ЗаполнитьПроизводственныеТаблицыПоРецептуре(ВыпускБлюд, Ложь, УчетВПродажныхЦенах,,,УточненияАвтовыбора);
		ТаблицаПроизводства.Колонки.Добавить("КоличествоИнгредиентаФактВДокументе",ОписаниеТиповЧисло);
		ТаблицаПроизводства.Колонки.Добавить("КоличествоУпаковокИнгредиентаФактВДокументе",ОписаниеТиповЧисло);
		
		ТаблицаЗамен = Новый ТаблицаЗначений;
		ТаблицаЗамен.Колонки.Добавить("ИнгредиентИсходный",		ОписаниеТиповНоменклатура);
		ТаблицаЗамен.Колонки.Добавить("ХарактеристикаИнгредиентаИсходного",	ОписаниеТиповХарактеристика);
		ТаблицаЗамен.Колонки.Добавить("НазначениеИнгредиентаИсходного",	ОписаниеТиповНазначение);
		ТаблицаЗамен.Колонки.Добавить("ИнгредиентЗамена",		ОписаниеТиповНоменклатура);
		ТаблицаЗамен.Колонки.Добавить("ХарактеристикаИнгредиентаЗамена",	ОписаниеТиповХарактеристика);
		ТаблицаЗамен.Колонки.Добавить("НазначениеИнгредиентаЗамена",	ОписаниеТиповНазначение);
		ТаблицаЗамен.Колонки.Добавить("Блюдо",					ОписаниеТиповНоменклатура);
		ТаблицаЗамен.Колонки.Добавить("ХарактеристикаБлюда",	ОписаниеТиповХарактеристика);
		ТаблицаЗамен.Колонки.Добавить("КоличествоИнгредиентаИсходный",		ОписаниеТиповЧисло);
		ТаблицаЗамен.Колонки.Добавить("КоличествоУпаковокИнгредиентаИсходный",		ОписаниеТиповЧисло);
		ТаблицаЗамен.Колонки.Добавить("УпаковкаИнгредиентаИсходный",ОписаниеТиповЕдиница);
		ТаблицаЗамен.Колонки.Добавить("КоличествоИнгредиентаЗамена",		ОписаниеТиповЧисло);
		ТаблицаЗамен.Колонки.Добавить("КоличествоУпаковокИнгредиентаЗамена",		ОписаниеТиповЧисло);
		ТаблицаЗамен.Колонки.Добавить("КоличествоИнгредиентаЗаменаФакт",	ОписаниеТиповЧисло);
		ТаблицаЗамен.Колонки.Добавить("КоличествоУпаковокИнгредиентаЗаменаФакт",	ОписаниеТиповЧисло);
		ТаблицаЗамен.Колонки.Добавить("УпаковкаИнгредиентаЗамена",	ОписаниеТиповЕдиница);
		ТаблицаЗамен.Колонки.Добавить("КоэффициентЗамена",		ОписаниеТиповЧисло);
		ТаблицаЗамен.Колонки.Добавить("Автопередел",			ОписаниеТиповБулево);
		ТаблицаЗамен.Колонки.Добавить("Рецептура",				Новый ОписаниеТипов("ДокументСсылка.питРецептура"));
		ТаблицаЗамен.Колонки.Добавить("СтатьяКалькуляции",		Новый ОписаниеТипов("СправочникСсылка.СтатьиКалькуляции"));
		
		// Норму копируем в "исходное"
		ТаблицаПроизводства.Колонки.Добавить("КоличествоИнгредиентаИсходное",ОписаниеТиповЧисло);
		ТаблицаПроизводства.Колонки.Добавить("КоличествоУпаковокИнгредиентаИсходное",ОписаниеТиповЧисло);
		Для каждого ТекСтрокаТаблицы Из ТаблицаПроизводства Цикл
			ТекСтрокаТаблицы.КоличествоИнгредиентаИсходное = ТекСтрокаТаблицы.КоличествоИнгредиентаНорма;
			ТекСтрокаТаблицы.КоличествоУпаковокИнгредиентаИсходное = ТекСтрокаТаблицы.КоличествоУпаковокИнгредиентаНорма;
		КонецЦикла;
		
		//////////////////////////////////////////////////////////////////////
		// 4.1.2 Дополняем фактическим количеством на складе и возможными заменами.
		
		СтруктураПараметров.СчитатьФактВТаблицеЗамен = Ложь;
		ЗаполнитьПроизводственныеТаблицыФактическимКоличеством(ТаблицаПроизводства, ТаблицаЗамен, СтруктураПараметров);
		
	Иначе
		
		//////////////////////////////////////////////////////////////////////
		// 4.2.1 Не пересчитываем таблицы производства, берем из документа "ВыпускБлюд"
		
		ОписаниеТиповЧисло = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(18,6));
		// В случае "По факту" выгружаем соответствующие таблицы и проводим их "инвентаризацию",
		// то есть приводим факт в норму и подсчитываем реальные остатки на складах.
		ТаблицаПроизводства = ПолучитьТаблицуПроизводстваПоВыпускуБлюд(ВыпускБлюд);
		
		ТаблицаПроизводства.Колонки.Добавить("КоличествоИнгредиентаФактВДокументе",ОписаниеТиповЧисло);
		ТаблицаПроизводства.Колонки.Добавить("КоличествоУпаковокИнгредиентаФактВДокументе",ОписаниеТиповЧисло);
		ТаблицаЗамен = ВыпускБлюд.Замены.Выгрузить();
		ТаблицаЗамен.Колонки.Добавить("КоличествоИнгредиентаЗаменаФакт",ОписаниеТиповЧисло);
		ТаблицаЗамен.Колонки.Добавить("КоличествоУпаковокИнгредиентаЗаменаФакт",ОписаниеТиповЧисло);
		
		// Норму копируем в "исходное" и делаем фактом
		ТаблицаПроизводства.Колонки.Добавить("КоличествоИнгредиентаИсходное",ОписаниеТиповЧисло);
		ТаблицаПроизводства.Колонки.Добавить("КоличествоУпаковокИнгредиентаИсходное",ОписаниеТиповЧисло);
		Для каждого ТекСтрокаТаблицы Из ТаблицаПроизводства Цикл
			ТекСтрокаТаблицы.КоличествоИнгредиентаИсходное = ТекСтрокаТаблицы.КоличествоИнгредиентаНорма;
			ТекСтрокаТаблицы.КоличествоУпаковокИнгредиентаИсходное = ТекСтрокаТаблицы.КоличествоУпаковокИнгредиентаНорма;
			ТекСтрокаТаблицы.КоличествоИнгредиентаНорма = ТекСтрокаТаблицы.КоличествоИнгредиентаФакт;
			ТекСтрокаТаблицы.КоличествоУпаковокИнгредиентаНорма = ТекСтрокаТаблицы.КоличествоУпаковокИнгредиентаФакт;
		КонецЦикла;
		
		//////////////////////////////////////////////////////////////////////
		// 4.2.2 Дополняем фактическим количеством на складе и возможными заменами.
		
		СтруктураПараметров.СчитатьФактВТаблицеЗамен = Истина;
		ЗаполнитьПроизводственныеТаблицыФактическимКоличеством(ТаблицаПроизводства, ТаблицаЗамен, СтруктураПараметров);
		
	КонецЕсли;
	
	//////////////////////////////////////////////////////////////////////
	// 5. Вернуть результат
	
	СтруктураТаблицПроизводства = Новый Структура("ТаблицаПроизводства, ТаблицаЗамен", ТаблицаПроизводства, ТаблицаЗамен);
	Возврат СтруктураТаблицПроизводства;
	
Конецфункции

// Анализирует рассчитанную таблицу производства на наличие недовложений.
//
// Параметры:
//	ВыпускБлюд - ссылка на документ ВыпускБлюд, для которого анализируются недовложения.
//	СтруктураТаблицПроизводства - структура значений, содержащая таблицу производства и таблицу замен.
//	ВсеИнгредиенты - признак, определяющий необходимость анализировать все ингредиенты или только нехватающие.
//	УчитыватьЗаменыВРецептурах - признак необходимости учета замен в рецептурах.
//  
// Возвращаемое значение:
//	Структура значений, содержащая таблицу недовложений производства, 
//	таблицу недовложений в заменах и признак наличия недовложений в заменах.
//
Функция АнализТаблицПроизводстваНаНедовложения(ВыпускБлюд, СтруктураТаблицПроизводства, ВсеИнгредиенты=Ложь,УчитыватьЗаменыВРецептурах=Ложь) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	//1. Таблица производства и таблица замен
	
	ТаблицаПроизводства = СтруктураТаблицПроизводства.ТаблицаПроизводства;
	ТаблицаЗамен = СтруктураТаблицПроизводства.ТаблицаЗамен;
	
	////////////////////////////////////////////////////////////////////////////
	// 2. Структура поиска в таблице замен.
	
	ПоляОтбораАналогов = "Блюдо, ХарактеристикаБлюда, Рецептура, Автопередел, ИнгредиентИсходный, ХарактеристикаИнгредиентаИсходного, НазначениеИнгредиентаИсходного, УпаковкаИнгредиентаИсходный";
	СтрокаОтбораАналогов = Новый Структура(ПоляОтбораАналогов);
	ТаблицаЗамен.Индексы.Добавить(ПоляОтбораАналогов);
	
	////////////////////////////////////////////////////////////////////////////
	// 3. Инициализация таблицы недовложений и таблицы замен недовложений.
	
	ОписаниеТиповНоменклатура   = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	ОписаниеТиповХарактеристика = Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры");
	ОписаниеТиповНазначение     = Новый ОписаниеТипов("СправочникСсылка.Назначения");
	ОписаниеТиповЕдиница        = Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения");
	ОписаниеТиповРецептура      = Новый ОписаниеТипов("ДокументСсылка.питРецептура");
	ОписаниеТиповЧисло          = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(18,6));
	ОписаниеТиповБулево         = Новый ОписаниеТипов("Булево");
	
	ТаблицаНедовложений = Новый ТаблицаЗначений;
	ТаблицаНедовложений.Колонки.Добавить("Номенклатура", ОписаниеТиповНоменклатура);
	ТаблицаНедовложений.Колонки.Добавить("Характеристика", ОписаниеТиповХарактеристика);
	ТаблицаНедовложений.Колонки.Добавить("Назначение", ОписаниеТиповНазначение);
	ТаблицаНедовложений.Колонки.Добавить("УпаковкаНоменклатуры",ОписаниеТиповЕдиница);
	ТаблицаНедовложений.Колонки.Добавить("КоличествоНоменклатурыНеобходимо", ОписаниеТиповЧисло);
	ТаблицаНедовложений.Колонки.Добавить("КоличествоНоменклатурыВНаличии", ОписаниеТиповЧисло);
	ТаблицаНедовложений.Колонки.Добавить("КоличествоНоменклатурыНедовложение", ОписаниеТиповЧисло);
	ТаблицаНедовложений.Колонки.Добавить("КоличествоУпаковокНоменклатурыНеобходимо", ОписаниеТиповЧисло);
	ТаблицаНедовложений.Колонки.Добавить("КоличествоУпаковокНоменклатурыВНаличии", ОписаниеТиповЧисло);
	ТаблицаНедовложений.Колонки.Добавить("КоличествоУпаковокНоменклатурыНедовложение", ОписаниеТиповЧисло);
	
	ТаблицаЗаменНедовложений = Новый ТаблицаЗначений;
	ТаблицаЗаменНедовложений.Колонки.Добавить("ИнгредиентИсходный", ОписаниеТиповНоменклатура);
	ТаблицаЗаменНедовложений.Колонки.Добавить("ХарактеристикаИнгредиентаИсходного", ОписаниеТиповХарактеристика);
	ТаблицаЗаменНедовложений.Колонки.Добавить("НазначениеИнгредиентаИсходного", ОписаниеТиповНазначение);
	ТаблицаЗаменНедовложений.Колонки.Добавить("УпаковкаИнгредиентаИсходный",ОписаниеТиповЕдиница);
	ТаблицаЗаменНедовложений.Колонки.Добавить("КоличествоИнгредиентИсходный", ОписаниеТиповЧисло);
	ТаблицаЗаменНедовложений.Колонки.Добавить("КоличествоУпаковокИнгредиентИсходный", ОписаниеТиповЧисло);
	ТаблицаЗаменНедовложений.Колонки.Добавить("ИнгредиентЗамена", ОписаниеТиповНоменклатура);
	ТаблицаЗаменНедовложений.Колонки.Добавить("ХарактеристикаИнгредиентаЗамена", ОписаниеТиповХарактеристика);
	ТаблицаЗаменНедовложений.Колонки.Добавить("НазначениеИнгредиентаЗамена", ОписаниеТиповНазначение);
	ТаблицаЗаменНедовложений.Колонки.Добавить("УпаковкаИнгредиентаЗамена",ОписаниеТиповЕдиница);
	ТаблицаЗаменНедовложений.Колонки.Добавить("КоличествоИнгредиентЗамена", ОписаниеТиповЧисло);
	ТаблицаЗаменНедовложений.Колонки.Добавить("КоличествоИнгредиентЗаменаВНаличии", ОписаниеТиповЧисло);
	ТаблицаЗаменНедовложений.Колонки.Добавить("КоличествоИнгредиентЗаменаНедовложение", ОписаниеТиповЧисло);
	ТаблицаЗаменНедовложений.Колонки.Добавить("КоличествоУпаковокИнгредиентЗамена", ОписаниеТиповЧисло);
	ТаблицаЗаменНедовложений.Колонки.Добавить("КоличествоУпаковокИнгредиентЗаменаВНаличии", ОписаниеТиповЧисло);
	ТаблицаЗаменНедовложений.Колонки.Добавить("КоличествоУпаковокИнгредиентЗаменаНедовложение", ОписаниеТиповЧисло);
	// Если надо учесть конкретные указания на замены, например при заполнении Перемещения, расширим структуру таблиц.
	Если УчитыватьЗаменыВРецептурах Тогда
		ТаблицаНедовложений.Колонки.Добавить("Рецептура", ОписаниеТиповРецептура );
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// 4. Определение режима заполнения производства по норме или по факту.
	
	Если ТипЗнч(ВыпускБлюд) = Тип("ДокументОбъект.питВыпускБлюд")
		ИЛИ ТипЗнч(ВыпускБлюд) = Тип("ДокументОбъект.питПланМеню")
		ИЛИ ТипЗнч(ВыпускБлюд) = Тип("ДокументСсылка.питВыпускБлюд")
		ИЛИ ТипЗнч(ВыпускБлюд) = Тип("ДокументСсылка.питПланМеню") Тогда
		ПоНорме = (ВыпускБлюд.РежимРасчетаСписанияВПроизводство=Перечисления.питРежимыРасчетаСписанияВПроизводство.ПоНорме);
	Иначе
		ПоНорме = Истина;
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// 6. Обход таблицы производства, анализ недовложений.
	
	Для Каждого СтрокаПроизводства Из ТаблицаПроизводства Цикл
		
		////////////////////////////////////////////////////////////////////////////
		// 6.1 Не анализируются специи, полуфабрикатные строки и технологические ингредиенты
		Если СтрокаПроизводства.Специя Тогда
			Продолжить;
		КонецЕсли;
		Если СтрокаПроизводства.Ингредиент.питВидНоменклатуры = Перечисления.питВидыНоменклатуры.ТехнологическийИнгредиент Тогда
			Продолжить;
		КонецЕсли;
		Если СтрокаПроизводства.ИзАвтопеределов Тогда
			Продолжить;
		КонецЕсли;
		
		////////////////////////////////////////////////////////////////////////////
		// 6.2 Нормативное количество списать
		// Анализ нехваток производим в базовых единицах измерения.
		// Если это заполнение произошло по норме, то количество факта совпадает со складским.
		КоличествоНеобходимоСписатьПоНорме = СтрокаПроизводства.КоличествоИнгредиентаНорма;
		
		Если ВсеИнгредиенты Тогда
			////////////////////////////////////////////////////////////////////////////
			// 6.3 Обход всех ингредиентов.
			
			////////////////////////////////////////////////////////////////////////////
			// 6.5 Ищем, вдруг строка по текущему ингредиенту уже добавлена в таблицу недовложений.
			СтрокаОтбора = Новый Структура();
			СтрокаОтбора.Вставить("Номенклатура"	,СтрокаПроизводства.Ингредиент);
			СтрокаОтбора.Вставить("Характеристика"	,СтрокаПроизводства.ХарактеристикаИнгредиента);
			СтрокаОтбора.Вставить("Назначение"	,СтрокаПроизводства.НазначениеИнгредиента);
			Если НЕ УчитыватьЗаменыВРецептурах Тогда // Ищем по номенклатуре
				Строки = ТаблицаНедовложений.НайтиСтроки(СтрокаОтбора);
			Иначе // Применяем расширенный фильтр
				СтрокаОтбора.Вставить("Рецептура",СтрокаПроизводства.Рецептура);
				Строки = ТаблицаНедовложений.НайтиСтроки(СтрокаОтбора);
			КонецЕсли;
			Если НЕ Строки.Количество() Тогда
				////////////////////////////////////////////////////////////////////////////
				// 6.5.1 Строка не найдена, добавляем новую.
				СтрокаТаблицыНедовложений = ТаблицаНедовложений.Добавить();
				СтрокаТаблицыНедовложений.Номенклатура				= СтрокаПроизводства.Ингредиент;
				СтрокаТаблицыНедовложений.Характеристика			= СтрокаПроизводства.ХарактеристикаИнгредиента;
				СтрокаТаблицыНедовложений.Назначение				= СтрокаПроизводства.НазначениеИнгредиента;
				// В качестве упаковки будет базовая единица измерения.
				СтрокаТаблицыНедовложений.УпаковкаНоменклатуры		= СтрокаПроизводства.ЕдиницаИзмеренияИнгредиента;
				Если УчитыватьЗаменыВРецептурах Тогда
					СтрокаТаблицыНедовложений.Рецептура = СтрокаПроизводства.Рецептура;
				КонецЕсли;
				//СтрокаТаблицыНедовложений.КоэффициентНоменклатуры = питДокументы.ПолучитьКоэффициентЕдиницыИзмерения(СтрокаТаблицыНедовложений.Номенклатура,СтрокаТаблицыНедовложений.УпаковкаНоменклатуры);
			Иначе
				////////////////////////////////////////////////////////////////////////////
				// 6.5.2 Строка найдена, обновляем существующую.
				СтрокаТаблицыНедовложений = Строки[0];
			КонецЕсли;
				
			////////////////////////////////////////////////////////////////////////////
			// 6.6 Обновляем ресурсы в строке.
			Если ПоНорме Тогда
				СтрокаТаблицыНедовложений.КоличествоНоменклатурыНеобходимо =  СтрокаТаблицыНедовложений.КоличествоНоменклатурыНеобходимо + КоличествоНеобходимоСписатьПоНорме;
			Иначе
				СтрокаТаблицыНедовложений.КоличествоНоменклатурыНеобходимо =  СтрокаТаблицыНедовложений.КоличествоНоменклатурыНеобходимо + СтрокаПроизводства.КоличествоИнгредиентаФактВДокументе;
			КонецЕсли;
			
			Если НЕ ПоНорме Тогда
				////////////////////////////////////////////////////////////////////////////
				// 6.7 Анализ недовложений в таблице замен по текущей строке производства(только в режиме по факту).
				
				////////////////////////////////////////////////////////////////////////////
				// 6.7.1 Поиск в таблице замен по структуре поиска по таблице замен из (2).
				СтрокаОтбораАналогов.Блюдо								= СтрокаПроизводства.Номенклатура;
				СтрокаОтбораАналогов.ХарактеристикаБлюда				= СтрокаПроизводства.Характеристика;
				СтрокаОтбораАналогов.Рецептура							= СтрокаПроизводства.Рецептура;
				СтрокаОтбораАналогов.Автопередел						= СтрокаПроизводства.Автопередел;
				СтрокаОтбораАналогов.ИнгредиентИсходный					= СтрокаПроизводства.Ингредиент;
				СтрокаОтбораАналогов.ХарактеристикаИнгредиентаИсходного	= СтрокаПроизводства.ХарактеристикаИнгредиента;
				СтрокаОтбораАналогов.НазначениеИнгредиентаИсходного		= СтрокаПроизводства.НазначениеИнгредиента;
				СтрокаОтбораАналогов.УпаковкаИнгредиентаИсходный		= СтрокаПроизводства.ЕдиницаИзмеренияИнгредиента;
				СтрокиАналогов = ТаблицаЗамен.НайтиСтроки(СтрокаОтбораАналогов);
				
				////////////////////////////////////////////////////////////////////////////
				// 6.7.2 Обход найденных замен (СтрокиАналогов).
				Для Инд=1 По СтрокиАналогов.Количество() Цикл
					
					////////////////////////////////////////////////////////////////////////////
					// 6.7.2.1 Анализируем, сколько надо списать и сколько можно списать.
					// Анализ в базовых единицах измерения.
					КоличествоЕстьЗамены				= СтрокиАналогов[Инд-1].КоличествоИнгредиентаЗамена;
					ЕдиницаЗамена						= СтрокиАналогов[Инд-1].УпаковкаИнгредиентаЗамена;
					ИнгредиентЗамена					= СтрокиАналогов[Инд-1].ИнгредиентЗамена;
					ХарактеристикаИнгредиентаЗамена		= СтрокиАналогов[Инд-1].ХарактеристикаИнгредиентаЗамена;
					НазначениеИнгредиентаЗамена			= СтрокиАналогов[Инд-1].НазначениеИнгредиентаЗамена;
					
					////////////////////////////////////////////////////////////////////////////
					// 6.7.2.2 Ищем, вдруг строка по текущей замене уже добавлена в таблицу замен недовложений.
					СтрокаОтбора = Новый Структура();
					СтрокаОтбора.Вставить("Номенклатура",ИнгредиентЗамена);
					СтрокаОтбора.Вставить("Характеристика",ХарактеристикаИнгредиентаЗамена);
					СтрокаОтбора.Вставить("Назначение",НазначениеИнгредиентаЗамена);
					Если НЕ УчитыватьЗаменыВРецептурах Тогда // Ищем по номенклатуре
						Строки = ТаблицаНедовложений.НайтиСтроки(СтрокаОтбора);
					Иначе // Применяем расширенный фильтр
						СтрокаОтбора.Вставить("Рецептура",СтрокаПроизводства.Рецептура);
						Строки = ТаблицаНедовложений.НайтиСтроки(СтрокаОтбора);
					КонецЕсли;	
					
					Если Строки.Количество() = 0 Тогда
						////////////////////////////////////////////////////////////////////////////
						// 6.7.2.3 Строка не найдена, добавляем новую.
						СтрокаТаблицыНедовложений = ТаблицаНедовложений.Добавить();
						СтрокаТаблицыНедовложений.Номенклатура			= ИнгредиентЗамена;
						СтрокаТаблицыНедовложений.Характеристика		= ХарактеристикаИнгредиентаЗамена;
						СтрокаТаблицыНедовложений.Назначение			= НазначениеИнгредиентаЗамена;
						СтрокаТаблицыНедовложений.УпаковкаНоменклатуры	= ЕдиницаЗамена;
						Если УчитыватьЗаменыВРецептурах Тогда
							СтрокаТаблицыНедовложений.Рецептура = СтрокаПроизводства.Рецептура;
						КонецЕсли;
					Иначе
						////////////////////////////////////////////////////////////////////////////
						// 6.7.2.4 Строка найдена, обновляем существующую.
						СтрокаТаблицыНедовложений = Строки[0];
					КонецЕсли;
					
					////////////////////////////////////////////////////////////////////////////
					// 6.7.2.5 Обновляем ресурсы в строке.
					СтрокаТаблицыНедовложений.КоличествоНоменклатурыНеобходимо = СтрокаТаблицыНедовложений.КоличествоНоменклатурыНеобходимо + КоличествоЕстьЗамены;
					
				КонецЦикла;
			КонецЕсли;
		Иначе
			////////////////////////////////////////////////////////////////////////////
			// 6.8 Обход только недостающих ингредиентов.
			
			////////////////////////////////////////////////////////////////////////////
			// 6.9 Запоминаем факт в базовых единицах.
			КоличествоФактЕсть = СтрокаПроизводства.КоличествоИнгредиентаФакт;
			
			Если КоличествоНеобходимоСписатьПоНорме > КоличествоФактЕсть Тогда
				////////////////////////////////////////////////////////////////////////////
				// 6.10 Рассматриваем только строки, где факта меньше чем нормы.
				
				////////////////////////////////////////////////////////////////////////////
				// 6.10 Поиск по таблице замен (СтрокиАналогов).
				// Если есть аналоги, то это недовложение не считается.
				СтрокаОтбораАналогов.Блюдо								= СтрокаПроизводства.Номенклатура;
				СтрокаОтбораАналогов.ХарактеристикаБлюда				= СтрокаПроизводства.Характеристика;
				СтрокаОтбораАналогов.Рецептура							= СтрокаПроизводства.Рецептура;
				СтрокаОтбораАналогов.Автопередел						= СтрокаПроизводства.Автопередел;
				СтрокаОтбораАналогов.ИнгредиентИсходный					= СтрокаПроизводства.Ингредиент;
				СтрокаОтбораАналогов.ХарактеристикаИнгредиентаИсходного	= СтрокаПроизводства.ХарактеристикаИнгредиента;
				СтрокаОтбораАналогов.НазначениеИнгредиентаИсходного		= СтрокаПроизводства.НазначениеИнгредиента;
				СтрокаОтбораАналогов.УпаковкаИнгредиентаИсходный		= СтрокаПроизводства.ЕдиницаИзмеренияИнгредиента;
				СтрокиАналогов = ТаблицаЗамен.НайтиСтроки(СтрокаОтбораАналогов);
				
				////////////////////////////////////////////////////////////////////////////
				// 6.11 Обход найденных замен (СтрокиАналогов).
				Для  Инд=1 По СтрокиАналогов.Количество() Цикл
					
					////////////////////////////////////////////////////////////////////////////
					// 6.11.1 Анализируем, сколько надо списать и сколько можно списать.
					// Анализ в базовых единицах измерения.
					КоличествоЕстьЗамены			= СтрокиАналогов[Инд-1].КоличествоИнгредиентаЗамена;
					КоличествоЕстьИсходный			= СтрокиАналогов[Инд-1].КоличествоИнгредиентаИсходный;
					
					ЕдиницаЗамена					= СтрокиАналогов[Инд-1].УпаковкаИнгредиентаЗамена;
					ЕдиницаИсходный					= СтрокиАналогов[Инд-1].УпаковкаИнгредиентаИсходный;
					
					ИнгредиентЗамена				= СтрокиАналогов[Инд-1].ИнгредиентЗамена;
					ХарактеристикаИнгредиентаЗамена	= СтрокиАналогов[Инд-1].ХарактеристикаИнгредиентаЗамена;
					НазначениеИнгредиентаЗамена		= СтрокиАналогов[Инд-1].НазначениеИнгредиентаЗамена;
					
					////////////////////////////////////////////////////////////////////////////
					// 6.11.3 Ищем, вдруг строка по текущей замене уже добавлена в таблицу замен недовложений.
					СтруктураОтбора = Новый Структура();
					СтруктураОтбора.Вставить("ИнгредиентИсходный"					,СтрокаПроизводства.Ингредиент);
					СтруктураОтбора.Вставить("ХарактеристикаИнгредиентаИсходного"	,СтрокаПроизводства.ХарактеристикаИнгредиента);
					СтруктураОтбора.Вставить("НазначениеИнгредиентаИсходного"		,СтрокаПроизводства.НазначениеИнгредиента);
					СтруктураОтбора.Вставить("ИнгредиентЗамена"						,ИнгредиентЗамена);
					СтруктураОтбора.Вставить("ХарактеристикаИнгредиентаЗамена"		,ХарактеристикаИнгредиентаЗамена);
					СтруктураОтбора.Вставить("НазначениеИнгредиентаЗамена"			,НазначениеИнгредиентаЗамена);
					СтрокиТаблицыЗаменНедовложений = ТаблицаЗаменНедовложений.НайтиСтроки(СтруктураОтбора);
					
					Если НЕ СтрокиТаблицыЗаменНедовложений.Количество() Тогда
						////////////////////////////////////////////////////////////////////////////
						// 6.11.4 Строка не найдена, добавляем новую.
						СтрокаТаблицыЗаменНедовложений = ТаблицаЗаменНедовложений.Добавить();
						СтрокаТаблицыЗаменНедовложений.ИнгредиентИсходный					= СтрокаПроизводства.Ингредиент;
						СтрокаТаблицыЗаменНедовложений.ХарактеристикаИнгредиентаИсходного	= СтрокаПроизводства.ХарактеристикаИнгредиента;
						СтрокаТаблицыЗаменНедовложений.НазначениеИнгредиентаИсходного		= СтрокаПроизводства.НазначениеИнгредиента;
						СтрокаТаблицыЗаменНедовложений.УпаковкаИнгредиентаИсходный			= СтрокаПроизводства.ЕдиницаИзмеренияИнгредиента;
						СтрокаТаблицыЗаменНедовложений.ИнгредиентЗамена						= ИнгредиентЗамена;
						СтрокаТаблицыЗаменНедовложений.ХарактеристикаИнгредиентаЗамена		= ХарактеристикаИнгредиентаЗамена;
						СтрокаТаблицыЗаменНедовложений.НазначениеИнгредиентаЗамена		= НазначениеИнгредиентаЗамена;
						СтрокаТаблицыЗаменНедовложений.УпаковкаИнгредиентаЗамена			= ЕдиницаЗамена;
					Иначе
						////////////////////////////////////////////////////////////////////////////
						// 6.11.5 Строка найдена, используем существующую.
						СтрокаТаблицыЗаменНедовложений = СтрокиТаблицыЗаменНедовложений.Получить(0);
					КонецЕсли;
					
					////////////////////////////////////////////////////////////////////////////
					// 6.11.6 Обновляем ресурсы в строке.
					СтрокаТаблицыЗаменНедовложений.КоличествоИнгредиентИсходный	= СтрокаТаблицыЗаменНедовложений.КоличествоИнгредиентИсходный	+ КоличествоЕстьИсходный;
					СтрокаТаблицыЗаменНедовложений.КоличествоИнгредиентЗамена	= СтрокаТаблицыЗаменНедовложений.КоличествоИнгредиентЗамена		+ КоличествоЕстьЗамены;
					
					////////////////////////////////////////////////////////////////////////////
					// 6.11.7 Изменяем текущее количество факт.
					КоличествоФактЕсть = КоличествоФактЕсть + КоличествоЕстьИсходный;
					
				КонецЦикла;	// Цикл по строкам замен.
			КонецЕсли;		// Условие, что факт меньше нормы.
			
			////////////////////////////////////////////////////////////////////////////
			// 6.12 После того, как поработали с заменами, определим финальное количество недовложения.
			Недовложение		= КоличествоНеобходимоСписатьПоНорме - КоличествоФактЕсть;
			
			////////////////////////////////////////////////////////////////////////////
			// 6.13 Ищем, вдруг строка по текущему ингредиенту уже добавлена в таблицу недовложений.
			СтрокаОтбора = Новый Структура();
			СтрокаОтбора.Вставить("Номенклатура",СтрокаПроизводства.Ингредиент);
			СтрокаОтбора.Вставить("Характеристика",СтрокаПроизводства.ХарактеристикаИнгредиента);
			СтрокаОтбора.Вставить("Назначение",СтрокаПроизводства.НазначениеИнгредиента);
			Если НЕ УчитыватьЗаменыВРецептурах Тогда // Ищем по номенклатуре
				Строки = ТаблицаНедовложений.НайтиСтроки(СтрокаОтбора);
			Иначе // Применяем расширенный фильтр
				СтрокаОтбора.Вставить("Рецептура",СтрокаПроизводства.Рецептура);
				Строки = ТаблицаНедовложений.НайтиСтроки(СтрокаОтбора);
			КонецЕсли;	
			
			Если Строки.Количество() = 0 Тогда
				////////////////////////////////////////////////////////////////////////////
				// 6.14 Строка не найдена, добавляем новую.
				СтрокаТаблицыНедовложений = ТаблицаНедовложений.Добавить();
				СтрокаТаблицыНедовложений.Номенклатура						= СтрокаПроизводства.Ингредиент;
				СтрокаТаблицыНедовложений.Характеристика					= СтрокаПроизводства.ХарактеристикаИнгредиента;
				СтрокаТаблицыНедовложений.Назначение						= СтрокаПроизводства.НазначениеИнгредиента;
				СтрокаТаблицыНедовложений.УпаковкаНоменклатуры				= СтрокаПроизводства.ЕдиницаИзмеренияИнгредиента;
				Если УчитыватьЗаменыВРецептурах Тогда
					СтрокаТаблицыНедовложений.Рецептура = СтрокаПроизводства.Рецептура;
				КонецЕсли;
			Иначе
				////////////////////////////////////////////////////////////////////////////
				// 6.14 Строка найдена, используем существующую.
				СтрокаТаблицыНедовложений = Строки[0];
			КонецЕсли;
			
			////////////////////////////////////////////////////////////////////////////
			// 6.15 Обновляем ресурсы в строке.
			СтрокаТаблицыНедовложений.КоличествоНоменклатурыНеобходимо		= СтрокаТаблицыНедовложений.КоличествоНоменклатурыНеобходимо + КоличествоНеобходимоСписатьПоНорме;
			СтрокаТаблицыНедовложений.КоличествоНоменклатурыВНаличии		= СтрокаТаблицыНедовложений.КоличествоНоменклатурыВНаличии + КоличествоФактЕсть;
			СтрокаТаблицыНедовложений.КоличествоНоменклатурыНедовложение	= СтрокаТаблицыНедовложений.КоличествоНоменклатурыНедовложение + Недовложение;
			
		КонецЕсли;	// Обход всех/недостающих ингредиентов.
	КонецЦикла;		// Цикл по таблице производства.
	
	////////////////////////////////////////////////////////////////////////////
	// 7. Вычислим приведенное значение недовложения и удалим ненужные строки.
	// То есть, если замены полностью покрыли нехватку ингредиента, то недовложения не будет,
	// строку из таблицы недовложений можно удалить.
	Если НЕ ВсеИнгредиенты Тогда
		КоличествоСтрок = ТаблицаНедовложений.Количество();
		Инд = 0;
		Пока Инд < КоличествоСтрок Цикл
			СтрокаНедовложения = ТаблицаНедовложений.Получить(Инд);
			// Проверка в базовых единицах
			КоличествоНоменклатурыНеобходимо	= Окр(СтрокаНедовложения.КоличествоНоменклатурыНеобходимо,3);
			КоличествоНоменклатурыВНаличии		= Окр(СтрокаНедовложения.КоличествоНоменклатурыВНаличии,3);
			Если КоличествоНоменклатурыВНаличии >= КоличествоНоменклатурыНеобходимо Тогда
				ТаблицаНедовложений.Удалить(СтрокаНедовложения);
				КоличествоСтрок = КоличествоСтрок - 1;
			Иначе
				Инд=Инд+1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// 8. Расчет количества в упаковках(до этого все считали в базовых)
	КэшированныеЗначенияУпаковок = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	Для каждого ТекСтрокаНедовложений Из ТаблицаНедовложений Цикл
		ТекКоэффициентУпаковки = питОбщегоНазначения.ПолучитьКоэффициентУпаковки(ТекСтрокаНедовложений.Номенклатура, ТекСтрокаНедовложений.УпаковкаНоменклатуры, КэшированныеЗначенияУпаковок);
		Если ТекКоэффициентУпаковки = 0 Тогда
			ТекКоэффициентУпаковки = 1;
		КонецЕсли;
		ТекСтрокаНедовложений.КоличествоУпаковокНоменклатурыВНаличии		= ТекСтрокаНедовложений.КоличествоНоменклатурыВНаличии / ТекКоэффициентУпаковки;
		ТекСтрокаНедовложений.КоличествоУпаковокНоменклатурыНедовложение	= ТекСтрокаНедовложений.КоличествоНоменклатурыНедовложение / ТекКоэффициентУпаковки;
		ТекСтрокаНедовложений.КоличествоУпаковокНоменклатурыНеобходимо		= ТекСтрокаНедовложений.КоличествоНоменклатурыНеобходимо / ТекКоэффициентУпаковки;
	КонецЦикла;
	Для каждого ТекСтрокаЗаменНедовложений Из ТаблицаЗаменНедовложений Цикл
		ТекКоэффициентУпаковкиЗамена = питОбщегоНазначения.ПолучитьКоэффициентУпаковки(ТекСтрокаЗаменНедовложений.ИнгредиентЗамена, ТекСтрокаЗаменНедовложений.УпаковкаИнгредиентаЗамена, КэшированныеЗначенияУпаковок);
		Если ТекКоэффициентУпаковкиЗамена = 0 Тогда
			ТекКоэффициентУпаковкиЗамена = 1;
		КонецЕсли;
		ТекКоэффициентУпаковкиИсходный = питОбщегоНазначения.ПолучитьКоэффициентУпаковки(ТекСтрокаЗаменНедовложений.ИнгредиентИсходный, ТекСтрокаЗаменНедовложений.УпаковкаИнгредиентаИсходный, КэшированныеЗначенияУпаковок);
		Если ТекКоэффициентУпаковкиИсходный = 0 Тогда
			ТекКоэффициентУпаковкиИсходный = 1;
		КонецЕсли;
		
		ТекСтрокаЗаменНедовложений.КоличествоУпаковокИнгредиентЗамена				= ТекСтрокаЗаменНедовложений.КоличествоИнгредиентЗамена / ТекКоэффициентУпаковкиЗамена;
		ТекСтрокаЗаменНедовложений.КоличествоУпаковокИнгредиентЗаменаВНаличии		= ТекСтрокаЗаменНедовложений.КоличествоИнгредиентЗаменаВНаличии / ТекКоэффициентУпаковкиЗамена;
		ТекСтрокаЗаменНедовложений.КоличествоУпаковокИнгредиентЗаменаНедовложение	= ТекСтрокаЗаменНедовложений.КоличествоИнгредиентЗаменаНедовложение / ТекКоэффициентУпаковкиЗамена;
		ТекСтрокаЗаменНедовложений.КоличествоУпаковокИнгредиентИсходный				= ТекСтрокаЗаменНедовложений.КоличествоИнгредиентИсходный / ТекКоэффициентУпаковкиИсходный;
	КонецЦикла;
	
	////////////////////////////////////////////////////////////////////////////
	// 9. Возврат результата
	СтруктураТаблицНедовложений = Новый Структура("ТаблицаНедовложений, ТаблицаЗаменНедовложений", ТаблицаНедовложений, ТаблицаЗаменНедовложений);
	Возврат СтруктураТаблицНедовложений;
	
КонецФункции	

// Функция получает таблицу производства по выпуску блюд или плану-меню, возможно с учетом замен.
//
// Параметры:
//  ДокументПроизводства	 - ДокументСсылка.питВыпускБлюд, ДокументСсылка.питПланМеню, ДокументОбъект.питВыпускБлюд, ДокументОбъект.питПланМеню, Массив - Ссылка или объект документа или массив ссылок.
//  СУчетомЗамен - Булево - Учитывать или нет таблицу змен.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица производства.
//
Функция ПолучитьТаблицуПроизводстваПоВыпускуБлюд(ДокументПроизводства, СУчетомЗамен = Ложь) Экспорт
	
	// Можно передать сразу массив документов.
	Если ТипЗнч(ДокументПроизводства) = Тип("Массив") Тогда
		Если НЕ ДокументПроизводства.Количество() Тогда
			// Пустой массив никак не обрабатываем.
			Возврат Новый ТаблицаЗначений;
		КонецЕсли;
		МассивДокументов = ДокументПроизводства;
	Иначе
		МассивДокументов = Новый Массив;
		МассивДокументов.Добавить(ДокументПроизводства);
	КонецЕсли;
	
	// Проверка типа документа.
	Если ТипЗнч(МассивДокументов[0]) = Тип("ДокументСсылка.питВыпускБлюд") Тогда
		ЭтоОбъект = Ложь;
		ИмяДокумента = "питВыпускБлюд";
	ИначеЕсли ТипЗнч(МассивДокументов[0]) = Тип("ДокументОбъект.питВыпускБлюд") Тогда
		ЭтоОбъект = Истина;
		ИмяДокумента = "питВыпускБлюд";
	ИначеЕсли ТипЗнч(МассивДокументов[0]) = Тип("ДокументСсылка.питПланМеню") Тогда
		ЭтоОбъект = Ложь;
		ИмяДокумента = "питПланМеню";
	ИначеЕсли ТипЗнч(МассивДокументов[0]) = Тип("ДокументОбъект.питПланМеню") Тогда
		ЭтоОбъект = Истина;
		ИмяДокумента = "питПланМеню";
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	// Если передан объект а не ссылка, то для отбора в запросе все равно нужна ссылка.
	МассивСсылок = Новый Массив;
	Если ЭтоОбъект Тогда
		Для каждого ТекДокумент Из МассивДокументов Цикл
			МассивСсылок.Добавить(ТекДокумент.Ссылка);
		КонецЦикла;
	Иначе
		МассивСсылок = МассивДокументов;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	питВыпускБлюдБлюда.Ссылка КАК ДокументСсылка,
		|	питВыпускБлюдБлюда.Номенклатура КАК Номенклатура,
		|	питВыпускБлюдБлюда.Характеристика КАК Характеристика,
		|	" + ?(ИмяДокумента = "питВыпускБлюд", "питВыпускБлюдБлюда.Серия", "ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)") + " КАК Серия,
		|	питВыпускБлюдБлюда.Назначение КАК Назначение,
		|	питВыпускБлюдБлюда.УпаковкаНоменклатуры КАК ЕдиницаИзмеренияНоменклатуры,
		|	ЕСТЬNULL(питВыпускБлюдИнгредиенты.Ингредиент, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Ингредиент,
		|	ЕСТЬNULL(питВыпускБлюдИнгредиенты.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК ХарактеристикаИнгредиента,
		|	ЕСТЬNULL(питВыпускБлюдИнгредиенты.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК НазначениеИнгредиента,
		|	ЕСТЬNULL(питВыпускБлюдИнгредиенты.УпаковкаИнгредиента, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмеренияИнгредиента,
		|	питВыпускБлюдБлюда.Рецептура КАК Рецептура,
		|	питВыпускБлюдБлюда.КоличествоНоменклатуры КАК КоличествоНоменклатуры,
		|	питВыпускБлюдБлюда.КоличествоУпаковокНоменклатуры КАК КоличествоУпаковокНоменклатуры,
		|	ЕСТЬNULL(питВыпускБлюдИнгредиенты.КоличествоИнгредиентаФакт, 0) КАК КоличествоИнгредиентаФакт,
		|	ЕСТЬNULL(питВыпускБлюдИнгредиенты.КоличествоУпаковокИнгредиентаФакт, 0) КАК КоличествоУпаковокИнгредиентаФакт,
		|	питВыпускБлюдБлюда.Автопередел КАК Автопередел,
		|	ЕСТЬNULL(питВыпускБлюдИнгредиенты.СтатьяКалькуляции, ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)) КАК СтатьяКалькуляции,
		|	" + ?(ИмяДокумента = "питВыпускБлюд", "ЕСТЬNULL(питВыпускБлюдИнгредиенты.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))", "ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)") + " КАК СерияИнгредиента,
		|	питВыпускБлюдБлюда.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ЕСТЬNULL(питВыпускБлюдИнгредиенты.КоличествоИнгредиентаНорма, 0) КАК КоличествоИнгредиентаНорма,
		|	ЕСТЬNULL(питВыпускБлюдИнгредиенты.КоличествоУпаковокИнгредиентаНорма, 0) КАК КоличествоУпаковокИнгредиентаНорма,
		|	ЕСТЬNULL(питВыпускБлюдИнгредиенты.СезонныйПроцент, 0) КАК СезонныйПроцент,
		|	ЕСТЬNULL(питВыпускБлюдИнгредиенты.ИзАвтопеределов, ЛОЖЬ) КАК ИзАвтопеределов,
		|	ЕСТЬNULL(питВыпускБлюдИнгредиенты.РецептураАвтопередела, ЗНАЧЕНИЕ(Документ.питРецептура.ПустаяСсылка)) КАК РецептураАвтопередела,
		|	ЕСТЬNULL(питВыпускБлюдИнгредиенты.Специя, ЛОЖЬ) КАК Специя,
		|	ЕСТЬNULL(питВыпускБлюдИнгредиенты.ЗапретитьЗамены, ЛОЖЬ) КАК ЗапретитьЗамены
		|ПОМЕСТИТЬ втПроизводствоПред
		|ИЗ
		|	Документ.питПланМеню.Блюда КАК питВыпускБлюдБлюда
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.питПланМеню.Ингредиенты КАК питВыпускБлюдИнгредиенты
		|		ПО питВыпускБлюдБлюда.ИдентификаторСтроки = питВыпускБлюдИнгредиенты.ИдентификаторБлюда
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.питПланМеню КАК питВыпускБлюд
		|		ПО питВыпускБлюдБлюда.Ссылка = питВыпускБлюд.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.питРецептура КАК питРецептура
		|		ПО питВыпускБлюдБлюда.Рецептура = питРецептура.Ссылка
		|ГДЕ
		|	питВыпускБлюдБлюда.Ссылка В (&МассивСсылок)
		|	И питВыпускБлюдИнгредиенты.Ссылка В (&МассивСсылок)
		|	И НЕ питВыпускБлюдИнгредиенты.Ингредиент.питВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.питВидыНоменклатуры.ТехнологическийИнгредиент)
		|;
		|";
		Если СУчетомЗамен Тогда
			Запрос.Текст = Запрос.Текст + "
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	питВыпускБлюдБлюда.Ссылка КАК ДокументСсылка,
			|	питВыпускБлюдБлюда.Номенклатура КАК Номенклатура,
			|	питВыпускБлюдБлюда.Характеристика КАК Характеристика,
			|	" + ?(ИмяДокумента = "питВыпускБлюд", "питВыпускБлюдБлюда.Серия", "ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)") + " КАК Серия,
			|	питВыпускБлюдБлюда.Назначение КАК Назначение,
			|	питВыпускБлюдБлюда.УпаковкаНоменклатуры КАК УпаковкаНоменклатуры,
			|	ЕСТЬNULL(питВыпускБлюдЗамены.ИнгредиентЗамена, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК ИнгредиентЗамена,
			|	ЕСТЬNULL(питВыпускБлюдЗамены.ХарактеристикаИнгредиентаЗамена, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК ХарактеристикаИнгредиентаЗамена,
			|	ЕСТЬNULL(питВыпускБлюдЗамены.НазначениеИнгредиентаЗамена, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК НазначениеИнгредиентаЗамена,
			|	ЕСТЬNULL(питВыпускБлюдЗамены.УпаковкаИнгредиентаЗамена, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК УпаковкаИнгредиентаЗамена,
			|	питВыпускБлюдБлюда.Рецептура КАК Рецептура,
			|	питВыпускБлюдБлюда.КоличествоНоменклатуры КАК КоличествоНоменклатуры,
			|	питВыпускБлюдБлюда.КоличествоУпаковокНоменклатуры КАК КоличествоУпаковокНоменклатуры,
			|	ЕСТЬNULL(питВыпускБлюдЗамены.КоличествоИнгредиентаЗамена, 0) КАК КоличествоИнгредиентаЗамена,
			|	ЕСТЬNULL(питВыпускБлюдЗамены.КоличествоУпаковокИнгредиентаЗамена, 0) КАК КоличествоУпаковокИнгредиентаЗамена,
			|	питВыпускБлюдБлюда.Автопередел КАК Автопередел,
			|	ЕСТЬNULL(питВыпускБлюдЗамены.СтатьяКалькуляции, ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)) КАК СтатьяКалькуляции,
			|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК СерииНоменклатуры,
			|	питВыпускБлюдБлюда.ИдентификаторСтроки КАК ИдентификаторСтроки,
			|	ЕСТЬNULL(питВыпускБлюдЗамены.ИнгредиентИсходный, ЗНАЧЕНИЕ(Справочник.Номенклатура.Пустаяссылка)) КАК ИнгредиентИсходный,
			|	ЕСТЬNULL(питВыпускБлюдЗамены.ХарактеристикаИнгредиентаИсходного, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК ХарактеристикаИнгредиентаИсходного,
			|	ЕСТЬNULL(питВыпускБлюдЗамены.НазначениеИнгредиентаИсходного, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК НазначениеИнгредиентаИсходного,
			|	ЕСТЬNULL(питВыпускБлюдЗамены.УпаковкаИнгредиентаИсходный, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК УпаковкаИнгредиентаИсходный
			|ПОМЕСТИТЬ втЗамены
			|ИЗ
			|	Документ.питПланМеню.Блюда КАК питВыпускБлюдБлюда
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.питПланМеню.Замены КАК питВыпускБлюдЗамены
			|		ПО питВыпускБлюдБлюда.Номенклатура = питВыпускБлюдЗамены.Блюдо
			|			И питВыпускБлюдБлюда.Характеристика = питВыпускБлюдЗамены.ХарактеристикаБлюда
			|			И питВыпускБлюдБлюда.Рецептура = питВыпускБлюдЗамены.Рецептура
			|			И питВыпускБлюдБлюда.Автопередел = питВыпускБлюдЗамены.Автопередел
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.питРецептура КАК питРецептура
			|		ПО питВыпускБлюдБлюда.Рецептура = питРецептура.Ссылка
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.питПланМеню КАК питВыпускБлюд
			|		ПО питВыпускБлюдБлюда.Ссылка = питВыпускБлюд.Ссылка
			|ГДЕ
			|	питВыпускБлюдБлюда.Ссылка В (&МассивСсылок)
			|	И питВыпускБлюдЗамены.Ссылка В (&МассивСсылок)
			|	И НЕ питВыпускБлюдЗамены.ИнгредиентЗамена.питВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.питВидыНоменклатуры.ТехнологическийИнгредиент)
			|;";
		КонецЕсли;
		Запрос.Текст = Запрос.Текст + "
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втПроизводствоПред.ДокументСсылка КАК ДокументСсылка,
		|	втПроизводствоПред.Номенклатура КАК Номенклатура,
		|	втПроизводствоПред.Характеристика КАК Характеристика,
		|	втПроизводствоПред.Серия КАК Серия,
		|	втПроизводствоПред.Назначение КАК Назначение,
		|	втПроизводствоПред.ЕдиницаИзмеренияНоменклатуры КАК ЕдиницаИзмеренияНоменклатуры,
		|	втПроизводствоПред.Ингредиент КАК Ингредиент,
		|	втПроизводствоПред.ХарактеристикаИнгредиента КАК ХарактеристикаИнгредиента,
		|	втПроизводствоПред.НазначениеИнгредиента КАК НазначениеИнгредиента,
		|	втПроизводствоПред.ЕдиницаИзмеренияИнгредиента КАК ЕдиницаИзмеренияИнгредиента,
		|	втПроизводствоПред.Рецептура КАК Рецептура,
		|	втПроизводствоПред.КоличествоНоменклатуры КАК КоличествоНоменклатуры,
		|	втПроизводствоПред.КоличествоУпаковокНоменклатуры КАК КоличествоУпаковокНоменклатуры,
		|	втПроизводствоПред.КоличествоИнгредиентаФакт КАК КоличествоИнгредиентаФакт,
		|	втПроизводствоПред.КоличествоИнгредиентаНорма КАК КоличествоИнгредиентаНорма,
		|	втПроизводствоПред.КоличествоУпаковокИнгредиентаФакт КАК КоличествоУпаковокИнгредиентаФакт,
		|	втПроизводствоПред.КоличествоУпаковокИнгредиентаНорма КАК КоличествоУпаковокИнгредиентаНорма,
		|	втПроизводствоПред.СезонныйПроцент КАК СезонныйПроцент,
		|	втПроизводствоПред.Автопередел КАК Автопередел,
		|	втПроизводствоПред.ИзАвтопеределов КАК ИзАвтопеределов,
		|	втПроизводствоПред.РецептураАвтопередела КАК РецептураАвтопередела,
		|	втПроизводствоПред.Специя КАК Специя,
		|	втПроизводствоПред.ЗапретитьЗамены КАК ЗапретитьЗамены,
		|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Замена,
		|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК ХарактеристикаЗамены,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК НазначениеЗамены,
		|	1 КАК КоэффициентЗамены,
		|	втПроизводствоПред.СтатьяКалькуляции КАК СтатьяКалькуляции
		|ИЗ
		|	втПроизводствоПред КАК втПроизводствоПред";
		Если СУчетомЗамен Тогда
			Запрос.Текст = Запрос.Текст + "
			|		ЛЕВОЕ СОЕДИНЕНИЕ втЗамены КАК втЗамены
			|		ПО втПроизводствоПред.ДокументСсылка = втЗамены.ДокументСсылка
			|			И втПроизводствоПред.ИдентификаторСтроки = втЗамены.ИдентификаторСтроки
			|			И втПроизводствоПред.Ингредиент = втЗамены.ИнгредиентИсходный
			|			И втПроизводствоПред.ХарактеристикаИнгредиента = втЗамены.ХарактеристикаИнгредиентаИсходного
			|			И втПроизводствоПред.НазначениеИнгредиента = втЗамены.НазначениеИнгредиентаИсходного
			|			И втПроизводствоПред.ЕдиницаИзмеренияИнгредиента = втЗамены.УпаковкаИнгредиентаИсходный";
		КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.питПланМеню", "Документ." + ИмяДокумента);
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

Функция СтруктураОбъектаРасчетПроизводства() Экспорт
	
	КЧ = Новый КвалификаторыЧисла(18,6);
	КС = Новый КвалификаторыСтроки(36);
	Массив = Новый Массив;
	Массив.Добавить(Тип("Строка"));
	ОписаниеТиповС = Новый ОписаниеТипов(Массив, , КС);
	Массив.Очистить();
	Массив.Добавить(Тип("Число"));
	ОписаниеТиповЧ = Новый ОписаниеТипов(Массив, , ,КЧ);
	
	СтруктураОбъекта = Новый Структура();
	СтруктураОбъекта.Вставить("ТипДокумента"					, Неопределено);
	СтруктураОбъекта.Вставить("Ссылка"							, Неопределено);
	СтруктураОбъекта.Вставить("Дата"							, Неопределено);
	СтруктураОбъекта.Вставить("Организация"						, Неопределено);
	СтруктураОбъекта.Вставить("Подразделение"					, Неопределено);
	СтруктураОбъекта.Вставить("Склад"							, Неопределено);
	СтруктураОбъекта.Вставить("УчитыватьОстаткиБлюдНаСкладе"	, Ложь);
	СтруктураОбъекта.Вставить("НеИспользоватьСезонныеПроценты"	, Истина);
	СтруктураОбъекта.Вставить("ВидПроизводства"					, Перечисления.питВидыПроизводства.ПеределыВнутриВыпуска);
	СтруктураОбъекта.Вставить("МоментВремени"					, Неопределено);
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("Номенклатура"			, Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаТоваров.Колонки.Добавить("Характеристика"		, Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаТоваров.Колонки.Добавить("Серия"					, Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	ТаблицаТоваров.Колонки.Добавить("Назначение"			, Новый ОписаниеТипов("СправочникСсылка.Назначения"));
	ТаблицаТоваров.Колонки.Добавить("НазначениеИнгредиентов", Новый ОписаниеТипов("СправочникСсылка.Назначения"));
	ТаблицаТоваров.Колонки.Добавить("Количество"			, ОписаниеТиповЧ);
	ТаблицаТоваров.Колонки.Добавить("КоличествоУпаковок"	, ОписаниеТиповЧ);
	ТаблицаТоваров.Колонки.Добавить("Упаковка"				, Новый ОписаниеТипов("СправочникСсылка.УпаковкиЕдиницыИзмерения"));
	ТаблицаТоваров.Колонки.Добавить("Рецептура"				, Новый ОписаниеТипов("ДокументСсылка.питРецептура"));
	ТаблицаТоваров.Колонки.Добавить("ИдентификаторСтроки"	, ОписаниеТиповС);
	Если НЕ питПроизводствоПовтИсп.ПолучитьИмяОсновнойКонфигурации() = "ERPWE" Тогда
		ТаблицаТоваров.Колонки.Добавить("СчетУчета"			, Новый ОписаниеТипов("ПланСчетовСсылка.Хозрасчетный"));
	Иначе
		ТаблицаТоваров.Колонки.Добавить("СчетУчета"			, Новый ОписаниеТипов("Строка"));
	КонецЕсли;
	СтруктураОбъекта.Вставить("Товары", ТаблицаТоваров);
	
	Возврат СтруктураОбъекта;
	
КонецФункции







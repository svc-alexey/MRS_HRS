#Область ПрограммныйИнтерфейс

#Область РегламентныеПроцедуры

// Очищает историю синхронизации старше 30 дней
//
Процедура ОчиститьИсториюСинхронизации() Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		ДатаГраницы = ТекущаяДатаСеанса() - 90 * 24 * 3600; // 30 дней назад
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	МенюSimphonyИстория.ПозицияМеню КАК ПозицияМеню,
			|	МенюSimphonyИстория.ДатаВремя КАК ДатаВремя,
			|	МенюSimphonyИстория.УникальныйИдентификатор КАК УникальныйИдентификатор
			|ИЗ
			|	РегистрСведений.MRS_ИсторияСинхронизацииМенюSimphony КАК МенюSimphonyИстория
			|ГДЕ
			|	МенюSimphonyИстория.ДатаВремя < &ДатаГраницы";
		
		Запрос.УстановитьПараметр("ДатаГраницы", ДатаГраницы);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			НаборЗаписей = РегистрыСведений.MRS_ИсторияСинхронизацииМенюSimphony.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ПозицияМеню.Установить(Выборка.ПозицияМеню);
			НаборЗаписей.Отбор.ДатаВремя.Установить(Выборка.ДатаВремя);
			НаборЗаписей.Отбор.УникальныйИдентификатор.Установить(Выборка.УникальныйИдентификатор);
			НаборЗаписей.Записать();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
		ЗаписьЖурналаРегистрации("MRS.Simphony.ОчисткаИстории", УровеньЖурналаРегистрации.Информация,,,
			СтрШаблон("Удалено записей истории синхронизации: %1", Выборка.Количество()));
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("MRS.Simphony.ОчисткаИстории", УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

// Выполняет автоматическую выгрузку позиций меню из очереди
//
Процедура ВыполнитьАвтоматическуюВыгрузку() Экспорт
	
	МаксимальноПозицийЗаСеанс = 50;
	
	ЗаписьЖурналаРегистрации("MRS.Simphony.АвтоВыгрузка", УровеньЖурналаРегистрации.Информация,,,
		"Запуск автоматической выгрузки меню");
	
	// Выборка позиций из очереди
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 50
		|	Очередь.ПозицияМеню КАК ПозицияМеню,
		|	Очередь.КассовыйУзел КАК КассовыйУзел,
		|	Очередь.Операция КАК Операция,
		|	Очередь.КоличествоПопыток КАК КоличествоПопыток
		|ИЗ
		|	РегистрСведений.MRS_ОчередьВыгрузкиМенюSimphony КАК Очередь
		|ГДЕ
		|	(Очередь.Статус = ЗНАЧЕНИЕ(Перечисление.MRS_СтатусыСинхронизацииSimphony.ВОчереди)
		|			ИЛИ Очередь.Статус = ЗНАЧЕНИЕ(Перечисление.MRS_СтатусыСинхронизацииSimphony.Ошибка)
		|				И Очередь.КоличествоПопыток < 3
		|				И (Очередь.СледующаяПопытка = ДАТАВРЕМЯ(1, 1, 1)
		|					ИЛИ Очередь.СледующаяПопытка <= &ТекущаяДата))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Очередь.ДатаПостановкиВОчередь";
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		ЗаписьЖурналаРегистрации("MRS.Simphony.АвтоВыгрузка", УровеньЖурналаРегистрации.Информация,,,
			"Очередь пуста");
		Возврат;
		
	КонецЕсли;
	
	// Группировка по кассовым узлам для bulk-операций
	ТаблицаОчереди = Результат.Выгрузить();
	ТаблицаОчереди.Свернуть("КассовыйУзел", "");
	
	ВсегоОбработано = 0;
	ВсегоУспешно = 0;
	ВсегоОшибок = 0;
	
	Для Каждого СтрокаУзла Из ТаблицаОчереди Цикл
		
		// Получаем позиции для данного узла
		Отбор = Новый Структура("КассовыйУзел", СтрокаУзла.КассовыйУзел);
		МассивПозиций = ТаблицаОчереди.НайтиСтроки(Отбор);
		
		// Выгружаем пакетом
		РезультатВыгрузки = ВыгрузитьПакетПозиций(МассивПозиций, СтрокаУзла.КассовыйУзел);
		
		ВсегоОбработано = ВсегоОбработано + РезультатВыгрузки.Обработано;
		ВсегоУспешно = ВсегоУспешно + РезультатВыгрузки.Успешно;
		ВсегоОшибок = ВсегоОшибок + РезультатВыгрузки.Ошибок;
		
	КонецЦикла;
	
	ЗаписьЖурналаРегистрации("MRS.Simphony.АвтоВыгрузка", УровеньЖурналаРегистрации.Информация,,,
		СтрШаблон("Завершено. Обработано: %1, Успешно: %2, Ошибок: %3", ВсегоОбработано, ВсегоУспешно, ВсегоОшибок));
	
КонецПроцедуры

// Синхронизирует справочники из Simphony (RVC, Major Groups, Family Groups)
//
Процедура СинхронизироватьСправочники() Экспорт
	
	ЗаписьЖурналаРегистрации("MRS.Simphony.СинхронизацияСправочников", УровеньЖурналаРегистрации.Информация,,,
		"Запуск синхронизации справочников");
	
	Попытка
		
		СинхронизироватьRVCИерархию();
		СинхронизироватьMajorGroups();
		СинхронизироватьFamilyGroups();
		
		ЗаписьЖурналаРегистрации("MRS.Simphony.СинхронизацияСправочников", УровеньЖурналаРегистрации.Информация,,,
			"Синхронизация справочников завершена успешно");
		
	Исключение
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("MRS.Simphony.СинхронизацияСправочников", УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСПозициямиМеню

// Выгружает позицию меню в Simphony
//
// Параметры:
//  Позиция - СправочникСсылка.питМеню - позиция меню для выгрузки
//  КассовыйУзел - ПланОбменаСсылка.питУдаленныеКассы - кассовый узел
//
// Возвращаемое значение:
//  Структура:
//   * Успешно - Булево - признак успешной выгрузки
//   * Сообщение - Строка - сообщение о результате
//   * HTTPСтатус - Число - код HTTP ответа
//
Функция ВыгрузитьПозициюВSimphony(Позиция, КассовыйУзел) Экспорт
	
	Результат = Новый Структура("Успешно, Сообщение, HTTPСтатус", Ложь, "", 0);
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	CorrelationId = Строка(Новый УникальныйИдентификатор);
	
	Попытка
		
		// Валидация
		РезультатВалидации = ВалидироватьПозициюМеню(Позиция, КассовыйУзел);
		
		Если Не РезультатВалидации.Валидна Тогда
			
			Результат.Сообщение = MRS_ИнтеграцияSimphonyКлиентСервер.СформироватьТекстОшибкиВалидации(РезультатВалидации.Ошибки);
			ЗаписатьВИсториюСинхронизации(Позиция, "CREATE", "Ошибка", 400, Результат.Сообщение, CorrelationId, 0);
			ОбработатьОшибкуВыгрузки(Позиция, КассовыйУзел, 400, Результат.Сообщение);
			Возврат Результат;
			
		КонецЕсли;
		
	// Формирование JSON запроса
	ТелоЗапроса = СформироватьЗапросПозицииМеню(Позиция, КассовыйУзел);
	
	// Формирование заголовков
	Заголовки = СформироватьЗаголовки(ИмяПользователя(), CorrelationId);
	
	// Определение операции и endpoint
	Операция = "CREATE";
	Endpoint = "/menu-item";
	Метод = "PUT";
	
	// Проверяем наличие записей в истории синхронизации для определения операции
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Поле1
	|ИЗ
	|	РегистрСведений.MRS_ИсторияСинхронизацииМенюSimphony КАК ИсторияСинхронизации
	|ГДЕ
	|	ИсторияСинхронизации.ПозицияМеню = &ПозицияМеню";
	
	Запрос.УстановитьПараметр("ПозицияМеню", Позиция);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Операция = "UPDATE";
		//Метод = "PATCH";
	КонецЕсли;
	
	// Отправка запроса
	HTTPОтвет = ОтправитьЗапросAPI(Метод, Endpoint, ТелоЗапроса, Заголовки);
	
	ДлительностьМс = ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера;
	
	// Обработка ответа
	Результат.HTTPСтатус = HTTPОтвет.КодСостояния;
	
	// Парсинг JSON ответа для всех статусов
	ОтветJSON = Неопределено;
	
	Попытка
		
		ОтветJSON = ПарсироватьОтветJSON(HTTPОтвет);
		
	Исключение
		
		// Если не удалось распарсить JSON (например, HTML при ошибке сервера)
		ОтветJSON = Неопределено;
		
	КонецПопытки;
	
	Если HTTPОтвет.КодСостояния = 200 Тогда
		
		// Успешная выгрузка
		УдалитьИзОчереди(Позиция, КассовыйУзел);
		
		Результат.Успешно = Истина;
		Результат.Сообщение = "Успешно выгружено";
		
		ЗаписатьВИсториюСинхронизации(Позиция, Операция, Перечисления.MRS_СтатусыСинхронизацииSimphony.Выгружено, 200, "OK", CorrelationId, ДлительностьМс);
		
	ИначеЕсли HTTPОтвет.КодСостояния = 400 Тогда
		
		// Ошибка валидации
		Если ОтветJSON <> Неопределено И ОтветJSON.Свойство("code") И ОтветJSON.Свойство("message") Тогда
			
			Результат.Сообщение = СтрШаблон("Ошибка API (код %1): %2", ОтветJSON.code, ОтветJSON.message);
			
		Иначе
			
			Результат.Сообщение = СтрШаблон("HTTP 400: %1", HTTPОтвет.ПолучитьТелоКакСтроку());
			
		КонецЕсли;
		
		// Проверка на дублирование stringNumberId
		Если ОтветJSON <> Неопределено И ОтветJSON.Свойство("code") И ОтветJSON.code = 100001 Тогда
			
			// Генерация нового ID и повторная попытка
			ЗаписатьВИсториюСинхронизации(Позиция, Операция, Перечисления.MRS_СтатусыСинхронизацииSimphony.Ошибка, 400, Результат.Сообщение, CorrelationId, ДлительностьМс);
			// TODO: Реализовать перегенерацию stringNumberId и повтор
			
		Иначе
			
			// Другие ошибки валидации
			ОбработатьОшибкуВыгрузки(Позиция, КассовыйУзел, 400, Результат.Сообщение);
			ЗаписатьВИсториюСинхронизации(Позиция, Операция, Перечисления.MRS_СтатусыСинхронизацииSimphony.Ошибка, 400, Результат.Сообщение, CorrelationId, ДлительностьМс);
			
		КонецЕсли;
		
	ИначеЕсли HTTPОтвет.КодСостояния = 500 Тогда
		
		// Ошибка сервера - повторяем
		Если ОтветJSON <> Неопределено И ОтветJSON.Свойство("code") И ОтветJSON.Свойство("message") Тогда
			
			Результат.Сообщение = СтрШаблон("Ошибка сервера (код %1): %2", ОтветJSON.code, ОтветJSON.message);
			
		Иначе
			
			Результат.Сообщение = "Внутренняя ошибка сервера Simphony";
			
		КонецЕсли;
		
		ОбработатьОшибкуВыгрузки(Позиция, КассовыйУзел, 500, Результат.Сообщение);
		ЗаписатьВИсториюСинхронизации(Позиция, Операция, Перечисления.MRS_СтатусыСинхронизацииSimphony.Ошибка, 500, Результат.Сообщение, CorrelationId, ДлительностьМс);
		
	Иначе
		
		// Прочие ошибки
		Если ОтветJSON <> Неопределено И ОтветJSON.Свойство("code") И ОтветJSON.Свойство("message") Тогда
			
			Результат.Сообщение = СтрШаблон("HTTP %1 (код %2): %3", HTTPОтвет.КодСостояния, ОтветJSON.code, ОтветJSON.message);
			
		Иначе
			
			Результат.Сообщение = СтрШаблон("HTTP %1: %2", HTTPОтвет.КодСостояния, HTTPОтвет.ПолучитьТелоКакСтроку());
			
		КонецЕсли;
		
		ОбработатьОшибкуВыгрузки(Позиция, КассовыйУзел, HTTPОтвет.КодСостояния, Результат.Сообщение);
		ЗаписатьВИсториюСинхронизации(Позиция, Операция, "Ошибка", HTTPОтвет.КодСостояния, Результат.Сообщение, CorrelationId, ДлительностьМс);
		
	КонецЕсли;
		
	Исключение
		
		// Таймаут или другая ошибка
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат.Сообщение = "Connection timeout или ошибка соединения";
		
		ДлительностьМс = ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера;
		
		ОбработатьОшибкуВыгрузки(Позиция, КассовыйУзел, 0, ТекстОшибки);
		ЗаписатьВИсториюСинхронизации(Позиция, "CREATE", Перечисления.MRS_СтатусыСинхронизацииSimphony.Ошибка, 0, ТекстОшибки, CorrelationId, ДлительностьМс);
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Удаляет позицию меню из Simphony синхронно (без очереди)
//
// Параметры:
//  StringNumberId - Число - код Simphony позиции меню
//  HierStrucId - Число - код узла Simphony
//  Наименование - Строка - наименование позиции (для логирования)
//
// Возвращаемое значение:
//  Структура:
//   * Успешно - Булево - признак успешного удаления
//   * Сообщение - Строка - сообщение о результате
//   * HTTPСтатус - Число - код HTTP ответа
//
Функция УдалитьПозициюИзSimphonyСинхронно(StringNumberId, HierStrucId, ПозицияМеню, КассовыйУзел) Экспорт
	
	Результат = Новый Структура("Успешно, Сообщение, HTTPСтатус", Ложь, "", 0);
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	CorrelationId = Строка(Новый УникальныйИдентификатор);
	
	Попытка
		
		// Проверка параметров
		Если Не ЗначениеЗаполнено(StringNumberId) Тогда
			Результат.Сообщение = "Не указан код Simphony";
			Возврат Результат;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(HierStrucId) Тогда
			Результат.Сообщение = "Не указан hierStrucId";
			Возврат Результат;
		КонецЕсли;
		
		// Формирование заголовков
		Заголовки = СформироватьЗаголовки(ИмяПользователя(), CorrelationId);
		
		// Формирование endpoint с параметрами
		Endpoint = СтрШаблон("/menu-item/%1?hierStrucId=%2", ФОРМАТ(StringNumberId,"ЧГ=0"), HierStrucId);
		
		// Отправка DELETE запроса
		HTTPОтвет = ОтправитьЗапросAPI("DELETE", Endpoint, "", Заголовки);
		
		ДлительностьМс = ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера;
		
		// Обработка ответа
		Результат.HTTPСтатус = HTTPОтвет.КодСостояния;
		
		// Парсинг JSON ответа
		ОтветJSON = Неопределено;
		
		Попытка
			ОтветJSON = ПарсироватьОтветJSON(HTTPОтвет);
		Исключение
			// Если не удалось распарсить JSON
			ОтветJSON = Неопределено;
		КонецПопытки;
		
		Если HTTPОтвет.КодСостояния = 200 Тогда
			
			// Успешное удаление
			Результат.Успешно = Истина;
			Результат.Сообщение = "Успешно удалено из Simphony";
			
			ЗаписьЖурналаРегистрации("MRS.Simphony.Удаление", УровеньЖурналаРегистрации.Информация,,,
				СтрШаблон("Удалено: %1 (ID: %2, hierStrucId: %3)", ПозицияМеню.Наименование, StringNumberId, HierStrucId));
				
			УдалитьИзОчереди(ПозицияМеню,КассовыйУзел);
			
		ИначеЕсли HTTPОтвет.КодСостояния = 404 Тогда
			
			// Позиция не найдена в Simphony - считаем это успехом
			Результат.Успешно = Истина;
			Результат.Сообщение = "Позиция не найдена в Simphony (уже удалена)";
			
			ЗаписьЖурналаРегистрации("MRS.Simphony.Удаление", УровеньЖурналаРегистрации.Предупреждение,,,
				СтрШаблон("Позиция не найдена: %1 (ID: %2)", ПозицияМеню.Наименование, StringNumberId));
			
		ИначеЕсли HTTPОтвет.КодСостояния = 400 Тогда
			
			// Ошибка валидации
			Если ОтветJSON <> Неопределено И ОтветJSON.Свойство("code") И ОтветJSON.Свойство("message") Тогда
				Результат.Сообщение = СтрШаблон("Ошибка API (код %1): %2", ОтветJSON.code, ОтветJSON.message);
			Иначе
				Результат.Сообщение = СтрШаблон("HTTP 400: %1", HTTPОтвет.ПолучитьТелоКакСтроку());
			КонецЕсли;
			
			ЗаписьЖурналаРегистрации("MRS.Simphony.Удаление", УровеньЖурналаРегистрации.Ошибка,,,
				СтрШаблон("Ошибка удаления: %1 - %2", ПозицияМеню.Наименование, Результат.Сообщение));
			
		ИначеЕсли HTTPОтвет.КодСостояния = 500 Тогда
			
			// Ошибка сервера
			Если ОтветJSON <> Неопределено И ОтветJSON.Свойство("code") И ОтветJSON.Свойство("message") Тогда
				Результат.Сообщение = СтрШаблон("Ошибка сервера (код %1): %2", ОтветJSON.code, ОтветJSON.message);
			Иначе
				Результат.Сообщение = "Внутренняя ошибка сервера Simphony";
			КонецЕсли;
			
			ЗаписьЖурналаРегистрации("MRS.Simphony.Удаление", УровеньЖурналаРегистрации.Ошибка,,,
				СтрШаблон("Ошибка сервера: %1 - %2", ПозицияМеню.Наименование, Результат.Сообщение));
			
		Иначе
			
			// Прочие ошибки
			Если ОтветJSON <> Неопределено И ОтветJSON.Свойство("code") И ОтветJSON.Свойство("message") Тогда
				Результат.Сообщение = СтрШаблон("HTTP %1 (код %2): %3", HTTPОтвет.КодСостояния, ОтветJSON.code, ОтветJSON.message);
			Иначе
				Результат.Сообщение = СтрШаблон("HTTP %1: %2", HTTPОтвет.КодСостояния, HTTPОтвет.ПолучитьТелоКакСтроку());
			КонецЕсли;
			
			ЗаписьЖурналаРегистрации("MRS.Simphony.Удаление", УровеньЖурналаРегистрации.Ошибка,,,
				СтрШаблон("Ошибка HTTP %1: %2 - %3", HTTPОтвет.КодСостояния, ПозицияМеню.Наименование, Результат.Сообщение));
			
		КонецЕсли;
		
	Исключение
		
		// Таймаут или другая ошибка
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат.Сообщение = "Connection timeout или ошибка соединения";
		
		ЗаписьЖурналаРегистрации("MRS.Simphony.Удаление", УровеньЖурналаРегистрации.Ошибка,,,
			СтрШаблон("Ошибка соединения: %1 - %2", ПозицияМеню.Наименование, ТекстОшибки));
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Удаляет позицию меню из Simphony
//
// Параметры:
//  Позиция - СправочникСсылка.питМеню - позиция меню для удаления
//  КассовыйУзел - ПланОбменаСсылка.питУдаленныеКассы - кассовый узел
//
// Возвращаемое значение:
//  Структура:
//   * Успешно - Булево - признак успешного удаления
//   * Сообщение - Строка - сообщение о результате
//   * HTTPСтатус - Число - код HTTP ответа
//
Функция УдалитьПозициюИзSimphony(Позиция, КассовыйУзел) Экспорт
	
	Результат = Новый Структура("Успешно, Сообщение, HTTPСтатус", Ложь, "", 0);
	
	НачалоЗамера = ТекущаяУниверсальнаяДатаВМиллисекундах();
	CorrelationId = Строка(Новый УникальныйИдентификатор);
	
	Попытка
		
		// Получение stringNumberId из очереди (сохранен при добавлении в ПередУдалением)
		// или из справочника, если позиция еще не удалена
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Очередь.ТекстОшибки КАК ТекстОшибки,
			|	питМеню.MRS_КодSimphony КАК КодSimphony
			|ИЗ
			|	РегистрСведений.MRS_ОчередьВыгрузкиМенюSimphony КАК Очередь
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.питМеню КАК питМеню
			|		ПО Очередь.ПозицияМеню = питМеню.Ссылка
			|ГДЕ
			|	Очередь.ПозицияМеню = &Позиция
			|	И Очередь.КассовыйУзел = &КассовыйУзел";
		
		Запрос.УстановитьПараметр("Позиция", Позиция);
		Запрос.УстановитьПараметр("КассовыйУзел", КассовыйУзел);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		КодSimphony = 0;
		
		Если Выборка.Следующий() Тогда
			
			// Сначала пытаемся получить из справочника (если позиция еще существует)
			Если ЗначениеЗаполнено(Выборка.КодSimphony) Тогда
				
				КодSimphony = Выборка.КодSimphony;
				
			ИначеЕсли СтрНачинаетсяС(Выборка.ТекстОшибки, "stringNumberId:") Тогда
				
				// Извлекаем stringNumberId из поля ТекстОшибки
				СтрокаКода = СтрЗаменить(Выборка.ТекстОшибки, "stringNumberId:", "");
				
				Попытка
					КодSimphony = Число(СтрокаКода);
				Исключение
					// Не удалось преобразовать в число
				КонецПопытки;
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Проверка наличия кода Simphony
		Если Не ЗначениеЗаполнено(КодSimphony) Тогда
			
			Результат.Сообщение = "Не удалось получить код Simphony для удаленной позиции меню";
			ЗаписатьВИсториюСинхронизации(Позиция, "DELETE", "Ошибка", 0, Результат.Сообщение, CorrelationId, 0);
			ОбработатьОшибкуВыгрузки(Позиция, КассовыйУзел, 0, Результат.Сообщение);
			Возврат Результат;
			
		КонецЕсли;
		
		// Получение hierStrucId
		HierStrucId = ПолучитьКодSimphonyУзла(КассовыйУзел);
		
		Если Не ЗначениеЗаполнено(HierStrucId) Тогда
			
			Результат.Сообщение = "Не удалось получить hierStrucId для кассового узла";
			ЗаписатьВИсториюСинхронизации(Позиция, "DELETE", "Ошибка", 0, Результат.Сообщение, CorrelationId, 0);
			ОбработатьОшибкуВыгрузки(Позиция, КассовыйУзел, 0, Результат.Сообщение);
			Возврат Результат;
			
		КонецЕсли;
		
		// Формирование заголовков
		Заголовки = СформироватьЗаголовки(ИмяПользователя(), CorrelationId);
		
		// Формирование endpoint с параметрами
		Endpoint = СтрШаблон("/menu-item/%1?hierStrucId=%2", ФОРМАТ(КодSimphony,"ЧГ=0"), HierStrucId);
		
		// Отправка DELETE запроса
		HTTPОтвет = ОтправитьЗапросAPI("DELETE", Endpoint, "", Заголовки);
		
		ДлительностьМс = ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера;
		
		// Обработка ответа
		Результат.HTTPСтатус = HTTPОтвет.КодСостояния;
		
		// Парсинг JSON ответа
		ОтветJSON = Неопределено;
		
		Попытка
			
			ОтветJSON = ПарсироватьОтветJSON(HTTPОтвет);
			
		Исключение
			
			// Если не удалось распарсить JSON
			ОтветJSON = Неопределено;
			
		КонецПопытки;
		
		Если HTTPОтвет.КодСостояния = 200 Тогда
			
			// Успешное удаление
			УдалитьИзОчереди(Позиция, КассовыйУзел);
			
			Результат.Успешно = Истина;
			Результат.Сообщение = "Успешно удалено из Simphony";
			
			ЗаписатьВИсториюСинхронизации(Позиция, "DELETE", Перечисления.MRS_СтатусыСинхронизацииSimphony.Выгружено, 200, "OK", CorrelationId, ДлительностьМс);
			
		ИначеЕсли HTTPОтвет.КодСостояния = 404 Тогда
			
			// Позиция не найдена в Simphony - считаем это успехом
			УдалитьИзОчереди(Позиция, КассовыйУзел);
			
			Результат.Успешно = Истина;
			Результат.Сообщение = "Позиция не найдена в Simphony (уже удалена)";
			
			ЗаписатьВИсториюСинхронизации(Позиция, "DELETE", Перечисления.MRS_СтатусыСинхронизацииSimphony.Выгружено, 404, "Not Found", CorrelationId, ДлительностьМс);
			
		ИначеЕсли HTTPОтвет.КодСостояния = 400 Тогда
			
			// Ошибка валидации
			Если ОтветJSON <> Неопределено И ОтветJSON.Свойство("code") И ОтветJSON.Свойство("message") Тогда
				
				Результат.Сообщение = СтрШаблон("Ошибка API (код %1): %2", ОтветJSON.code, ОтветJSON.message);
				
			Иначе
				
				Результат.Сообщение = СтрШаблон("HTTP 400: %1", HTTPОтвет.ПолучитьТелоКакСтроку());
				
			КонецЕсли;
			
			ОбработатьОшибкуВыгрузки(Позиция, КассовыйУзел, 400, Результат.Сообщение);
			ЗаписатьВИсториюСинхронизации(Позиция, "DELETE", Перечисления.MRS_СтатусыСинхронизацииSimphony.Ошибка, 400, Результат.Сообщение, CorrelationId, ДлительностьМс);
			
		ИначеЕсли HTTPОтвет.КодСостояния = 500 Тогда
			
			// Ошибка сервера - повторяем
			Если ОтветJSON <> Неопределено И ОтветJSON.Свойство("code") И ОтветJSON.Свойство("message") Тогда
				
				Результат.Сообщение = СтрШаблон("Ошибка сервера (код %1): %2", ОтветJSON.code, ОтветJSON.message);
				
			Иначе
				
				Результат.Сообщение = "Внутренняя ошибка сервера Simphony";
				
			КонецЕсли;
			
			ОбработатьОшибкуВыгрузки(Позиция, КассовыйУзел, 500, Результат.Сообщение);
			ЗаписатьВИсториюСинхронизации(Позиция, "DELETE", Перечисления.MRS_СтатусыСинхронизацииSimphony.Ошибка, 500, Результат.Сообщение, CorrelationId, ДлительностьМс);
			
		Иначе
			
			// Прочие ошибки
			Если ОтветJSON <> Неопределено И ОтветJSON.Свойство("code") И ОтветJSON.Свойство("message") Тогда
				
				Результат.Сообщение = СтрШаблон("HTTP %1 (код %2): %3", HTTPОтвет.КодСостояния, ОтветJSON.code, ОтветJSON.message);
				
			Иначе
				
				Результат.Сообщение = СтрШаблон("HTTP %1: %2", HTTPОтвет.КодСостояния, HTTPОтвет.ПолучитьТелоКакСтроку());
				
			КонецЕсли;
			
			ОбработатьОшибкуВыгрузки(Позиция, КассовыйУзел, HTTPОтвет.КодСостояния, Результат.Сообщение);
			ЗаписатьВИсториюСинхронизации(Позиция, "DELETE", "Ошибка", HTTPОтвет.КодСостояния, Результат.Сообщение, CorrelationId, ДлительностьМс);
			
		КонецЕсли;
		
	Исключение
		
		// Таймаут или другая ошибка
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат.Сообщение = "Connection timeout или ошибка соединения";
		
		ДлительностьМс = ТекущаяУниверсальнаяДатаВМиллисекундах() - НачалоЗамера;
		
		ОбработатьОшибкуВыгрузки(Позиция, КассовыйУзел, 0, ТекстОшибки);
		ЗаписатьВИсториюСинхронизации(Позиция, "DELETE", Перечисления.MRS_СтатусыСинхронизацииSimphony.Ошибка, 0, ТекстОшибки, CorrelationId, ДлительностьМс);
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Выгружает пакет позиций меню в Simphony
//
// Параметры:
//  МассивПозиций - Массив из СправочникСсылка.питМеню
//  КассовыйУзел - ПланОбменаСсылка.питУдаленныеКассы
//
// Возвращаемое значение:
//  Структура - результаты выгрузки
//
Функция ВыгрузитьПакетПозиций(МассивПозиций, КассовыйУзел) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Обработано", 0);
	Результат.Вставить("Успешно", 0);
	Результат.Вставить("Ошибок", 0);
	
	Для Каждого СтрокаПозиции Из МассивПозиций Цикл
		
		Результат.Обработано = Результат.Обработано + 1;
		
		// Определение типа операции
		Операция = "";
		
		Если СтрокаПозиции.Свойство("Операция") Тогда
			Операция = СтрокаПозиции.Операция;
		КонецЕсли;
		
		РезультатВыгрузки = Неопределено;
		
		Если Операция = "DELETE" Тогда
			
			// Удаление позиции
			РезультатВыгрузки = УдалитьПозициюИзSimphony(СтрокаПозиции.ПозицияМеню, КассовыйУзел);
			
		Иначе
			
			// Создание или обновление позиции
			РезультатВыгрузки = ВыгрузитьПозициюВSimphony(СтрокаПозиции.ПозицияМеню, КассовыйУзел);
			
		КонецЕсли;
		
		Если РезультатВыгрузки.Успешно Тогда
			Результат.Успешно = Результат.Успешно + 1;
		Иначе
			Результат.Ошибок = Результат.Ошибок + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Формирует JSON запрос для отправки позиции меню в Simphony
//
// Параметры:
//  Позиция - СправочникСсылка.питМеню
//  КассовыйУзел - ПланОбменаСсылка.питУдаленныеКассы
//
// Возвращаемое значение:
//  Строка - JSON для отправки
//
Функция СформироватьЗапросПозицииМеню(Позиция, КассовыйУзел) Экспорт
	
	// Получение данных позиции
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	питМеню.Наименование КАК Наименование,
		|	питМеню.Номенклатура КАК Номенклатура,
		|	питМеню.Выключен КАК Выключен,
		|	питМеню.Номенклатура.ГруппаАналитическогоУчета.Родитель.MRS_КодMajorGroup КАК MajorGroup,
		|	питМеню.Номенклатура.ГруппаАналитическогоУчета.MRS_КодFamilyGroup КАК FamilyGroup,
		|	питМеню.MRS_КодSimphony КАК КодSimphony,
		|	питМеню.Родитель.MRS_КодSimphony КАК SLUINDEX,
		|	питМеню.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	питМеню.MRS_СтавкаНДС КАК MRS_СтавкаНДС,
		|	питМеню.Номенклатура.питПродажаПоСвободнойЦене КАК НоменклатурапитПродажаПоСвободнойЦене
		|ИЗ
		|	Справочник.питМеню КАК питМеню
		|ГДЕ
		|	питМеню.Ссылка = &Позиция";
	
	Запрос.УстановитьПараметр("Позиция", Позиция);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		ВызватьИсключение "Позиция меню не найдена";
	КонецЕсли;
	
	// Получение цены
	Цена = ПолучитьЦенуНоменклатуры(Выборка.Номенклатура, КассовыйУзел);
	
	// Получение КодSimphony узла
	КодSimphonyУзла = ПолучитьКодSimphonyУзла(КассовыйУзел);

	StringNumberId = Выборка.КодSimphony;
	
	MenuItemClass = ПолучитьКлассМенюПозиции(Выборка);
	
	// Формирование структуры JSON
	ЗапросJSON = Новый Структура;
	ЗапросJSON.Вставить("name", Выборка.Наименование);
	ЗапросJSON.Вставить("stringNumberId", StringNumberId);
	ЗапросJSON.Вставить("majGrpObjNum", Выборка.MajorGroup);
	ЗапросJSON.Вставить("famGrpObjNum", Выборка.FamilyGroup);
	ЗапросJSON.Вставить("isVisible", ?(Выборка.Выключен, 0, 1)); // Инвертирование логики
	ЗапросJSON.Вставить("hierStrucId", КодSimphonyУзла);
	ЗапросJSON.Вставить("menuItemClass", MenuItemClass); // Характеристики позиции меню
	ЗапросJSON.Вставить("price", Цена);
	ЗапросJSON.Вставить("sluindex", Выборка.SLUINDEX);
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, ЗапросJSON);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

// Обновляет видимость позиции меню в Simphony (синхронизирует флаг Выключен с isVisible)
//
// Параметры:
//  Позиция - СправочникСсылка.питМеню - позиция меню для обновления
//  ЗначениеВыключен - Булево - новое значение флага Выключен
//
// Возвращаемое значение:
//  Структура:
//   * Успешно - Булево - признак успешного обновления
//   * Сообщение - Строка - сообщение о результате
//
Функция ОбновитьВидимостьПозиции(Позиция, ЗначениеВыключен) Экспорт
	
	Результат = Новый Структура("Успешно, Сообщение", Истина, "");
	
	// Получаем данные позиции
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	питМеню.Наименование КАК Наименование,
		|	питМеню.Номенклатура КАК Номенклатура,
		|	питМеню.Номенклатура.ГруппаАналитическогоУчета.Родитель.MRS_КодMajorGroup КАК MajorGroup,
		|	питМеню.Номенклатура.ГруппаАналитическогоУчета.MRS_КодFamilyGroup КАК FamilyGroup,
		|	питМеню.MRS_КодSimphony КАК КодSimphony,
		|	питМеню.Родитель.MRS_КодSimphony КАК SLUINDEX,
		|	питМеню.Владелец КАК ВидМеню
		|ИЗ
		|	Справочник.питМеню КАК питМеню
		|ГДЕ
		|	питМеню.Ссылка = &Позиция";
	
	Запрос.УстановитьПараметр("Позиция", Позиция);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если НЕ Выборка.Следующий() Тогда
		
		Результат.Успешно = Ложь;
		Результат.Сообщение = "Позиция меню не найдена";
		Возврат Результат;
		
	КонецЕсли;
	
	// Проверяем, что позиция имеет код Simphony
	Если НЕ ЗначениеЗаполнено(Выборка.КодSimphony) Тогда
		
		Результат.Успешно = Ложь;
		Результат.Сообщение = "Позиция меню не имеет кода Simphony";
		Возврат Результат;
		
	КонецЕсли;
	
	// Находим кассовый узел, связанный с видом меню
	ЗапросУзлов = Новый Запрос;
	ЗапросУзлов.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	питУдаленныеКассы.Ссылка КАК КассовыйУзел,
		|	питУдаленныеКассы.Префикс КАК Префикс
		|ИЗ
		|	ПланОбмена.питУдаленныеКассы КАК питУдаленныеКассы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.питУдаленныеКассы.ВидыМеню КАК питУдаленныеКассыВидыМеню
		|		ПО питУдаленныеКассы.Ссылка = питУдаленныеКассыВидыМеню.Ссылка
		|ГДЕ
		|	питУдаленныеКассыВидыМеню.ВидМеню = &ВидМеню";
	
	ЗапросУзлов.УстановитьПараметр("ВидМеню", Выборка.ВидМеню);
		
	ВыборкаУзлов = ЗапросУзлов.Выполнить().Выбрать();
	
	Если НЕ ВыборкаУзлов.Следующий() Тогда
		
		Результат.Успешно = Ложь;
		Результат.Сообщение = "Не найден кассовый узел для синхронизации";
		Возврат Результат;
		
	КонецЕсли;
	
	// Проверяем статус синхронизации
	ЗапросСтатуса = Новый Запрос;
	ЗапросСтатуса.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Объединенные.Статус КАК Статус
		|ИЗ
		|	(ВЫБРАТЬ
		|		Очередь.Статус КАК Статус,
		|		Очередь.ДатаПостановкиВОчередь КАК ДатаВремя
		|	ИЗ
		|		РегистрСведений.MRS_ОчередьВыгрузкиМенюSimphony КАК Очередь
		|	ГДЕ
		|		Очередь.ПозицияМеню = &Позиция
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		История.Статус,
		|		История.ДатаВремя
		|	ИЗ
		|		РегистрСведений.MRS_ИсторияСинхронизацииМенюSimphony КАК История
		|	ГДЕ
		|		История.ПозицияМеню = &Позиция) КАК Объединенные
		|
		|УПОРЯДОЧИТЬ ПО
		|	Объединенные.ДатаВремя УБЫВ";
	
	ЗапросСтатуса.УстановитьПараметр("Позиция", Позиция);
	
	РезультатСтатуса = ЗапросСтатуса.Выполнить();
	
	// Если есть запись в очереди и статус НЕ "Выгружено", не выполняем синхронизацию
	Если НЕ РезультатСтатуса.Пустой() Тогда
		
		ВыборкаСтатуса = РезультатСтатуса.Выбрать();
		ВыборкаСтатуса.Следующий();
		
		Если ВыборкаСтатуса.Статус <> Перечисления.MRS_СтатусыСинхронизацииSimphony.Выгружено Тогда
			
			Результат.Успешно = Ложь;
			Результат.Сообщение = "Позиция еще не выгружена в Simphony. Дождитесь завершения первичной синхронизации";
			Возврат Результат;
			
		КонецЕсли;
		
	Иначе
		
		// Если записи в очереди нет, значит позиция еще не выгружалась
		Результат.Успешно = Ложь;
		Результат.Сообщение = "Позиция еще не выгружалась в Simphony. Выполните первичную синхронизацию";
		Возврат Результат;
		
	КонецЕсли;
		
	// Получаем hierStrucId
	HierStrucId = Число(СокрЛП(ВыборкаУзлов.Префикс));
	
	Если НЕ ЗначениеЗаполнено(HierStrucId) Тогда
		
		Результат.Успешно = Ложь;
		Результат.Сообщение = СтрШаблон("Не заполнен префикс (hierStrucId) для кассового узла %1", ВыборкаУзлов.КассовыйУзел);
		Возврат Результат;
		
	КонецЕсли;
	
	Цена = ПолучитьЦенуНоменклатуры(Выборка.Номенклатура,ВыборкаУзлов.КассовыйУзел);
	
	// Формируем JSON для PATCH запроса
	ЗапросJSON = Новый Структура;
	ЗапросJSON.Вставить("hierStrucId", HierStrucId);
	
	МассивПозиций = Новый Массив;
	ПозицияJSON = Новый Структура;
	ПозицияJSON.Вставить("stringNumberId", Выборка.КодSimphony);
	ПозицияJSON.Вставить("majGrpObjNum", Выборка.MajorGroup);
	ПозицияJSON.Вставить("famGrpObjNum", Выборка.FamilyGroup);
	ПозицияJSON.Вставить("name", Выборка.Наименование); 
	ПозицияJSON.Вставить("price", Цена);
	ПозицияJSON.Вставить("isVisible", ?(ЗначениеВыключен, 0, 1)); // Инвертирование логики
	
	МассивПозиций.Добавить(ПозицияJSON);
	ЗапросJSON.Вставить("items", МассивПозиций);
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, ЗапросJSON);
	ТелоЗапроса = ЗаписьJSON.Закрыть();
	
	// Отправляем PATCH запрос
	Попытка
		
		CorrelationId = Строка(Новый УникальныйИдентификатор);
		Заголовки = СформироватьЗаголовки(ИмяПользователя(), CorrelationId);
		HTTPОтвет = ОтправитьЗапросAPI("PATCH", "/menu-items", ТелоЗапроса, Заголовки);
		
		Если HTTPОтвет.КодСостояния = 200 Тогда
			
			Результат.Успешно = Истина;
			Результат.Сообщение = "Видимость успешно обновлена в Simphony";
			
		Иначе
			
			// Парсим ответ для получения деталей ошибки
			ОтветJSON = Неопределено;
			
			Попытка
				ОтветJSON = ПарсироватьОтветJSON(HTTPОтвет);
			Исключение
				ОтветJSON = Неопределено;
			КонецПопытки;
			
			ТекстОшибкиAPI = "";
			
			Если ОтветJSON <> Неопределено И ОтветJSON.Свойство("message") Тогда
				ТекстОшибкиAPI = ОтветJSON.message;
			Иначе
				ТекстОшибкиAPI = HTTPОтвет.ПолучитьТелоКакСтроку();
			КонецЕсли;
			
			Результат.Успешно = Ложь;
			Результат.Сообщение = СтрШаблон("HTTP %1: %2", HTTPОтвет.КодСостояния, ТекстОшибкиAPI);
			
			// Записываем в журнал регистрации
			ЗаписьЖурналаРегистрации("MRS.Simphony.ОбновлениеВидимости", УровеньЖурналаРегистрации.Ошибка, , ,
				СтрШаблон("Ошибка обновления видимости позиции %1: %2", Позиция, Результат.Сообщение));
			
		КонецЕсли;
		
	Исключение
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат.Успешно = Ложь;
		Результат.Сообщение = СтрШаблон("Ошибка соединения: %1", ТекстОшибки);
		
		ЗаписьЖурналаРегистрации("MRS.Simphony.ОбновлениеВидимости", УровеньЖурналаРегистрации.Ошибка, , ,
			СтрШаблон("Ошибка обновления видимости позиции %1: %2", Позиция, Результат.Сообщение));
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Валидирует позицию меню перед выгрузкой
//
// Параметры:
//  Позиция - СправочникСсылка.питМеню
//  КассовыйУзел - ПланОбменаСсылка.питУдаленныеКассы
//
// Возвращаемое значение:
//  Структура:
//   * Валидна - Булево
//   * Ошибки - Массив из Строка
//
Функция ВалидироватьПозициюМеню(Позиция, КассовыйУзел) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Валидна", Истина);
	Результат.Вставить("Ошибки", Новый Массив);
	
	// Получение данных для валидации
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	питМеню.Наименование КАК Наименование,
		|	питМеню.Номенклатура КАК Номенклатура,
		|	питМеню.Номенклатура.ГруппаАналитическогоУчета.Родитель.MRS_КодMajorGroup КАК MajorGroup,
		|	питМеню.Номенклатура.ГруппаАналитическогоУчета.MRS_КодFamilyGroup КАК FamilyGroup
		|ИЗ
		|	Справочник.питМеню КАК питМеню
		|ГДЕ
		|	питМеню.Ссылка = &Позиция";
	
	Запрос.УстановитьПараметр("Позиция", Позиция);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Результат.Ошибки.Добавить("Позиция меню не найдена");
		Результат.Валидна = Ложь;
		Возврат Результат;
	КонецЕсли;
	
	// Проверка номенклатуры
	Если Не ЗначениеЗаполнено(Выборка.Номенклатура) Тогда
		Результат.Ошибки.Добавить("Не заполнена номенклатура");
	КонецЕсли;
	
	// Проверка MajorGroup
	Если Не ЗначениеЗаполнено(Выборка.MajorGroup) Тогда
		Результат.Ошибки.Добавить("Не заполнен код MajorGroup в классификаторе ГАУ");
	Иначе
		
		//РезультатВалидацииMajor = MRS_ИнтеграцияSimphonyКлиентСервер.ВалидироватьДиапазонMajorGroup(Выборка.MajorGroup);
		//
		//Если Не РезультатВалидацииMajor.Валидно Тогда
		//	Результат.Ошибки.Добавить(РезультатВалидацииMajor.Сообщение);
		//КонецЕсли;
		
	КонецЕсли;
	
	// Проверка FamilyGroup
	Если Не ЗначениеЗаполнено(Выборка.FamilyGroup) Тогда
		Результат.Ошибки.Добавить("Не заполнен код FamilyGroup в классификаторе ГАУ");
	Иначе
		
		//РезультатВалидацииFamily = MRS_ИнтеграцияSimphonyКлиентСервер.ВалидироватьFamilyGroup(Выборка.FamilyGroup);
		//
		//Если Не РезультатВалидацииFamily.Валидно Тогда
		//	Результат.Ошибки.Добавить(РезультатВалидацииFamily.Сообщение);
		//КонецЕсли;
		
	КонецЕсли;
	
	// Проверка наличия записи о цене (сама цена может быть нулевой)
	ЕстьЗаписьОЦене = ЕстьЗаписьОЦенеНоменклатуры(Выборка.Номенклатура, КассовыйУзел);
	
	Если Не ЕстьЗаписьОЦене Тогда
		Результат.Ошибки.Добавить(СтрШаблон("Не найдена запись о цене для номенклатуры ""%1""", Выборка.Наименование));
	КонецЕсли;
	
	// Проверка кассового узла
	Если Не ЗначениеЗаполнено(КассовыйУзел) Тогда
		Результат.Ошибки.Добавить("Не указан кассовый узел");
	КонецЕсли;
	
	Если Результат.Ошибки.Количество() > 0 Тогда
		Результат.Валидна = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область РаботаСОчередью

// Постановка позиций меню в очередь выгрузки в Simphony на сервере при изменении цен.
//
// Параметры:
//  Объект - ДокументОбъект.УстановкаЦенНоменклатуры - Документ установки цен номенклатуры
//
// Возвращаемое значение:
//  Структура - Результат постановки в очередь:
//    * Успешно - Булево - Признак успешного выполнения операции
//    * Сообщение - Строка - Сообщение об ошибке (если Успешно = Ложь)
//    * КоличествоПозиций - Число - Количество позиций, добавленных в очередь выгрузки
//
Функция ПоставитьВОчередьВыгрузкиНаСервереПриИзмененииЦен(Объект) Экспорт
	
	Результат = Новый Структура("Успешно, Сообщение, КоличествоПозиций", Ложь, "", 0);
	
	// Валидация: проверка наличия видов цен
	Если Объект.ВидыЦен.Количество() = 0 Тогда
		
		Результат.Сообщение = "Не указан вид цены в документе";
		Возврат Результат;
		
	КонецЕсли;
	
	// Получение вида цены (используется первый из списка)
	ВидЦены = Объект.ВидыЦен[0].ВидЦены;
	
	// Валидация: поиск кассового узла по виду цены
	ДанныеВидаЦены = ПолучитьДанныеПоВидуЦен(ВидЦены);
	
	Если ДанныеВидаЦены.КассовыйУзел = Неопределено Тогда
		
		Результат.Сообщение = "Не найден кассовый узел для вида цены """ + Строка(ВидЦены) + """";
		Возврат Результат;
		
	КонецЕсли;
	
	// Постановка всех позиций в очередь выгрузки
	КоличествоДобавлено = 0;
	
	Для Каждого СтрокаПозиции Из Объект.Товары Цикл
		ПозицияМеню = Справочники.питМеню.НайтиПоРеквизиту("Номенклатура", СтрокаПозиции.Номенклатура, ,ДанныеВидаЦены.ВидМеню);
		MRS_ИнтеграцияSimphonyСервер.ДобавитьВОчередьВыгрузки(ПозицияМеню, "CREATE", ДанныеВидаЦены.КассовыйУзел);
		КоличествоДобавлено = КоличествоДобавлено + 1;
		
	КонецЦикла;
	
	Результат.Успешно = Истина;
	Результат.КоличествоПозиций = КоличествоДобавлено;
	
	Возврат Результат;

КонецФункции

// Добавляет позицию в очередь выгрузки
//
// Параметры:
//  Позиция - СправочникСсылка.питМеню
//  Операция - Строка - CREATE, UPDATE, DELETE
//  КассовыйУзел - ПланОбменаСсылка.питУдаленныеКассы
//
Процедура ДобавитьВОчередьВыгрузки(Позиция, Операция, КассовыйУзел) Экспорт
	
	ДобавитьВОчередьВыгрузкиСПараметрами(Позиция, Операция, КассовыйУзел, 0);
	
КонецПроцедуры

// Добавляет позицию в очередь выгрузки с дополнительными параметрами
//
// Параметры:
//  Позиция - СправочникСсылка.питМеню
//  Операция - Строка - CREATE, UPDATE, DELETE
//  КассовыйУзел - ПланОбменаСсылка.питУдаленныеКассы
//  StringNumberId - Число - код Simphony (для операции DELETE, когда позиция уже удалена)
//
Процедура ДобавитьВОчередьВыгрузкиСПараметрами(Позиция, Операция, КассовыйУзел, StringNumberId = 0) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.MRS_ОчередьВыгрузкиМенюSimphony.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПозицияМеню.Установить(Позиция);   
	НаборЗаписей.Отбор.КассовыйУзел.Установить(КассовыйУзел);
	
	Запись = НаборЗаписей.Добавить();
	Запись.ПозицияМеню = Позиция;
	Запись.КассовыйУзел = КассовыйУзел;
	Запись.Операция = Операция;
	Запись.Статус = Перечисления.MRS_СтатусыСинхронизацииSimphony.ВОчереди;
	Запись.ДатаПостановкиВОчередь = ТекущаяДатаСеанса();
	Запись.КоличествоПопыток = 0;
	Запись.СледующаяПопытка = Дата(1, 1, 1);
	
	// Для операции DELETE сохраняем stringNumberId в ТекстОшибки (временно)
	Если Операция = "DELETE" И ЗначениеЗаполнено(StringNumberId) Тогда
		Запись.ТекстОшибки = СтрШаблон("stringNumberId:%1", StringNumberId);
	Иначе
		Запись.ТекстОшибки = "";
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Обрабатывает ошибку выгрузки, увеличивает счетчик попыток
//
// Параметры:
//  Позиция - СправочникСсылка.питМеню
//  КассовыйУзел - ПланОбменаСсылка.питУдаленныеКассы
//  КодОшибки - Число
//  Сообщение - Строка
//
Процедура ОбработатьОшибкуВыгрузки(Позиция, КассовыйУзел, КодОшибки, Сообщение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Получаем текущую запись очереди
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Очередь.КоличествоПопыток КАК КоличествоПопыток
		|ИЗ
		|	РегистрСведений.MRS_ОчередьВыгрузкиМенюSimphony КАК Очередь
		|ГДЕ
		|	Очередь.ПозицияМеню = &Позиция
		|	И Очередь.КассовыйУзел = &КассовыйУзел";
	
	Запрос.УстановитьПараметр("Позиция", Позиция);
	Запрос.УстановитьПараметр("КассовыйУзел", КассовыйУзел);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	КоличествоПопыток = 0;
	
	Если Выборка.Следующий() Тогда
		КоличествоПопыток = Выборка.КоличествоПопыток;
	КонецЕсли;
	
	КоличествоПопыток = КоличествоПопыток + 1;
	
	// Обновляем запись
	НаборЗаписей = РегистрыСведений.MRS_ОчередьВыгрузкиМенюSimphony.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПозицияМеню.Установить(Позиция);
	НаборЗаписей.Отбор.КассовыйУзел.Установить(КассовыйУзел);
	
	Запись = НаборЗаписей.Добавить();
	Запись.ПозицияМеню = Позиция;
	Запись.КассовыйУзел = КассовыйУзел;
	Запись.КоличествоПопыток = КоличествоПопыток;
	Запись.ТекстОшибки = Лев(Сообщение, 500);
	
	Если КоличествоПопыток < 3 Тогда
		
		Запись.Статус = Перечисления.MRS_СтатусыСинхронизацииSimphony.Ошибка;
		Запись.СледующаяПопытка = ТекущаяДатаСеанса() + 300; // +5 минут
			
	Иначе
		
		Запись.Статус = Перечисления.MRS_СтатусыСинхронизацииSimphony.ТребуетВнимания;
		Запись.СледующаяПопытка = Дата(1, 1, 1);
		
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Удаляет позицию из очереди
//
// Параметры:
//  Позиция - СправочникСсылка.питМеню
//  КассовыйУзел - ПланОбменаСсылка.питУдаленныеКассы
//
Процедура УдалитьИзОчереди(Позиция, КассовыйУзел) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.MRS_ОчередьВыгрузкиМенюSimphony.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПозицияМеню.Установить(Позиция);
	НаборЗаписей.Отбор.КассовыйУзел.Установить(КассовыйУзел);
	НаборЗаписей.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСAPI

// Отправляет HTTP запрос к API Simphony
//
// Параметры:
//  Метод - Строка - GET, POST, PUT, DELETE
//  Endpoint - Строка - /menu-item, /menu-items и т.д.
//  ТелоJSON - Строка - JSON тело запроса
//  Заголовки - Соответствие - HTTP заголовки
//
// Возвращаемое значение:
//  HTTPОтвет
//
Функция ОтправитьЗапросAPI(Метод, Endpoint, ТелоJSON, Заголовки)
	
	URLСервиса = Константы.MRS_URLСервисаSimphony.Получить();
	
	Если ПустаяСтрока(URLСервиса) Тогда
		ВызватьИсключение "Не установлен URL сервиса Simphony в константах";
	КонецЕсли;
	
	// Парсинг URL
	СтруктураURL = MRS_ИнтеграцияSimphonyКлиентСервер.РазобратьURL(URLСервиса);
	
	// Создание HTTP соединения
	ЗащищенноеСоединение = Неопределено;
	
	Если СтруктураURL.Протокол = "https" Тогда
		ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL(Неопределено, Неопределено);
	КонецЕсли;
	
	Соединение = Новый HTTPСоединение(
		СтруктураURL.Сервер,
		СтруктураURL.Порт,
		,
		,
		,
		30, // Таймаут 30 секунд
		ЗащищенноеСоединение
	);
	
	// Формирование запроса
	ПутьЗапроса = СтруктураURL.Путь + Endpoint;
	
	HTTPЗапрос = Новый HTTPЗапрос(ПутьЗапроса);
	HTTPЗапрос.Заголовки = Заголовки;
	
	Если ЗначениеЗаполнено(ТелоJSON) Тогда
		HTTPЗапрос.УстановитьТелоИзСтроки(ТелоJSON, КодировкаТекста.UTF8);
	КонецЕсли;
	
	// Отправка запроса
	Если Метод = "GET" Тогда
		HTTPОтвет = Соединение.Получить(HTTPЗапрос);
	ИначеЕсли Метод = "POST" Тогда
		HTTPОтвет = Соединение.ОтправитьДляОбработки(HTTPЗапрос);
	ИначеЕсли Метод = "PUT" Тогда
		HTTPОтвет = Соединение.ВызватьHTTPМетод("PUT", HTTPЗапрос);
	ИначеЕсли Метод = "PATCH" Тогда
		HTTPОтвет = Соединение.ВызватьHTTPМетод("PATCH", HTTPЗапрос);
	ИначеЕсли Метод = "DELETE" Тогда
		HTTPОтвет = Соединение.Удалить(HTTPЗапрос);
	Иначе
		ВызватьИсключение СтрШаблон("Неподдерживаемый HTTP метод: %1", Метод);
	КонецЕсли;
	
	Возврат HTTPОтвет;
	
КонецФункции

// Парсит JSON ответ от API
//
// Параметры:
//  HTTPОтвет - HTTPОтвет
//
// Возвращаемое значение:
//  Структура - распарсенный JSON
//
Функция ПарсироватьОтветJSON(HTTPОтвет)
	
	ТелоОтвета = HTTPОтвет.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
	
	Если ПустаяСтрока(ТелоОтвета) Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ТелоОтвета);
	
	Результат = ПрочитатьJSON(ЧтениеJSON);
	ЧтениеJSON.Закрыть();
	
	Возврат Результат;
	
КонецФункции

// Формирует HTTP заголовки для запроса к API
//
// Параметры:
//  ИмяПользователя - Строка
//  CorrelationId - Строка - UUID запроса (опционально)
//
// Возвращаемое значение:
//  Соответствие - заголовки
//
Функция СформироватьЗаголовки(ИмяПользователя, CorrelationId = "")
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("X-User-Id", "1C-ERP");
	
	Если ПустаяСтрока(CorrelationId) Тогда
		CorrelationId = Строка(Новый УникальныйИдентификатор);
	КонецЕсли;
	
	Заголовки.Вставить("X-Correlation-Id", CorrelationId);
	Заголовки.Вставить("X-Request-Id", Строка(Новый УникальныйИдентификатор));
	
	Возврат Заголовки;
	
КонецФункции

#КонецОбласти

#Область СинхронизацияСправочников

// Синхронизирует иерархию RVC (кассовые узлы)
//
Процедура СинхронизироватьRVCИерархию()
	
	Попытка
		
		Заголовки = СформироватьЗаголовки(ИмяПользователя());
		HTTPОтвет = ОтправитьЗапросAPI("GET", "/referencedata/rvc-hierarchy", "", Заголовки);
		
		Если HTTPОтвет.КодСостояния = 200 Тогда
			
			ОтветJSON = ПарсироватьОтветJSON(HTTPОтвет);
			
			Если ОтветJSON.Свойство("data") И ТипЗнч(ОтветJSON.data) = Тип("Массив") Тогда
				
				Для Каждого ЭлементRVC Из ОтветJSON.data Цикл
					
					// TODO: Обновление справочника питУдаленныеКассы
					// Сопоставление RVC из Simphony с узлами по реквизиту Префикс
					
				КонецЦикла;
				
			КонецЕсли;
			
		Иначе
			
			ЗаписьЖурналаРегистрации("MRS.Simphony.СинхронизацияRVC", УровеньЖурналаРегистрации.Ошибка,,,
				СтрШаблон("Ошибка получения RVC: HTTP %1", HTTPОтвет.КодСостояния));
			
		КонецЕсли;
		
	Исключение
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("MRS.Simphony.СинхронизацияRVC", УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

// Синхронизирует справочник Major Groups
//
Процедура СинхронизироватьMajorGroups()
	
	Попытка
		
		Заголовки = СформироватьЗаголовки(ИмяПользователя());
		HTTPОтвет = ОтправитьЗапросAPI("GET", "/referencedata/major-groups", "", Заголовки);
		
		Если HTTPОтвет.КодСостояния = 200 Тогда
			
			ОтветJSON = ПарсироватьОтветJSON(HTTPОтвет);
						
			ЗаписьЖурналаРегистрации("MRS.Simphony.СинхронизацияMajorGroups", УровеньЖурналаРегистрации.Информация,,,
				"Синхронизация Major Groups выполнена");
			
		Иначе
			
			ЗаписьЖурналаРегистрации("MRS.Simphony.СинхронизацияMajorGroups", УровеньЖурналаРегистрации.Ошибка,,,
				СтрШаблон("Ошибка получения Major Groups: HTTP %1", HTTPОтвет.КодСостояния));
			
		КонецЕсли;
		
	Исключение
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("MRS.Simphony.СинхронизацияMajorGroups", УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

// Синхронизирует справочник Family Groups
//
Процедура СинхронизироватьFamilyGroups()
	
	Попытка
		
		Заголовки = СформироватьЗаголовки(ИмяПользователя());
		HTTPОтвет = ОтправитьЗапросAPI("GET", "/referencedata/family-groups", "", Заголовки);
		
		Если HTTPОтвет.КодСостояния = 200 Тогда
			
			ОтветJSON = ПарсироватьОтветJSON(HTTPОтвет);
			                                              			
			ЗаписьЖурналаРегистрации("MRS.Simphony.СинхронизацияFamilyGroups", УровеньЖурналаРегистрации.Информация,,,
				"Синхронизация Family Groups выполнена");
			
		Иначе
			
			ЗаписьЖурналаРегистрации("MRS.Simphony.СинхронизацияFamilyGroups", УровеньЖурналаРегистрации.Ошибка,,,
				СтрШаблон("Ошибка получения Family Groups: HTTP %1", HTTPОтвет.КодСостояния));
			
		КонецЕсли;
		
	Исключение
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("MRS.Simphony.СинхронизацияFamilyGroups", УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеФункции

// Проверяет наличие записи о цене номенклатуры для кассового узла
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура
//  КассовыйУзел - ПланОбменаСсылка.питУдаленныеКассы
//
// Возвращаемое значение:
//  Булево - Истина, если запись о цене существует (даже с нулевой ценой)
//
Функция ЕстьЗаписьОЦенеНоменклатуры(Номенклатура, КассовыйУзел)
	
	// Получаем вид цен из кассового узла
	ВидЦен = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	питУдаленныеКассы.ВидЦен КАК ВидЦен
		|ИЗ
		|	ПланОбмена.питУдаленныеКассы КАК питУдаленныеКассы
		|ГДЕ
		|	питУдаленныеКассы.Ссылка = &КассовыйУзел";
	
	Запрос.УстановитьПараметр("КассовыйУзел", КассовыйУзел);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ВидЦен = Выборка.ВидЦен;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидЦен) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Проверяем наличие записи о цене
	ЗапросЦены = Новый Запрос;
	ЗапросЦены.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	1 КАК ОК
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|ГДЕ
		|	ЦеныНоменклатуры.Номенклатура = &Номенклатура
		|	И ЦеныНоменклатуры.ВидЦены = &ВидЦены
		|	И ЦеныНоменклатуры.Период <= &Период";
	
	ЗапросЦены.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	ЗапросЦены.УстановитьПараметр("Номенклатура", Номенклатура);
	ЗапросЦены.УстановитьПараметр("ВидЦены", ВидЦен);
	
	Результат = ЗапросЦены.Выполнить();
	
	Возврат Не Результат.Пустой();
	
КонецФункции

// Получает цену номенклатуры для кассового узла
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура
//  КассовыйУзел - ПланОбменаСсылка.питУдаленныеКассы
//
// Возвращаемое значение:
//  Число - цена (может быть 0, если цена не установлена или нулевая)
//
Функция ПолучитьЦенуНоменклатуры(Номенклатура, КассовыйУзел)
	
	// Получаем вид цен из кассового узла
	ВидЦен = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	питУдаленныеКассы.ВидЦен КАК ВидЦен
		|ИЗ
		|	ПланОбмена.питУдаленныеКассы КАК питУдаленныеКассы
		|ГДЕ
		|	питУдаленныеКассы.Ссылка = &КассовыйУзел";
	
	Запрос.УстановитьПараметр("КассовыйУзел", КассовыйУзел);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ВидЦен = Выборка.ВидЦен;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидЦен) Тогда
		Возврат 0;
	КонецЕсли;
	
	// Получаем цену
	ЗапросЦены = Новый Запрос;
	ЗапросЦены.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период, Номенклатура = &Номенклатура И ВидЦены = &ВидЦены) КАК ЦеныНоменклатурыСрезПоследних";
	
	ЗапросЦены.УстановитьПараметр("Период", ТекущаяДатаСеанса());
	ЗапросЦены.УстановитьПараметр("Номенклатура", Номенклатура);
	ЗапросЦены.УстановитьПараметр("ВидЦены", ВидЦен);
	
	ВыборкаЦены = ЗапросЦены.Выполнить().Выбрать();
	
	Если ВыборкаЦены.Следующий() Тогда
		Возврат ВыборкаЦены.Цена;
	КонецЕсли;
	
	Возврат 0;
	
КонецФункции

// Получает hierStrucId для API Simphony
//
// Параметры:
//  КассовыйУзел - ПланОбменаСсылка.питУдаленныеКассы
//
// Возвращаемое значение:
//  Строка - hierStrucId из реквизита Префикс
//
Функция ПолучитьКодSimphonyУзла(КассовыйУзел) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	питУдаленныеКассы.Префикс КАК Префикс
		|ИЗ
		|	ПланОбмена.питУдаленныеКассы КАК питУдаленныеКассы
		|ГДЕ
		|	питУдаленныеКассы.Ссылка = &КассовыйУзел";
	
	Запрос.УстановитьПараметр("КассовыйУзел", КассовыйУзел);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Число(СокрЛП(Выборка.Префикс));
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

// Получает MenuItemsClass для API Simphony
//
// Параметры:
//  Выборка - РезультатЗапроса к позиции меню
//
// Возвращаемое значение:
//  Число - MenuItemClass для simphony
//
Функция ПолучитьКлассМенюПозиции(Выборка) Экспорт
	
	// проверить 
	ЭтоМернаяЕдиница = Справочники.УпаковкиЕдиницыИзмерения.ЭтоМернаяЕдиница(Выборка.ЕдиницаИзмерения);
	СтавкаНДС = Выборка.MRS_СтавкаНДС;
	
	Выборка.НоменклатурапитПродажаПоСвободнойЦене
КонецФункции

// Записывает событие в историю синхронизации
//
// Параметры:
//  Позиция - СправочникСсылка.питМеню
//  Операция - Строка
//  Статус - Строка
//  HTTPСтатус - Число
//  Сообщение - Строка
//  CorrelationId - Строка
//  ДлительностьМс - Число
//
Процедура ЗаписатьВИсториюСинхронизации(Позиция, Операция, Статус, HTTPСтатус, Сообщение, CorrelationId, ДлительностьМс)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	НаборЗаписей = РегистрыСведений.MRS_ИсторияСинхронизацииМенюSimphony.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПозицияМеню.Установить(Позиция);
	НаборЗаписей.Отбор.ДатаВремя.Установить(ТекущаяДата);
	НаборЗаписей.Отбор.УникальныйИдентификатор.Установить(CorrelationId);
	
	Запись = НаборЗаписей.Добавить();
	Запись.ПозицияМеню = Позиция;
	Запись.ДатаВремя = ТекущаяДата;
	Запись.УникальныйИдентификатор = CorrelationId;
	Запись.Операция = Операция;
	Запись.Статус = Статус;
	Запись.HTTPСтатус = HTTPСтатус;
	Запись.Сообщение = Лев(Сообщение, 1000);
	Запись.Пользователь = Пользователи.ТекущийПользователь();
	Запись.ДлительностьМс = ДлительностьМс;
	
	НаборЗаписей.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Получаем реквизиты узла
//
// Параметры:
//  Позиция - СправочникСсылка.питВидМеню
// Возвращаемое значение:
//  Структура - СтруктураВозврата
Функция ПолучитьРеквизитыВидаМеню(ВидМеню) Экспорт 
	
	СтруктураВозврата = Новый Структура;
	
	Запрос = Новый Запрос;

	Запрос.Текст =
	"ВЫБРАТЬ
	|	питУдаленныеКассы.Ссылка КАК КассовыйУзел,
	|	питУдаленныеКассы.Префикс КАК Префикс,
	|	питУдаленныеКассы.Организация КАК Организация,
	|	питУдаленныеКассы.Подразделение КАК Подразделение,
	|	питУдаленныеКассы.MRS_СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	ПланОбмена.питУдаленныеКассы КАК питУдаленныеКассы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.питУдаленныеКассы.ВидыМеню КАК питУдаленныеКассыВидыМеню
	|		ПО питУдаленныеКассы.Ссылка = питУдаленныеКассыВидыМеню.Ссылка
	|ГДЕ
	|	питУдаленныеКассыВидыМеню.ВидМеню = &ВидМеню";

	Запрос.УстановитьПараметр("ВидМеню", ВидМеню);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтруктураВозврата.Вставить("Организация", Выборка.Организация);
		СтруктураВозврата.Вставить("Подразделение", Выборка.Подразделение);
		СтруктураВозврата.Вставить("СтавкаНДС", Выборка.СтавкаНДС);
	КонецЦикла;	
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция ПолучитьДанныеПоВидуЦен(ВидЦены) Экспорт
	
	СтруктураВозврата = Новый Структура;
	
	Запрос = Новый Запрос;

	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	питУдаленныеКассыВидыМеню.Ссылка КАК КассовыйУзел,
	|	питУдаленныеКассыВидыМеню.ВидМеню КАК ВидМеню
	|ИЗ
	|	ПланОбмена.питУдаленныеКассы.ВидыМеню КАК питУдаленныеКассыВидыМеню
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланОбмена.питУдаленныеКассы КАК питУдаленныеКассы
	|		ПО питУдаленныеКассыВидыМеню.Ссылка = питУдаленныеКассы.Ссылка
	|ГДЕ
	|	питУдаленныеКассыВидыМеню.Ссылка.ВидЦен = &ВидЦен";

	Запрос.УстановитьПараметр("ВидЦен", ВидЦены);
	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтруктураВозврата.Вставить("КассовыйУзел",Выборка.КассовыйУзел);
		СтруктураВозврата.Вставить("ВидМеню",Выборка.ВидМеню);
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции
#КонецОбласти

#КонецОбласти

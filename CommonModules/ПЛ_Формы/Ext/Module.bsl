//<< Портал-Юг, Свинцов И.И., 2019.03.19 [5.2.1 Автоматическое создание документа «перемещение товаров» на основании документа поступления]  
Процедура докПриобретениеТоваровУслугПослеЗаписиНаСервере(Форма, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ ТекущийОбъект.Проведен Тогда
		Возврат;
	КонецЕсли;	
	
	// Проверим, запускался ли механизм автоматического создания перемещений ранее
	ДопСвойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ПЛ_ПеремещенияСформированыАвтоматически"); 
	Если НЕ ЗначениеЗаполнено(ДопСвойство) Тогда
		ТекстСообщения = НСтр("ru='Для документа не определено дополнительное сведение ""Перемещения сформированы автоматически"". Обработка формирования перемещений запущена не будет.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ТекущийОбъект.Ссылка); 
		Возврат;
	Иначе
		Если УправлениеСвойствами.ЗначениеСвойства(ТекущийОбъект.Ссылка, ДопСвойство) = Истина Тогда 	
			Возврат;
		КонецЕсли;	
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПриобретениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
	|	ПриобретениеТоваровУслугТовары.Количество КАК Количество,
	|	ПриобретениеТоваровУслугТовары.КодСтроки КАК КодСтроки,
	|	ЗаказПоставщикуТовары.Номенклатура КАК НоменклатураЗаказа,
	|	ВЫБОР
	|		КОГДА ПриобретениеТоваровУслугТовары.Номенклатура = ЗаказПоставщикуТовары.Номенклатура
	|			ТОГДА 1
	|		ИНАЧЕ ПриобретениеТоваровУслугТовары.Номенклатура.ОбъемДАЛ * 10
	|	КОНЕЦ КАК КоэфПересчета,
	|	ВЫБОР
	|		КОГДА ЗаказПоставщикуТовары.ПЛ_Заказ ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА ТИПЗНАЧЕНИЯ(ЗаказПоставщикуТовары.Ссылка.ДокументОснование) = ТИП(Документ.ЗаказПоставщику)
	|						ТОГДА ЗаказПоставщикуТовары.Ссылка.ДокументОснование
	|					ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказНаПеремещение.ПустаяСсылка)
	|				КОНЕЦ
	|		ИНАЧЕ ЗаказПоставщикуТовары.ПЛ_Заказ
	|	КОНЕЦ КАК ЗаказНаПеремещение,
	|	ПриобретениеТоваровУслугТовары.Характеристика КАК Характеристика,
	|	ПриобретениеТоваровУслугТовары.Назначение КАК Назначение
	|ПОМЕСТИТЬ ВТ_ПриобретениеТиУ
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	|		ПО ПриобретениеТоваровУслугТовары.ЗаказПоставщику = ЗаказПоставщикуТовары.Ссылка
	|			И ПриобретениеТоваровУслугТовары.Номенклатура = ЗаказПоставщикуТовары.Номенклатура
	|			И ПриобретениеТоваровУслугТовары.Характеристика = ЗаказПоставщикуТовары.Характеристика
	|			И ПриобретениеТоваровУслугТовары.Назначение = ЗаказПоставщикуТовары.Назначение
	|ГДЕ
	|	ПриобретениеТоваровУслугТовары.Ссылка = &Ссылка
	|	И ПриобретениеТоваровУслугТовары.КодСтроки <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_ПриобретениеТиУ.Количество * ВТ_ПриобретениеТиУ.КоэфПересчета) КАК Количество,
	|	ВТ_ПриобретениеТиУ.НоменклатураЗаказа КАК НоменклатураЗаказа,
	|	ВТ_ПриобретениеТиУ.Назначение КАК Назначение,
	|	ВТ_ПриобретениеТиУ.Характеристика КАК Характеристика,
	|	ВТ_ПриобретениеТиУ.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ВТ_ПриобретениеТиУ.КоэфПересчета) КАК КоэфПересчета,
	|	ВТ_ПриобретениеТиУ.ЗаказНаПеремещение КАК ЗаказНаПеремещение
	|ПОМЕСТИТЬ ВТ_ТоварыКОтгрузке
	|ИЗ
	|	ВТ_ПриобретениеТиУ КАК ВТ_ПриобретениеТиУ
	|ГДЕ
	|	ВТ_ПриобретениеТиУ.ЗаказНаПеремещение <> ЗНАЧЕНИЕ(Документ.ЗаказНаПеремещение.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПриобретениеТиУ.Назначение,
	|	ВТ_ПриобретениеТиУ.Характеристика,
	|	ВТ_ПриобретениеТиУ.Номенклатура,
	|	ВТ_ПриобретениеТиУ.НоменклатураЗаказа,
	|	ВТ_ПриобретениеТиУ.ЗаказНаПеремещение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказНаПеремещениеТовары.Номенклатура КАК Номенклатура,
	|	ЗаказНаПеремещениеТовары.Характеристика КАК Характеристика,
	|	ЗаказНаПеремещениеТовары.КодСтроки КАК КодСтроки,
	|	ЗаказНаПеремещениеТовары.Ссылка КАК ЗаказНаПеремещение,
	|	ЗаказНаПеремещениеТовары.Количество КАК КоличествоВЗаказе,
	|	ЗаказНаПеремещениеТовары.НомерСтроки КАК НомерСтроки,
	|	ЗаказНаПеремещениеТовары.Ссылка.СкладПолучатель КАК СкладПолучатель,
	|	ЗаказНаПеремещениеТовары.Назначение КАК Назначение
	|ПОМЕСТИТЬ ВТ_ЗаказыНаПеремещение
	|ИЗ
	|	Документ.ЗаказНаПеремещение.Товары КАК ЗаказНаПеремещениеТовары
	|ГДЕ
	|	ЗаказНаПеремещениеТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТ_ПриобретениеТиУ.ЗаказНаПеремещение КАК ЗаказНаПеремещение
	|			ИЗ
	|				ВТ_ПриобретениеТиУ КАК ВТ_ПриобретениеТиУ)
	|	И ЗаказНаПеремещениеТовары.Номенклатура В
	|			(ВЫБРАТЬ
	|				ВТ_ТоварыКОтгрузке.НоменклатураЗаказа КАК НоменклатураЗаказа
	|			ИЗ
	|				ВТ_ТоварыКОтгрузке КАК ВТ_ТоварыКОтгрузке)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗаказыНаПеремещение.Номенклатура КАК НоменклатураЗаказа,
	|	ВТ_ЗаказыНаПеремещение.ЗаказНаПеремещение КАК ЗаказНаПеремещение,
	|	ВТ_ЗаказыНаПеремещение.Характеристика КАК Характеристика,
	|	ВТ_ЗаказыНаПеремещение.КоличествоВЗаказе КАК Количество,
	|	ВТ_ТоварыКОтгрузке.Номенклатура КАК Номенклатура,
	|	ВТ_ТоварыКОтгрузке.КоэфПересчета КАК КоэфПересчета,
	|	ВТ_ТоварыКОтгрузке.Назначение КАК Назначение
	|ИЗ
	|	ВТ_ЗаказыНаПеремещение КАК ВТ_ЗаказыНаПеремещение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТоварыКОтгрузке КАК ВТ_ТоварыКОтгрузке
	|		ПО ВТ_ЗаказыНаПеремещение.ЗаказНаПеремещение = ВТ_ТоварыКОтгрузке.ЗаказНаПеремещение
	|			И ВТ_ЗаказыНаПеремещение.Номенклатура = ВТ_ТоварыКОтгрузке.НоменклатураЗаказа
	|			И ВТ_ЗаказыНаПеремещение.Характеристика = ВТ_ТоварыКОтгрузке.Характеристика
	|
	|УПОРЯДОЧИТЬ ПО
	|	НоменклатураЗаказа,
	|	Характеристика,
	|	Назначение,
	|	ЗаказНаПеремещение
	|ИТОГИ ПО
	|	НоменклатураЗаказа,
	|	Характеристика,
	|	Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ЗаказыНаПеремещение.Номенклатура КАК Номенклатура,
	|	ВТ_ЗаказыНаПеремещение.Характеристика КАК Характеристика,
	|	ВТ_ЗаказыНаПеремещение.КодСтроки КАК КодСтроки,
	|	ВТ_ЗаказыНаПеремещение.ЗаказНаПеремещение КАК ЗаказНаПеремещение,
	|	ВТ_ЗаказыНаПеремещение.КоличествоВЗаказе КАК Количество,
	|	ВТ_ЗаказыНаПеремещение.НомерСтроки КАК НомерСтроки,
	|	ИСТИНА КАК Использовать,
	|	ВТ_ЗаказыНаПеремещение.СкладПолучатель КАК СкладПолучатель,
	|	ВТ_ЗаказыНаПеремещение.Назначение КАК Назначение
	|ИЗ
	|	ВТ_ЗаказыНаПеремещение КАК ВТ_ЗаказыНаПеремещение
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_ЗаказыНаПеремещение.ЗаказНаПеремещение.Дата,
	|	ВТ_ЗаказыНаПеремещение.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПриобретениеТоваровУслугСерии.Номенклатура КАК Номенклатура,
	|	ПриобретениеТоваровУслугСерии.Характеристика КАК Характеристика,
	|	ПриобретениеТоваровУслугСерии.Назначение КАК НазначениеОтправителя,
	|	СУММА(-ПриобретениеТоваровУслугСерии.Количество) КАК Количество
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Серии КАК ПриобретениеТоваровУслугСерии
	|ГДЕ
	|	ПриобретениеТоваровУслугСерии.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриобретениеТоваровУслугСерии.Номенклатура,
	|	ПриобретениеТоваровУслугСерии.Характеристика,
	|	ПриобретениеТоваровУслугСерии.Назначение";
	
	МассивПеремещений = Новый Массив;
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(ТекущийОбъект);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
		
	СоответствиеЗаказовПеремещения = Новый Соответствие;
	
	МассивСкладов = Новый Массив;
	ИспользоватьПеремещениеПоНесколькимЗаказам = ПолучитьФункциональнуюОпцию("ИспользоватьПеремещениеПоНесколькимЗаказам");
	
	ТЗ_ПодменнаяНоменклатура = Новый ТаблицаЗначений;
	ТЗ_ПодменнаяНоменклатура.Колонки.Добавить("ЗаказНаПеремещение");
	ТЗ_ПодменнаяНоменклатура.Колонки.Добавить("КодСтроки");
	ТЗ_ПодменнаяНоменклатура.Колонки.Добавить("Номенклатура");
	ТЗ_ПодменнаяНоменклатура.Колонки.Добавить("КоэфПересчета");
	ТЗ_ПодменнаяНоменклатура.Колонки.Добавить("НоменклатураЗаказа");
		
	Запрос.УстановитьПараметр("Ссылка", ТекущийОбъект.Ссылка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТЗ_Заказы = РезультатЗапроса[4].Выгрузить();
	
	ВыборкаНоменклатураЗаказа = РезультатЗапроса[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	НоменклатураССериямиИзПоступления = РезультатЗапроса[5].Выгрузить();
		
	НачатьТранзакцию();
	Попытка
		
		Пока ВыборкаНоменклатураЗаказа.Следующий() Цикл
			ВыборкаХарактеристика = ВыборкаНоменклатураЗаказа.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаХарактеристика.Следующий() Цикл
				ВыборкаНазначение = ВыборкаХарактеристика.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаНазначение.Следующий() Цикл
					
					ВДЗ = ВыборкаНазначение.Выбрать();
					Пока ВДЗ.Следующий() Цикл
						
						НераспределенноеКоличество = ВДЗ.Количество;
						
						СтруктураПоиска = Новый Структура();
						СтруктураПоиска.Вставить("Номенклатура",   ВДЗ.НоменклатураЗаказа);
						СтруктураПоиска.Вставить("Характеристика", ВДЗ.Характеристика);
						СтруктураПоиска.Вставить("Использовать", Истина);
						
						
						НайденныеСтроки = ТЗ_Заказы.НайтиСтроки(СтруктураПоиска);
						
						Для Каждого ТекСтрока Из НайденныеСтроки Цикл
							
							ЗаказНаПеремещение = СоответствиеЗаказовПеремещения.Получить(ТекСтрока.ЗаказНаПеремещение);
							Если  ЗаказНаПеремещение = Неопределено Тогда
								ЗаказНаПеремещение  = ТекСтрока.ЗаказНаПеремещение.ПолучитьОбъект();
								ЗаказНаПеремещение.Заблокировать();
								СоответствиеЗаказовПеремещения.Вставить(ТекСтрока.ЗаказНаПеремещение, ЗаказНаПеремещение); 
								
								Если МассивСкладов.Найти(ТекСтрока.СкладПолучатель) = Неопределено Тогда
									МассивСкладов.Добавить(ТекСтрока.СкладПолучатель);
								КонецЕсли;
								
							КонецЕсли;
							
							СтрокаТовары = ЗаказНаПеремещение.Товары[ТекСтрока.НомерСтроки-1];
							НоваяДатаНачалаОтгрузки = ЗаказНаПеремещение.Дата;
							Если СтрокаТовары.Количество = НераспределенноеКоличество Тогда
								
								// К отгрузке всю строку
								СтрокаТовары.ВариантОбеспечения = ?(ЗначениеЗаполнено(ВДЗ.Назначение), Перечисления.ВариантыОбеспечения.УдалитьОтгрузитьОбособленно, Перечисления.ВариантыОбеспечения.Отгрузить);
								СтрокаТовары.НачалоОтгрузки = ?(СтрокаТовары.НачалоОтгрузки = Дата(1,1,1), НоваяДатаНачалаОтгрузки, СтрокаТовары.НачалоОтгрузки); 
								ТекСтрока.Использовать = Ложь;
								НераспределенноеКоличество = 0;
								
							ИначеЕсли СтрокаТовары.Количество > НераспределенноеКоличество Тогда
								
								// Разбиваем строку на 2 части:
								// К отгрузке - НераспределенноеКоличество
								// Обеспечение - Разница 
								Разница = СтрокаТовары.Количество - НераспределенноеКоличество;
								
								НоваяСтрокаТовары = ЗаказНаПеремещение.Товары.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары, СтрокаТовары);
								ЗаказНаПеремещение.МаксимальныйКодСтроки = ЗаказНаПеремещение.МаксимальныйКодСтроки + 1;
								НоваяСтрокаТовары.КодСтроки = ЗаказНаПеремещение.МаксимальныйКодСтроки;
								НоваяСтрокаТовары.Количество = Разница;
								
								// Заполним текущую строку новыми значениями для дальнейшего использования
								ЗаполнитьЗначенияСвойств(ТекСтрока, НоваяСтрокаТовары);
								
								
								СтрокаТовары.ВариантОбеспечения = ?(ЗначениеЗаполнено(ВДЗ.Назначение), Перечисления.ВариантыОбеспечения.УдалитьОтгрузитьОбособленно, Перечисления.ВариантыОбеспечения.Отгрузить);
								СтрокаТовары.НачалоОтгрузки = ?(СтрокаТовары.НачалоОтгрузки = Дата(1,1,1), НоваяДатаНачалаОтгрузки, СтрокаТовары.НачалоОтгрузки);
								СтрокаТовары.Количество = НераспределенноеКоличество;
								НераспределенноеКоличество = 0;
								
								// Пересчитаем реквизиты строк
								ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрокаТовары, СтруктураДействий, Неопределено);
								ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТовары, СтруктураДействий, Неопределено);
								
							ИначеЕсли СтрокаТовары.Количество < НераспределенноеКоличество Тогда
								
								// К отгрузке всю строку
								СтрокаТовары.ВариантОбеспечения = ?(ЗначениеЗаполнено(ВДЗ.Назначение), Перечисления.ВариантыОбеспечения.УдалитьОтгрузитьОбособленно, Перечисления.ВариантыОбеспечения.Отгрузить);
								СтрокаТовары.НачалоОтгрузки = ?(СтрокаТовары.НачалоОтгрузки = Дата(1,1,1), НоваяДатаНачалаОтгрузки, СтрокаТовары.НачалоОтгрузки);
								ТекСтрока.Использовать = Ложь;
								НераспределенноеКоличество = НераспределенноеКоличество - СтрокаТовары.Количество;
								
							КонецЕсли;
							
							// Заменять ведь нужно будет только там, где номенклатура отличается
							Если ВДЗ.Номенклатура <> ВДЗ.НоменклатураЗаказа Тогда 
								СтрокаНоменклатура  = ТЗ_ПодменнаяНоменклатура.Добавить();
								СтрокаНоменклатура.ЗаказНаПеремещение = ТекСтрока.ЗаказНаПеремещение;
								СтрокаНоменклатура.КодСтроки = СтрокаТовары.КодСтроки;
								СтрокаНоменклатура.Номенклатура = ВДЗ.Номенклатура; 
								СтрокаНоменклатура.КоэфПересчета = ВДЗ.КоэфПересчета;
								СтрокаНоменклатура.НоменклатураЗаказа = ВДЗ.НоменклатураЗаказа;
							КонецЕсли;
													
							Если НераспределенноеКоличество = 0 Тогда
								Прервать;
							КонецЕсли;
							
						КонецЦикла;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		
		// ИспользоватьПеремещениеПоНесколькимЗаказам;
		// По каждому складу на основании  ПТиУ создаем перемещение
		// Распределяем по заказам на премещение автоматически
		// Удаляем из перемещения строки без заказа, сохраняем и в массив для вывода документа пользователю
	// Для каждого нового склада перемещение на основании ПТиУ за вычетом ранне распределенной номенклатуры
	Если ИспользоватьПеремещениеПоНесколькимЗаказам Тогда
		
		// Подготовка таблицы строк ПТУ с учетом распределения
		ТЗ_СтрокиПТУ = Новый ТаблицаЗначений;
		ТЗ_СтрокиПТУ.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
		ТЗ_СтрокиПТУ.Колонки.Добавить("Номенклатура");
		ТЗ_СтрокиПТУ.Колонки.Добавить("Характеристика");
		ТЗ_СтрокиПТУ.Колонки.Добавить("Назначение");
		ТЗ_СтрокиПТУ.Колонки.Добавить("КоличествоВсего", Новый ОписаниеТипов("Число"));
		ТЗ_СтрокиПТУ.Колонки.Добавить("КоличествоИспользовано", Новый ОписаниеТипов("Число"));
		
		НомерСтроки = 0;
		Для Каждого СтрокаПТУ Из ТекущийОбъект.Товары Цикл
			НоваяСтрока = ТЗ_СтрокиПТУ.Добавить();
			НоваяСтрока.НомерСтроки = НомерСтроки;
			НоваяСтрока.Номенклатура = СтрокаПТУ.Номенклатура;
			НоваяСтрока.Характеристика = СтрокаПТУ.Характеристика;
			НоваяСтрока.Назначение = СтрокаПТУ.Назначение;
			НоваяСтрока.КоличествоВсего = СтрокаПТУ.Количество;
			НоваяСтрока.КоличествоИспользовано = 0;
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
		
		// Шаг 1: Построить ГЛОБАЛЬНУЮ карту распределения товаров по ВСЕМ заказам
		ТЗ_ГлобальноеРаспределение = Новый ТаблицаЗначений;
		ТЗ_ГлобальноеРаспределение.Колонки.Добавить("ЗаказНаПеремещение");
		ТЗ_ГлобальноеРаспределение.Колонки.Добавить("КодСтрокиЗаказа", Новый ОписаниеТипов("Число"));
		ТЗ_ГлобальноеРаспределение.Колонки.Добавить("Номенклатура");
		ТЗ_ГлобальноеРаспределение.Колонки.Добавить("Характеристика");
		ТЗ_ГлобальноеРаспределение.Колонки.Добавить("Назначение");
		ТЗ_ГлобальноеРаспределение.Колонки.Добавить("КоличествоВЗаказе", Новый ОписаниеТипов("Число"));
		ТЗ_ГлобальноеРаспределение.Колонки.Добавить("КоличествоРаспределено", Новый ОписаниеТипов("Число"));
		ТЗ_ГлобальноеРаспределение.Колонки.Добавить("НоменклатураПТУ");
		ТЗ_ГлобальноеРаспределение.Колонки.Добавить("КоэфПересчета", Новый ОписаниеТипов("Число"));
		ТЗ_ГлобальноеРаспределение.Колонки.Добавить("ДатаЗаказа", Новый ОписаниеТипов("Дата"));
		ТЗ_ГлобальноеРаспределение.Колонки.Добавить("СкладПолучатель");
		
		// Заполняем из ТЗ_Заказы для всех складов
		Для Каждого СтрокаЗаказа Из ТЗ_Заказы Цикл
			НоваяСтрока = ТЗ_ГлобальноеРаспределение.Добавить();
			НоваяСтрока.ЗаказНаПеремещение = СтрокаЗаказа.ЗаказНаПеремещение;
			НоваяСтрока.КодСтрокиЗаказа = СтрокаЗаказа.КодСтроки;
			НоваяСтрока.Номенклатура = СтрокаЗаказа.Номенклатура;
			НоваяСтрока.Характеристика = СтрокаЗаказа.Характеристика;
			НоваяСтрока.Назначение = СтрокаЗаказа.Назначение;
			НоваяСтрока.КоличествоВЗаказе = СтрокаЗаказа.Количество;
			НоваяСтрока.КоличествоРаспределено = 0;
			НоваяСтрока.ДатаЗаказа = СтрокаЗаказа.ЗаказНаПеремещение.Дата;
			НоваяСтрока.СкладПолучатель = СтрокаЗаказа.СкладПолучатель;
			
			// Найти номенклатуру ПТУ (может отличаться при подмене)
			СтруктураПоискаПодмены = Новый Структура("ЗаказНаПеремещение, КодСтроки");
			СтруктураПоискаПодмены.ЗаказНаПеремещение = СтрокаЗаказа.ЗаказНаПеремещение;
			СтруктураПоискаПодмены.КодСтроки = СтрокаЗаказа.КодСтроки;
			
			СтрокиПодмены = ТЗ_ПодменнаяНоменклатура.НайтиСтроки(СтруктураПоискаПодмены);
			Если СтрокиПодмены.Количество() > 0 Тогда
				НоваяСтрока.НоменклатураПТУ = СтрокиПодмены[0].Номенклатура;
				НоваяСтрока.КоэфПересчета = СтрокиПодмены[0].КоэфПересчета;
			Иначе
				НоваяСтрока.НоменклатураПТУ = СтрокаЗаказа.Номенклатура;
				НоваяСтрока.КоэфПересчета = 1;
			КонецЕсли;
		КонецЦикла;
		
		// Сортируем по дате заказа (приоритет)
		ТЗ_ГлобальноеРаспределение.Сортировать("ДатаЗаказа, КодСтрокиЗаказа");
		
		// Шаг 2: Записать заказы с проведением
		Для Каждого элЗаказПеремещение Из СоответствиеЗаказовПеремещения Цикл
			элЗаказПеремещение.Значение.Статус = Перечисления.СтатусыВнутреннихЗаказов.КВыполнению;
			элЗаказПеремещение.Значение.Записать(РежимЗаписиДокумента.Проведение);
		КонецЦикла;
		
		// Шаг 3: Распределить товары из ПТУ по ВСЕМ заказам
		Для Каждого СтрокаПТУ Из ТЗ_СтрокиПТУ Цикл
			ДоступноеКоличество = СтрокаПТУ.КоличествоВсего - СтрокаПТУ.КоличествоИспользовано;
			
			Если ДоступноеКоличество <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			// Перебираем ВСЮ таблицу распределения (она уже отсортирована по приоритету)
			// НЕ используем НайтиСтроки, т.к. он не учитывает сортировку
			Для Каждого СтрокаЗаказа Из ТЗ_ГлобальноеРаспределение Цикл
				
				// Проверяем соответствие номенклатуры
				Если СтрокаЗаказа.НоменклатураПТУ <> СтрокаПТУ.Номенклатура
					ИЛИ СтрокаЗаказа.Характеристика <> СтрокаПТУ.Характеристика
					ИЛИ СтрокаЗаказа.Назначение <> СтрокаПТУ.Назначение Тогда
					Продолжить;
				КонецЕсли;
				
				НеобеспеченноеКоличество = СтрокаЗаказа.КоличествоВЗаказе - СтрокаЗаказа.КоличествоРаспределено;
				
				Если НеобеспеченноеКоличество <= 0 Тогда
					Продолжить;
				КонецЕсли;
				
				// Количество с учетом коэффициента пересчета
				ДоступноеКоличествоВЕдиницахЗаказа = ДоступноеКоличество * СтрокаЗаказа.КоэфПересчета;
				КоличествоДляЗаказа = Мин(ДоступноеКоличествоВЕдиницахЗаказа, НеобеспеченноеКоличество);
				
				// Записать распределение
				СтрокаЗаказа.КоличествоРаспределено = СтрокаЗаказа.КоличествоРаспределено + КоличествоДляЗаказа;
				
				// Пересчитываем в единицы ПТУ
				КоличествоИзПТУ = КоличествоДляЗаказа / СтрокаЗаказа.КоэфПересчета;
				СтрокаПТУ.КоличествоИспользовано = СтрокаПТУ.КоличествоИспользовано + КоличествоИзПТУ;
				ДоступноеКоличество = ДоступноеКоличество - КоличествоИзПТУ;
				
				Если ДоступноеКоличество <= 0 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		// Шаг 4: Обработка излишков (ОДИН РАЗ для всех складов)
		СписокВсехЗаказовОбъектов = Новый Массив;
		Для Каждого элЗаказПеремещение Из СоответствиеЗаказовПеремещения Цикл
			СписокВсехЗаказовОбъектов.Добавить(элЗаказПеремещение.Значение);
		КонецЦикла;
		
		Для Каждого СтрокаПТУ Из ТЗ_СтрокиПТУ Цикл
			Излишек = СтрокаПТУ.КоличествоВсего - СтрокаПТУ.КоличествоИспользовано;
			
			Если Излишек > 0 Тогда
				
				// Определить приоритетный заказ для излишков среди ВСЕХ заказов
				ПриоритетныйЗаказ = Неопределено;
				
				// Ищем склад без организации
				Для Каждого ЗаказОбъект Из СписокВсехЗаказовОбъектов Цикл
					ОргСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаказОбъект.СкладПолучатель, "ПЛ_Организация");
					Если НЕ ЗначениеЗаполнено(ОргСклада) Тогда
						ПриоритетныйЗаказ = ЗаказОбъект.Ссылка;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				// Если не нашли склад без организации, берем самый ранний заказ
				Если ПриоритетныйЗаказ = Неопределено И СписокВсехЗаказовОбъектов.Количество() > 0 Тогда
					МинимальнаяДата = Дата(3999, 12, 31);
					Для Каждого ЗаказОбъект Из СписокВсехЗаказовОбъектов Цикл
						Если ЗаказОбъект.Дата < МинимальнаяДата Тогда
							МинимальнаяДата = ЗаказОбъект.Дата;
							ПриоритетныйЗаказ = ЗаказОбъект.Ссылка;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				Если ПриоритетныйЗаказ <> Неопределено Тогда
					
					// Найти или создать строку распределения для излишков
					СтрокаРаспределения = Неопределено;
					
					// Ищем существующую строку для этого заказа и номенклатуры
					Для Каждого СтрокаГлобРаспр Из ТЗ_ГлобальноеРаспределение Цикл
						Если СтрокаГлобРаспр.ЗаказНаПеремещение = ПриоритетныйЗаказ
							И СтрокаГлобРаспр.НоменклатураПТУ = СтрокаПТУ.Номенклатура
							И СтрокаГлобРаспр.Характеристика = СтрокаПТУ.Характеристика
							И СтрокаГлобРаспр.Назначение = СтрокаПТУ.Назначение Тогда
							СтрокаРаспределения = СтрокаГлобРаспр;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
					Если СтрокаРаспределения = Неопределено Тогда
						// Создаем новую строку для излишков
						СтрокаРаспределения = ТЗ_ГлобальноеРаспределение.Добавить();
						СтрокаРаспределения.ЗаказНаПеремещение = ПриоритетныйЗаказ;
						СтрокаРаспределения.КодСтрокиЗаказа = 0;
						СтрокаРаспределения.Номенклатура = СтрокаПТУ.Номенклатура;
						СтрокаРаспределения.Характеристика = СтрокаПТУ.Характеристика;
						СтрокаРаспределения.Назначение = СтрокаПТУ.Назначение;
						СтрокаРаспределения.НоменклатураПТУ = СтрокаПТУ.Номенклатура;
						СтрокаРаспределения.КоличествоВЗаказе = 0;
						СтрокаРаспределения.КоличествоРаспределено = 0;
						СтрокаРаспределения.КоэфПересчета = 1;
						// Найти склад получателя для приоритетного заказа
						Для Каждого ЗаказОбъект Из СписокВсехЗаказовОбъектов Цикл
							Если ЗаказОбъект.Ссылка = ПриоритетныйЗаказ Тогда
								СтрокаРаспределения.СкладПолучатель = ЗаказОбъект.СкладПолучатель;
								СтрокаРаспределения.ДатаЗаказа = ЗаказОбъект.Дата;
								Прервать;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
					
					// Добавляем излишек
					ИзлишекВЕдиницахЗаказа = Излишек * СтрокаРаспределения.КоэфПересчета;
					СтрокаРаспределения.КоличествоРаспределено = СтрокаРаспределения.КоличествоРаспределено + ИзлишекВЕдиницахЗаказа;
					СтрокаПТУ.КоличествоИспользовано = СтрокаПТУ.КоличествоИспользовано + Излишек;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		// Шаг 5: Для каждого склада создаем перемещение на основе глобального распределения
		Для Каждого элСклад Из МассивСкладов Цикл
			
			// Получить заказы для этого склада
			МассивЗаказовНаПеремещение = Новый Массив;
			СписокЗаказовОбъектов = Новый Массив;
			
			Для Каждого элЗаказПеремещение Из СоответствиеЗаказовПеремещения Цикл
				Если элЗаказПеремещение.Значение.СкладПолучатель = элСклад Тогда
					МассивЗаказовНаПеремещение.Добавить(элЗаказПеремещение.Ключ);
					СписокЗаказовОбъектов.Добавить(элЗаказПеремещение.Значение);
				КонецЕсли;
			КонецЦикла;
			
			Если МассивЗаказовНаПеремещение.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			// Получить распределение для этого склада
			СтруктураПоиска = Новый Структура("СкладПолучатель", элСклад);
			ТЗ_РаспределениеПоЗаказам = ТЗ_ГлобальноеРаспределение.Скопировать(ТЗ_ГлобальноеРаспределение.НайтиСтроки(СтруктураПоиска));
			
			// Шаг 4: Создать документ перемещения
			ТЗ_СтрокиПеремещения = ТЗ_РаспределениеПоЗаказам.Скопировать();
			
			// Убираем строки с нулевым распределением
			НомерСтроки = ТЗ_СтрокиПеремещения.Количество() - 1;
			Пока НомерСтроки >= 0 Цикл
				Если ТЗ_СтрокиПеремещения[НомерСтроки].КоличествоРаспределено = 0 Тогда
					ТЗ_СтрокиПеремещения.Удалить(НомерСтроки);
				КонецЕсли;
				НомерСтроки = НомерСтроки - 1;
			КонецЦикла;
			
			// Если нет строк для перемещения - пропускаем этот склад
			Если ТЗ_СтрокиПеремещения.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			// Создаем документ перемещения
			ДокПеремещение = Документы.ПеремещениеТоваров.СоздатьДокумент();
			ДокПеремещение.Дата = ТекущаяДатаСеанса();
			ДокПеремещение.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеТоваров;
			ДокПеремещение.СкладОтправитель = ТекущийОбъект.Склад;
			ДокПеремещение.СкладПолучатель = элСклад;
			ДокПеремещение.Организация = МассивЗаказовНаПеремещение[0].Организация;
			ДокПеремещение.ЗаказНаПеремещение = МассивЗаказовНаПеремещение[0];
			ДокПеремещение.ПеремещениеПоЗаказам = Истина;
			ДокПеремещение.Сделка = Справочники.СделкиСКлиентами.ПустаяСсылка();
			ДокПеремещение.СпособДоставки = МассивЗаказовНаПеремещение[0].СпособДоставки;
			ДокПеремещение.ВариантПриемкиТоваров = МассивЗаказовНаПеремещение[0].ВариантПриемкиТоваров;
			ДокПеремещение.ДокументОснование = ТекущийОбъект.Ссылка; 
			ДокПеремещение.Статус = Перечисления.СтатусыПеремещенийТоваров.Принято;
			ДокПеремещение.Ответственный = Пользователи.ТекущийПользователь();
			
			// Заполняем ТЧ Товары
			Для Каждого СтрокаРаспр Из ТЗ_СтрокиПеремещения Цикл
				
				// Найти исходную строку ПТУ для копирования реквизитов
				СтруктураПоискаПТУ = Новый Структура("Номенклатура, Характеристика, Назначение");
				СтруктураПоискаПТУ.Номенклатура = СтрокаРаспр.НоменклатураПТУ;
				СтруктураПоискаПТУ.Характеристика = СтрокаРаспр.Характеристика;
				СтруктураПоискаПТУ.Назначение = СтрокаРаспр.Назначение;
				
				СтрокиПТУНайденные = ТЗ_СтрокиПТУ.НайтиСтроки(СтруктураПоискаПТУ);
				Если СтрокиПТУНайденные.Количество() = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				НомерСтрокиПТУ = СтрокиПТУНайденные[0].НомерСтроки;
				СтрокаПТУИсходная = ТекущийОбъект.Товары[НомерСтрокиПТУ];
				
				НоваяСтрока = ДокПеремещение.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПТУИсходная);
				
				НоваяСтрока.ЗаказНаПеремещение = СтрокаРаспр.ЗаказНаПеремещение;
				НоваяСтрока.КодСтроки = СтрокаРаспр.КодСтрокиЗаказа;
				НоваяСтрока.Количество = СтрокаРаспр.КоличествоРаспределено / СтрокаРаспр.КоэфПересчета;
				НоваяСтрока.Номенклатура = СтрокаРаспр.НоменклатураПТУ;
			КонецЦикла;
			
			// Обработка серий
			ИндексСтрокиСерии = ДокПеремещение.Серии.Количество();
			Если ИндексСтрокиСерии > 0 Тогда
				КолонкиКлюч = "Номенклатура, Характеристика, НазначениеОтправителя";
				КлючиСерий = ДокПеремещение.Серии.Выгрузить(, КолонкиКлюч);
				КлючиСерий.Свернуть(КолонкиКлюч);
				
				ТоварыИтоги = НоменклатураССериямиИзПоступления.Скопировать();
				СтруктураПоискаДляСерий = Новый Структура(КолонкиКлюч);
				
				Для Каждого КлючСерий Из КлючиСерий Цикл
					ЗаполнитьЗначенияСвойств(СтруктураПоискаДляСерий, КлючСерий);
					СтрокиТоваров = ДокПеремещение.Товары.НайтиСтроки(СтруктураПоискаДляСерий);
					Для Каждого СтрокаТоваров Из СтрокиТоваров Цикл
						ЗаполнитьЗначенияСвойств(ТоварыИтоги.Добавить(), СтрокаТоваров);
					КонецЦикла;
				КонецЦикла;
				
				ТоварыИтоги.Свернуть(КолонкиКлюч, "Количество");
				
				Для Каждого КлючСерий Из КлючиСерий Цикл
					ЗаполнитьЗначенияСвойств(СтруктураПоискаДляСерий, КлючСерий);
					СтрокиТоваров = ТоварыИтоги.НайтиСтроки(СтруктураПоискаДляСерий);
					Если СтрокиТоваров.Количество() = 1 И СтрокиТоваров[0].Количество = 0 Тогда
						Продолжить;
					КонецЕсли;
					СтрокиСерий = ДокПеремещение.Серии.НайтиСтроки(СтруктураПоискаДляСерий);
					Для Каждого СтрокаСерий Из СтрокиСерий Цикл
						ДокПеремещение.Серии.Удалить(СтрокаСерий);
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
			
			// Пересчет и заполнение
			Документы.ПеремещениеТоваров.ОбновитьЗависимыеРеквизитыТабличнойЧасти(ДокПеремещение.Товары, Неопределено);
			ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(НоменклатураСервер.ПараметрыУказанияСерий(ДокПеремещение, Документы.ПеремещениеТоваров));
			НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокПеремещение, ПараметрыУказанияСерий);
			ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(ДокПеремещение);
			
			// Записываем документ
			ДокПеремещение.Записать();
			МассивПеремещений.Добавить(ДокПеремещение.Ссылка);
			
		КонецЦикла;
						
		Иначе	
			Для каждого элЗаказПеремещение Из СоответствиеЗаказовПеремещения Цикл
				
				// Запишем заказ на перемещение
				элЗаказПеремещение.Значение.Статус = Перечисления.СтатусыВнутреннихЗаказов.КВыполнению;
				элЗаказПеремещение.Значение.Записать(РежимЗаписиДокумента.Проведение);	
				
				// Создаем перемещение на основании заказа на перемещение
				// Для заказа на перемещение необходимо передавать структурой параметры в обработку заполнения перемещения
				ПараметрыОткрытия = СозданиеНаОснованииУТВызовСервера.ПараметрыОткрытияФормыНакладнойНаОснованииЗаказа(элЗаказПеремещение.Ключ, "ПеремещениеТоваров");
				ДокПеремещение = Документы.ПеремещениеТоваров.СоздатьДокумент();
				ДокПеремещение.Дата = ТекущаяДатаСеанса();
				ДокПеремещение.Заполнить(ПараметрыОткрытия.ПараметрыФормы.Основание); 
				ДокПеремещение.Статус = Перечисления.СтатусыПеремещенийТоваров.Принято;
				ДокПеремещение.ДокументОснование = ТекущийОбъект.Ссылка;
				ДокПеремещение.Ответственный = Пользователи.ТекущийПользователь();
				
				Если ДокПеремещение.Товары.Количество() Тогда
					
					// Подменим вскрытую на закрытую номенклатуру
					// И пересчитаем количество
					Для каждого текСтрока  Из ДокПеремещение.Товары Цикл
						
						СтруктураПоиска = Новый Структура();
						СтруктураПоиска.Вставить("ЗаказНаПеремещение", элЗаказПеремещение.Ключ);
						СтруктураПоиска.Вставить("КодСтроки", текСтрока.КодСтроки);
						НайденныеСтроки = ТЗ_ПодменнаяНоменклатура.НайтиСтроки(СтруктураПоиска);
						
						Если НайденныеСтроки.Количество() Тогда
							текСтрока.Количество =  текСтрока.Количество / НайденныеСтроки[0].КоэфПересчета;
							текСтрока.Номенклатура = НайденныеСтроки[0].Номенклатура;	
							
							ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(текСтрока, СтруктураДействий, Неопределено);
						КонецЕсли;	
						
					КонецЦикла;

					ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(НоменклатураСервер.ПараметрыУказанияСерий(ДокПеремещение, Документы.ПеремещениеТоваров));
					НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокПеремещение, ПараметрыУказанияСерий);
									
					// Запишем перемещение и добавим в массив созданных перемещений, который потом откроем на клиенте
					ДокПеремещение.Записать();
					МассивПеремещений.Добавить(ДокПеремещение.Ссылка);
					
				КонецЕсли;	
				
			КонецЦикла;
			
		КонецЕсли;
	
		Если МассивПеремещений.Количество() Тогда
			// Пометим, что обработка формирования перемещений сработала.
			МассивСтруктур = Новый Массив;
			МассивСтруктур.Добавить(Новый Структура("Свойство, Значение", ДопСвойство, Истина));
			
			ЗначенияДополнительныхРеквизитов = Новый ТаблицаЗначений;
			
			ЗначенияДополнительныхРеквизитов.Колонки.Добавить("Свойство");
			ЗначенияДополнительныхРеквизитов.Колонки.Добавить("Значение");
			
			Для каждого Реквизит Из МассивСтруктур Цикл
				
			ЗаполнитьЗначенияСвойств(ЗначенияДополнительныхРеквизитов.Добавить(), Реквизит);	
				
			КонецЦикла;
			
			УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(ТекущийОбъект.Ссылка, ЗначенияДополнительныхРеквизитов);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		МассивПеремещений.Очистить();
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если МассивПеремещений.Количество() Тогда
		// Передадим массив перемещений для дальнейшего открытия
		ПараметрыЗаписи.Вставить("МассивПеремещений", МассивПеремещений);
	КонецЕсли;	
		
	// Проверим остатки по заказу и запишем в документ очередь на создание задачи
	УстановитьПривилегированныйРежим(Истина);
	СоздаватьЗадачу = Константы.ПЛ_СоздаватьЗадачуНаЗакрытиеЗаказаПоставщику.Получить();
	УстановитьПривилегированныйРежим(Ложь);
	Если СоздаватьЗадачу Тогда
		РегистрыСведений.ПЛ_ОчередьЗаказовДляСозданияЗадач.ДобавитьПоступлениеВОчередьДляСозданияЗадачПоЗаказам(ТекущийОбъект.Ссылка);
	КонецЕсли;

КонецПроцедуры

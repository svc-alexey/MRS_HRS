#Область ПрограммныйИнтерфейс

// Получение таблицы межфирменных оборотов за период
//
// Параметры:
//  Дата - Строка - дата в формате "ГГГГ-ММ-ДД", за месяц которой нужно получить данные
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица с межфирменными оборотами со структурой:
//   * СкладОтправительПЛ_Организация - Строка - наименование организации-отправителя
//   * ОрганизацияОтправительИНН - Строка - ИНН организации-отправителя
//   * СкладОтправитель - Строка - наименование склада-отправителя
//   * СкладПолучательПЛ_Организация - Строка - наименование организации-получателя
//   * ОрганизацияПолучательИНН - Строка - ИНН организации-получателя
//   * СкладПолучатель - Строка - наименование склада-получателя
//   * Номенклатура - Строка - наименование номенклатуры
//   * НоменклатураКод - Строка - код номенклатуры
//   * Количество - Число - количество
//   * СУММА - Число - сумма
//   * ПодразделениеБПОтправитель - Строка - подразделение БП отправителя
//   * ПодразделениеБППолучатель - Строка - подразделение БП получателя
//
&НаСервере
Функция ПолучитьТаблицуМежфирменных(Дата) Экспорт
	
	РезультатДляВыгрузки = Новый ТаблицаЗначений;
	РезультатЗапроса = Новый ТаблицаЗначений;
	МассивСсылокОрганизации = ПолучитьСписокОрганизаций();
	
	Для Каждого Организация из МассивСсылокОрганизации Цикл
		
		Если РезультатДляВыгрузки.Количество() = 0 Тогда
			
			РезультатДляВыгрузки = РезультатЗапроса.Скопировать(); //сохраним результат запроса
					
		ИначеЕсли РезультатДляВыгрузки.Количество() <> 0 Тогда
			//Объеденим если несколько таблиц
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РезультатЗапроса, РезультатДляВыгрузки);//здесь уже нужно объеденить
		
		КонецЕсли;
		
		//>>Швецов. ERP-5015. 07.04.2025 Замена на головную организацию в запросе
		// << MRS #ERP-5035 ДенисламовАД <Adel.Denislamov@mriyaresort.com> 30.09.25, (Получение вида цен из склада в самом запросе)
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ПеремещениеТоваровТовары.Номенклатура КАК Номенклатура,
		               |	СУММА(ПеремещениеТоваровТовары.Количество) КАК Количество,
		               |	ПеремещениеТоваровТовары.Ссылка.СкладОтправитель КАК СкладОтправитель,
		               |	ПеремещениеТоваровТовары.Ссылка.СкладПолучатель КАК СкладПолучатель,
		               |	ПеремещениеТоваровТовары.Ссылка.СкладОтправитель.ПЛ_Организация.ГоловнаяОрганизация КАК СкладОтправительПЛ_Организация,
				   |	ПеремещениеТоваровТовары.Ссылка.СкладПолучатель.ПЛ_Организация.ГоловнаяОрганизация КАК СкладПолучательПЛ_Организация,
				   |	ПеремещениеТоваровТовары.Ссылка.СкладОтправитель.ПЛ_Организация.ГоловнаяОрганизация.ИНН КАК СкладОтправительПЛ_ОрганизацияИНН,
				   //|	ПеремещениеТоваровТовары.Ссылка.СкладПолучатель.ПЛ_Организация.ГоловнаяОрганизация.ИНН КАК СкладПолучательПЛ_ОрганизацияИНН
				   |	ПеремещениеТоваровТовары.Ссылка.СкладПолучатель.ПЛ_Организация.ГоловнаяОрганизация.ИНН КАК СкладПолучательПЛ_ОрганизацияИНН,
				   |	ПеремещениеТоваровТовары.Ссылка.СкладОтправитель.УчетныйВидЦены КАК УчетныйВидЦены
				   |ПОМЕСТИТЬ ВТ_ПрямыеМежфирменныеПеремещения
				   |ИЗ
				   |	Документ.ПеремещениеТоваров.Товары КАК ПеремещениеТоваровТовары
		               |ГДЕ
		               |	ПеремещениеТоваровТовары.Ссылка.Дата МЕЖДУ &Период1 И &Период2
		               |	И ПеремещениеТоваровТовары.Ссылка.Проведен
		               |	И ПеремещениеТоваровТовары.Ссылка.СкладОтправитель.ПЛ_Организация <> ЗНАЧЕНИЕ(справочник.организации.пустаяСсылка)
		               |	И ПеремещениеТоваровТовары.Ссылка.СкладОтправитель.ПЛ_Организация <> НЕОПРЕДЕЛЕНО
		               |	И ПеремещениеТоваровТовары.Ссылка.СкладПолучатель.ПЛ_Организация <> ЗНАЧЕНИЕ(справочник.организации.пустаяСсылка)
		               |	И ПеремещениеТоваровТовары.Ссылка.СкладПолучатель.ПЛ_Организация <> НЕОПРЕДЕЛЕНО
		               |	И ПеремещениеТоваровТовары.Ссылка.СкладПолучатель.ПЛ_Организация.ГоловнаяОрганизация <> ПеремещениеТоваровТовары.Ссылка.СкладОтправитель.ПЛ_Организация.ГоловнаяОрганизация
		               |	И ПеремещениеТоваровТовары.Ссылка.СкладОтправитель.ПЛ_Организация.ГоловнаяОрганизация = &ОрганизацияОткуда
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ПеремещениеТоваровТовары.Ссылка.СкладОтправитель,
		               |	ПеремещениеТоваровТовары.Ссылка.СкладПолучатель,
		               |	ПеремещениеТоваровТовары.Номенклатура,
		               |	ПеремещениеТоваровТовары.Ссылка.СкладОтправитель.ПЛ_Организация.ГоловнаяОрганизация,
		               |	ПеремещениеТоваровТовары.Ссылка.СкладПолучатель.ПЛ_Организация.ГоловнаяОрганизация,
		               |	ПеремещениеТоваровТовары.Ссылка.СкладОтправитель.ПЛ_Организация.ГоловнаяОрганизация.ИНН,
				   //|	ПеремещениеТоваровТовары.Ссылка.СкладПолучатель.ПЛ_Организация.ГоловнаяОрганизация.ИНН
				   |	ПеремещениеТоваровТовары.Ссылка.СкладПолучатель.ПЛ_Организация.ГоловнаяОрганизация.ИНН,
				   |	ПеремещениеТоваровТовары.Ссылка.СкладОтправитель.УчетныйВидЦены
				   |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ВТ_ПрямыеМежфирменныеПеремещения.Номенклатура КАК Номенклатура,
		               |	СУММА(ВТ_ПрямыеМежфирменныеПеремещения.Количество) КАК Количество,
		               |	ВТ_ПрямыеМежфирменныеПеремещения.СкладОтправительПЛ_Организация КАК СкладОтправительПЛ_Организация,
		               |	ВТ_ПрямыеМежфирменныеПеремещения.СкладОтправитель КАК СкладОтправитель,
		               |	ВТ_ПрямыеМежфирменныеПеремещения.СкладОтправительПЛ_ОрганизацияИНН КАК СкладОтправительПЛ_ОрганизацияИНН
		               |ПОМЕСТИТЬ ВТ_СводПрямыхМежфирменных
		               |ИЗ
		               |	ВТ_ПрямыеМежфирменныеПеремещения КАК ВТ_ПрямыеМежфирменныеПеремещения
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ВТ_ПрямыеМежфирменныеПеремещения.СкладОтправительПЛ_Организация,
		               |	ВТ_ПрямыеМежфирменныеПеремещения.СкладОтправитель,
		               |	ВТ_ПрямыеМежфирменныеПеремещения.Номенклатура,
		               |	ВТ_ПрямыеМежфирменныеПеремещения.СкладОтправительПЛ_ОрганизацияИНН
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ПередачаТоваровМеждуОрганизациямиТовары.Ссылка.Склад КАК Склад,
		               |	ПередачаТоваровМеждуОрганизациямиТовары.Ссылка.Организация.ГоловнаяОрганизация КАК Организация,
		               |	ПередачаТоваровМеждуОрганизациямиТовары.Номенклатура КАК Номенклатура,
		               |	СУММА(ПередачаТоваровМеждуОрганизациямиТовары.Количество) КАК Количество,
		               |	СУММА(ПередачаТоваровМеждуОрганизациямиТовары.Сумма) КАК Сумма,
		               |	ПередачаТоваровМеждуОрганизациямиТовары.Ссылка.ОрганизацияПолучатель.ГоловнаяОрганизация КАК ОрганизацияПолучатель,
		               |	ПередачаТоваровМеждуОрганизациямиТовары.Ссылка.Организация.ГоловнаяОрганизация.ИНН КАК ОрганизацияИНН,
		               |	ПередачаТоваровМеждуОрганизациямиТовары.Ссылка.ОрганизацияПолучатель.ГоловнаяОрганизация.ИНН КАК ОрганизацияПолучательИНН
		               |ПОМЕСТИТЬ ВТ_ТиповыеПередачиБезВычитания
		               |ИЗ
		               |	Документ.ПередачаТоваровМеждуОрганизациями.Товары КАК ПередачаТоваровМеждуОрганизациямиТовары
		               |ГДЕ
		               |	ПередачаТоваровМеждуОрганизациямиТовары.Ссылка.Организация.ГоловнаяОрганизация = &ОрганизацияОткуда
		               |	И ПередачаТоваровМеждуОрганизациямиТовары.Ссылка.Проведен
		               |	И ПередачаТоваровМеждуОрганизациямиТовары.Ссылка.Дата МЕЖДУ &Период1 И &Период2
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ПередачаТоваровМеждуОрганизациямиТовары.Ссылка.Склад,
		               |	ПередачаТоваровМеждуОрганизациямиТовары.Номенклатура,
		               |	ПередачаТоваровМеждуОрганизациямиТовары.Ссылка.Организация.ГоловнаяОрганизация,
		               |	ПередачаТоваровМеждуОрганизациямиТовары.Ссылка.ОрганизацияПолучатель.ГоловнаяОрганизация,
		               |	ПередачаТоваровМеждуОрганизациямиТовары.Ссылка.Организация.ГоловнаяОрганизация.ИНН,
		               |	ПередачаТоваровМеждуОрганизациямиТовары.Ссылка.ОрганизацияПолучатель.ГоловнаяОрганизация.ИНН
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ВТ_ТиповыеПередачиБезВычитания.Склад КАК Склад,
		               |	ВТ_ТиповыеПередачиБезВычитания.Организация КАК Организация,
		               |	ВТ_ТиповыеПередачиБезВычитания.Номенклатура КАК Номенклатура,
		               |	СУММА(ВТ_ТиповыеПередачиБезВычитания.Количество) КАК КоличествоПолное,
		               |	СУММА(ВТ_ТиповыеПередачиБезВычитания.Сумма) КАК СуммаПолная,
		               |	СУММА(ВТ_ТиповыеПередачиБезВычитания.Количество - ЕСТЬNULL(ВТ_СводПрямыхМежфирменных.Количество, 0)) КАК Количество,
		               |	СУММА(ВЫБОР
		               |			КОГДА ВТ_ТиповыеПередачиБезВычитания.Количество = 0
		               |				ТОГДА 0
		               |			ИНАЧЕ ВТ_ТиповыеПередачиБезВычитания.Сумма * (ВТ_ТиповыеПередачиБезВычитания.Количество - ЕСТЬNULL(ВТ_СводПрямыхМежфирменных.Количество, 0)) / ВТ_ТиповыеПередачиБезВычитания.Количество
		               |		КОНЕЦ) КАК Сумма,
		               |	ВТ_ТиповыеПередачиБезВычитания.ОрганизацияПолучатель КАК ОрганизацияПолучатель,
		               |	ВТ_ТиповыеПередачиБезВычитания.ОрганизацияИНН КАК ОрганизацияИНН,
		               |	ВТ_ТиповыеПередачиБезВычитания.ОрганизацияПолучательИНН КАК ОрганизацияПолучательИНН
		               |ПОМЕСТИТЬ ВТ_ТиповыеПередачи
		               |ИЗ
		               |	ВТ_ТиповыеПередачиБезВычитания КАК ВТ_ТиповыеПередачиБезВычитания
		               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СводПрямыхМежфирменных КАК ВТ_СводПрямыхМежфирменных
		               |		ПО ВТ_ТиповыеПередачиБезВычитания.Организация = ВТ_СводПрямыхМежфирменных.СкладОтправительПЛ_Организация
		               |			И ВТ_ТиповыеПередачиБезВычитания.Склад = ВТ_СводПрямыхМежфирменных.СкладОтправитель
		               |			И ВТ_ТиповыеПередачиБезВычитания.Номенклатура = ВТ_СводПрямыхМежфирменных.Номенклатура
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ВТ_ТиповыеПередачиБезВычитания.Склад,
		               |	ВТ_ТиповыеПередачиБезВычитания.Организация,
		               |	ВТ_ТиповыеПередачиБезВычитания.Номенклатура,
		               |	ВТ_ТиповыеПередачиБезВычитания.ОрганизацияПолучатель,
		               |	ВТ_ТиповыеПередачиБезВычитания.ОрганизацияИНН,
		               |	ВТ_ТиповыеПередачиБезВычитания.ОрганизацияПолучательИНН
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ВТ_ТиповыеПередачи.Склад КАК Склад,
		               |	ВТ_ТиповыеПередачи.Организация КАК Организация,
		               |	ВТ_ТиповыеПередачи.Номенклатура КАК Номенклатура,
		               |	СРЕДНЕЕ(ВТ_ТиповыеПередачи.СуммаПолная / ВТ_ТиповыеПередачи.КоличествоПолное) КАК Цена,
		               |	ВТ_ТиповыеПередачи.ОрганизацияИНН КАК ОрганизацияИНН
		               |ПОМЕСТИТЬ ВТ_Цены
		               |ИЗ
		               |	ВТ_ТиповыеПередачи КАК ВТ_ТиповыеПередачи
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ВТ_ТиповыеПередачи.Склад,
		               |	ВТ_ТиповыеПередачи.Организация,
		               |	ВТ_ТиповыеПередачи.Номенклатура,
		               |	ВТ_ТиповыеПередачи.ОрганизацияИНН
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.СкладОтправительПЛ_Организация) КАК СкладОтправительПЛ_Организация,
		               |	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.СкладОтправительПЛ_ОрганизацияИНН) КАК ОрганизацияОтправительИНН,
		               |	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.СкладОтправитель) КАК СкладОтправитель,
		               |	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.СкладПолучательПЛ_Организация) КАК СкладПолучательПЛ_Организация,
		               |	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.СкладПолучательПЛ_ОрганизацияИНН) КАК ОрганизацияПолучательИНН,
		               |	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.СкладПолучатель) КАК СкладПолучатель,
		               |	ПРЕДСТАВЛЕНИЕ(ВложенныйЗапрос.Номенклатура) КАК Номенклатура,
		               |	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
		               |	ВЫБОР
		               |		КОГДА СУММА(ВложенныйЗапрос.Сумма) > 0
		               |			ТОГДА СУММА(ВложенныйЗапрос.Сумма)
		               |		ИНАЧЕ СУММА(ВложенныйЗапрос.Количество) * ЦеныНоменклатурыСрезПоследних.Цена
		               |	КОНЕЦ КАК СУММА,
		               |	ПРЕДСТАВЛЕНИЕ(ПЛ_СоответствияПодразделенийБПОтправитель.ПодразделениеБП) КАК ПодразделениеБПОтправитель,
		               |	ПРЕДСТАВЛЕНИЕ(ПЛ_СоответствияПодразделенийБППолучатель.ПодразделениеБП) КАК ПодразделениеБППолучатель,
		               |	ВложенныйЗапрос.Номенклатура.Код КАК НоменклатураКод
		               |ИЗ
		               |	(ВЫБРАТЬ
		               |		ВТ_ПрямыеМежфирменныеПеремещения.СкладОтправительПЛ_Организация КАК СкладОтправительПЛ_Организация,
		               |		ВТ_ПрямыеМежфирменныеПеремещения.СкладОтправитель КАК СкладОтправитель,
		               |		ВТ_ПрямыеМежфирменныеПеремещения.СкладПолучательПЛ_Организация КАК СкладПолучательПЛ_Организация,
		               |		ВТ_ПрямыеМежфирменныеПеремещения.СкладПолучатель КАК СкладПолучатель,
		               |		ВТ_ПрямыеМежфирменныеПеремещения.Номенклатура КАК Номенклатура,
		               |		СУММА(ВТ_ПрямыеМежфирменныеПеремещения.Количество) КАК Количество,
		               |		СУММА(ВТ_ПрямыеМежфирменныеПеремещения.Количество * ВТ_Цены.Цена) КАК Сумма,
		               |		ВТ_ПрямыеМежфирменныеПеремещения.СкладОтправительПЛ_ОрганизацияИНН КАК СкладОтправительПЛ_ОрганизацияИНН,
				   //|		ВТ_ПрямыеМежфирменныеПеремещения.СкладПолучательПЛ_ОрганизацияИНН КАК СкладПолучательПЛ_ОрганизацияИНН
				   |		ВТ_ПрямыеМежфирменныеПеремещения.СкладПолучательПЛ_ОрганизацияИНН КАК СкладПолучательПЛ_ОрганизацияИНН,
				   |		ВТ_ПрямыеМежфирменныеПеремещения.УчетныйВидЦены КАК УчетныйВидЦены
				   |	ИЗ
				   |		ВТ_ПрямыеМежфирменныеПеремещения КАК ВТ_ПрямыеМежфирменныеПеремещения
				   |			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Цены КАК ВТ_Цены
		               |			ПО ВТ_ПрямыеМежфирменныеПеремещения.СкладОтправитель = ВТ_Цены.Склад
		               |				И ВТ_ПрямыеМежфирменныеПеремещения.Номенклатура = ВТ_Цены.Номенклатура
		               |	
		               |	СГРУППИРОВАТЬ ПО
		               |		ВТ_ПрямыеМежфирменныеПеремещения.СкладОтправительПЛ_Организация,
		               |		ВТ_ПрямыеМежфирменныеПеремещения.СкладОтправитель,
		               |		ВТ_ПрямыеМежфирменныеПеремещения.СкладПолучательПЛ_Организация,
		               |		ВТ_ПрямыеМежфирменныеПеремещения.СкладПолучатель,
		               |		ВТ_ПрямыеМежфирменныеПеремещения.Номенклатура,
				   |		ВТ_ПрямыеМежфирменныеПеремещения.СкладОтправительПЛ_ОрганизацияИНН,
				   //|		ВТ_ПрямыеМежфирменныеПеремещения.СкладПолучательПЛ_ОрганизацияИНН
				   |		ВТ_ПрямыеМежфирменныеПеремещения.СкладПолучательПЛ_ОрганизацияИНН,
				   |		ВТ_ПрямыеМежфирменныеПеремещения.УчетныйВидЦены
				   |	
		               |	ОБЪЕДИНИТЬ ВСЕ
		               |	
		               |	ВЫБРАТЬ
		               |		ВТ_ТиповыеПередачи.Организация,
		               |		ВТ_ТиповыеПередачи.Склад,
		               |		ВТ_ТиповыеПередачи.ОрганизацияПолучатель,
		               |		ВТ_ТиповыеПередачи.Склад,
		               |		ВТ_ТиповыеПередачи.Номенклатура,
		               |		СУММА(ВТ_ТиповыеПередачи.Количество),
		               |		СУММА(ВТ_ТиповыеПередачи.Сумма),
		               |		ВТ_ТиповыеПередачи.ОрганизацияИНН,
				   //|		ВТ_ТиповыеПередачи.ОрганизацияПолучательИНН
				   |		ВТ_ТиповыеПередачи.ОрганизацияПолучательИНН,
				   |		ВТ_ТиповыеПередачи.Склад.УчетныйВидЦены
		               |	ИЗ
		               |		ВТ_ТиповыеПередачи КАК ВТ_ТиповыеПередачи
		               |	
		               |	СГРУППИРОВАТЬ ПО
		               |		ВТ_ТиповыеПередачи.Склад,
		               |		ВТ_ТиповыеПередачи.Организация,
		               |		ВТ_ТиповыеПередачи.Номенклатура,
		               |		ВТ_ТиповыеПередачи.ОрганизацияПолучатель,
		               |		ВТ_ТиповыеПередачи.ОрганизацияИНН,
		               |		ВТ_ТиповыеПередачи.ОрганизацияПолучательИНН,
		               |		ВТ_ТиповыеПередачи.Склад) КАК ВложенныйЗапрос
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПЛ_СоответствияПодразделенийБП КАК ПЛ_СоответствияПодразделенийБПОтправитель
		               |		ПО ВложенныйЗапрос.СкладОтправитель.Подразделение = ПЛ_СоответствияПодразделенийБПОтправитель.Подразделение
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПЛ_СоответствияПодразделенийБП КАК ПЛ_СоответствияПодразделенийБППолучатель
		               |		ПО ВложенныйЗапрос.СкладПолучатель.Подразделение = ПЛ_СоответствияПодразделенийБППолучатель.Подразделение
				   //|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период1, ВидЦены = &ВидЦены) КАК ЦеныНоменклатурыСрезПоследних
				   //|		ПО ВложенныйЗапрос.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
				   |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период1, ) КАК ЦеныНоменклатурыСрезПоследних
				   |		ПО ВложенныйЗапрос.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
				   |			И ВложенныйЗапрос.УчетныйВидЦены = ЦеныНоменклатурыСрезПоследних.ВидЦены
		               |ГДЕ
		               |	ЕСТЬNULL(ВложенныйЗапрос.Количество, 0) <> 0
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	ВложенныйЗапрос.СкладОтправительПЛ_Организация,
		               |	ВложенныйЗапрос.СкладОтправитель,
		               |	ВложенныйЗапрос.СкладПолучательПЛ_Организация,
		               |	ВложенныйЗапрос.СкладПолучатель,
		               |	ВложенныйЗапрос.Номенклатура,
		               |	ПЛ_СоответствияПодразделенийБПОтправитель.ПодразделениеБП,
		               |	ПЛ_СоответствияПодразделенийБППолучатель.ПодразделениеБП,
		               |	ВложенныйЗапрос.Номенклатура.Код,
		               |	ЦеныНоменклатурыСрезПоследних.Цена,
		               |	ВложенныйЗапрос.СкладОтправительПЛ_ОрганизацияИНН,
		               |	ВложенныйЗапрос.СкладПолучательПЛ_ОрганизацияИНН";
		
		// Присвоение значений переменным параметров.
		// << MRS # ДенисламовАД <Adel.Denislamov@mriyaresort.com> 30.09.25, (Получение вида цен из склада в самом запросе)
		//// << MRS # ДенисламовАД <Adel.Denislamov@mriyaresort.com> 09.04.25, ()
		////ВидЦены = Справочники.ВидыЦен.НайтиПоНаименованию("Себестоимость (тип цен)");
		//ВидЦены = MRS_Сервер.ПолучитьВидЦены("ЗакупочныйВидЦеныКрым");
		//// >> MRS # ДенисламовАД <Adel.Denislamov@mriyaresort.com> 09.04.25, ()				
		Месяц = Дата(СтрЗаменить(Лев(Дата, 10),"-","")); 
		
		// Установка параметров.
		// << MRS #ERP-5035 ДенисламовАД <Adel.Denislamov@mriyaresort.com> 30.09.25, (Получение вида цен из склада в самом запросе)
		//Запрос.УстановитьПараметр("ВидЦены", ВидЦены);
		// >> MRS #ERP-5035 ДенисламовАД <Adel.Denislamov@mriyaresort.com> 30.09.25, (Получение вида цен из склада в самом запросе)
		Запрос.УстановитьПараметр("ОрганизацияОткуда", Организация);
		Запрос.УстановитьПараметр("Период1", НачалоМесяца(Месяц));
		Запрос.УстановитьПараметр("Период2", КонецМесяца(Месяц));
		
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		
	КонецЦикла;
	
	Если РезультатДляВыгрузки.Количество() <> 0 Тогда
		//после выхода из цикла
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РезультатЗапроса, РезультатДляВыгрузки);
		
	КонецЕсли;
		
	Возврат РезультатДляВыгрузки;
		
КонецФункции

// Получение таблицы пересортицы товаров за период
//
// Параметры:
//  Дата - Строка - дата в формате "ГГГГ-ММ-ДД", за месяц которой нужно получить данные
//
// Возвращаемое значение:
//  ТаблицаЗначений - таблица с данными по пересортице товаров со структурой:
//   * Номенклатура - Строка - наименование номенклатуры списания
//   * НоменклатураКод - Строка - код номенклатуры списания
//   * НоменклатураОприходование - Строка - наименование номенклатуры оприходования
//   * НоменклатураОприходованияКод - Строка - код номенклатуры оприходования
//   * Количество - Число - количество
//   * Склад - Строка - наименование склада
//   * Дата - Строка - дата документа
//   * Номер - Строка - номер документа
//   * Подразделение - Строка - наименование подразделения
//   * Организация - Строка - наименование организации
//   * ОрганизацияИНН - Строка - ИНН организации (10 символов)
//   * НомерСтрокиДокумента - Число - номер строки в документе
//
Функция ПолучитьТаблицуПересортицы(Дата) Экспорт
	
	РезультатДляВыгрузки = Новый ТаблицаЗначений;
	РезультатЗапроса = Новый ТаблицаЗначений;
	МассивСсылокОрганизации = ПолучитьСписокОрганизаций();
	
	Для Каждого Организация из МассивСсылокОрганизации Цикл
		
		Если РезультатДляВыгрузки.Количество() = 0 Тогда
			
			РезультатДляВыгрузки = РезультатЗапроса.Скопировать(); //сохраним результат запроса
					
		ИначеЕсли РезультатДляВыгрузки.Количество() <> 0 Тогда
			//Объеденим если несколько таблиц
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РезультатЗапроса, РезультатДляВыгрузки);//здесь уже нужно объеденить
		
		КонецЕсли;
				
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(ПересортицаТоваровТовары.Номенклатура) КАК Номенклатура,
		|	ПРЕДСТАВЛЕНИЕ(ПересортицаТоваровТовары.НоменклатураОприходование) КАК НоменклатураОприходование,
		|	СУММА(ПересортицаТоваровТовары.Количество) КАК Количество,
		|	ПРЕДСТАВЛЕНИЕ(ПересортицаТоваровТовары.Ссылка.Склад) КАК Склад,
		|	СТРОКА(ПересортицаТоваровТовары.Ссылка.Дата) КАК Дата,
		|	ПересортицаТоваровТовары.Ссылка.Номер КАК Номер,
		|	ПРЕДСТАВЛЕНИЕ(ПересортицаТоваровТовары.Ссылка.Подразделение) КАК Подразделение,
		|	ПРЕДСТАВЛЕНИЕ(ПересортицаТоваровТовары.Ссылка.Организация) КАК Организация,
		|	ПересортицаТоваровТовары.Номенклатура.Код КАК НоменклатураКод,
		|	ПересортицаТоваровТовары.НоменклатураОприходование.Код КАК НоменклатураОприходованияКод,
		|	ПОДСТРОКА(СТРОКА(ПересортицаТоваровТовары.Ссылка.Организация.ИНН), 1, 10) КАК ОрганизацияИНН,
		|	ПересортицаТоваровТовары.НомерСтроки КАК НомерСтрокиДокумента
		|ИЗ
		|	Документ.ПересортицаТоваров.Товары КАК ПересортицаТоваровТовары
		|ГДЕ
		|	ПересортицаТоваровТовары.Ссылка.Дата МЕЖДУ &ДатаН И &ДатаК
		|	И ПересортицаТоваровТовары.Ссылка.Проведен
		|	И ПересортицаТоваровТовары.Ссылка.Организация = &Организация
		|
		|СГРУППИРОВАТЬ ПО
		|	ПересортицаТоваровТовары.Ссылка.Номер,
		|	ПересортицаТоваровТовары.Номенклатура.Код,
		|	ПересортицаТоваровТовары.НоменклатураОприходование.Код,
		|	СТРОКА(ПересортицаТоваровТовары.Ссылка.Дата),
		|	ПОДСТРОКА(СТРОКА(ПересортицаТоваровТовары.Ссылка.Организация.ИНН), 1, 10),
		|	ПересортицаТоваровТовары.НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕ(ПересортицаТоваровТовары.Номенклатура),
		|	ПРЕДСТАВЛЕНИЕ(ПересортицаТоваровТовары.НоменклатураОприходование),
		|	ПРЕДСТАВЛЕНИЕ(ПересортицаТоваровТовары.Ссылка.Склад),
		|	ПРЕДСТАВЛЕНИЕ(ПересортицаТоваровТовары.Ссылка.Подразделение),
		|	ПРЕДСТАВЛЕНИЕ(ПересортицаТоваровТовары.Ссылка.Организация)
		|";
		
	Месяц = Дата(СтрЗаменить(Лев(Дата, 10),"-",""));
		
	Запрос.УстановитьПараметр("ДатаН", НачалоМесяца(Месяц));
	Запрос.УстановитьПараметр("ДатаК", КонецМесяца(Месяц));
	Запрос.УстановитьПараметр("Организация", Организация);
		
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		
	КонецЦикла;
	
	Если РезультатДляВыгрузки.Количество() <> 0 Тогда
		//после выхода из цикла
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РезультатЗапроса, РезультатДляВыгрузки);
		
	КонецЕсли;
		
	Возврат РезультатДляВыгрузки;
	
КонецФункции

// Добавление регламентного задания на создание сверки из БП
//
// Параметры:
//  Дата - Строка - дата в формате "ГГГГ-ММ-ДД", за месяц которой нужно создать сверку
//  Принудительно - Булево - признак принудительного создания документов свертки,
//                           даже если они уже были созданы ранее
//
// Возвращаемое значение:
//  Строка - результат выполнения операции с информацией о статусе по каждой организации
//
&НаСервере
Функция ДобавитьРегламентноеЗаданиеНаСервере(Дата, Принудительно) Экспорт
	
	Месяц = Дата(СтрЗаменить(Лев(Дата, 10),"-","")); //дата из БП
	
	МассивСсылокОрганизации = ПолучитьСписокОрганизацийВГО(); //организации из константы
	
	Ответ = "";
		
	Для Каждого Организация из МассивСсылокОрганизации Цикл
		
		ОписаниеЗадания = РегистрыСведений.ПЛ_ЗаданияСверткиДокументовДляБП.ПолучитьРегламентноеЗаданиеПоОрганизации(Организация);	
		
		ИмяПользователя = ОписаниеЗадания.Задание.ИмяПользователя;
		
		Если ПроверитьДатуЗапрета(ИмяПользователя, Месяц) Тогда
			
			Ответ = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Для пользователя в 1С ERP %1, установлен запрет измения на требуемую дату.  
					| Для создания документов свертки, откройте период в 1С ERP для пользователя %2 , 
					| на последний день требуемого месяца.'"), ИмяПользователя, ИмяПользователя);	
			
		Иначе	
		
			Если ПроверитьСтатусЗаданияСвертки(ОписаниеЗадания, Месяц, Принудительно) Тогда
				
				Организации = Новый Массив;
				Организации.Добавить(Организация);
			
				ПараметрыЗадания = Новый Структура;
				ПараметрыЗадания.Вставить("Использование", Истина);
				ПараметрыЗадания.Вставить("Параметры",     Организации);
				
				Если ОписаниеЗадания.Задание = Неопределено Тогда
				
					ПараметрыЗадания.Вставить("Наименование", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Свёртка документов для БП: %1'"), СокрЛП(Организация)));
					ПараметрыЗадания.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ПЛ_СверткаДокументовДляБП);
					
					ОписаниеЗадания.Задание = РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
						
				Иначе
				
					РегламентныеЗаданияСервер.ИзменитьЗадание(ОписаниеЗадания.Задание, ПараметрыЗадания);
				
				КонецЕсли;
			
				ОписаниеЗадания.ИдентификаторЗадания = РегламентныеЗаданияСервер.УникальныйИдентификатор(ОписаниеЗадания.Задание);
				ОписаниеЗадания.СостояниеЗадания 	 = ОбщегоНазначенияУТ.ПолучитьСостояниеПоследнегоЗадания(ОписаниеЗадания.Задание);
				ОписаниеЗадания.Использование 		 = Истина;
				ОписаниеЗадания.Месяц	 			 = Месяц;
				
				Ответ = Ответ + Символы.ПС + РегистрыСведений.ПЛ_ЗаданияСверткиДокументовДляБП.ЗаписатьРегламентноеЗаданиеПоОрганизации(ОписаниеЗадания);
				
			Иначе	
				
				ДатаОтвет = Формат(КонецМесяца(ОписаниеЗадания.Месяц), "ДФ=dd.MM.yyyy");
				
				Ответ = Ответ + Символы.ПС + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Создание документов по организации: %1, на дату %2 , было выполнено ранее.
					| Отправте ""принудильно"" для повторного создания документов свертки.
					| Внимание! Документы будут перезаписаны и зарегистрированы к обмену в Бухгалтерия Предприятия'"), ОписаниеЗадания.Организация, ДатаОтвет);
			  			
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ответ;
	
КонецФункции

// Проверка статуса задания свертки документов
//
// Параметры:
//  ОписаниеЗадания - Структура - описание задания свертки
//  Месяц - Дата - месяц для проверки
//  Принудительно - Булево - признак принудительного выполнения
//
// Возвращаемое значение:
//  Булево - Истина, если задание нужно выполнять
//
Функция ПроверитьСтатусЗаданияСвертки(ОписаниеЗадания, Месяц, Принудительно)
	
	Если Принудительно Тогда
		
		Возврат Истина;
	
	ИначеЕсли СТРОКА(ОписаниеЗадания.СостояниеЗадания) = "Задание выполнено" И ОписаниеЗадания.Месяц = Месяц Тогда
		
		Возврат Ложь;
		
	Иначе
		
		Возврат Истина;
		
	КонецЕсли
		
КонецФункции

// Получение списка физических лиц из внешней базы и синхронизация с текущей базой
//
// Описание:
//  Процедура предназначена для вызова из регламентного задания.
//  Выполняет синхронизацию физических лиц из внешней базы ЗУП:
//   - Получает данные физических лиц из внешней базы
//   - Обрабатывает найденные физические лица (обновляет дополнительные сведения)
//   - Создает новые физические лица для не найденных записей
//   - Связывает физические лица с пользователями системы
//  Все ошибки обрабатываются и записываются в журнал регистрации.
//
// Параметры:
//  КоличествоЗаписей - Число - количество записей для обработки за один вызов (по умолчанию 50)
//
Процедура ПолучитьСписокФизическихЛиц(КоличествоЗаписей = 50) Экспорт
	
	ИмяСобытия = "MRS.СинхронизацияФизическихЛиц";
	НачалоВыполнения = ТекущаяУниверсальнаяДата();
	
	ЗаписьЖурналаРегистрации(
		ИмяСобытия,
		УровеньЖурналаРегистрации.Информация,
		,
		,
		"Начало синхронизации физических лиц из внешней базы ЗУП");
	
	Попытка
		
		// Получение данных из внешней базы
		РезультатМетода = ПолучитьДанныеФизическихЛицИзВнешнейБазы(КоличествоЗаписей);
		
		Если РезультатМетода = Неопределено Тогда
			
			ВызватьИсключение "Не удалось получить данные из внешней базы.";
			
		КонецЕсли;
		
		// Обработка полученных данных
		РезультатОбработки = ОбработатьДанныеФизическихЛиц(РезультатМетода);
		
		// Формирование итогового сообщения
		ДлительностьВыполнения = ТекущаяУниверсальнаяДата() - НачалоВыполнения;
		
		ТекстСообщения = СтрШаблон(
			"Синхронизация завершена успешно за %1 сек.
			|Обработано физических лиц: найдено %2, создано %3, обновлено %4, пропущено с ошибками %5",
			Окр(ДлительностьВыполнения, 2),
			РезультатОбработки.КоличествоНайденных,
			РезультатОбработки.КоличествоСозданных,
			РезультатОбработки.КоличествоОбновленных,
			РезультатОбработки.КоличествоОшибок);
		
		ЗаписьЖурналаРегистрации(
			ИмяСобытия,
			УровеньЖурналаРегистрации.Информация,
			,
			,
			ТекстСообщения);
		
	Исключение
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(
			ИмяСобытия,
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			СтрШаблон("Ошибка синхронизации физических лиц: %1", ТекстОшибки));
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

// Получение списка всех организаций из справочника
//
// Возвращаемое значение:
//  Массив - массив ссылок на организации
//
&НаСервере
Функция ПолучитьСписокОрганизаций()
	
	МассивОрганизаций = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	Организации.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.НаименованиеПолное <> """"
		|";
	Результат = Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() Цикл 
		
		МассивОрганизаций.Добавить(Результат.Ссылка);	
		
	КонецЦикла;
	
	Возврат МассивОрганизаций; 
	
КонецФункции 

// Получение списка организаций для сверок по дополнительной константе
//
// Возвращаемое значение:
//  Массив - массив ссылок на организации ВГО
//
Функция ПолучитьСписокОрганизацийВГО()
	
	ИннОрганизацийВГОСтрокой = ПЛ_ДополнительныеКонстантыПовтИсп.ВернутьДополнительнуюКонстанту("MRS_ОрганизацииВГО");
	
	МассивИННОрганизацийВГО = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИннОрганизацийВГОСтрокой,";");
	
	МассивОрганизаций = Новый Массив;
	
	Для Каждого ИНН из МассивИННОрганизацийВГО Цикл 
	
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	Организации.Ссылка КАК Ссылка
		               |ИЗ
		               |	Справочник.Организации КАК Организации
		               |ГДЕ
		               |	Организации.ИНН = &ИНН";
		Запрос.УстановитьПараметр("ИНН",ИНН);
		
		Результат = Запрос.Выполнить().Выбрать();
		
		Пока Результат.Следующий() Цикл 
			
			МассивОрганизаций.Добавить(Результат.Ссылка);	
			
		КонецЦикла;
	
	КонецЦикла;

	Возврат МассивОрганизаций;
	
КонецФункции

// Получение статусов заданий свертки или синхронизации
//
// Параметры:
//  Дата - Строка - дата в формате "ГГГГ-ММ-ДД", за месяц которой нужно получить статус
//  Тип - Строка - тип задания: "Свертки" или "Синхронизация"
//
// Возвращаемое значение:
//  Строка - информация о статусах заданий
//
Функция ПолучитьСтатусыЗаданий(Дата, Тип) Экспорт
	
	Месяц = Дата(СтрЗаменить(Лев(Дата, 10),"-","")); //дата из БП
	
	Ответ = "";
	
	Если Тип = "Свертки" Тогда
		
		МассивСсылокОрганизации = ПолучитьСписокОрганизацийВГО(); //организации из константы
		
		Для Каждого Организация из МассивСсылокОрганизации Цикл
			
			ОписаниеЗадания = РегистрыСведений.ПЛ_ЗаданияСверткиДокументовДляБП.ПолучитьРегламентноеЗаданиеПоОрганизации(Организация);	
			
			Ответ = Ответ + Символы.ПС + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Создание документов по организации: %1, на дату %2 .
					| Статус: %3.
					| Статус предоставлен по последнему инициированному заданию.'"), ОписаниеЗадания.Организация, Формат(КонецМесяца(ОписаниеЗадания.Месяц), "ДФ=dd.MM.yyyy"), СТРОКА(ОписаниеЗадания.СостояниеЗадания));
			
		КонецЦикла;	
		
	ИначеЕсли Тип = "Синхронизация" Тогда
		
		Текст = "
			|ВЫБРАТЬ
			|	ПРЕДСТАВЛЕНИЕ(СостоянияОбменовДанными.УзелИнформационнойБазы) КАК УзелИнформационнойБазы,
			|	СостоянияОбменовДанными.РезультатВыполненияОбмена КАК РезультатВыполненияОбмена,
			|	СостоянияОбменовДанными.ДатаНачала КАК ДатаНачала,
			|	СостоянияОбменовДанными.ДатаОкончания КАК ДатаОкончания
			|ИЗ
			|	РегистрСведений.СостоянияОбменовДанными КАК СостоянияОбменовДанными
			|ГДЕ
			|	СостоянияОбменовДанными.УзелИнформационнойБазы.Код = &Код
			|	И СостоянияОбменовДанными.ДействиеПриОбмене = &ДействиеПриОбмене
			|";

		Запрос = Новый Запрос(Текст);

		Код = "23877860-c6dd-4f63-9b2e-e3952b38b693"; //код синх. гарант\кипарис

		ДействиеПриОбмене = Перечисления.ДействияПриОбмене.ВыгрузкаДанных; // Отправка данных

		// Установка параметров.
		Запрос.УстановитьПараметр("ДействиеПриОбмене", ДействиеПриОбмене);
		Запрос.УстановитьПараметр("Код", Код);


		Выборка = Запрос.Выполнить().Выбрать();

		Пока Выборка.Следующий() Цикл

			Узел = Выборка.УзелИнформационнойБазы;
			Результат = Выборка.РезультатВыполненияОбмена;
			ДатаНачала = Строка(Выборка.ДатаНачала);
			ДатаОкончания = Строка(Выборка.ДатаОкончания);
			
		КонецЦикла;	
		
		Ответ = Ответ + Символы.ПС + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Статус синхронизации ERP - Бухалтерия Предприятия:
					| Узел: %1.
					| Статус: %2
					| Дата Начала: %3
					| Дата Окончания: %4
					| Внимание! При статусе ошибка уточните статус у сотрудников отдела 1С.'"), Узел, Результат, ДатаНачала, ДатаОкончания);
		
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции

// Проверка даты запрета изменения для пользователя
//
// Параметры:
//  Пользователь - Строка - имя пользователя информационной базы
//  Дата - Дата - дата для проверки запрета
//
// Возвращаемое значение:
//  Булево - Истина, если дата запрета установлена и больше или равна дате проверки, иначе Ложь
//
Функция ПроверитьДатуЗапрета(Пользователь, Дата)

	ПользовательИБ = Пользователи.НайтиПоИмени(Пользователь);
	
	Текст = "
		|ВЫБРАТЬ
		|	ДатыЗапретаИзменения.ДатаЗапрета КАК ДатаЗапрета
		|ИЗ
		|	РегистрСведений.ДатыЗапретаИзменения КАК ДатыЗапретаИзменения
		|ГДЕ
		|	ДатыЗапретаИзменения.Пользователь = &Пользователь
		|";

	Запрос = Новый Запрос(Текст);
                          	
	Запрос.УстановитьПараметр("Пользователь", ПользовательИБ);

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл

		ДатаЗапретаПользователя = Выборка.ДатаЗапрета;
		
	КонецЦикла;
	
	Если ДатаЗапретаПользователя = Неопределено Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Если ДатаЗапретаПользователя >= КонецМесяца(Дата) Тогда
		
		Возврат Истина;	
		
	Иначе 	
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

// Получение данных физических лиц из внешней базы ЗУП
//
// Параметры:
//  КоличествоЗаписей - Число - количество записей для обработки
//
// Возвращаемое значение:
//  ТаблицаЗначений, Неопределено - таблица с данными физических лиц или Неопределено в случае ошибки
//
Функция ПолучитьДанныеФизическихЛицИзВнешнейБазы(КоличествоЗаписей)
	
	ИмяСобытия = "MRS.СинхронизацияФизическихЛиц";
	
	Попытка
		
		ОписаниеЗапроса = ОДМ_ОбменДанными.Конструктор_ОписаниеЗапросаВнешнейБазы();
		ОписаниеЗапроса.ТекстЗапроса = 
			"ВЫБРАТЬ
			|	СоответствиеВнешнимИБ.Ссылка КАК Настройка
			|ПОМЕСТИТЬ ВТНастройка
			|ИЗ
			|	Справочник.СоответствиеВнешнимИБ КАК СоответствиеВнешнимИБ
			|ГДЕ
			|	СоответствиеВнешнимИБ.ИмяОбъектаМетаданных = ""ФизическиеЛица""
			|	И СоответствиеВнешнимИБ.Владелец.Наименование ПОДОБНО ""%ЗУП%""
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ ПЕРВЫЕ " + Формат(КоличествоЗаписей, "ЧГ=") + "
			|	СоответствиеОбъектовУИД.ОбъектВнешнейИБ КАК УИДЗУП,
			|	ФизическиеЛица.Наименование КАК Наименование,
			|	ФизическиеЛица.ДатаРождения КАК ДатаРождения,
			|	ФизическиеЛица.Пол КАК Пол,
			|	ФизическиеЛица.ИНН КАК ИНН,
			|	Почта.АдресЭП КАК АдресЭП,
			|	Телефон.НомерТелефона КАК НомерТелефона
			|ИЗ
			|	РегистрСведений.СоответствиеОбъектовТекущейИВнешнихИБ КАК СоответствиеОбъектовУИД
			|		ПРАВОЕ СОЕДИНЕНИЕ ВТНастройка КАК ВТНастройка
			|		ПО (СоответствиеОбъектовУИД.НастройкаСоответствия В (ВТНастройка.Настройка))
			|		ПРАВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
			|		ПО СоответствиеОбъектовУИД.ОбъектТекущейИБ = ФизическиеЛица.Ссылка
			|		ПРАВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица.КонтактнаяИнформация КАК Почта
			|		ПО СоответствиеОбъектовУИД.ОбъектТекущейИБ = Почта.Ссылка
			|		ПРАВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица.КонтактнаяИнформация КАК Телефон
			|		ПО СоответствиеОбъектовУИД.ОбъектТекущейИБ = Телефон.Ссылка
			|ГДЕ
			|	Почта.Вид.ИдентификаторДляФормул = ""Email""
			|	И Телефон.Вид.ИдентификаторДляФормул = ""МобильныйТелефон""";
		
		ВнешнийИнтерфейс = ОДМ_ОбменДанными.Конструктор_ВнешнийИнтерфейс();
		ВнешнийИнтерфейс.ЗаполнитьПараметрыПодключения("UH");
		РезультатМетода = ВнешнийИнтерфейс.ОтправитьЗапросНаСервер(ОписаниеЗапроса, Ложь);
		
		Если РезультатМетода = Неопределено Или ТипЗнч(РезультатМетода) <> Тип("ТаблицаЗначений") Тогда
			
			ЗаписьЖурналаРегистрации(
				ИмяСобытия,
				УровеньЖурналаРегистрации.Предупреждение,
				,
				,
				"Получен некорректный результат от внешней базы.");
			
			Возврат Неопределено;
			
		КонецЕсли;
		
		ЗаписьЖурналаРегистрации(
			ИмяСобытия,
			УровеньЖурналаРегистрации.Информация,
			,
			,
			СтрШаблон("Получено записей из внешней базы ЗУП: %1", РезультатМетода.Количество()));
		
		Возврат РезультатМетода;
		
	Исключение
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(
			ИмяСобытия,
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			СтрШаблон("Ошибка получения данных из внешней базы: %1", ТекстОшибки));
		
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

// Обработка данных физических лиц, полученных из внешней базы
//
// Параметры:
//  РезультатМетода - ТаблицаЗначений - данные физических лиц из внешней базы
//
// Возвращаемое значение:
//  Структура - статистика обработки:
//   * КоличествоНайденных - Число - количество найденных физических лиц
//   * КоличествоСозданных - Число - количество созданных физических лиц
//   * КоличествоОбновленных - Число - количество обновленных физических лиц
//   * КоличествоОшибок - Число - количество ошибок при обработке
//
Функция ОбработатьДанныеФизическихЛиц(РезультатМетода)
	
	Результат = Новый Структура;
	Результат.Вставить("КоличествоНайденных", 0);
	Результат.Вставить("КоличествоСозданных", 0);
	Результат.Вставить("КоличествоОбновленных", 0);
	Результат.Вставить("КоличествоОшибок", 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|   РезультатЗапроса.УИДЗУП КАК УИДЗУП,
		|   РезультатЗапроса.Наименование КАК Наименование,
		|   РезультатЗапроса.ДатаРождения КАК ДатаРождения,
		|   РезультатЗапроса.Пол КАК Пол,
		|   РезультатЗапроса.ИНН КАК ИНН,
		|   РезультатЗапроса.АдресЭП КАК АдресЭП,
		|   РезультатЗапроса.НомерТелефона КАК НомерТелефона,
		|   ВЫБОР
		|       КОГДА СТРНАЙТИ(РезультатЗапроса.АдресЭП, ""@"") > 0
		|           ТОГДА ПОДСТРОКА(
		|                   РезультатЗапроса.АдресЭП,
		|                   1,
		|                   СТРНАЙТИ(РезультатЗапроса.АдресЭП, ""@"") - 1)
		|       ИНАЧЕ """"""""
		|   КОНЕЦ КАК ЛогинЭП
		|ПОМЕСТИТЬ ВТ_РезультатЗапроса
		|ИЗ
		|   &РезультатЗапроса КАК РезультатЗапроса
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|   ВТ_РезультатЗапроса.УИДЗУП КАК УИДЗУП,
		|   ВТ_РезультатЗапроса.Наименование КАК Наименование,
		|   ВТ_РезультатЗапроса.ДатаРождения КАК ДатаРождения,
		|   ВТ_РезультатЗапроса.Пол КАК Пол,
		|   ВТ_РезультатЗапроса.ИНН КАК ИНН,
		|   ВТ_РезультатЗапроса.АдресЭП КАК АдресЭП,
		|   ВТ_РезультатЗапроса.НомерТелефона КАК НомерТелефона,
		|   ФизическиеЛица.Ссылка КАК ФизическоеЛицо,
		|   СведенияОПользователях.Пользователь КАК Пользователь,
		|   СведенияОПользователях.ПользовательОС КАК ПользовательОС
		|ИЗ
		|   ВТ_РезультатЗапроса КАК ВТ_РезультатЗапроса
		|   ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|       ПО (ПОДСТРОКА(ВТ_РезультатЗапроса.Наименование, 1, 100)
		|           = ПОДСТРОКА(ФизическиеЛица.Наименование, 1, 100))
		|   ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
		|       ПО СведенияОПользователях.ВходВПрограммуРазрешен
		|          И СведенияОПользователях.ПользовательОС <> """"""""
		|           И СТРНАЙТИ(СведенияОПользователях.ПользовательОС, ВТ_РезультатЗапроса.ЛогинЭП) > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|    ВТ_РезультатЗапроса.УИДЗУП КАК УИДЗУП,
		|    ВТ_РезультатЗапроса.Наименование КАК Наименование,
		|    ВТ_РезультатЗапроса.ДатаРождения КАК ДатаРождения,
		|    ВТ_РезультатЗапроса.Пол КАК Пол,
		|    ВТ_РезультатЗапроса.ИНН КАК ИНН,
		|    ВТ_РезультатЗапроса.АдресЭП КАК АдресЭП,
		|    ВТ_РезультатЗапроса.НомерТелефона КАК НомерТелефона,
		|    ВТ_РезультатЗапроса.ЛогинЭП КАК ЛогинЭП,
		|    СведенияОПользователях.Пользователь КАК Пользователь,
		|    СведенияОПользователях.ПользовательОС КАК ПользовательОС
		|ИЗ
		|    ВТ_РезультатЗапроса КАК ВТ_РезультатЗапроса
		|    ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|        ПО (ПОДСТРОКА(ВТ_РезультатЗапроса.Наименование, 1, 100)
		|            = ПОДСТРОКА(ФизическиеЛица.Наименование, 1, 100))
		|    ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОПользователях КАК СведенияОПользователях
		|        ПО СведенияОПользователях.ВходВПрограммуРазрешен
		|           И СведенияОПользователях.ПользовательОС <> """"
		|           И СТРНАЙТИ(СведенияОПользователях.ПользовательОС, ВТ_РезультатЗапроса.ЛогинЭП) > 0
		|ГДЕ
		|    ФизическиеЛица.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("РезультатЗапроса", РезультатМетода);
	РезультатПакета = Запрос.ВыполнитьПакет();
	
	РезультатНайденных   = РезультатПакета[1];
	РезультатНеНайденных = РезультатПакета[2];
	
	ТаблицаНайденных   = РезультатНайденных.Выгрузить();
	ТаблицаНеНайденных = РезультатНеНайденных.Выгрузить();
	
	Результат.КоличествоНайденных = ТаблицаНайденных.Количество();
	
	// Получение дополнительного реквизита ГУИД ЗУП
	ГУИДЗУП_Доп = ПолучитьДопРеквизитГУИДЗУП();
	
	// Обработка найденных физических лиц
	КоличествоОбновленных = ОбработатьНайденныхФизическихЛиц(ТаблицаНайденных, ГУИДЗУП_Доп);
	Результат.КоличествоОбновленных = КоличествоОбновленных;
	
	// Обработка не найденных физических лиц (создание новых)
	КоличествоСозданных = ОбработатьНеНайденныхФизическихЛиц(ТаблицаНеНайденных);
	Результат.КоличествоСозданных = КоличествоСозданных;
	
	Возврат Результат;
	
КонецФункции

// Получение дополнительного реквизита ГУИД ЗУП
//
// Возвращаемое значение:
//  ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения, Неопределено - ссылка на дополнительный реквизит
//
Функция ПолучитьДопРеквизитГУИДЗУП()
	
	ИмяСобытия = "MRS.СинхронизацияФизическихЛиц";
	
	ГУИДЗУП_Доп = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("ИдентификаторДляФормул", "ГУИДЗУП");
	
	ДопРекиСведПустаяСсылка = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка();
	
	Если ГУИДЗУП_Доп = ДопРекиСведПустаяСсылка Тогда
		
		ТекстОшибки = "Не найден дополнительный реквизит 'ГУИДЗУП' в плане видов характеристик 'ДополнительныеРеквизитыИСведения'";
		
		ЗаписьЖурналаРегистрации(
			ИмяСобытия,
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ТекстОшибки);
		
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	Возврат ГУИДЗУП_Доп;
	
КонецФункции

// Обработка найденных физических лиц (обновление дополнительных сведений)
//
// Параметры:
//  ТаблицаНайденных - ТаблицаЗначений - таблица с найденными физическими лицами
//  ГУИДЗУП_Доп - ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения - дополнительный реквизит ГУИД ЗУП
//
// Возвращаемое значение:
//  Число - количество обновленных записей
//
Функция ОбработатьНайденныхФизическихЛиц(ТаблицаНайденных, ГУИДЗУП_Доп)
	
	ИмяСобытия = "MRS.СинхронизацияФизическихЛиц";
	КоличествоОбновленных = 0;
	
	Для Каждого СтрокаНайденныхФЛ Из ТаблицаНайденных Цикл
		
		Попытка
			
			// Запись дополнительных сведений (ГУИД ЗУП)
			ДопСведенияНаборЗаписей = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
			ДопСведенияНаборЗаписей.Отбор.Объект.Установить(СтрокаНайденныхФЛ.ФизическоеЛицо);
			ДопСведенияНаборЗаписей.Отбор.Свойство.Установить(ГУИДЗУП_Доп);
			ДопСведенияНаборЗаписей.Прочитать();
			
			Если ДопСведенияНаборЗаписей.Количество() = 0 Тогда
				НовЗапись = ДопСведенияНаборЗаписей.Добавить();
			Иначе
				НовЗапись = ДопСведенияНаборЗаписей[0];
			КонецЕсли;
			
			НовЗапись.Объект = СтрокаНайденныхФЛ.ФизическоеЛицо;
			НовЗапись.Свойство = ГУИДЗУП_Доп;
			НовЗапись.Значение = СтрокаНайденныхФЛ.УИДЗУП;
			ДопСведенияНаборЗаписей.Записать(Истина);
			
			// Связывание с пользователем, если пользователь заполнен
			Если ЗначениеЗаполнено(СтрокаНайденныхФЛ.Пользователь) Тогда
				
				ПользовательОбъект = СтрокаНайденныхФЛ.Пользователь.ПолучитьОбъект();
				
				Если НЕ ЗначениеЗаполнено(ПользовательОбъект.ФизическоеЛицо) Тогда
					
					ПользовательОбъект.ФизическоеЛицо = СтрокаНайденныхФЛ.ФизическоеЛицо;
					ПользовательОбъект.Записать();
					
				КонецЕсли;
				
			КонецЕсли;
			
			КоличествоОбновленных = КоличествоОбновленных + 1;
			
		Исключение
			
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ЗаписьЖурналаРегистрации(
				ИмяСобытия,
				УровеньЖурналаРегистрации.Предупреждение,
				,
				СтрокаНайденныхФЛ.ФизическоеЛицо,
				СтрШаблон("Ошибка обновления физического лица '%1': %2", СтрокаНайденныхФЛ.Наименование, ТекстОшибки));
			
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат КоличествоОбновленных;
	
КонецФункции

// Обработка не найденных физических лиц (создание новых)
//
// Параметры:
//  ТаблицаНеНайденных - ТаблицаЗначений - таблица с не найденными физическими лицами
//
// Возвращаемое значение:
//  Число - количество созданных записей
//
Функция ОбработатьНеНайденныхФизическихЛиц(ТаблицаНеНайденных)
	
	ИмяСобытия = "MRS.СинхронизацияФизическихЛиц";
	КоличествоСозданных = 0;
	
	Для Каждого СтрокаНеНайденныхФЛ Из ТаблицаНеНайденных Цикл
		
		Попытка
			
			// Создание нового физического лица
			НовоеФизЛицо = СоздатьФизическоеЛицо(СтрокаНеНайденныхФЛ);
			
			Если НовоеФизЛицо = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			// Связывание с пользователем
			Если ЗначениеЗаполнено(СтрокаНеНайденныхФЛ.Пользователь) Тогда
				
				ПользовательОбъект = СтрокаНеНайденныхФЛ.Пользователь.ПолучитьОбъект();
				ПользовательОбъект.ФизическоеЛицо = НовоеФизЛицо;
				ПользовательОбъект.Записать();
				
			КонецЕсли;
			
			КоличествоСозданных = КоличествоСозданных + 1;
			
		Исключение
			
			ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ЗаписьЖурналаРегистрации(
				ИмяСобытия,
				УровеньЖурналаРегистрации.Предупреждение,
				,
				,
				СтрШаблон("Ошибка создания физического лица '%1': %2", СтрокаНеНайденныхФЛ.Наименование, ТекстОшибки));
			
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат КоличествоСозданных;
	
КонецФункции

// Создание нового физического лица
//
// Параметры:
//  ДанныеФизЛица - СтрокаТаблицыЗначений - данные физического лица из внешней базы
//
// Возвращаемое значение:
//  СправочникСсылка.ФизическиеЛица, Неопределено - ссылка на созданное физическое лицо или Неопределено при ошибке
//
Функция СоздатьФизическоеЛицо(ДанныеФизЛица)
	
	ИмяСобытия = "MRS.СинхронизацияФизическихЛиц";
	
	// Проверка обязательных данных
	Если НЕ ЗначениеЗаполнено(ДанныеФизЛица.Наименование) Тогда
		
		ЗаписьЖурналаРегистрации(
			ИмяСобытия,
			УровеньЖурналаРегистрации.Предупреждение,
			,
			,
			"Пропуск создания физического лица: не заполнено наименование");
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	ФизЛицоОбъект = Справочники.ФизическиеЛица.СоздатьЭлемент();
	
	ФизЛицоОбъект.Наименование = ДанныеФизЛица.Наименование;
	ФизЛицоОбъект.ФИО = ДанныеФизЛица.Наименование;
	
	// Разбор ФИО на составляющие
	ФИОСтрока = СокрЛП(ДанныеФизЛица.Наименование); 
	МассивФИО = СтрРазделить(ФИОСтрока, " ");
	
	Фамилия  = ?(МассивФИО.Количество() > 0, МассивФИО[0], "");
	Имя      = ?(МассивФИО.Количество() > 1, МассивФИО[1], "");
	Отчество = ?(МассивФИО.Количество() > 2, МассивФИО[2], "");
	
	ФизЛицоОбъект.Фамилия    = Фамилия;
	ФизЛицоОбъект.Имя        = Имя;
	ФизЛицоОбъект.Отчество   = Отчество;
	
	// Установка пола
	Если ДанныеФизЛица.Пол = "Мужской" Тогда 
		ФизЛицоОбъект.Пол = Перечисления.ПолФизическогоЛица.Мужской;
	Иначе
		ФизЛицоОбъект.Пол = Перечисления.ПолФизическогоЛица.Женский;
	КонецЕсли;
	
	// Установка даты рождения
	Если ЗначениеЗаполнено(ДанныеФизЛица.ДатаРождения) Тогда
		ФизЛицоОбъект.ДатаРождения = ДанныеФизЛица.ДатаРождения;
	КонецЕсли;
	
	// Добавление email
	Если ЗначениеЗаполнено(ДанныеФизЛица.АдресЭП) Тогда
		
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(
			ФизЛицоОбъект,
			ДанныеФизЛица.АдресЭП,
			Справочники.ВидыКонтактнойИнформации.EMailФизическиеЛица,
			ТекущаяДатаСеанса(),
			Истина); // Замещать
		
	КонецЕсли;
	
	// Добавление телефона
	Если ЗначениеЗаполнено(ДанныеФизЛица.НомерТелефона) Тогда
		
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(
			ФизЛицоОбъект,
			ДанныеФизЛица.НомерТелефона,
			Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйФизическиеЛица,
			ТекущаяДатаСеанса(),
			Истина); // Замещать
		
	КонецЕсли;
	
	ФизЛицоОбъект.Записать();
	
	Возврат ФизЛицоОбъект.Ссылка;
	
КонецФункции

#КонецОбласти // СлужебныеПроцедурыИФункции

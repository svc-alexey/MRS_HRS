#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
    ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
    ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительныйОтчет();
    ПараметрыРегистрации.Версия = "1.0"; 
    ПараметрыРегистрации.Наименование = "Себестоимость Рецептур ДМИО (mrs)";
    ПараметрыРегистрации.БезопасныйРежим = Ложь; 
    ПараметрыРегистрации.Информация = 
        "Дополнительный отчет: Себестоимость Рецептур ДМИО (mrs)";
    
    ПараметрыРегистрации.Команды = ПолучитьТаблицуКоманд();
    ДобавитьКоманду(ПараметрыРегистрации.Команды,
        "Себестоимость Рецептур ДМИО (mrs)",
        "Себестоимость Рецептур ДМИО (mrs)",
        ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы(),
        Истина);
    Возврат ПараметрыРегистрации;
КонецФункции 

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТаблицуКоманд()
    Команды = Новый ТаблицаЗначений;
    Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка")); 
    Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка"));
    Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка"));
    Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево"));
    Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка"));
    Возврат Команды;
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
    НоваяКоманда = ТаблицаКоманд.Добавить();
    НоваяКоманда.Представление = Представление;
    НоваяКоманда.Идентификатор = Идентификатор;
    НоваяКоманда.Использование = Использование;
    НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
    НоваяКоманда.Модификатор = Модификатор;
КонецПроцедуры

// Процедура формирования отчета о себестоимости рецептур ДМИО
// Формирует табличный документ с калькуляционными карточками актуальных рецептур с расчетом себестоимости
//
// Параметры:
//  ДокументРезультат - ТабличныйДокумент - результирующий документ
//  ДанныеРасшифровки - ДанныеРасшифровкиКомпоновкиДанных - данные расшифровки
//  СтандартнаяОбработка - Булево - флаг стандартной обработки
//
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	// Отключаем стандартную обработку СКД - формируем отчет программно
	СтандартнаяОбработка = Ложь;
	
	НастройкиКомпоновки = КомпоновщикНастроек.ПолучитьНастройки();
    
    ЭлементыКомпоновки = НастройкиКомпоновки.ПараметрыДанных.Элементы;
    Подразделения = ЭлементыКомпоновки.Найти("Подразделения").Значение; 
	
	// Параметры для расчета себестоимости
	ДатаКалькуляции = ТекущаяДатаСеанса();
	
	// Оптимизированный запрос с группировкой кнопок
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ Первые 1
	|	АктРец.Рецептура КАК Рецептура,
	|	АктРец.Рецептура.Номер КАК КодРецептуры,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(АктРец.Рецептура) КАК НаименованиеРецептуры,
	|	АктРец.Номенклатура КАК Номенклатура,
	|	АктРец.Организация КАК Организация,
	|	АктРец.Подразделение КАК Подразделение
	|ИЗ
	|	РегистрСведений.питАктуальныеРецептуры КАК АктРец
	|ГДЕ
	|	АктРец.ДатаОкончания = &ДатаОкончания
	|	И АктРец.Подразделение В(&Подразделения)";
	
	Запрос.УстановитьПараметр("Подразделения", Подразделения);
	Запрос.УстановитьПараметр("ДатаОкончания", Дата(1, 1, 1));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		Возврат;
		
	КонецЕсли;
	
	// Получаем привязки к кнопкам фронт-систем
	ЗапросКнопок = Новый Запрос;
	ЗапросКнопок.Текст =
	"ВЫБРАТЬ
	|	Кнопки.Объект КАК Номенклатура,
	|	Кнопки.Код КАК Код,
	|	Кнопки.НаименованиеКнопки КАК НаименованиеКнопки
	|ИЗ
	|	РегистрСведений.ПЛ_СоответствиеСФронтСистемами КАК Кнопки";
	
	ТаблицаКнопок = ЗапросКнопок.Выполнить().Выгрузить();
	ТаблицаКнопок.Индексы.Добавить("Номенклатура");
	
	// Таблица значений для накопления данных отчета
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("КодРецептуры", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("НаименованиеРецептуры", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("Статус", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("КнопкиФронтСистемы", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("Материал", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("Строка"));
	ТаблицаДанных.Колонки.Добавить("НормаРасхода", Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("ЦенаМатериала", Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("СебестоимостьЕдиницы", Новый ОписаниеТипов("Число"));
	
	// Константы для расчета
	НеИспользоватьСезонныеПроценты = питОбщегоНазначенияСервер.ПолучитьЗначениеПоУмолчанию("питНеИспользоватьСезонныеПроценты");
	ТипЦенНормативная = Константы.питНормативнаяЦена.Получить();
	Валюта = Константы.ВалютаРегламентированногоУчета.Получить();
	
	// Обработка каждой рецептуры
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Рецептура = Выборка.Рецептура;
		Организация = Выборка.Организация;
		ПодразделениеРецептуры = Выборка.Подразделение;
		КодРецептуры = Выборка.КодРецептуры;
		НаименованиеРецептуры = Выборка.НаименованиеРецептуры;
		// Формируем строку с кнопками для данной номенклатуры
		СтрокиКнопок = ТаблицаКнопок.НайтиСтроки(Новый Структура("Номенклатура", Выборка.Номенклатура));
		
		Если СтрокиКнопок.Количество() > 0 Тогда
			
			МассивКнопок = Новый Массив;
			
			Для Каждого СтрокаКнопки Из СтрокиКнопок Цикл
				
				Если ЗначениеЗаполнено(СтрокаКнопки.Код) Тогда
					
					МассивКнопок.Добавить(СтрокаКнопки.Код + " / " + СтрокаКнопки.НаименованиеКнопки);
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если МассивКнопок.Количество() > 0 Тогда
				
				КнопкиФронтСистемы = СтрСоединить(МассивКнопок, " - ");
				
			Иначе
				
				КнопкиФронтСистемы = "Не привязана";
				
			КонецЕсли;
			
		Иначе
			
			КнопкиФронтСистемы = "Не привязана";
			
		КонецЕсли;
		
		// Получаем реквизиты рецептуры
		РецептураРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Рецептура, 
			"Количество, КоличествоУпаковок, Упаковка, ХозяйственнаяОперация, Номенклатура, Характеристика");
		
		// Таблица уточнений автовыбора
		УточненияАвтовыбора = питУправлениеДаннымиОбИзделиях.ПолучитьТаблицуУточненийАвтовыбораПоДокументу(Рецептура);
		
		// Структура для расчета блюда
		СтруктураРассчитываемогоБлюдо = Новый Структура("Блюдо,Характеристика,ЕдиницаИзмерения", 
			РецептураРеквизиты.Номенклатура, 
			РецептураРеквизиты.Характеристика, 
			РецептураРеквизиты.Упаковка);
		
		// Рассчитываем дерево состава рецептуры
		ДеревоСостава = питПроизводство.РассчитатьДеревоСоставаРецептуры(
			Рецептура,
			,
			РецептураРеквизиты.Количество,
			,
			,
			РецептураРеквизиты.КоличествоУпаковок,
			,
			,
			,
			,
			,
			,
			СтруктураРассчитываемогоБлюдо,
			,
			,
			,
			,
			,
			,
			,
			,
			ПодразделениеРецептуры,
			,
			УточненияАвтовыбора);
		
		// Проверяем наличие строк в дереве состава
		Если ДеревоСостава.Строки.Количество() = 0 Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		СтрокиДерева = ДеревоСостава.Строки[0].Строки;
		
		// Коэффициент разделки
		КоэффициентРазделки = ?(НЕ ЗначениеЗаполнено(Рецептура), 1, 
			?(РецептураРеквизиты.ХозяйственнаяОперация = Перечисления.питХозяйственныеОперации.РецептураРазделка, 0.01, 1));
		
		КоэффициентПересчета = 1;
		
		// Кэш для уменьшения запросов к базе
		КэшЗаполненияДерева = питПроизводство.ИнициализироватьСтруктуруКэшированныхЗначенийДляЗаполненияДереваСостава();
		
		// Структура для расчета себестоимости ингредиентов
		РасчетСебестоимостиСтруктура = Новый Структура;
		РасчетСебестоимостиСтруктура.Вставить("Организация", Организация);
		РасчетСебестоимостиСтруктура.Вставить("Подразделение", ПодразделениеРецептуры);
		РасчетСебестоимостиСтруктура.Вставить("Склад", Неопределено);
		
		// Переменная для накопления итоговой себестоимости
		ИтогоСумма = 0;
		
		// Обрабатываем каждый ингредиент рецептуры
		Для Каждого СтрокаДерева Из СтрокиДерева Цикл
			
			// Сезонный коэффициент
			Если НЕ НеИспользоватьСезонныеПроценты И КоэффициентРазделки = 1 Тогда
				
				СезонныйПроцент = питЛицензированиеСервер.ПолучитьСезонныйПроцент(
					СтрокаДерева.Номенклатура, 
					ДатаКалькуляции, 
					КэшЗаполненияДерева.КэшСезонныхПроцентов);
				СезонныйКоэффициент = 1 + СезонныйПроцент / 100;
				
			Иначе
				
				СезонныйКоэффициент = 1;
				
			КонецЕсли;
			
			// Единица измерения
			ЕдиницаИзмерения = ?(ЗначениеЗаполнено(СтрокаДерева.ЕдиницаИзмерения), 
				СтрокаДерева.ЕдиницаИзмерения, 
				СтрокаДерева.Номенклатура.ЕдиницаИзмерения);
			
			// Количество на блюдо
			НаКоличествоБлюда = Окр(СтрокаДерева.КоличествоУпаковок * КоэффициентПересчета * СезонныйКоэффициент * КоэффициентРазделки, 3);
			
			ТекВидНоменклатуры = СтрокаДерева.Номенклатура.питВидНоменклатуры;
			
			// Вычисляем цену ингредиента
			Цена = 0;
			
			Если ТекВидНоменклатуры = Перечисления.питВидыНоменклатуры.ТехнологическийИнгредиент Тогда
				
				// Технологический ингредиент не имеет цены
				
			ИначеЕсли ТекВидНоменклатуры = Перечисления.питВидыНоменклатуры.Блюдо Тогда
				
				// Блюдо - рассчитываем по рецептуре передела
				РецептураПередела = СтрокаДерева.Рецептура;
				
				Если ЗначениеЗаполнено(РецептураПередела) Тогда
					
					КоличествоВРецептуре = РецептураПередела.Количество;
					СуммаНорма = питПроизводство.ПолучитьСтоимостьПеределаПоРецептуре(
						РецептураПередела, 
						ТипЦенНормативная, 
						ДатаКалькуляции, 
						Валюта,
						,
						,
						НеИспользоватьСезонныеПроценты, 
						КэшЗаполненияДерева, 
						УточненияАвтовыбора, 
						Неопределено, 
						Ложь,
						, 
						РасчетСебестоимостиСтруктура);
					Цена = СуммаНорма / ?(КоличествоВРецептуре = 0, 1, КоличествоВРецептуре);
					
				Иначе
					
					Цена = питПроизводство.ПолучитьСтоимостьИнгредиента(
						СтрокаДерева.Номенклатура, 
						ТипЦенНормативная, 
						ДатаКалькуляции, 
						Валюта,
						,
						,
						,
						,
						ЕдиницаИзмерения,
						СтрокаДерева.Характеристика, 
						РасчетСебестоимостиСтруктура);
					
				КонецЕсли;
				
			Иначе
				
				// Обычный ингредиент
				Цена = питПроизводство.ПолучитьСтоимостьИнгредиента(
					СтрокаДерева.Номенклатура, 
					ТипЦенНормативная, 
					ДатаКалькуляции, 
					Валюта,
					,
					,
					,
					,
					ЕдиницаИзмерения,
					СтрокаДерева.Характеристика, 
					РасчетСебестоимостиСтруктура);
				
			КонецЕсли;
			
			// Накапливаем итоговую сумму
			СуммаИнгредиента = Окр(Цена * НаКоличествоБлюда, 2);
			ИтогоСумма = ИтогоСумма + СуммаИнгредиента;
			
			// Добавляем строку в таблицу данных
			НоваяСтрока = ТаблицаДанных.Добавить();
			НоваяСтрока.КодРецептуры = КодРецептуры;
			НоваяСтрока.НаименованиеРецептуры = НаименованиеРецептуры;
			НоваяСтрока.Статус = "Актуальная";
			НоваяСтрока.КнопкиФронтСистемы = КнопкиФронтСистемы;
			НоваяСтрока.Материал = Строка(СтрокаДерева.Номенклатура);
			НоваяСтрока.ЕдиницаИзмерения = Строка(ЕдиницаИзмерения);
			НоваяСтрока.НормаРасхода = НаКоличествоБлюда;
			НоваяСтрока.ЦенаМатериала = Цена;
			НоваяСтрока.СебестоимостьЕдиницы = ИтогоСумма;
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Вывод результата в СКД
    ТаблицыСтруктура = Новый Структура;
    ТаблицыСтруктура.Вставить("ТаблицаДанных", ТаблицаДанных);
    
    КомпНастроек = Новый КомпоновщикМакетаКомпоновкиДанных;
    МакетКомпоновки = КомпНастроек.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновки, ДанныеРасшифровки);
    
    ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
    ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ТаблицыСтруктура, ДанныеРасшифровки, Истина);
    
    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
    ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
    ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры
    #КонецОбласти